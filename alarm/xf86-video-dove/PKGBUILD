# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# Armada 510/Dove FrameBuffer driver for Xorg

buildarch=4

pkgname="xf86-video-dove"
pkgver=0.3.4
pkgrel=10
arch=('armv7h')
url="http://www.solid-run.com/mw/index.php/CuBox_Drivers#GPU_Drivers"
license=('GPL2')
depends=('marvell-libgfx')
makedepends=('pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool' '!strip')
source=("http://archlinuxarm.org/builder/src/xf86-video-dove-0.3.4.tar.gz"
        "xorg.conf"
        "dovefb-port-to-compat-api-for-new-server.patch"
        "rm-mibstore.patch")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/dovefb-port-to-compat-api-for-new-server.patch"
  CFLAGS="${CFLAGS} -I/opt/marvell-libgfx/include/HAL -g -DMRVL_SUPPORT_RANDR=1 -DMRVL_SUPPORT_EXA=1 -DDUMP_RAW_VIDEO=0 -DMRVL_USE_OFFSCREEN_HEAP=0 -DMRVL_EXA_MODE=2 -DMRVL_EXA_ENABLE_UP_DOWNLOAD=2 -DMRVL_EXA_FORCE_HW_LOAD=0 -DMRVL_EXA_ALLOC_PIXMAP_FROM_SYSTEM=0 -DMRVL_EXA_PERF_PROFILING=0 -DMRVL_EXA_TRACE_FALLBACK=0 -DMRVL_EXA_XBGR_SUPPORT=1 -DMRVL_XV_SUPPORT_RGB_FORMAT=1 -DMRVL_XV_TEX_VIDEO=1 -DMRVL_XV_OVERLAY_VIDEO=1 -DMRVL_XV_DEFERRED_STALL_GPU=1 -DMRVL_XV_USE_FAKE_FENCE_STALL=1 -DMRVL_RANDR_EDID_MODES=1  -DMRVL_CRTC_SUPPORT_ROTATION=1 -DMRVL_PLATFORM_INFO=1"
  LDFLAGS="$LDFLAGS -L/opt/marvell-libgfx/lib"

  patch -Np1 -i "${srcdir}/rm-mibstore.patch"

  # autotools fix.
  sed "s/^AM_CONFIG_HEADER/AC_CONFIG_HEADERS/" -i configure.ac

  autoreconf -i
  ./configure --prefix=/usr CFLAGS="${CFLAGS}"

  make
}

package() {
  pkgdesc="Armada 510/Dove framebuffer driver for X"

  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}/etc/X11"
  cp "${srcdir}"/xorg.conf "${pkgdir}/etc/X11"
}

md5sums=('b48a969dfab79a66c1817463380fa89c'
         '03294f82a3df0ccdf63f62ea1e4d027a'
         '0cf362fde4ca25572f8f264fb424e082'
         '9ee6940923cd8513c131fc83adc4f6b0')
