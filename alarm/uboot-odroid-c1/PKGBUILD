# U-Boot: ODROID-C1
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-odroid-c1
pkgver=2011.03
pkgrel=3
pkgdesc="U-Boot for ODROID-C1"
arch=('armv7h')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('git' 'bc')
backup=('boot/boot.ini')
_commit=388b4ae60f25e5e1a7a625b2253a66d05ab725d0
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-add-ext4-support.patch'
        '0002-remove-cross-compiling.patch'
        '0003-sd_fusing-tweaks.patch')
md5sums=('b8b66d02d1fab2a4745a7b769bff4b0c'
         '5f0868d53453af2f95f36d5094fa406f'
         'c8d1d5911a15ed995cf7fd57ef86b8fe'
         '127c91d5af47a6dd78fdaf19a77a5263'
         '645b00a999368d95c36d6cb5ef2f1076')

prepare() {
  cd u-boot-${_commit}

  git apply ../0001-add-ext4-support.patch
  git apply ../0002-remove-cross-compiling.patch
  git apply ../0003-sd_fusing-tweaks.patch
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  
  make distclean
  make odroidc_config
  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp bl1.bin.hardkernel sd_fusing.sh u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
}
