# Contributor Arno Rehn <arno@arnorehn.de>

buildarch=16

pkgname=qt5-eglfs-rpb
pkgver=5.3.1
pkgrel=1
pkgdesc="Qt5 eglfs plugin built against Broadcom's GL libs."
url="http://www.qt-project.org"
arch=('armv6h')
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('qt5-base' 'mtdev')
optdepends=()
makedepends=()
conflicts=()
replaces=()
backup=()
install=
source=("https://download.qt-project.org/official_releases/qt/${pkgver%.*}/${pkgver}/submodules/qtbase-opensource-src-${pkgver}.tar.xz"
        "eglfs-rpb-qmake.conf"
        "eglfs-rpb.patch")
md5sums=('572c9953847d391f2d33b420bbcdca46'
         'bf626b48b7960b0a558dd475b19a8dd2'
         'f06a0610e386a8c647d8c3c50364b1da')

prepare() {
  cd "${srcdir}"
  rm -rf "${pkgname}-${pkgver}"
  cp -r qtbase-opensource-src-${pkgver}/src/plugins/platforms/eglfs "${pkgname}-${pkgver}"
  cp qtbase-opensource-src-${pkgver}/mkspecs/devices/linux-rasp-pi-g++/qeglfshooks_pi.cpp "${pkgname}-${pkgver}/"
  cp eglfs-rpb-qmake.conf "${pkgname}-${pkgver}"/.qmake.conf
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../eglfs-rpb.patch
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install

  cd "${srcdir}/qtbase-opensource-src-${pkgver}"
  install -D -m644 LGPL_EXCEPTION.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LGPL_EXCEPTION.txt
}

# vim:set ts=2 sw=2 et:
