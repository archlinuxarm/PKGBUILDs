# U-Boot: Raspberry Pi
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=12

pkgname=uboot-raspberrypi
pkgver=2020.07
pkgrel=3
pkgdesc="U-Boot for Raspberry Pi"
arch=('armv7h' 'aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr' 'boot/config.txt')
makedepends=('bc' 'dtc' 'git')
conflicts_armv7h=('linux-raspberrypi')
_commit=d016a6eb01c8c7326a89cb42809fed2a21525de5
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "https://github.com/raspberrypi/firmware/raw/${_commit}/boot/bcm2710-rpi-3-b.dtb"
        "https://github.com/raspberrypi/firmware/raw/${_commit}/boot/bcm2710-rpi-3-b-plus.dtb"
        "https://github.com/raspberrypi/firmware/raw/${_commit}/boot/bcm2710-rpi-cm3.dtb"
        "https://github.com/raspberrypi/firmware/raw/${_commit}/boot/bcm2711-rpi-4-b.dtb"
        '0001-rpi-increase-space-for-kernel.patch'
        'boot.txt.v2'
        'boot.txt.v3'
        'mkscr')
md5sums=('86e51eeccd15e658ad1df943a0edf622'
         '5334e61fe2dd43d5ebfa16344b51ffbf'
         'd1c389740c3898aa172f63116f4dc295'
         '887920b6a052070b69f2d6f47e57ee42'
         '00c5929bd3417f0489745c53d06135a7'
         '728c4a0a542db702b8d88ffe1994660c'
         '69e883f0b8d1686b32bdf79684623f06'
         'be8abe44b86d63428d7ac3acc64ee3bf'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${pkgver/rc/-rc}

  patch -p1 -i ../0001-rpi-increase-space-for-kernel.patch
}

build() {
  cd u-boot-${pkgver/rc/-rc}

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  make distclean
  [[ $CARCH == "armv7h" ]] && make rpi_2_config
  [[ $CARCH == "aarch64" ]] && make rpi_arm64_config
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}"/boot

  if [[ $CARCH == "armv7h" ]]; then
    cp u-boot.bin ${pkgdir}/boot/kernel7.img
    cp ../boot.txt.v2 ../boot.txt
  elif [[ $CARCH == "aarch64" ]]; then
    cp u-boot.bin ${pkgdir}/boot/kernel8.img
    cp ../*.dtb ${pkgdir}/boot
    cp ../boot.txt.v3 ../boot.txt
    echo "enable_uart=1" > ${pkgdir}/boot/config.txt
  fi

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}"/boot/boot.scr
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
