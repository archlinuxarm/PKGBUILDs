# U-Boot: ODROID-C2
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# Note: must be built on x86 with an old cross toolchain

buildarch=8
noautobuild=1

pkgname=uboot-odroid-c2
pkgver=2015.01
pkgrel=12
pkgdesc="U-Boot for ODROID-C2"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('git' 'bc')
backup=('boot/boot.ini')
_commit=d6b6563424fa08c6f5a25cd53575a6d28579a699
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-sd_fusing-tweaks.patch'
        '0002-arch-linux-arm-modifications.patch')
md5sums=('ce59cbff0c5057d05847cc536dac4d8a'
         '5c464f1a0b57ce6bb5bce82e043a9840'
         '316b458415515edfc18abd54a52d43d4'
         'de51b9b3345954238c455214c5947ea3')

prepare() {
  cd u-boot-${_commit}

  git apply ../0001-sd_fusing-tweaks.patch
  git apply ../0002-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  
  make distclean
  make odroidc2_config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp bl1.bin.hardkernel sd_fusing.sh u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
}
