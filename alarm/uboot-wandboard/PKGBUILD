# U-Boot: Wandboard
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-wandboard
pkgname=('uboot-wandboard-solo' 'uboot-wandboard-dual' 'uboot-wandboard-quad')
pkgver=2013.07
pkgrel=2
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git')
source=("uboot::git://git.denx.de/u-boot.git#tag=v${pkgver}"
        'alarm.patch')
md5sums=('SKIP'
         '8a9f8214097fac0825911698b90986d3')

prepare() {
  cd uboot
  patch -p1 -i "${srcdir}"/alarm.patch
  rm -rf .git
}

build() {
  cd uboot
  
  unset CFLAGS
  unset CXXFLAGS
  
  for i in solo dl quad; do
    make distclean
    make wandboard_${i}_config
    make
    mv u-boot.imx u-boot-${i}.imx
  done
}

package_uboot-wandboard-solo() {
  pkgdesc="U-Boot for Wandboard Solo"
  conflicts=('uboot-wandboard-dual' 'uboot-wandboard-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp uboot/u-boot-solo.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-wandboard-dual() {
  pkgdesc="U-Boot for Wandboard Dual"
  conflicts=('uboot-wandboard-solo' 'uboot-wandboard-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp uboot/u-boot-dl.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-wandboard-quad() {
  pkgdesc="U-Boot for Wandboard Quad"
  conflicts=('uboot-wandboard-solo' 'uboot-wandboard-dual')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp uboot/u-boot-quad.imx "${pkgdir}"/boot/u-boot.imx
}
