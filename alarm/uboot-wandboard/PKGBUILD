# U-Boot: Wandboard
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-wandboard
pkgver=2015.10
pkgrel=1
pkgdesc="U-Boot for Wandboard"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc')
replaces=('uboot-wandboard-solo' 'uboot-wandboard-dual' 'uboot-wandboard-quad')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch')
md5sums=('7c203b0fc3390a122d8e8b75f147eac5'
         '710dbb0d9d4a2385f68ba14e86e97fdc')

prepare() {
  cd u-boot-${pkgver}
  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}
  
  unset LDFLAGS
  
  make distclean
  make wandboard_defconfig
  make
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}"/boot
  cp SPL u-boot.img "${pkgdir}"/boot
}
