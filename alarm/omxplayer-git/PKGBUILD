# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Huulivoide <gmail.com: jesse.jaara>
# Contributor: Jonathan Hudson <daria.co.uk: jh+arch>

buildarch=20

pkgname=omxplayer-git
pkgver=434.fe66e57
pkgrel=1
pkgdesc="omxplayer is a command line media player for the RaspberryPi"
arch=('armv6h' 'armv7h')
url="https://github.com/popcornmix/omxplayer"
license=(GPL2)
depends=('ffmpeg' 'raspberrypi-firmware-tools' 'fbset')
makedepends=('git' 'boost')
optdepends=('ttf-freefont')
provides=(omxplayer)
conflicts=('omxplayer' 'omxplayer-bin')
source=('git://github.com/popcornmix/omxplayer.git'
        '0001-alarm-fixes.patch')
md5sums=('SKIP'
         '13895e966d734d927bec43376a1c1de8')

pkgver() {
  cd omxplayer
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/omxplayer"

  git apply ../0001-alarm-fixes.patch
}

build() {
  cd "${srcdir}/omxplayer"

  make
}

package() {
  cd "${srcdir}/omxplayer"

  make DESTDIR="${pkgdir}" install
}
