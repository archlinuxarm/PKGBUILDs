# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Huulivoide <gmail.com: jesse.jaara>
# Contributor: Jonathan Hudson <daria.co.uk: jh+arch>

buildarch=16

pkgname=omxplayer-git
pkgver=380.6ee9a0a
pkgrel=1
pkgdesc="omxplayer is a command line media player for the RaspberryPi"
arch=('arm' 'armv6h')
url="https://github.com/popcornmix/omxplayer"
license=(GPL2)
depends=('ffmpeg' 'raspberrypi-firmware-tools' 'fbset')
makedepends=('git' 'boost')
optdepends=('ttf-freefont')
provides=(omxplayer)
conflicts=('omxplayer' 'omxplayer-bin')
source=('git://github.com/popcornmix/omxplayer.git'
        'alarm.patch')
md5sums=('SKIP'
         '4f21babb3985e7e029a799383938f8ce')

pkgver() {
  cd omxplayer
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/omxplayer"

  patch -p1 -i ../alarm.patch

  sed 's|truetype/freefont|TTF|g' -i omxplayer.cpp
}

build() {
  cd "${srcdir}/omxplayer"

  make
}

package() {
  cd "${srcdir}/omxplayer"

  make DESTDIR="${pkgdir}" install
}
