# ARMSoC Mali FrameBuffer driver for ODROID
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=xf86-video-armsoc-odroid
_commit=3bdf799bbb0d087cc1a206dd913ed1d10dec86a9
pkgver=237.3bdf799
pkgrel=3
arch=('armv7h')
url="https://github.com/mdrjr/xf86-video-armsoc/tree/r4p0"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=(xf86-video-armsoc)
provides=(xf86-video-armsoc)
source=("$pkgname::git+https://github.com/mdrjr/xf86-video-armsoc.git#commit=${_commit}"
        '0001-compat-for-newer-xorg.patch')
md5sums=('SKIP'
         'dd9885df7c3a5ec817a859c863860bc0')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

prepare() {
  cd $pkgname
  git apply ../0001-compat-for-newer-xorg.patch
}

build() {
  cd $pkgname

  CFLAGS=`echo $CFLAGS | sed -e 's/vfpv3-d16/neon/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}
