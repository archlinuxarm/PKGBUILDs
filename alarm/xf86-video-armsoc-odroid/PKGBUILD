# ARMSoC Mali FrameBuffer driver for ODROID
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=xf86-video-armsoc-odroid
pkgver=240.78c3cda
pkgrel=1
pkgdesc='X.org graphics driver for ARM graphics - Exynos ODROID'
arch=('armv7h')
url="https://github.com/mdrjr/xf86-video-armsoc/tree/r4p0"
license=('GPL2')
makedepends=('git' 'xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23')
conflicts=('xf86-video-armsoc' 'xorg-server<1.19' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
provides=(xf86-video-armsoc)
source=("$pkgname::git+https://github.com/mdrjr/xf86-video-armsoc.git#branch=r4p0"
        '0001-Adapt-Block-WakeupHandler-signature-for-ABI-23.patch'
        '0002-Use-NotifyFd-for-drm-fd.patch')
md5sums=('SKIP'
         '9d06b5add32485a340c771bb87f3c81c'
         '742f19941f6ef38a344c26c5785b8b79')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  git apply ../0001-Adapt-Block-WakeupHandler-signature-for-ABI-23.patch
  git apply ../0002-Use-NotifyFd-for-drm-fd.patch
}

build() {
  cd $pkgname

  CFLAGS=`echo $CFLAGS | sed -e 's/vfpv3-d16/neon/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}
