# vim:set ts=2 sw=2 et:
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: popcornmix & asavah from kodi forums - adapting for makechrootpkg
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] com>
# Contributor: tomasgroth at yahoo.dk
# Contributor: WarheadsSE <max@warheads.net>
# Contributor: Adrian Fedoreanu <adrian [dot] fedoreanu [at] gmail [dot] com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>
# Contributor: Ulf Winkelvos <ulf [at] winkelvos [dot] de>
# Contributor: Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: B & monty - Thanks for your hints :)
# Contributor: marzoul
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>

buildarch=4

_prefix=/usr

pkgbase=kodi-rbp4
pkgname=('kodi-rbp4' 'kodi-rbp4-eventclients' 'kodi-rbp4-tools-texturepacker' 'kodi-rbp4-dev')
pkgver=18.4
pkgrel=1
_codename=Leia
_tag="18.4-$_codename"
_ffmpeg_version="4.0.4-$_codename-18.4"
_libdvdcss_version="1.4.2-$_codename-Beta-5"
_libdvdnav_version="6.0.0-$_codename-Alpha-3"
_libdvdread_version="6.0.0-$_codename-Alpha-3"
_fmt_version="5.1.0"
_crossguid_version="8f399e8bd4"
_fstrcmp_version="0.7.D001"
_flatbuffers_version="1.9.0"
arch=('armv7h')
url="https://kodi.tv"
license=('GPL2')
makedepends=(
  'afpfs-ng' 'bluez-libs' 'cmake' 'curl' 'doxygen' 'glew'
  'gperf' 'hicolor-icon-theme' 'java-runtime' 'libaacs' 'libass'
  'libbluray' 'libcdio' 'libcec-rpi' 'libgl' 'mariadb-libs' 'libmicrohttpd'
  'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libpulse' 'libva'
  'libvdpau' 'libxrandr' 'libxslt' 'lirc' 'lzo' 'mesa' 'nasm'
  'python2-pycryptodome' 'python2-pillow' 'python2-pybluez' 'python2-simplejson'
  'shairplay' 'smbclient' 'taglib' 'tinyxml' 'swig'
  'upower' 'giflib' 'rapidjson' 'ghostscript'
  'libinput' 'libxkbcommon'
)

# leia_rpi4 branch contains patches to enable hardware decoding on rpi4
# expect kodi v19 to not to use MMAL but to use ffmpeg+v4l2
# https://github.com/popcornmix/xbmc/commits/leia_pi4
_commit=3487ce18e30be7eaf9aab4fba0ec91d08060a305

source=(
  "xbmc-$pkgver-$_codename.zip::https://github.com/popcornmix/xbmc/archive/$_commit.zip"
  'kodi.service'
  '99-kodi.rules'
  'polkit.rules'
  '00-fix.building.with.mariadb.patch::https://github.com/wsnipex/xbmc/commit/cd20c8eb8a0394db1f028b118c4ca9b91b7e746a.patch'
  "ffmpeg-$_ffmpeg_version.tar.gz::https://github.com/xbmc/FFmpeg/archive/$_ffmpeg_version.tar.gz"
  "libdvdcss-$_libdvdcss_version.tar.gz::https://github.com/xbmc/libdvdcss/archive/$_libdvdcss_version.tar.gz"
  "libdvdnav-$_libdvdnav_version.tar.gz::https://github.com/xbmc/libdvdnav/archive/$_libdvdnav_version.tar.gz"
  "libdvdread-$_libdvdread_version.tar.gz::https://github.com/xbmc/libdvdread/archive/$_libdvdread_version.tar.gz"
  "http://mirrors.kodi.tv/build-deps/sources/fmt-$_fmt_version.tar.gz"
  "http://mirrors.kodi.tv/build-deps/sources/crossguid-$_crossguid_version.tar.gz"
  "http://mirrors.kodi.tv/build-deps/sources/fstrcmp-$_fstrcmp_version.tar.gz"
  "http://mirrors.kodi.tv/build-deps/sources/flatbuffers-$_flatbuffers_version.tar.gz"
  01-fix.in-tree.ffmpeg.paths.for.makechrootpkg.hack.patch
)
noextract=(
  "libdvdcss-$_libdvdcss_version.tar.gz"
  "libdvdnav-$_libdvdnav_version.tar.gz"
  "libdvdread-$_libdvdread_version.tar.gz"
  "ffmpeg-$_ffmpeg_version.tar.gz"
  "fmt-$_fmt_version.tar.gz"
  "crossguid-$_crossguid_version.tar.gz"
  "fstrcmp-$_fstrcmp_version.tar.gz"
  "flatbuffers-$_flatbuffers_version.tar.gz"
)
sha256sums=('effff0f1f8170fd0c5dbbe665c10c7e65cd8bd422f888a2f7fb2004b5e3cee19'
            '585796d8a7f1ece32c24ff67b38885f0e3397372a4b9879eee3160a59391e333'
            '8e14cf455898246d11edd4ff7f4ed11a44c729fb40c34455a5b336a90ccc1f7b'
            '9ea592205023ba861603d74b63cdb73126c56372a366dc4cb7beb379073cbb96'
            '849daf1d5b081ef6d0e428bbc7d448799fc43a8ac9e79cd7513de0eb5a91b0bb'
            'e11e7594af35f36ab2711252c3d6bb106908f26605498aef4a9be2d7bc001db2'
            '38816f8373e243bc5950449b4f3b18938c4e1c59348e3411e23f31db4072e40d'
            '071e414e61b795f2ff9015b21a85fc009dde967f27780d23092643916538a57a'
            'a30b6aa0aad0f2c505bc77948af2d5531a80b6e68112addb4c123fca24d5d3bf'
            '73d4cab4fa8a3482643d8703de4d9522d7a56981c938eca42d929106ff474b44'
            '3d77d09a5df0de510aeeb940df4cb534787ddff3bb1828779753f5dfa1229d10'
            'e4018e850f80700acee8da296e56e15b1eef711ab15157e542e7d7e1237c3476'
            '5ca5491e4260cacae30f1a5786d109230db3f3a6e5a0eb45d0d0608293d247e3'
            '70b4c7db8766761ca060c038c8abb36c8c678246ca6c5efbde84c58442748ef2')

prepare() {
  # Must use MMAL headers and pc files rather than opengl ones so we copy the
  # contents of raspberrypi-firmware dependency to $srcdir and delete the offending
  # directories therein as well as redirect cmake

  mkdir -p "$srcdir/opt"
  cp -a /opt/vc "$srcdir/opt"
  rm -rf "$srcdir"/opt/vc/include/{EGL,GLES,GLES2}

  # Just these three are needed in a directory all by themselves
  # to which we point cmake via PKG_CONFIG_PATH

  mkdir "$srcdir/pkgconfig"
  for i in bcm_host.pc mmal.pc vcsm.pc; do
    cp "$srcdir/opt/vc/lib/pkgconfig/$i" "$srcdir/pkgconfig"
  done

  # force python 'binary' as python2
  [[ -d "$srcdir/path" ]] && rm -rf "$srcdir/path"
  mkdir "$srcdir/path"
  ln -s /usr/bin/python2 "$srcdir/path/python"

  cd "xbmc-$_commit"

  [[ -d kodi-build ]] && rm -rf kodi-build
  mkdir "$srcdir/kodi-build"

  patch -Np1 -i ../00-fix.building.with.mariadb.patch

  # Big thanks to asavah for helpful advice here and in the cmake step
  patch -Np1 -i ../01-fix.in-tree.ffmpeg.paths.for.makechrootpkg.hack.patch
}

build() {
  export PATH="$srcdir/path:$PATH"

  cd "$srcdir/kodi-build"

  # building for RPi4
  unset CFLAGS CXXFLAGS
  export CFLAGS="-march=armv8-a+crc+simd -mtune=cortex-a72 -mfpu=neon-fp-armv8 -mfloat-abi=hard -O2 -pipe -fstack-protector-strong -fno-plt"
  export CXXFLAGS="${CFLAGS}"

  # exporting PKG_CONFIG_PATH and building with the extra lib definitions
  # are part of the workaround discussed above

  export PKG_CONFIG_PATH="$srcdir/pkgconfig"

  _rootopt="$srcdir/opt/vc"
  _libdir="$_rootopt/lib"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DMMAL_INCLUDE_DIR="$_rootopt/include" \
    -DMMAL_LIBRARY="$_libdir/libmmal.so" \
    -DMMALCORE_LIBRARY="$_libdir/libmmal_core.so" \
    -DMMALUTIL_LIBRARY="$_libdir/libmmal_util.so" \
    -DMMALCLIENT_LIBRARY="$_libdir/libmmal_vc_client.so" \
    -DMMALCOMPONENT_LIBRARY="$_libdir/libmmal_components.so" \
    -DBCM_LIBRARY="$_libdir/libbcm_host.so" \
    -DVCHIQ_LIBRARY="$_libdir/libvchiq_arm.so" \
    -DVCHOSTIF_LIBRARY="$_libdir/libvchostif.a" \
    -DVCILCS_LIBRARY="$_libdir/libvcilcs.a" \
    -DVCOS_LIBRARY="$_libdir/libvcos.so" \
    -DVCSM_LIBRARY="$_libdir/libvcsm.so" \
    -DCONTAINER_LIBRARY="$_libdir/libcontainers.so" \
    -DVERBOSE=ON \
    -DENABLE_INTERNAL_FFMPEG=ON \
    -DENABLE_INTERNAL_FMT=ON \
    -DENABLE_INTERNAL_CROSSGUID=ON \
    -DENABLE_INTERNAL_FSTRCMP=ON \
    -DENABLE_INTERNAL_FLATBUFFERS=ON \
    -DENABLE_VAAPI=OFF \
    -DENABLE_VDPAU=OFF \
    -DENABLE_OPENGL=OFF \
    -DENABLE_MARIADBCLIENT=ON \
    -DENABLE_MYSQLCLIENT=OFF \
    -Dlibdvdcss_URL="$srcdir/libdvdcss-$_libdvdcss_version.tar.gz" \
    -Dlibdvdnav_URL="$srcdir/libdvdnav-$_libdvdnav_version.tar.gz" \
    -Dlibdvdread_URL="$srcdir/libdvdread-$_libdvdread_version.tar.gz" \
    -DFFMPEG_URL="$srcdir/ffmpeg-$_ffmpeg_version.tar.gz" \
    -DFMT_URL="$srcdir/fmt-$_fmt_version.tar.gz" \
    -DCROSSGUID_URL="$srcdir/crossguid-$_crossguid_version.tar.gz" \
    -DFSTRCMP_URL="$srcdir/fstrcmp-$_fstrcmp_version.tar.gz" \
    -DFLATBUFFERS_URL="$srcdir/flatbuffers-$_flatbuffers_version.tar.gz" \
    -DCORE_PLATFORM_NAME=gbm \
    -DGBM_RENDER_SYSTEM=gles \
    ../xbmc-"$_commit"
  make
  make preinstall
}

package_kodi-rbp4() {
  pkgdesc="A software media player and entertainment hub for digital media (Raspberry Pi 4)"
  depends=(
    'desktop-file-utils' 'hicolor-icon-theme' 'mesa' 'python2-pycryptodome'
    'python2-pillow' 'python2-simplejson' 'xorg-xdpyinfo'
    'bluez-libs' 'curl' 'lcms2' 'libass' 'libbluray' 'libcdio' 'libcec-rpi'
    'libinput' 'libmicrohttpd' 'libnfs' 'libpulse' 'libva' 'libxkbcommon'
    'libxslt' 'lirc' 'mariadb-libs' 'python2' 'smbclient' 'taglib'
    'tinyxml' 'polkit'
  )
  optdepends=(
    'afpfs-ng: Apple shares support'
    'bluez: Blutooth support'
    'python2-pybluez: Bluetooth support'
    'libplist: Limited AirPlay support'
    'pulseaudio: PulseAudio support'
    'shairplay: Limited AirPlay support'
    'upower: Display battery level'
  )
  install='kodi.install'
  provides=('xbmc' 'kodi')
  conflicts=('xbmc' 'kodi' 'arm-mem-git' 'shairplay-git')
  replaces=('xbmc-rbp-git')
  _components=('kodi' 'kodi-bin')

  export PATH="$srcdir/path:$PATH"

  cd kodi-build
  for _cmp in ${_components[@]}; do
  DESTDIR="$pkgdir" /usr/bin/cmake \
    -DCMAKE_INSTALL_COMPONENT="$_cmp" \
     -P cmake_install.cmake
  done

  # python2 is being used
  cd "$pkgdir"
  grep -lR '#!.*python' * | \
    while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "$file"; done

  install -Dm755 $srcdir/kodi-build/kodi-gbm "$pkgdir/usr/lib/kodi/kodi-gbm"
  install -Dm0644 "$srcdir/kodi.service" "$pkgdir/usr/lib/systemd/system/kodi.service"
  install -Dm0644 "$srcdir/polkit.rules" "$pkgdir/usr/share/polkit-1/rules.d/10-kodi.rules"
  chmod 0750 "$pkgdir/usr/share/polkit-1/rules.d/"

  # fix permissions necessary for accelerated video playback
  install -Dm0644 "$srcdir/99-kodi.rules" "$pkgdir/etc/udev/rules.d/99-kodi.rules"
}

package_kodi-rbp4-eventclients() {
  pkgdesc="Kodi Event Clients (Raspberry Pi4)"
  provides=('kodi-eventclients')
  conflicts=('kodi-eventclients')
  optdepends=('python2: most eventclients are implemented in python2')

  _components=(
    'kodi-eventclients-common'
    'kodi-eventclients-ps3'
    'kodi-eventclients-kodi-send'
  )

  export PATH="$srcdir/path:$PATH"

  cd kodi-build
  for _cmp in ${_components[@]}; do
    DESTDIR="$pkgdir" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="$_cmp" \
      -P cmake_install.cmake
  done

  # python2 is being used
  cd "$pkgdir"
  grep -lR '#!.*python' * | \
    while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "$file"; done
}

package_kodi-rbp4-tools-texturepacker() {
  pkgdesc="Kodi Texturepacker tool (Raspberry Pi4)"
  depends=('libpng' 'giflib' 'libjpeg-turbo' 'lzo')
  _components=('kodi-tools-texturepacker')

  cd kodi-build
  for _cmp in ${_components[@]}; do
    DESTDIR="$pkgdir" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="$_cmp" \
      -P cmake_install.cmake
  done
}

package_kodi-rbp4-dev() {
  pkgdesc="Kodi dev files (Raspberry Pi4)"
  depends=('kodi')
  provides=('kodi-dev')

  _components=('kodi-addon-dev'
    'kodi-audio-dev'
    'kodi-eventclients-dev'
    'kodi-game-dev'
    'kodi-inputstream-dev'
    'kodi-peripheral-dev'
    'kodi-pvr-dev'
    'kodi-screensaver-dev'
    'kodi-visualization-dev')

  export PATH="$srcdir/path:$PATH"

  cd kodi-build
  for _cmp in ${_components[@]}; do
    DESTDIR="$pkgdir" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="$_cmp" \
      -P cmake_install.cmake
  done

  # python2 is being used
  cd "$pkgdir"
  grep -lR '#!.*python' * | \
    while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "$file"; done
}
