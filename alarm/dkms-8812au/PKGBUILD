# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgname=dkms-8812au
_pkgbase=8812au
_commit=4fa0ce3ef9640f1de29f55cf1b839c29d8b1c69b
pkgver=4.2.2.g${_commit:0:7}
pkgrel=1
pkgdesc="Driver for Realtek RTL8812AU chipset wireless adaptors."
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
url="http://www.realtek.com.tw/"
license=('GPL')
depends=('dkms' 'linux-headers')
conflicts=()
options=(!strip)
source=(
  "https://github.com/gnab/rtl8812au/archive/${_commit}.zip"
  'dkms.conf'
  'rtl8812au.conf'
)
sha512sums=('20805281fd8a1913623ff897c348068a9247405ec7f57371b1646d33ada939e8acb1cfda7aa11885e4191e5ee53711380bdd9d8d13dfb73f9fe5013cf66fa2e8'
            '842a88dd23b1cbc77164e0771c112d64c533c970a804b4e8ac09a62a37a468a833d4a32d9f6f434a4d103e8effb7ed4b6fe73644e783b66d501e0c672ffe322c'
            'a76c9f1f8160639fdb172e7ea0ac4c52b9793bc0c769a3f9be3b454e8022fe5a45425cdb69e2eefe1cf68582df56caf72a36654b2bac6785faa81595f66da97a')

package() {
  cd ${srcdir}/rtl8812au-$_commit

  install -d ${pkgdir}/etc/modprobe.d
  install -d ${pkgdir}/usr/src/${_pkgbase}-${pkgver}

  cp -pr * ${pkgdir}/usr/src/${_pkgbase}-${pkgver}/
  cp ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_pkgbase}-${pkgver}/
  cp ${srcdir}/rtl8812au.conf ${pkgdir}/etc/modprobe.d/

  _arch="arm"
  case "${CARCH}" in
    aarch64|arm64)
      _arch="arm64"
      ;;
    *)
  esac

  # Set name and version
  sed -e "s/@PKGBASE@/${_pkgbase}/" \
    -e "s/@PKGVER@/${pkgver}/" \
    -e "s/@ARCH@/${_arch}/" \
    -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf
}
