# Maintainer: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>

pkgname=cryptodev-dkms
_pkgname=${pkgname%-*}
cryptodev_commit=a705360197260d28535746ae98c461ba2cfb7a9e

pkgver=1.9
pkgrel=3

pkgdesc="Cryptodev module to take advantage of hardware crypto engines in userspace"
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/cryptodev-linux/cryptodev-linux"
license=('GPL2')
depends=('dkms')
install=${pkgname}.install
provides=('cryptodev_friendly')
source=("cryptodev-${cryptodev_commit}.tar.gz::https://github.com/cryptodev-linux/cryptodev-linux/archive/${cryptodev_commit}.tar.gz"
        'dkms.conf'
        'linux-4.13+fix.patch::https://github.com/cryptodev-linux/cryptodev-linux/commit/569f253958513d83a76749500b92e91c5362b0ae.patch')
sha256sums=('559697e5217c01988b6c24f6ff1ed87f2e9071c86b344e3ec4bcbcac588dd498'
            'c42865a4a800a4927619ac5ed742be59a6d960af8295727af909e9ea9587f3da'
            '1f05d431298b7d76e2bc367c84181de35ab0d6aec05c46886c65b39d22612d19')

prepare() {
  cd "${srcdir}/cryptodev-linux-${cryptodev_commit}"
  patch -Np1 -i ${srcdir}/linux-4.13+fix.patch
}

package() {
  cd "${srcdir}"

  local install_dir="${pkgdir}/usr/src/${_pkgname}-${pkgver}"

  # Copy dkms.conf
  install -Dm644 dkms.conf "${install_dir}/dkms.conf"

  # Set name and version
  sed -e "s/@_PKGNAME@/${_pkgname}/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i "${install_dir}/dkms.conf"

  # Copy sources
  cd "cryptodev-linux-${cryptodev_commit}"

  for d in $(find . -type d)
  do
    install -dm755 "${install_dir}/$d"
  done

  for f in $(find . -type f)
  do
    install -m644 "$f" "${install_dir}/$f"
  done
}
