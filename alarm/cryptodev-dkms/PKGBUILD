# Maintainer: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>

pkgname=cryptodev-dkms
_pkgname=${pkgname%-*}
cryptodev_commit=26e167f8527b386e6bcb5d69eb2e12e587376a28

pkgver=1.8
pkgrel=6

pkgdesc="Cryptodev module to take advantage of hardware crypto engines in userspace"
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/cryptodev-linux/cryptodev-linux"
license=('GPL2')
depends=('dkms')
install=${pkgname}.install
provides=('cryptodev_friendly')
source=("cryptodev-${cryptodev_commit}.tar.gz::https://github.com/cryptodev-linux/cryptodev-linux/archive/${cryptodev_commit}.tar.gz"
        'dkms.conf')
sha256sums=('f875cc1e0237380beca6a6d87a3fcdc94b5d0e23019ab20f91cb3532cbffe2dc'
            'c42865a4a800a4927619ac5ed742be59a6d960af8295727af909e9ea9587f3da')
package() {
  cd "${srcdir}"

  local install_dir="${pkgdir}/usr/src/${_pkgname}-${pkgver}"

  # Copy dkms.conf
  install -Dm644 dkms.conf "${install_dir}/dkms.conf"

  # Set name and version
  sed -e "s/@_PKGNAME@/${_pkgname}/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i "${install_dir}/dkms.conf"

  # Copy sources
  cd "cryptodev-linux-${cryptodev_commit}"

  for d in $(find . -type d)
  do
    install -dm755 "${install_dir}/$d"
  done

  for f in $(find . -type f)
  do
    install -m644 "$f" "${install_dir}/$f"
  done
}
