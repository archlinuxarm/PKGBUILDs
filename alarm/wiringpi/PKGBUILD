# Maintainer: Mike Brown <mbrown@archlinuxarm.org>
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=20

_pkgname='wiringPi'
pkgname=wiringpi
pkgver=105.e8da87f
pkgrel=1
pkgdesc="WiringPi: An implementation of most of the Arduino Wiring functions for the Raspberry Pi"
arch=('arm' 'armv6h' 'armv7h')
makedepends=('git')
license=('LGPL')
url="http://wiringpi.com/"
source=("git://git.drogon.net/wiringPi")
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${_pkgname}"
  sed -i 's|/usr/local/bin/gpio|/usr/bin/gpio|' wiringPi/wiringPi.c
}

build() {
  cd "${_pkgname}"

  make -C wiringPi all static
  make -C devLib all static INCLUDE+="-I${srcdir}/${_pkgname}/wiringPi" LDFLAGS+="-L${srcdir}/${_pkgname}/wiringPi"
  make -C gpio INCLUDE+="-I${srcdir}/${_pkgname}/wiringPi -I${srcdir}/${_pkgname}/devLib" LDFLAGS+="-L${srcdir}/${_pkgname}/wiringPi -L${srcdir}/${_pkgname}/devLib"
}

package(){
  cd "${srcdir}/${_pkgname}/wiringPi"
  install -m 0755 -d "${pkgdir}/usr/lib"
  install -m 0755 -d "${pkgdir}/usr/include"
  install -m 0644 *.h "${pkgdir}/usr/include"
  install -m 0644 libwiringPi.{a,so*} "${pkgdir}/usr/lib"
  ln -s /usr/lib/libwiringPi.so.2.25 "${pkgdir}/usr/lib/libwiringPi.so"

  cd "${srcdir}/${_pkgname}/devLib"
  install -m 0644 *.h "${pkgdir}/usr/include"
  install -m 0644 libwiringPiDev.{a,so*} "${pkgdir}/usr/lib"
  ln -s /usr/lib/libwiringPiDev.so.2.25 "${pkgdir}/usr/lib/libwiringPiDev.so"

  cd "${srcdir}/${_pkgname}/gpio"
  install -m 0755 -d "${pkgdir}/usr/bin"
  install -m 4755 gpio "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/man/man1"
  install -m 0644 gpio.1 "${pkgdir}/usr/share/man/man1/"
}
