# U-Boot: Pandaboard and Pandaboard ES
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-pandaboard
pkgver=2015.10
pkgrel=2
pkgdesc="U-Boot for Pandaboard and Pandaboard ES"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
makedepends=('git' 'bc')
license=('GPL')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        '0002-ARM-Fix-pandaboard-es-and-a4-revision-ID.patch'
        'uEnv.txt')
md5sums=('7c203b0fc3390a122d8e8b75f147eac5'
         'bcdd217ecc19efcaddcb6ceb2cff79bf'
         'b93a61d2465a80ed7f0119c623719cb6'
         '86004660f2912861a040e335c9c0e85b')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
  git apply ../0002-ARM-Fix-pandaboard-es-and-a4-revision-ID.patch
}

build() {
  cd u-boot-${pkgver}

  unset LDFLAGS

  make distclean 
  make omap4_panda_config
  make
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
