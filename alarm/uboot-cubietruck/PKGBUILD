# U-Boot: sunXi
# Maintainer: Benjamin Hedrich <kiwisauce@pagenotfound.de>

buildarch=4

pkgbase=uboot-sunxi
pkgname=('uboot-cubietruck')
pkgver=2014.03
pkgrel=1
arch=('armv7h')
url="https://github.com/linux-sunxi/u-boot-sunxi/tree/sunxi-current"
license=('GPL')
makedepends=('sunxi-tools')
_commit=b5bd4c92b2d8c8790471c2eaf3971d511ab2339f
source=("https://github.com/linux-sunxi/u-boot-sunxi/archive/${_commit}.tar.gz"
        'cubietruck.fex' 'cubietruck.env')

md5sums=('e5ae66c56f684df2e25eda828f8983b5'
         '49a078a415176e394af96139698ecb12'
         '8bcb95928f659ea2f7d3ef69835641d1')

build() {
    cd u-boot-sunxi-${_commit}

    unset CFLAGS
    unset CXXFLAGS

    make cubietruck_config
    make
}

package_uboot-cubietruck() {
    pkgdesc="U-Boot for CubieTruck"
    install=cubietruck.install

    mkdir -p "${pkgdir}"/boot

    fex2bin cubietruck.fex "${pkgdir}"/boot/script.bin
    cp cubietruck.env "${pkgdir}"/boot/uEnv.txt

    cd u-boot-sunxi-${_commit}

    cp u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

}
