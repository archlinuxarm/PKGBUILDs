# Maintainer: Justin Dray <justin@dray.be>

buildarch=4

pkgname=chromium-pepper-flash
pkgdesc="Google Chrome's Pepper Flash plugin for Chromium on armv7h from Raspbian"
pkgver=24.0.0.221
pkgrel=1
arch=('armv7h')
url="http://www.google.com/chrome"
license=('custom:chrome')
depends=('chromium')
provides=("chromium-pepper-flash=${pkgver}")
optdepends=('pulseaudio-alsa: For PulseAudio users')
_modsver=20170213
source=("http://archive.raspberrypi.org/debian/pool/ui/r/rpi-chromium-mods/rpi-chromium-mods_${_modsver}_armhf.deb"
        "https://fpdownload.adobe.com/pub/flashplayer/pdc/$pkgver/flash_player_ppapi_linux.i386.tar.gz")
md5sums=('7d6abfa6227999601b3215e25fe7d2ec'
         '1e687be20a421d6844140afa56006cfd')

package() {
    bsdtar -xf data.tar.xz -C "$srcdir/" --strip-components=4 "usr/lib/chromium-browser/libpepflashplayer.so"
    sed -i 's/^    "x-ppapi-arch": "ia32",\s*$/    "x-ppapi-arch": "armv7", /' "$srcdir/manifest.json"
    install -Dm644 manifest.json libpepflashplayer.so -t "$pkgdir/usr/lib/PepperFlash/"
    install -Dm644 license.pdf -t "$pkgdir/usr/share/licenses/$pkgname/"
}
