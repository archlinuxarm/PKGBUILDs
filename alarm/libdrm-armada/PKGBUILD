# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=libdrm-armada
pkgver=r17.6b461c1
pkgrel=1
pkgdesc='Marvell Armada libdrm buffer object management module'
arch=('armv7h')
url="http://git.arm.linux.org.uk/cgit/libdrm-armada.git/"
license=('GPL2')
depends=('libdrm')
makedepends=('git')
options=('!libtool')
source=("git://git.arm.linux.org.uk/libdrm-armada.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $pkgname

  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}

