# U-Boot: Inverse Path USB armory
# Maintainer: Jason Plum <jplum@archlinuxarm.org>

buildarch=4

pkgname=uboot-usbarmory
pkgver=2014.07
pkgrel=3
pkgdesc="U-Boot for USB armory"
arch=('armv7h')
url="https://github.com/inversepath/u-boot-usbarmory.git"
license=('GPL')
makedepends=('git' 'bc')
source=("u-boot-${pkgver}::git+https://github.com/inversepath/u-boot-usbarmory.git"
        'alarm.patch'
        'uEnv.txt')
md5sums=('SKIP'
         '06742be0021c4a4123383e3790f21edf'
         '5e8a8854cc2a154e1a157535f2161913')

prepare() {
  cd u-boot-${pkgver}
  patch -p1 -i ../alarm.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS
  unset CXXFLAGS

  make distclean
  make usbarmory_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p "${pkgdir}"/boot
  cp u-boot.imx "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
