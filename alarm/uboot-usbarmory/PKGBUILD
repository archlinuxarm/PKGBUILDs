# U-Boot: Inverse Path USB armory
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Maintainer: Jason Plum <jplum@archlinuxarm.org>

buildarch=4

pkgname=uboot-usbarmory
pkgver=2015.04
pkgrel=1
pkgdesc="U-Boot for USB armory"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc')
install=${pkgname}.install
backup=(boot/boot.txt boot/boot.scr)
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'boot.txt'
        'mkscr')
md5sums=('570bdc2c47270c2a98ca60ff6c5c74cd'
         '145e9053347b729ff1d65b27c1accc9f'
         '3e9799661eec04d1ce0f3fe20ed21838'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS CXXFLAGS LDFLAGS

  make distclean
  make usbarmory_config
  make
}

package() {
  cd u-boot-${pkgver}

  install -d "${pkgdir}"/boot
  install -Dm644 u-boot.imx "${pkgdir}"/boot

  install -Dm644 ../boot.txt "${pkgdir}"/boot/boot.txt
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}"/boot/boot.scr
  install -Dm755 ../mkscr "${pkgdir}"/boot/mkscr
}
