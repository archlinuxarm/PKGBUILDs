# U-Boot: Inverse Path USB armory
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Jason Plum <jplum@archlinuxarm.org>

buildarch=4

pkgname=uboot-usbarmory
pkgver=2016.11
pkgrel=1
pkgdesc="U-Boot for USB armory"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc')
install=${pkgname}.install
backup=('boot/boot.txt' 'boot/boot.scr')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'boot.txt'
        'mkscr')
md5sums=('ca1f6e019d08aff8d0ca1beb2e66737d'
         '99bdb7f9887216634e688888d878efcb'
         '45535e1c506ce5083bb916492b6d820f'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS CXXFLAGS LDFLAGS

  make distclean
  make usbarmory_config
  make
}

package() {
  cd u-boot-${pkgver}

  install -d "${pkgdir}"/boot
  install -Dm644 u-boot.imx "${pkgdir}"/boot

  install -Dm644 ../boot.txt "${pkgdir}"/boot/boot.txt
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}"/boot/boot.scr
  install -Dm755 ../mkscr "${pkgdir}"/boot/mkscr
}
