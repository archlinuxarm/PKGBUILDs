# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Maintainer: graysky <graysky@archlnux.us>

buildarch=28

pkgname=firmware-raspberrypi
_firmcommit=bda9207acee945633464e89614f3717b856579b5
_bluezcommit=8445a53ce2c51a77472b908a0c8f6f8e1fa5c37a
pkgver=20211117
pkgrel=1
pkgdesc="Additional firmware for Raspberry Pi"
arch=('any')
url="https://github.com/RPi-Distro/firmware-nonfree/commits/master/brcm"
license=('custom')
options=('!strip')
source=(
        # bluetooth for RPi3+
        "BCM43430A1.hcd::https://github.com/RPi-Distro/bluez-firmware/raw/$_bluezcommit/broadcom/BCM43430A1.hcd"
        "BCM4345C0.hcd::https://github.com/RPi-Distro/bluez-firmware/raw/$_bluezcommit/broadcom/BCM4345C0.hcd"
        # bluetooth for Zero 2 W
        "BCM43430B0.hcd::https://github.com/RPi-Distro/bluez-firmware/raw/$_bluezcommit/broadcom/BCM43430B0.hcd"
        # fixes buggy linux-firmware for RPi4B
        'brcmfmac43455-sdio.raspberrypi,4-model-b.txt'
        # fixes buggy linux-firmware for Zero-W
        'https://archlinuxarm.org/builder/src/bcm43430/7.45.98.38/brcmfmac43430-sdio.txt'
        # Zero 2 W support
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43436-sdio.bin"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43436-sdio.clm_blob"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43436-sdio.txt"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43436s-sdio.bin"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43436s-sdio.txt"
        # BCM43456 on RPi-400
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43456-sdio.bin"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43456-sdio.clm_blob"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43456-sdio.txt"
        'RPi-WM8804.conf')

sha256sums=('c096ad4a5c3f06ed7d69eba246bf89ada9acba64a5b6f51b1e9c12f99bb1e1a7'
            'c723c903655086c9f3a1de94a291f495a0f087f1ee6d3ee7bc4c558497aae7bc'
            '338c2c6631131f516bfc7e64ef0872bd0402e1f98ef9d0c900eef0c814d90a25'
            'edb6f4e4fb19e18940004124feb4ffe160d72fc607243a07a4480338a28b2748'
            'e4ce7ad5ce72406f958ed758539b65d958d1c0eb3e46e3f8e9f02b510d88e0b0'
            '6dc7b3b53a1b69637a9e10e675e73ef56ca689d5bd279b78bd2f2719970cb80b'
            'fce7cbb62ffa6a5a65ca97b13f6fbf28d06c02d986c2072d65bf72164755fc34'
            '67b0e325bf76d096ce06044d2a442b95626f274096ce5724daaa8bcdd179b599'
            'bdc4fc14ca428130f474bb2f8bcb34c0684b0f9a0f31b05b4da39d64a2e1a333'
            '0441797884bbbd40a86e4579ff0e1c84ce69bb41f7bcf902a5c867ebb79b6ac3'
            'ddf83f2100885b166be52d21c8966db164fdd4e1d816aca2acc67ee9cc28d726'
            '2dbd7d22fc9af0eb560ceab45b19646d211bc7b34a1dd00c6bfac5dd6ba25e8a'
            '44e0bb322dc1f39a4b0a89f30ffdd28bc93f7d7aaf534d06d229fe56f6198194'
            'f978fbc40db75ba3213a4472023496d0716706eb1a6f078f207ac027c5753f43')

package() {
  install -d "$pkgdir/usr/lib/firmware/updates/brcm" "$pkgdir/usr/share/alsa/cards/"
  install -m 0644 -- *.bin *.txt *.clm_blob *.hcd "${pkgdir}/usr/lib/firmware/updates/brcm"
  install -m 0644 RPi-WM8804.conf "$pkgdir/usr/share/alsa/cards/"

  # make some symbolic links
  cd "$pkgdir/usr/lib/firmware/updates/brcm"
  ln -s ./brcmfmac43456-sdio.txt ./brcmfmac43456-sdio.raspberrypi,400.txt
  ln -s ./brcmfmac43456-sdio.txt ./brcmfmac43456-sdio.raspberrypi,4-compute-module.txt
  ln -s ./BCM43430B0.hcd ./BCM.hcd
}
