# Maintainer: graysky <therealgraysky AT protonmail DOT com>

pkgname=rpi-eeprom
_commit=e4f90a89e62051ff658c40c57150d13c7e67553b
pkgver=20220802
pkgrel=1
pkgdesc="Bootloader and VL805 USB controller EEPROM update tool for RPi4"
arch=('any')
url='https://github.com/raspberrypi/rpi-eeprom'
license=('custom')
depends=(python pciutils raspberrypi-firmware coreutils binutils)
backup=("etc/default/$pkgname-update")
source=("$pkgname-$pkgver-${_commit:0:10}.tar.gz::https://github.com/raspberrypi/rpi-eeprom/archive/$_commit.tar.gz")
md5sums=('79a279768ac5944d4a9be6fd1ef1e2d4')
install="$pkgname.install"

package() {
  cd "$pkgname-$_commit"
  install -pd "$pkgdir/usr/bin"
  install -pm755 rpi-eeprom-config "$pkgdir/usr/bin/rpi-eeprom-config"
  install -pm755 rpi-eeprom-digest "$pkgdir/usr/bin/rpi-eeprom-digest"
  install -pm755 rpi-eeprom-update "$pkgdir/usr/bin/rpi-eeprom-update"
  install -pDm644 "$pkgname-update-default" "$pkgdir/etc/default/$pkgname-update"

  # Arch ARM does not ship raspi-config
  sed -i '/to change the release/d' "$pkgdir/usr/bin/rpi-eeprom-update"

  install -pd "$pkgdir/usr/lib/firmware/raspberrypi/bootloader/backup"
  for target in beta critical stable; do
    cp -a "firmware/$target" "$pkgdir/usr/lib/firmware/raspberrypi/bootloader"
    # remove old images
    rm -f "$pkgdir/usr/lib/firmware/raspberrypi/bootloader/$target/"pieeprom-202[0,1]*.bin
  done
  ln -sf critical "$pkgdir/usr/lib/firmware/raspberrypi/bootloader/default"
  ln -sf stable "$pkgdir/usr/lib/firmware/raspberrypi/bootloader/latest"

  install -pDm644 LICENSE "$pkgdir/usr/share/doc/$pkgname"

  # firmware files should not be executable
  find "$pkgdir/usr/lib/firmware/raspberrypi/bootloader" -name '*.bin' -exec chmod 644 '{}' +

}
