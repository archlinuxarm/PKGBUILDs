# Maintainer: graysky <graysky@archlinux.us>

buildarch=12

pkgname=rpi-eeprom
_commit=3e56160f8b6d24b7fe6b3ae8eb9635f7904ef1c3
pkgver=20211122
pkgrel=1
pkgdesc="Bootloader and VL805 USB controller EEPROM update tool for RPi4"
arch=('any')
url='https://github.com/raspberrypi/rpi-eeprom'
license=('custom')
depends=(python pciutils raspberrypi-firmware coreutils binutils)
backup=("etc/default/$pkgname-update")
source=("https://github.com/raspberrypi/rpi-eeprom/archive/$_commit.tar.gz")
md5sums=('48bad6bc119efd34dfd7896afa82de43')
install="$pkgname.install"

package() {
  cd "$pkgname-$_commit"
  install -pd "$pkgdir/usr/bin"
  install -pm755 rpi-eeprom-config "$pkgdir/usr/bin/rpi-eeprom-config"
  install -pm755 rpi-eeprom-update "$pkgdir/usr/bin/rpi-eeprom-update"
  install -pDm644 "$pkgname-update-default" "$pkgdir/etc/default/$pkgname-update"

  # Arch ARM does not ship raspi-config
  sed -i '/to change the release/d' "$pkgdir/usr/bin/rpi-eeprom-update"

  install -pd "$pkgdir/usr/lib/firmware/raspberrypi/bootloader/backup"
  for target in beta critical stable; do
    cp -a "firmware/$target" "$pkgdir/usr/lib/firmware/raspberrypi/bootloader"
  done
  ln -sf critical "$pkgdir/usr/lib/firmware/raspberrypi/bootloader/default"
  ln -sf stable "$pkgdir/usr/lib/firmware/raspberrypi/bootloader/latest"

  install -pDm644 LICENSE "$pkgdir/usr/share/doc/$pkgname"
}
