# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=uboot-odroid-m1
pkgver=2025.10
pkgrel=1
pkgdesc="U-Boot for ODROID-M1"
arch=('aarch64')
url='https://u-boot.org/'
license=('GPL')
install=$pkgname.install
depends=('mtd-utils')
makedepends=('python-setuptools' 'python-pyelftools' 'swig')
source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        'https://github.com/rockchip-linux/rkbin/raw/refs/heads/master/bin/rk35/rk3568_bl31_v1.45.elf'
        'https://github.com/rockchip-linux/rkbin/raw/refs/heads/master/bin/rk35/rk3568_ddr_1560MHz_v1.23.bin')
sha256sums=('b4f032848e56cc8f213ad59f9132c084dbbb632bc29176d024e58220e0efdf4a'
            '76634f10e535bbe981fb9132fd6815a71586cc1b96aae1159bec6797579e5b9f'
            '039fbc3aca496eeaaaf35557202fbe43d005c3a28d7d7b876254b139960867af')

build() {
  cd u-boot-${pkgver/rc/-rc}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  export BL31=../rk3568_bl31_v1.45.elf
  export ROCKCHIP_TPL=../rk3568_ddr_1560MHz_v1.23.bin

  make distclean
  make odroid-m1-rk3568_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}
}

package() {
  install -d ${pkgdir}/boot

  cp u-boot-${pkgver/rc/-rc}/{u-boot-rockchip.bin,idbloader-spi.img,u-boot.itb} ${pkgdir}/boot
}
