# U-Boot: sunXi
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-sunxi
pkgname=() # to be filled below
pkgver=2017.01
pkgrel=2
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc' 'dtc' 'python2')
backup=(boot/boot.txt boot/boot.scr)
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'boot.txt'
        'mkscr')
md5sums=('ad2d82d5b4fa548b2b95bbc26c9bad79'
         '95f60c0ae1315e986d8a2aee15d5f854'
         '021623a04afd29ac3f368977140cfbfd')

_uboot_boards=(
        'A10-OLinuXino-Lime'
        'A10s-OLinuXino-M'
        'A13-OLinuXino'
        'A13-OLinuXinoM'
        'A20-OLinuXino-Lime'
        'A20-OLinuXino-Lime2'
        'A20-OLinuXino_MICRO'
        'Cubieboard'
        'Cubieboard2'
        'Cubieboard4'
        'Cubietruck'
        'Cubietruck_plus'
        'Linksprite_pcDuino'
        'Linksprite_pcDuino3'
        'Linksprite_pcDuino3_Nano'
        'Lamobo_R1'
        'Mele_M3'
        'Mele_M5'
        'Orangepi'
        'Orangepi_mini'
        'orangepi_2'
        'orangepi_lite'
        'orangepi_one'
        'orangepi_pc'
        'orangepi_pc_plus'
        'orangepi_plus2e'
        'orangepi_plus'
)

prepare() {
  cd u-boot-${pkgver}

  sed -i 's/env python$/&2/' tools/binman/binman{,.py}
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS CXXFLAGS LDFLAGS

  for i in ${_uboot_boards[@]}; do
    mkdir -p ../bin_${i}
    make distclean
    make ${i}_config
    echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
    make EXTRAVERSION=-${pkgrel}
    mv u-boot-sunxi-with-spl.bin ../bin_${i}
  done

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt ../boot.scr
}

for i in ${_uboot_boards[@]}; do
    p=$(echo $i | tr A-Z_ a-z-)
    n=$(echo $i | tr '_-' '  ')
    pkgname+=(uboot-$p)
    eval "
package_uboot-$p() {
  pkgdesc=\"U-Boot for $n\"
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi')

  install -d "${pkgdir}"/boot
  install -Dm644 bin_$i/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  install -Dm644 boot.txt "${pkgdir}"/boot/boot.txt
  install -Dm644 boot.scr "${pkgdir}"/boot/boot.scr
  install -Dm755 mkscr "${pkgdir}"/boot/mkscr
}
    "
done
