# U-Boot: sunXi
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-sunxi
pkgname=('uboot-cubieboard2' 'uboot-cubietruck')
pkgver=2014.01
pkgrel=2
arch=('armv7h')
url="https://github.com/linux-sunxi/u-boot-sunxi/tree/sunxi-current"
license=('GPL')
makedepends=('sunxi-tools')
backup=(boot/uEnv.txt)
_commit=660aa6fd9e1c25baabfcf4dfce42381547f5f59b
source=("https://github.com/linux-sunxi/u-boot-sunxi/archive/${_commit}.tar.gz"
        'alarm.patch'
        'cubieboard2.fex' 'cubieboard2.env'
        'cubietruck.fex' 'cubietruck.env')
md5sums=('019e2a420fa9d9a5e24aaf9cd8de2104'
         'f2f60fca0d93ef62c6a95c4f3254a829'
         '7423919c2ead208c5a04756edb6b948c'
         'd41d8cd98f00b204e9800998ecf8427e'
         'c898ab1b57f474d620f5704b2a53d87c'
         'd41d8cd98f00b204e9800998ecf8427e')

prepare() {
  cd u-boot-sunxi-${_commit}
  patch -p1 -i "${srcdir}"/alarm.patch
}

build() {
  cd u-boot-sunxi-${_commit}

  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  for i in Cubieboard2 Cubietruck; do
    mkdir ../bin_${i}
    make distclean
    make ${i}
    mv u-boot-sunxi-with-spl.bin ../bin_${i}
  done
}

package_uboot-cubieboard2() {
  pkgdesc="U-Boot for Cubieboard 2"
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp bin_Cubieboard2/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  fex2bin cubieboard2.fex "${pkgdir}"/boot/script.bin
  cp cubieboard2.env "${pkgdir}"/boot/uEnv.txt
}

package_uboot-cubietruck() {
  pkgdesc="U-Boot for Cubietruck"
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp bin_Cubietruck/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  fex2bin cubietruck.fex "${pkgdir}"/boot/script.bin
  cp cubietruck.env "${pkgdir}"/boot/uEnv.txt
}
