# U-Boot: BeagleBoard and BeagleBoard-xM
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-beagleboard
pkgver=2014.01
pkgrel=1
pkgdesc="U-Boot for BeagleBoard and BeagleBoard-xM"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://raw.github.com/eewiki/u-boot-patches/master/v${pkgver}/0001-omap3_beagle-uEnv.txt-bootz-n-fixes.patch"
        'alarm.patch'
        'uEnv.txt')
md5sums=('e531307578f6d32a7ccb1d04f1e08cbc'
         '6d6a394e07527d30330092ea3aa35cc9'
         '8c8e5ce2a03e99ed561185e9a6cffad8'
         '3704f05f308e3bb73a5b67a033b2ce5c')

prepare() {
  cd u-boot-${pkgver}

  patch -p1 -i "${srcdir}"/0001-omap3_beagle-uEnv.txt-bootz-n-fixes.patch
  patch -p1 -i "${srcdir}"/alarm.patch
}

build() {
  cd u-boot-${pkgver}

  unset LDFLAGS CFLAGS CXXFLAGS

  make distclean 
  make omap3_beagle_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
