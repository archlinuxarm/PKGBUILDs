# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=pybbio
pkgver=v0.5.98.g81a4a22
pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --always | sed 's|-|.|g'
}
pkgrel=1
pkgdesc="A Python library for Arduino-style hardware IO support on the TI BeagleBone"
arch=('armv7h')
url="https://github.com/alexanderhiam/PyBBIO"
license=('Apache')
depends=('python2')
makedepends=('git')
source=("$pkgname::git://github.com/alexanderhiam/PyBBIO.git")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"

  sed -i 's:env python:env python2:' examples/*.py tests/*.py
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}"

  python2 setup.py install --root=${pkgdir}
  install -m 0755 -d "${pkgdir}/usr/share/pybbio/examples"
  cp examples/* "${pkgdir}/usr/share/pybbio/examples"
}
