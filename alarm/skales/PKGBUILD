# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=skales
pkgver=r59.15ece94
pkgrel=1
arch=('any')
pkgdesc="Qualcomm boot image utilities"
url="https://www.codeaurora.org/cgit/quic/kernel/skales/"
license=('custom')
depends=('python2' 'dtc')
makedepends=('git')
source=('git://codeaurora.org/quic/kernel/skales')
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname}
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" dtbTool mkbootimg
}

package() {
  cd ${pkgname}
  install -d "${pkgdir}/usr/bin"
  install -m755 dtbTool "${pkgdir}/usr/bin"
  install -m755 mkbootimg "${pkgdir}/usr/bin"
}
