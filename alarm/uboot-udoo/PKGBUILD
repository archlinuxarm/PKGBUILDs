# U-Boot: UDOO
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-udoo
pkgname=('uboot-udoo-dual' 'uboot-udoo-quad')
pkgver=2013.10
pkgrel=2
arch=('armv7h')
url="https://github.com/UDOOboard/U-Boot_Unico-2013"
license=('GPL')
makedepends=('git')
option=('!strip')
source=("uboot::git+https://github.com/UDOOboard/U-Boot_Unico-2013.git"
        'alarm.patch')
md5sums=('SKIP'
         '6a7b2c125b8ff29fa47d1dd6cbd61f7f')

prepare() {
  cd uboot
  patch -p1 -i "${srcdir}"/alarm.patch
  rm -rf .git
}

build() {
  cd uboot
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  # Dual
  make distclean
  make udoo_dl_config
  make DDR_SIZE=2 DDR_TYPE=1 BOARD_TYPE=UDOO CPU_TYPE=DUAL_LITE OS_TYPE=LINUX ENV_DEVICE=ENV_MMC
  mv u-boot.imx u-boot-dual.imx

  # Quad
  make distclean
  make udoo_quad_config
  make DDR_SIZE=2 DDR_TYPE=1 BOARD_TYPE=UDOO CPU_TYPE=QUAD OS_TYPE=LINUX ENV_DEVICE=ENV_MMC
  mv u-boot.imx u-boot-quad.imx
}

package_uboot-udoo-dual() {
  pkgdesc="U-Boot for UDOO Dual"
  conflicts=('uboot-udoo-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp uboot/u-boot-dual.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-udoo-quad() {
  pkgdesc="U-Boot for UDOO Quad"
  conflicts=('uboot-udoo-dual')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp uboot/u-boot-quad.imx "${pkgdir}"/boot/u-boot.imx
}
