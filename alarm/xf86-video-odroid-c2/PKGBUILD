# ODROID-C2 Mali Xorg Driver
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=xf86-video-odroid-c2
pkgver=0.5
pkgrel=5
pkgdesc="X.org Mali video driver for ODROID-C2"
arch=('aarch64')
license=('custom')
url="http://www.hardkernel.com/"
depends=('odroid-c2-libgl-x11')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'git' 'odroid-c2-libgl-headers')
conflicts=('xorg-server<1.20' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25')
source=("git+https://github.com/mdrjr/c2_mali_ddx.git")
sha256sums=('SKIP')

build() {
  cd c2_mali_ddx

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  LDFLAGS+=" -L/usr/lib/mali-egl"
  ./configure --prefix=/usr
  make
}

package() {
  cd c2_mali_ddx
  install -Dm755 src/.libs/mali_drv.so "${pkgdir}/usr/lib/xorg/modules/drivers/mali_drv.so"
  install -Dm644 src/xorg.conf "${pkgdir}/etc/X11/xorg.conf.d/99-mali-c2.conf"
}
