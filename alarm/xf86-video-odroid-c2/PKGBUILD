# ODROID-C2 Mali Xorg Driver
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=xf86-video-odroid-c2
pkgver=0.5
pkgrel=3
pkgdesc="X.org Mali video driver for ODROID-C2"
arch=('aarch64')
license=('custom')
url="http://www.hardkernel.com/"
depends=('odroid-c2-libgl-x11')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23' 'git' 'odroid-c2-libgl-headers')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
source=("git+https://github.com/mdrjr/c2_mali_ddx.git")
sha256sums=('SKIP')

build() {
  cd c2_mali_ddx
  LDFLAGS+=" -L/usr/lib/mali-egl"
  ./configure --prefix=/usr
  make
}

package() {
  cd c2_mali_ddx
  install -Dm755 src/.libs/mali_drv.so "${pkgdir}/usr/lib/xorg/modules/drivers/mali_drv.so"
  install -Dm644 src/xorg.conf "${pkgdir}/etc/X11/xorg.conf.d/99-mali-c2.conf"
}
