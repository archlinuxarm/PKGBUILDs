# Maintainer: CruX <crux@project-insanity.org>
# Contributor: pepedog at archlinuxarm dot com

buildarch=4
pkgname=libfslvpuwrap
pkgver=3.10.31_1.1.0
_pkgver=1.0.54
pkgrel=1
pkgdesc="Wrapper library for the freescale proprietary VPU extensions"
url="https://community.freescale.com/docs/DOC-95560"
arch=('armv7h')
license=('proprietary')
depends=("imx-vpu=$pkgver")
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${_pkgver}.bin")
md5sums=('b07a2d4e7bf44225353106ed209f6dac')

prepare() {
  cd "${srcdir}"
  #extract the sources
  sh ${pkgname}-${_pkgver}.bin --force --auto-accept
  sed -n '/EOEULA/,/EOEULA/p' ${pkgname}-${_pkgver}.bin | grep -v EOEULA > LICENSE.$pkgname

}

build() {
  cd "${pkgname}-${_pkgver}"
  export CFLAGS="$CFLAGS -O2 -I/opt/fsl/include"
  export CPPFLAGS="$CPPFLAGS -O2 -I/opt/fsl/include"
  export LDFLAGS="$LDFLAGS -L/opt/fsl/lib"
  ./autogen.sh --prefix=/opt/fsl
  make all
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}/opt/fsl/licenses"
  cp "${srcdir}/LICENSE.$pkgname" "$pkgdir/opt/fsl/licenses"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/fsl/lib" > "${pkgdir}/etc/ld.so.conf.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
