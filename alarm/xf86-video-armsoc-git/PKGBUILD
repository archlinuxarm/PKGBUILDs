# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Linaro.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>

buildarch=4

pkgname=xf86-video-armsoc-git
_commit=8ca8513880697f9a34d4006c43342b830bdd1ff2
pkgver=243.8ca8513
pkgrel=1
epoch=1
pkgdesc='X.org graphics driver for ARM graphics'
arch=('armv7h')
url="http://cgit.freedesktop.org/xorg/driver/xf86-video-armsoc"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=(xf86-video-armsoc)
provides=(xf86-video-armsoc)
source=("$pkgname::git://anongit.freedesktop.org/xorg/driver/xf86-video-armsoc#commit=${_commit}"
        '0001-drmmode_display-include-sys-stat.h.patch')
md5sums=('SKIP'
         '89c59460f599996b0ac5669c0762567e')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

prepare() {
  cd $pkgname
  git apply ../0001-drmmode_display-include-sys-stat.h.patch
}

build() {
  cd $pkgname

  CFLAGS=`echo $CFLAGS | sed -e 's/vfpv3-d16/neon/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}

