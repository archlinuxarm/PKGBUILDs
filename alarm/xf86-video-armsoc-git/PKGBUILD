# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Linaro.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>

# Official upstream is at 
# http://git.linaro.org/gitweb?p=arm/xorg/driver/xf86-video-armsoc.git;a=summary

buildarch=4

pkgname=xf86-video-armsoc-git
pkgver=210.3516aa3
pkgrel=1
epoch=1
arch=('armv7h')
url="http://git.linaro.org/gitweb?p=arm/xorg/driver/xf86-video-armsoc.git"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=(xf86-video-armsoc)
replaces=(xf86-video-armsoc)
provides=(xf86-video-armsoc)
source=("$pkgname::git+http://git.linaro.org/git-ro/arm/xorg/driver/xf86-video-armsoc.git#commit=3516aa3")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

build() {
  cd "$srcdir/$pkgname"

  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  pkgdesc="armsoc framebuffer driver for X from chromium os git"

  make -C "$srcdir/$pkgname" DESTDIR="${pkgdir}" install
}

