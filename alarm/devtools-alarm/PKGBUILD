# $Id: PKGBUILD 182399 2013-04-10 05:34:09Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20130525
pkgrel=2
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
source=("ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz.sig"
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-logging.patch'
        '0006-machine-name.patch')
md5sums=('afec33a914471db8b8fb8ef65ee6b9ee'
         'SKIP'
         '2164cda16c7caf9789f2441d49fcdf77'
         '8d229b27e16c74a5308b1f452a17d97e'
         '1d0c06106bf633e555e2ce3cf26d30d5'
         'a53f7dea2392ffede8f2a1012707f201'
         '473f7128e088188121f5565e7ca34759'
         '2e7d45805bb6f989052fbf3a75ad4bea')

prepare() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	patch -p1 -i "${srcdir}"/0001-makechrootpkg-cache-dir.patch
	patch -p1 -i "${srcdir}"/0002-arch-nspawn-keep-mirrorlist.patch
	patch -p1 -i "${srcdir}"/0003-makechrootpkg-distcc.patch
	patch -p1 -i "${srcdir}"/0004-arch-nspawn-arm-fix.patch
	patch -p1 -i "${srcdir}"/0005-makechrootpkg-no-default-logging.patch
	patch -p1 -i "${srcdir}"/0006-machine-name.patch
}

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make PREFIX=/usr
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
