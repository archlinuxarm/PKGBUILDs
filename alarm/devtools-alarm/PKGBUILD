# $Id: PKGBUILD 182399 2013-04-10 05:34:09Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20171108
pkgrel=1
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
conflicts=('devtools')
provides=('devtools')
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-logging.patch'
        '0006-archbuild-no-setarch.patch'
        '0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch'
        '0008-fix-namcap-running-by-default.patch')
md5sums=('4a24b99bc1cffc63634eb7b3f5b116f1'
         '3cac269603dfd9c2f9c4c2be24e8b149'
         '1f74c22f44a4f2e39cafbd07d03595d5'
         '2a85106eb9b7ffab1eacf41276ce070c'
         'f3b14587bb007d84a7d6cba991680c41'
         '526448ae6f391606388a12e7d91e1fd1'
         '6f8453fd324e645ddf71b1009d1510a2'
         '60f7567ac617785c15e0a1a8b6f2abb7'
         '2d3fe5cf29b36a8b5abad93faa372c22')

prepare() {
	cd "${_pkgname}-${pkgver}"
	patch -p1 -i ../0001-makechrootpkg-cache-dir.patch
	patch -p1 -i ../0002-arch-nspawn-keep-mirrorlist.patch
	patch -p1 -i ../0003-makechrootpkg-distcc.patch
	patch -p1 -i ../0004-arch-nspawn-arm-fix.patch
	patch -p1 -i ../0005-makechrootpkg-no-default-logging.patch
	patch -p1 -i ../0006-archbuild-no-setarch.patch
	patch -p1 -i ../0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch
	patch -p1 -i ../0008-fix-namcap-running-by-default.patch
}

build() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
