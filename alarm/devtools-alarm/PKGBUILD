# $Id: PKGBUILD 182399 2013-04-10 05:34:09Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20151002
pkgrel=1
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
conflicts=('devtools')
provides=('devtools')
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-logging.patch'
        '0006-archbuild-no-setarch.patch'
        '0007-arch-nspawn-keep-stderr.patch')
md5sums=('a8d1234c6c7b936a850858e3bc03b2c8'
         '0e45d16790108fc111419ae9bd59dd46'
         '3e280d1d9cd35ae00c162540bd61b928'
         '5bd85e32b390eafbf6407ec49eba44f8'
         'f362832cb3f83b0fb1d7be837c03a21c'
         '89b2887b77b08a75056c47eb9496aadd'
         'b1ca4df008be9fa5bc6b09dc9c31d8c1'
         '2ee1b1680bd794187fbbe277eca32be1')

prepare() {
	cd "${_pkgname}-${pkgver}"
	patch -p1 -i ../0001-makechrootpkg-cache-dir.patch
	patch -p1 -i ../0002-arch-nspawn-keep-mirrorlist.patch
	patch -p1 -i ../0003-makechrootpkg-distcc.patch
	patch -p1 -i ../0004-arch-nspawn-arm-fix.patch
	patch -p1 -i ../0005-makechrootpkg-no-default-logging.patch
	patch -p1 -i ../0006-archbuild-no-setarch.patch
	patch -p1 -i ../0007-arch-nspawn-keep-stderr.patch
}

build() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
