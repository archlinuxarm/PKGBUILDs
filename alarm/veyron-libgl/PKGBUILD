# Veyron Chromebook Mali GL Driver
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=veyron-libgl
pkgver=r5p0
pkgrel=2
arch=('armv7h')
pkgdesc="Veyron Chromebook Mali GL Driver"
url="http://www.google.com/"
license=('Proprietary')
depends=('mesa-libgl')
source=("http://commondatastorage.googleapis.com/chromeos-localmirror/distfiles/mali-drivers-veyron-x11-1.20-r22.run"
        'mali-veyron.conf'
        '50-mali.rules')
md5sums=('eb48f11628dac74685e6f6b94469071a'
         '40f5104200cfceb12b4623d219646d4e'
         'd22820a21496fe3d9ccbae99643aac96')

prepare() {
  sed -i 's/^more/echo/' mali-drivers-veyron-x11-1.20-r22.run
  yes | sh mali-drivers-veyron-x11-1.20-r22.run
}

package() {
  install -d "${pkgdir}"/usr/lib/mali-egl
  install -d "${pkgdir}"/etc/ld.so.conf.d
  cp -a usr/lib/lib* "${pkgdir}"/usr/lib/mali-egl
  install -Dm644 mali-veyron.conf "${pkgdir}"/etc/ld.so.conf.d/mali-veyron.conf
  install -Dm644 50-mali.rules "${pkgdir}"/usr/lib/udev/rules.d/50-mali.rules
}
