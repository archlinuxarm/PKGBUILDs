# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Paul Adams <paul@thoughtcriminal.co.uk>

buildarch=28

pkgbase='wiringx-git'
pkgname=('wiringx-git' 'python-wiringx-git' 'python2-wiringx-git')
pkgver=r241.e1cc70e
pkgrel=2
pkgdesc="wiringX is a modular GPIO interface, derived from wiringPi"
arch=('armv6h' 'armv7h' 'aarch64')
url="https://github.com/wiringX/wiringX"
license=('GPL')
makedepends=('git' 'cmake' 'sed' 'python-setuptools' 'python2-setuptools')
source=(${pkgname}::git+https://github.com/wiringX/wiringX)
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  sed -i 's:DESTINATION sbin/:DESTINATION bin/:' "${pkgname}"/CMakeLists.txt
  msg2 "Removing insecure rpaths"
  sed -i 's|,-rpath=/usr/local/lib/||g' "${pkgname}"/CMakeLists.txt
}

build() {
  cd "${pkgname}"
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
}

package_wiringx-git() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}/" install
}

package_python-wiringx-git() {
  depends=('python' 'wiringx-git')
  pkgdesc="Python bindings for wiringX"
  cd "${pkgbase}/python"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-wiringx-git() {
  depends=('python2' 'wiringx-git')
  pkgdesc="Python2 bindings for wiringX"
  cd "${pkgbase}/python"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:

