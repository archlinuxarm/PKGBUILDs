# U-Boot: ROCKPro64

buildarch=8

pkgname=uboot-rockpro64
pkgver=2020.01
pkgrel=1
pkgdesc="U-Boot for ROCKPro64"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'git')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
	"https://github.com/rockchip-linux/rkbin/raw/master/bin/rk33/rk3399_bl31_v1.32.elf"
        'boot.txt'
        'mkscr')
md5sums=('b6b2e0787b6874e6b57da0a065a84f5a'
         '0335285697d60b103e94515e270f7f7b'
         'd0e5ad7b040242e837e67c12ab8bd4b7'
         '021623a04afd29ac3f368977140cfbfd')

build() {
  cd u-boot-${pkgver}
  cp ../rk3399_bl31_v1.32.elf bl31.elf

  make rockpro64-rk3399_defconfig
  sed -i 's/CONFIG_IDENT_STRING=""/CONFIG_IDENT_STRING=" Arch Linux ARM"/' .config
  # sed -i 's/CONFIG_BAUDRATE=1500000/CONFIG_BAUDRATE=115200/' .config

  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${pkgver}

  tools/mkimage -n rk3399 -T rksd -d tpl/u-boot-tpl-dtb.bin tpl-spl.img
  cat spl/u-boot-spl-dtb.bin >> tpl-spl.img

  mkdir -p "${pkgdir}/boot"
  cp tpl-spl.img u-boot.itb "${pkgdir}/boot"

  # sed -i 's/1500000/115200/' ../boot.txt 
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}/boot/boot.scr"
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
