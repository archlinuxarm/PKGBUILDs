# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

buildarch=20

pkgname=libbluray-kodi-rbp
pkgver=1.1.2
pkgrel=1
pkgdesc='Library to access Blu-Ray disks for video playback patched for kodi-rbp enhancements'
arch=('armv7h' 'armv6h')
url='http://www.videolan.org/developers/libbluray.html'
license=('LGPL2.1')
depends=('fontconfig' 'libxml2' 'libfreetype.so')
makedepends=('apache-ant' 'java-environment=8')
optdepends=('java-runtime=8: BD-J library')
provides=('libbluray.so' 'libbluray')
conflicts=('libbluray')
_path="raw.githubusercontent.com/LibreELEC/LibreELEC.tv/master/packages/multimedia/libbluray/patches"
source=("https://download.videolan.org/pub/videolan/libbluray/${pkgver}/libbluray-${pkgver}.tar.bz2"
https://$_path/libbluray-01-bump_to_Nevcairiel.patch
https://$_path/libbluray-02-install-extra-MVC-headers.patch
)
sha256sums=('a3dd452239b100dc9da0d01b30e1692693e2a332a7d29917bf84bb10ea7c0b42'
            '229370ad553e318559e5caa44e0dc6a588982106737d395c852a395fd2fc4d7c'
            'f29d0c1a8abc615ed8d6176b6bf39d256fe89f0841be1d4dac587399e5aaeed7')

prepare() {
  cd libbluray-${pkgver}
  patch -Np1 -i ../libbluray-01-bump_to_Nevcairiel.patch
  patch -Np1 -i ../libbluray-02-install-extra-MVC-headers.patch
}

build() {
  cd libbluray-${pkgver}

  autoreconf -fiv

  ./configure \
    --prefix=/usr \
    --disable-werror \
    --disable-extra-warnings \
    --disable-optimizations \
    --disable-examples \
    --disable-bdjava-jar \
    --disable-doxygen-doc \
    --disable-doxygen-dot \
    --disable-doxygen-man \
    --disable-doxygen-rtf \
    --disable-doxygen-xml \
    --disable-doxygen-chm \
    --disable-doxygen-chi \
    --disable-doxygen-html \
    --disable-doxygen-ps \
    --disable-doxygen-pdf \
    --with-freetype \
    --with-fontconfig \
    --with-libxml2 \
    --with-gnu-ld

  make
}

package() {
  cd libbluray-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
