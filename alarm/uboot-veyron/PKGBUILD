# U-Boot: Veyron Chromebooks
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-veyron
pkgname=('uboot-minnie' 'uboot-mickey' 'uboot-jerry' 'uboot-speedy')
pkgver=2018.07
pkgrel=1
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc' 'dtc' 'vboot-utils' 'python2' 'swig')
conflicts=('linux-veyron' 'linux-armv7-chromebook')
install=${pkgbase}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        'boot.txt'
        '0001-rockchip-add-support-for-veyron-speedy-ASUS-Chromebo.patch'
        '0002-rockchip-fix-incorrect-detection-of-ram-size.patch'
        '0003-Revert-doc-fix-incorrect-usage-of-DT-node-unit-addre.patch'
        '0004-rockchip-minnie-Add-a-.its-file-for-chromium.patch'
        '0005-rockchip-mickey-Add-a-.its-file-for-chromium.patch'
        '0006-rockchip-speedy-Add-a-.its-file-for-chromium.patch'
        '0007-veyron-set-CONFIG_SYS_TEXT_BASE-for-depthcharge.patch'
        '0008-dtoc-python2-compatibility.patch'
        'mkscr')
md5sums=('2b8eaa30dd118b29889669070da22bb0'
         '029cb473f59f08834b54cdc3e34c9672'
         '315442388197340887024e447638ae89'
         '9eac97ec89da2198997b638ba314f23f'
         '8f8023f23e6e917c169f5b8d2df4b729'
         'cbe75144ed16fb478df42e280f25b905'
         'e2ec620d5c3a27a84aa7fe583be0a5c8'
         '5424811723376398b9b7dc262f1db7c6'
         'edf1559c2d9ae707ef899bf9d2f116d8'
         '38db60779e9542ab4b6b89e24eac8b6d'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${pkgver/rc/-rc}
  git apply ../0001-rockchip-add-support-for-veyron-speedy-ASUS-Chromebo.patch
  git apply ../0002-rockchip-fix-incorrect-detection-of-ram-size.patch
  git apply ../0003-Revert-doc-fix-incorrect-usage-of-DT-node-unit-addre.patch
  git apply ../0004-rockchip-minnie-Add-a-.its-file-for-chromium.patch
  git apply ../0005-rockchip-mickey-Add-a-.its-file-for-chromium.patch
  git apply ../0006-rockchip-speedy-Add-a-.its-file-for-chromium.patch
  git apply ../0007-veyron-set-CONFIG_SYS_TEXT_BASE-for-depthcharge.patch
  git apply ../0008-dtoc-python2-compatibility.patch
}

build() {
  cd u-boot-${pkgver/rc/-rc}

  unset LDFLAGS CFLAGS CXXFLAGS
  export PYTHON=/usr/bin/python2

  for i in chromebook_minnie chromebit_mickey chromebook_jerry chromebook_speedy; do
    msg ${i}
    make O=b/${i} ${i}_defconfig
    echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> b/${i}/.config
    make O=b/${i} EXTRAVERSION=-${pkgrel}
  done
}

common_package() {
  name=$1
  dtb=$2

  cd u-boot-${pkgver/rc/-rc}

  b/${name}/tools/mkimage -f doc/chromium/${name}.its uboot.img
  echo "" > config
  dd if=/dev/zero of=bootloader.bin bs=512 count=1
  vbutil_kernel \
    --pack uboot.kpart \
    --version 1 \
    --vmlinuz uboot.img \
    --arch arm \
    --keyblock doc/chromium/devkeys/kernel.keyblock \
    --signprivate doc/chromium/devkeys/kernel_data_key.vbprivk \
    --config config \
    --bootloader bootloader.bin
  install -Dm644 uboot.kpart "${pkgdir}"/boot/uboot.kpart

  install -Dm644 ../boot.txt "${pkgdir}"/boot/boot.txt
  sed -i "s/@@FDTFILE@@/${dtb}/" "${pkgdir}"/boot/boot.txt
  b/${name}/tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d "${pkgdir}"/boot/boot.txt "${pkgdir}"/boot/boot.scr
  install -Dm755 ../mkscr "${pkgdir}"/boot/mkscr
}

package_uboot-minnie() {
  pkgdesc="U-Boot for Veyron Minnie Chromebook"

  common_package "chromebook_minnie" "rk3288-veyron-minnie.dtb"
}

package_uboot-mickey() {
  pkgdesc="U-Boot for Veyron Mickey Chromebit"

  common_package "chromebit_mickey" "rk3288-veyron-mickey.dtb"
}

package_uboot-jerry() {
  pkgdesc="U-Boot for Veyron Jerry Chromebook"

  common_package "chromebook_jerry" "rk3288-veyron-jerry.dtb"
}

package_uboot-speedy() {
  pkgdesc="U-Boot for Veyron Speedy Chromebook"

  common_package "chromebook_speedy" "rk3288-veyron-speedy.dtb"
}
