# U-Boot: UDOO
# Maintainer: Dave Higham <pepedog@archlinuxarm.org>

buildarch=4

pkgbase=uboot-cubox-i
pkgname=('uboot-cubox-i')
pkgver=2013.10
pkgrel=2
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git')
option=('!strip')
_commit=e509dd30e97bf2cbce21502e4462d9d7c1d85daa
source=("uboot-${_commit}.tar.gz::https://github.com/SolidRun/u-boot-imx6/archive/${_commit}.tar.gz"
        'kernel-add-support-for-gcc-5.patch'
        'arm_board_use_weak.patch'
        'leds_missing_include.patch'
        'remove_unnecessary_inits.patch'
        'u-boot-002-gcc5-use_gcc_inline_version_instead_c99.patch'
	'no-unalligned-access.patch'
        'common-main.c-make-show_boot_progress-__weak.patch')
md5sums=('3417d53a913949fd80c052fc8662c2a4'
         '721a46867e189d8dedc6b6f86a536a34'
         'b8cd082b76224d157d55404d0bc87831'
         'cf823fe2da67b8db5b9de9352a815f91'
         '6f3d537701904f0244e6d857e2545c5d'
         '8ed91e4257bfb7e0971fa988342537a3'
         '629d34349b5652e2d4274ad89e1c4481'
         '8087672256020417438b12ec4946e1cf')

prepare() {
  cd u-boot-imx6-${_commit}

  patch -Np1 -i ../kernel-add-support-for-gcc-5.patch
  patch -Np1 -i ../arm_board_use_weak.patch
  patch -Np1 -i ../leds_missing_include.patch
  patch -Np1 -i ../remove_unnecessary_inits.patch
  patch -Np1 -i ../u-boot-002-gcc5-use_gcc_inline_version_instead_c99.patch
  patch -Np1 -i ../no-unalligned-access.patch
  patch -Np1 -i ../common-main.c-make-show_boot_progress-__weak.patch
}

build() {
  cd u-boot-imx6-${_commit}

  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  make distclean
  make mx6_cubox-i_config
  make
}

package() {
  pkgdesc="U-Boot for all Cubox-i variants"
  install=${pkgbase}.install

  cd u-boot-imx6-${_commit}

  mkdir -p "${pkgdir}"/boot
  cp u-boot.img "${pkgdir}"/boot
  cp SPL "${pkgdir}"/boot
}

