# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>
# vim: ft=sh:

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - RPi support, no Exynos

buildarch=20

pkgname=libcec-rpi
_pkgname=libcec
pkgver=3.0.1
pkgrel=3
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter (Raspberry Pi)"
arch=('i686' 'x86_64')
url="http://libcec.pulse-eight.com/"
license=('GPL')
makedepends=('cmake')
depends=('udev' 'lockdev' 'libplatform' 'libxrandr' 'raspberrypi-firmware')
provides=('libcec')
conflicts=('libcec')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/Pulse-Eight/$_pkgname/archive/$_pkgname-$pkgver.tar.gz"
        'https://github.com/Pulse-Eight/libcec/commit/2f32a9debc1f148b5dfcfc463480f1432bb71725.patch')
sha256sums=('7e3670c8949a1964d6e5481f56dfff838857da10bdc60b506f6e9b7f117e253e'
            '83ca3b742831aa2e05297d11e238a0b0d1035a6404f60a6610acc8381d1c17ea')

prepare() {
  cd "$_pkgname-$_pkgname-$pkgver"
  patch -Np1 -i "$srcdir/2f32a9debc1f148b5dfcfc463480f1432bb71725.patch"
}

build() {
    cd "$_pkgname-$_pkgname-$pkgver"

    unset LDFLAGS

    mkdir build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBDIR_NOARCH=/usr/lib \
        -DHAVE_EXYNOS_API=0 \
        -DHAVE_RPI_API=1 \
        -DRPI_INCLUDE_DIR=/opt/vc/include \
        -DRPI_LIB_DIR=/opt/vc/lib
    make

}

package() {
    cd "$_pkgname-$_pkgname-$pkgver/build"
    make DESTDIR="$pkgdir" install
}
