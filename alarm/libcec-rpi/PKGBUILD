# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - RPi support, no Exynos

buildarch=20

pkgname=libcec-rpi
_pkgname=libcec
pkgver=2.2.0
pkgrel=1
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter (Raspberry Pi)"
arch=('i686' 'x86_64')
url="http://libcec.pulse-eight.com/"
license=('GPL')
depends=('udev' 'lockdev' 'raspberrypi-firmware')
provides=('libcec')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/Pulse-Eight/$_pkgname/archive/$_pkgname-$pkgver.tar.gz")
sha256sums=('fd4f47a18d6e0f4b9e6f5831280207ee2b2a5fc2741ae32ae09ad12a8aa52917')

build() {
  cd "$_pkgname-$_pkgname-$pkgver"
  autoreconf -vif

  unset LDFLAGS

  ./configure --prefix=/usr --disable-exynos --enable-rpi --with-rpi-include-path=/opt/vc/include --with-rpi-lib-path=/opt/vc/lib
  make
}

package() {
  cd "$_pkgname-$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
