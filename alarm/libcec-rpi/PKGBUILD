# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>

buildarch=16

pkgname=libcec-rpi
pkgver=2.1.1
pkgrel=1
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter with support for raspberry pi"
arch=('armv6h')
url="https://github.com/Pulse-Eight/libcec"
license=('GPL')
depends=('udev' 'lockdev' 'raspberrypi-firmware')
conflicts=('libcec')
provides=('libcec')
source=("https://github.com/Pulse-Eight/libcec/archive/libcec-$pkgver.tar.gz" "fix-boot_t.patch")
sha256sums=('f61378798d900d496ed3a5ff7747b0bb35bdfec9b5c4168dcea38396e3569bf1'
            'ba9b4030f3c2aa092a7c513629b60e82eeca7daf044576fa89b117409a8e883f')
options=(!libtool)

build() {

  cd "libcec-libcec-$pkgver"
  patch -p1 -i ../fix-boot_t.patch
  autoreconf -vif
  ./configure --prefix=/usr --enable-rpi \
   --with-rpi-include-path="/opt/vc/include" \
   --with-rpi-lib-path="/opt/vc/lib" 
  make
}

package() {
  cd "libcec-libcec-$pkgver"
  make DESTDIR="$pkgdir" install
}

