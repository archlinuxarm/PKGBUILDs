# Contributor: tomasgroth at yahoo.dk

buildarch=4

pkgname="libcec-cubox"
pkgver=2.0.4
pkgrel=1
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter with support for cubox"
arch=('armv7h')
url="https://github.com/Pulse-Eight/libcec"
license=('GPL')
depends=('udev' 'lockdev')
makedepends=('linux-headers-cubox')
conflicts=('libcec')
provides=('libcec')
source=("libcec-${pkgver}.tar.gz::https://github.com/Pulse-Eight/libcec/tarball/libcec-${pkgver}")
sha256sums=('652f8bddf8629eb4d14c93bc97efbeb7406482f69626302c8489df8e1fd8431f')
_srcfolder=Pulse-Eight-libcec-178d498
options=(!libtool)

build() {
  mv "$_srcfolder" "$pkgname-$pkgver"

  _kernel_release="$(pacman -Q linux-headers-cubox | grep -Eo "[^\ ]+$")-ARCH+"

  cd "$pkgname-$pkgver"
  autoreconf -vif
  ./configure --prefix=/usr --enable-cubox --enable-shared=lockdev \
    --with-tda995x-toolkit-path="/usr/src/linux-${_kernel_release}/include/nxp_hdmi"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

