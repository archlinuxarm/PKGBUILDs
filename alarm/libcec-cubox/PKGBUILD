# Contributor: tomasgroth at yahoo.dk

buildarch=4

pkgname="libcec-cubox"
pkgver=2.1.1
pkgrel=1
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter with support for cubox"
arch=('armv7h')
url="https://github.com/Pulse-Eight/libcec"
license=('GPL')
depends=('udev' 'lockdev')
makedepends=('linux-headers-cubox')
conflicts=('libcec')
provides=('libcec')
source=("libcec-${pkgver}.tar.gz::https://github.com/Pulse-Eight/libcec/tarball/libcec-${pkgver}")
sha256sums=('085cabf38bbe82c0a1c78fc4a0cc318b3c4f5fadae325b3d648821d2a4bf694d')
_srcfolder=Pulse-Eight-libcec-605d913
options=(!libtool)

build() {
  mv "$_srcfolder" "$pkgname-$pkgver"

  _kernel_release="$(pacman -Q linux-headers-cubox | grep -Eo "[^\ ]+$")-ARCH+"

  cd "$pkgname-$pkgver"
  export CFLAGS="$CFLAGS -fPIC"
  export CXXFLAGS="$CXXFLAGS -fPIC"
  export LDFLAGS="$LDFLAGS -fPIC"
  autoreconf -vif
  ./configure --prefix=/usr --enable-cubox \
    --with-tda995x-toolkit-path="/usr/src/linux-${_kernel_release}/include/nxp_hdmi"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

