# Contributor: tomasgroth at yahoo.dk

buildarch=4

pkgname="libcec-cubox"
pkgver=2.0.1
pkgrel=1
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter with support for cubox"
arch=('armv7h')
url="https://github.com/Pulse-Eight/libcec"
license=('GPL')
depends=('udev' 'lockdev')
makedepends=('linux-headers-cubox')
conflicts=('libcec')
provides=('libcec')
source=("libcec-2.0.1.tar.gz::https://github.com/Pulse-Eight/libcec/tarball/libcec-2.0.1" \
  "0001-Added-adapter-ID-interface.patch")
_srcfolder=Pulse-Eight-libcec-6be84fc
sha256sums=('3dc6d7080a98dbfddcab4605a1267c24751b4334a95f845583f34d9f24c9269a' \
  '4dd4a93d9cf29f383ae9ae10fd35c730160518caff9582fb4cc01cf0d316cd2c')
options=(!libtool)

build() {
  mv "$_srcfolder" "$pkgname-$pkgver"

  _kernel_release="$(pacman -Q linux-headers-cubox | grep -Eo "[^\ ]+$")-ARCH"

  cd "$pkgname-$pkgver"
  patch -p1 -i "${srcdir}/${source[1]}"
  autoreconf -vif
  ./configure --prefix=/usr --enable-cubox --enable-shared=lockdev \
    --with-tda995x-toolkit-path="/usr/src/linux-${_kernel_release}/include/nxp_hdmi"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

