buildarch=20

pkgbase=raspberrypi-firmware
pkgname=('raspberrypi-firmware'
         'raspberrypi-firmware-bootloader'
         'raspberrypi-firmware-bootloader-x'
         'raspberrypi-firmware-tools'
         'raspberrypi-firmware-examples')
pkgver=20150422
pkgrel=1
_commit=6cc27e02dde4f1e02cfddb3d0f23d54be87680cb
pkgdesc="Firmware files for Raspberry Pi"
arch=('any')
url="https://github.com/raspberrypi/firmware"
makedepends=('git')
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz")
md5sums=('d92514f3a266681a750a7cff76f426a8')

package_raspberrypi-firmware() {
  depends=('raspberrypi-firmware-bootloader'
           'raspberrypi-firmware-bootloader-x'
           'raspberrypi-firmware-tools'
           'raspberrypi-firmware-examples')
}

package_raspberrypi-firmware-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"

  mkdir -p "${pkgdir}"/boot/overlays
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/overlays/README "${pkgdir}"/boot/overlays
  rm "${pkgdir}"/boot/{start_x.elf,fixup_x.dat}
}

package_raspberrypi-firmware-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start_x.elf,fixup_x.dat} "${pkgdir}"/boot
}

package_raspberrypi-firmware-tools() {
  pkgdesc="Firmware tools and header files for Raspberry Pi"

  mkdir -p "${pkgdir}"/opt/vc

  cp -R "${srcdir}"/firmware-${_commit}/hardfp/opt/vc/{bin,include,lib,sbin,LICENCE} "${pkgdir}"/opt/vc

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
 
  # ensure we can load libs
  echo "/opt/vc/lib/" > "${pkgdir}"/etc/ld.so.conf.d/00-raspberrypi-firmware.conf
}

package_raspberrypi-firmware-examples() {
  pkgdesc="Firmware example sources for Raspberry Pi"

  mkdir -p "${pkgdir}"/opt/vc

  cp -R "${srcdir}"/firmware-${_commit}/hardfp/opt/vc/src "${pkgdir}"/opt/vc
}
