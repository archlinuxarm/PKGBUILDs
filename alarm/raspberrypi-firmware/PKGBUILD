# Maintainer: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Maintainer: graysky <graysky@archlinux.us>

buildarch=28

pkgname=raspberrypi-firmware
pkgver=20210111
pkgrel=1
_commitfirmware=e542b67e7f5487d222aa39b3e9ee780a5dbbb9eb
_commituserland=4a0a19b88b43e48c6b51b526b9378289fb712a4c
pkgdesc="Firmware tools, libraries, and headers for Raspberry Pi"
arch=('armv6h' 'armv7h' 'aarch64')
url="https://github.com/raspberrypi/firmware"
license=('custom')
makedepends_aarch64=('cmake')
replaces=('raspberrypi-firmware-tools')
provides=('raspberrypi-firmware-tools')
options=(!strip)
source=('00-raspberrypi-firmware.conf'
        '10-raspberrypi-firmware.rules')
source_armv6h=("https://github.com/raspberrypi/firmware/archive/$_commitfirmware.tar.gz")
source_armv7h=("https://github.com/raspberrypi/userland/archive/$_commitfirmware.tar.gz")
source_aarch64=("https://github.com/raspberrypi/userland/archive/$_commituserland.tar.gz"
                "https://raw.githubusercontent.com/raspberrypi/firmware/master/opt/vc/LICENCE")
md5sums=('72e0d5818fc513ece1b964f25f7e7882'
         '2ad54baf398afe59ff5a70eb655f1b96')
md5sums_armv6h=('f8f9a95e079fb05f649b1c6d06660e8a')
md5sums_armv7h=('f8f9a95e079fb05f649b1c6d06660e8a')
md5sums_aarch64=('3f5e1c9d4f8295065a283676794b3f57'
                 '86e53f5f5909ee66900418028de11780')

build() {
  if [[ "${CARCH}" = 'aarch64' ]]; then
    cd ${srcdir}/userland-${_commituserland}
    cmake -DCMAKE_BUILD_TYPE=Release -DARM64=ON
    make
  fi
}

package() {
  # setup permissions on video group for /opt/vc/bin/vcgencmd
  install -Dm0644 "$srcdir/10-raspberrypi-firmware.rules" "$pkgdir/usr/lib/udev/rules.d/10-raspberrypi-firmware.rules"

  if [[ "${CARCH}" = 'aarch64' ]]; then
    mkdir -p "${pkgdir}"/opt/vc

    cd ${srcdir}/userland-${_commituserland}
    make install DESTDIR="${pkgdir}"
    install -m644 ../LICENCE "${pkgdir}"/opt/vc/LICENCE
    rm -rf "${pkgdir}"/opt/vc/src

    # Remove executable flag on shared objects
    find "${pkgdir}" -type f -name '*.so' -print0 | xargs -0 chmod -x

    # Create lib links
    mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
    install -m644 ../00-raspberrypi-firmware.conf "${pkgdir}"/etc/ld.so.conf.d/00-raspberrypi-firmware.conf
  else
    mkdir -p "${pkgdir}"/opt/vc

    cp -R "${srcdir}"/firmware-${_commitfirmware}/hardfp/opt/vc/{bin,include,lib,LICENCE} "${pkgdir}"/opt/vc

    # Create lib links
    mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
    cp "${srcdir}/00-raspberrypi-firmware.conf" "${pkgdir}"/etc/ld.so.conf.d/
  fi
}
