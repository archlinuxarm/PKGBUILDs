# Maintainer: graysky <therealgraysky AT protonmail DOT com>
# Maintainer: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

pkgname=raspberrypi-firmware
pkgver=20220723
pkgrel=1
_commitfirmware=a75a129d59320b5828791ecc4c1b422fc110d3f1
_commituserland=54fd97ae4066a10b6b02089bc769ceed328737e0
pkgdesc="Firmware tools, libraries, and headers for Raspberry Pi"
arch=('armv7h' 'aarch64')
url="https://github.com/raspberrypi/firmware"
license=('custom')
makedepends_aarch64=('cmake')
replaces=('raspberrypi-firmware-tools')
provides=('raspberrypi-firmware-tools')
options=(!strip)
source=('00-raspberrypi-firmware.conf'
        '10-raspberrypi-firmware.rules'
        'raspberrypi-firmware.sh')
source_armv7h=("firmware-$pkgver-$pkgrel-${_commitfirmware:0:10}.tar.gz::https://github.com/raspberrypi/firmware/archive/$_commitfirmware.tar.gz")
source_aarch64=("userland-$pkgver-$pkgrel-${_commituserland:0:10}.tar.gz::https://github.com/raspberrypi/userland/archive/$_commituserland.tar.gz"
                "https://raw.githubusercontent.com/raspberrypi/firmware/master/opt/vc/LICENCE")
md5sums=('72e0d5818fc513ece1b964f25f7e7882'
         '2ad54baf398afe59ff5a70eb655f1b96'
         '60919cb17f31b4698b6650bbb46f18ed')
md5sums_armv7h=('8da36b92411dbe34eafc7e1a893e4138')
md5sums_aarch64=('9ce7f475fd3f38800d361cad7f3852c2'
                 '86e53f5f5909ee66900418028de11780')

build() {
  if [[ "${CARCH}" = 'aarch64' ]]; then
    cd "${srcdir}/userland-${_commituserland}"
    cmake -DCMAKE_BUILD_TYPE=Release -DARM64=ON
    make
  fi
}

package() {
  # setup permissions on video group for /opt/vc/bin/vcgencmd
  install -Dm0644 "$srcdir/10-raspberrypi-firmware.rules" "$pkgdir/usr/lib/udev/rules.d/10-raspberrypi-firmware.rules"

  # setup PATH to hit /opt/vc/bin/
  install -Dt "$pkgdir/etc/profile.d" -m644 raspberrypi-firmware.sh

  mkdir -p "${pkgdir}"/opt/vc

  if [[ "${CARCH}" = 'aarch64' ]]; then
    cd ${srcdir}/userland-${_commituserland}
    make install DESTDIR="${pkgdir}"
    install -m644 ../LICENCE "${pkgdir}"/opt/vc/LICENCE
    rm -rf "${pkgdir}"/opt/vc/src

    # Remove executable flag on shared objects
    find "${pkgdir}" -type f -name '*.so' -print0 | xargs -0 chmod -x

    # Create lib links
    mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
    install -m644 ../00-raspberrypi-firmware.conf "${pkgdir}"/etc/ld.so.conf.d/00-raspberrypi-firmware.conf
  else
    cp -R "${srcdir}"/firmware-${_commitfirmware}/hardfp/opt/vc/{bin,include,lib,LICENCE} "${pkgdir}"/opt/vc
    cp -R "${srcdir}"/firmware-${_commitfirmware}/opt/vc/man "${pkgdir}"/opt/vc

    # Create lib links
    mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
    cp "${srcdir}/00-raspberrypi-firmware.conf" "${pkgdir}"/etc/ld.so.conf.d/
  fi

  # Install man pages to /usr/share/man/ not to /opt/vc/man/
  mkdir -p "${pkgdir}"/usr/share/man/man{1,7}
  mv "${pkgdir}"/opt/vc/man/man1/*.1 "${pkgdir}"/usr/share/man/man1
  mv "${pkgdir}"/opt/vc/man/man7/*.7 "${pkgdir}"/usr/share/man/man7
  rm -rf "${pkgdir}"/opt/vc/man
}
