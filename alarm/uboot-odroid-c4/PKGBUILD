# U-Boot: ODROID-C4
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Jefferson Gonzalez <jgmdev@gmail.com>

# Note: must be built on x86 with an old cross toolchain
# Details: https://wiki.odroid.com/odroid-c4/software/building_u-boot

buildarch=8
noautobuild=1

pkgname=uboot-odroid-c4
pkgver=2015.01
pkgrel=1
pkgdesc="U-Boot for ODROID-C4"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot/tree/odroidg12-v2015.01"
license=('GPL')
install=$pkgname.install
depends=('uboot-tools')
makedepends=('git' 'bc')
backup=('boot/boot.ini')
_commit=a6c4094e8459281faa82c7d7b37212d1a9da1d12
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-sd_fusing-tweaks.patch'
        '0002-arch-linux-arm-modifications.patch'
        '91-uboot-uimg.hook')
md5sums=('adb7858d0f74b88e8ff7b28c1695308e'
         '32b8c35abad9d8d976bc480307a016c3'
         'cc18cda0bc75936e602341efd5a2fe93'
         '4d14405ba98f09c002505cbe53e2f6cb'
         '1931c8dfde7088530f173ca59fdb8989')

prepare() {
  cd u-boot-${_commit}

  git apply ../0001-sd_fusing-tweaks.patch
  git apply ../0002-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  make distclean mrproper
  make odroidc4_defconfig
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp sd_fusing.sh u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
  install -Dm644 "${srcdir}/91-uboot-uimg.hook" "${pkgdir}/usr/share/libalpm/hooks/91-uboot-uimg.hook"
}
