# U-Boot: ODROID-C4
# Maintainer: sfan5 <sfan5@live.de>

# Note: must be built on x86 with an old cross toolchain

buildarch=8
noautobuild=1

pkgname=uboot-odroid-c4
pkgver=2015.01
pkgrel=1
pkgdesc="U-Boot for ODROID-C4"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('bc')
backup=('boot/boot.ini')
_commit=e2abc5921e892c2a1a75003b5ff1fdbfa04b40a0
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-sd_fusing-tweaks.diff'
        '0002-arch-linux-arm-modifications.diff')
sha1sums=('652747f41fd318279526c2fb466ae3c27bcf6cb9'
          'b1ef9c359a2d370ef3f49a0c3fdd76e024d8a3ba'
          '0a9aac03daaa4ac1929d6b4ef33569ab93eb7804'
          '43f3e1f5ba825d16a51fe66e2169146d9b421f4d')

prepare() {
  cd u-boot-${_commit}

  patch -p0 --verbose <../0001-sd_fusing-tweaks.diff
  patch -p0 --verbose <../0002-arch-linux-arm-modifications.diff
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  make distclean
  make odroidc4_config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  install -d "${pkgdir}"/boot
  install -t "${pkgdir}"/boot sd_fusing.sh
  install -t "${pkgdir}"/boot -m 0644 u-boot.bin "${srcdir}"/boot.ini
}
