# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Jonny Gerold <jonny@fsk141.com>
# Contributor: Julian Langschaedel <meta.rb@gmail.com>

pkgname=uboot-tools
pkgver=2015.01
pkgrel=2
pkgdesc="U-Boot Tools"
arch=('arm' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
provides=('uboot-env' 'uboot-mkimage')
replaces=('uboot-env' 'uboot-mkimage')
backup=(etc/fw_env.config)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2
        fw_env.config)
md5sums=('7f08dc9e98a71652bd6968888ed6ec95'
         '514e86e0685d21fdaa0bbde9adf682ff')

build() {
  cd u-boot-${pkgver}

  make defconfig
  make tools-all
  make -C tools kwboot
}

package() {
  cd u-boot-${pkgver}

  install -dm755 ${pkgdir}/{usr/{bin,share/man/man1},etc}
  install -m755 tools/{fit_{check_sign,info},dumpimage,mkimage,mkenvimage,netconsole,ncb,proftool,env/fw_printenv,kwboot} ${pkgdir}/usr/bin
  ln -s /usr/bin/fw_printenv ${pkgdir}/usr/bin/fw_setenv
  install -m644 ${srcdir}/fw_env.config ${pkgdir}/etc
  install -m644 doc/{mkimage,kwboot}.1 ${pkgdir}/usr/share/man/man1
}
