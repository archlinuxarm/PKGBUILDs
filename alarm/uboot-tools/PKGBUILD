# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Jonny Gerold <jonny@fsk141.com>
# Contributor: Julian Langschaedel <meta.rb@gmail.com>

pkgname=uboot-tools
pkgver=2017.07
pkgrel=1
pkgdesc="U-Boot Tools"
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
license=('GPL' 'LGPL')
provides=('uboot-env' 'uboot-mkimage')
replaces=('uboot-env' 'uboot-mkimage')
backup=(etc/fw_env.config)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2
        fw_env.config)
md5sums=('b74741e7a9ad1cf9a5bfa87302b80f2b'
         '5a9dcbae5957f8621080d4bbc0e1520f')

build() {
  cd u-boot-${pkgver}

  make defconfig
  make tools-all CONFIG_KIRKWOOD=y
}

package() {
  cd u-boot-${pkgver}

  install -dm755 ${pkgdir}/{usr/{bin,share/man/man1},etc}
  install -m755 tools/{fit_{check_sign,info},dumpimage,mkimage,mkenvimage,netconsole,ncb,proftool,env/fw_printenv,kwboot} ${pkgdir}/usr/bin
  ln -s /usr/bin/fw_printenv ${pkgdir}/usr/bin/fw_setenv
  install -m644 ${srcdir}/fw_env.config ${pkgdir}/etc
  install -m644 doc/{mkimage,kwboot}.1 ${pkgdir}/usr/share/man/man1
}
