# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=xf86-video-armsoc-rockchip
pkgver=261.67d4cff
pkgrel=4
pkgdesc='X.org graphics driver for ARM graphics - Rockchip'
arch=('armv7h')
url="https://github.com/mmind/xf86-video-armsoc"
license=('GPL2')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'git')
conflicts=('xorg-server<1.20' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25' 'xf86-video-armsoc')
provides=(xf86-video-armsoc)
options=('!libtool')
source=("$pkgname::git+https://github.com/mmind/xf86-video-armsoc.git#branch=devel/rockchip"
        '0001-Adapt-Block-WakeupHandler-signature-for-ABI-23.patch'
        '0002-Use-NotifyFd-for-drm-fd.patch'
        '20-armsoc.conf')
md5sums=('SKIP'
         'c9ec23461d651841b128b68f07e25c7f'
         'e7a0f28ac376f0c97a05b1bb758d36e4'
         'ca34299695813b200f0d6054c45d1f94')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  git apply ../0001-Adapt-Block-WakeupHandler-signature-for-ABI-23.patch
  git apply ../0002-Use-NotifyFd-for-drm-fd.patch
}

build() {
  cd $pkgname

  CFLAGS=`echo $CFLAGS | sed -e 's/vfpv3-d16/neon/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh --prefix=/usr --with-drmmode=rockchip
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/etc/X11/xorg.conf.d/
  cp ../20-armsoc.conf "${pkgdir}"/etc/X11/xorg.conf.d/
}
