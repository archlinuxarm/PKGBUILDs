# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# Must be built on x86 due to amlogic utilities, for example:
#   CROSS_COMPILE=/path/to/x-tools8/aarch64-unknown-linux-gnu/bin/aarch64-unknown-linux-gnu- CARCH=aarch64 ARCH=arm64 makepkg -srf

buildarch=8
noautobuild=1

pkgname=uboot-odroid-n2
pkgver=2025.10
pkgrel=1
pkgdesc="U-Boot for ODROID-N2"
arch=('aarch64')
url='https://u-boot.org/'
license=('GPL')
install=$pkgname.install
makedepends=('git' 'python-setuptools')
source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "git+https://github.com/LibreELEC/amlogic-boot-fip")
sha256sums=('b4f032848e56cc8f213ad59f9132c084dbbb632bc29176d024e58220e0efdf4a'
            'SKIP')

build() {
  cd u-boot-${pkgver/rc/-rc}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  
  make distclean
  make odroid-n2_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}

  cd ../amlogic-boot-fip
  ./build-fip.sh odroid-n2 ../u-boot-${pkgver/rc/-rc}/u-boot.bin ..
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}"/boot
  cp ../u-boot.bin.sd.bin "${pkgdir}"/boot
}
