# U-Boot: ODROID-N2
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=uboot-odroid-n2
pkgver=2015.01
pkgrel=11
pkgdesc="U-Boot for ODROID-N2"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
depends=('uboot-tools')
makedepends=('git' 'bc')
backup=('boot/boot.ini')
_commit=12c58e94e533b85a19d2f83d1c0a34345764ca07
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-sd_fusing-tweaks.patch'
        '0002-arch-linux-arm-modifications.patch'
        '0003-gcc-10.patch'
        '91-uboot-uimg.hook')
md5sums=('b0e7dac5be81affa98d1d088cb2e91e3'
         '33de84d1cc619dba9b14d87b510684ff'
         '8ac2ffdfbef4f3ed4a08c718681c10a4'
         '4d14405ba98f09c002505cbe53e2f6cb'
         'b53452f35853abde875e3753fc3d1254'
         '1931c8dfde7088530f173ca59fdb8989')

prepare() {
  cd u-boot-${_commit}

  git apply ../0001-sd_fusing-tweaks.patch
  git apply ../0002-arch-linux-arm-modifications.patch
  git apply ../0003-gcc-10.patch
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  make CROSS_COMPILE= -j1 distclean mrproper
  make CROSS_COMPILE= -j1 odroidn2_config
  make CROSS_COMPILE= EXTRAVERSION=-${pkgrel} u-boot.bin
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp sd_fusing.sh ../build/u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
  install -Dm644 "${srcdir}/91-uboot-uimg.hook" "${pkgdir}/usr/share/libalpm/hooks/91-uboot-uimg.hook"
}
