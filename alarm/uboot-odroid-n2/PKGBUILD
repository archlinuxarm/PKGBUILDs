# U-Boot: ODROID-N2
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# Note: must be built on x86 with an old cross toolchain

buildarch=8
noautobuild=1

pkgname=uboot-odroid-n2
pkgver=2015.01
pkgrel=8
pkgdesc="U-Boot for ODROID-N2"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
depends=('uboot-tools')
makedepends=('git' 'bc')
backup=('boot/boot.ini')
_commit=8409a3f566f540745c01b8f9403bcf6c2fa6cd58
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-sd_fusing-tweaks.patch'
        '0002-arch-linux-arm-modifications.patch'
        '91-uboot-uimg.hook')
md5sums=('29f9c150b485ffc7043a613dc6a2e79d'
         '547e983abeaa98d14f8d72bb5d29a277'
         'cc18cda0bc75936e602341efd5a2fe93'
         '4d14405ba98f09c002505cbe53e2f6cb'
         '1931c8dfde7088530f173ca59fdb8989')

prepare() {
  cd u-boot-${_commit}

  git apply ../0001-sd_fusing-tweaks.patch
  git apply ../0002-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  
  make distclean mrproper
  make odroidn2_config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp sd_fusing.sh u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
  install -Dm644 "${srcdir}/91-uboot-uimg.hook" "${pkgdir}/usr/share/libalpm/hooks/91-uboot-uimg.hook"
}
