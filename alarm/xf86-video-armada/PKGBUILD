# Xorg video driver for Marvell Armada DRM and Freescale i.MX
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=xf86-video-armada
pkgver=430.e17e4d6
pkgrel=1
pkgdesc='X.org graphics driver for Marvell Armada DRM and Freescale i.MX'
arch=('armv7h')
url="http://git.arm.linux.org.uk/cgit/xf86-video-armada.git/"
license=('GPL2')
depends=('libdrm-armada')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'git' 'libdrm')
conflicts=('xorg-server<1.20' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25')
options=('!libtool')
backup=('etc/X11/xorg.conf.d/99-armada.conf')
source=("$pkgname::git://git.arm.linux.org.uk/xf86-video-armada.git#branch=unstable-devel"
        'git+https://github.com/laanwj/etna_viv.git'
        '0001-Remove-24bbp-support.patch'
        '0002-etnaviv-use-different-error-code.patch')
md5sums=('SKIP'
         'SKIP'
         '450299ebce8ee54d2d02400c9e685155'
         '15b1ef10e0ecda86f50af9249c0092ab')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

prepare() {
  cd $pkgname
  patch -p1 -i ../0001-Remove-24bbp-support.patch
  patch -p1 -i ../0002-etnaviv-use-different-error-code.patch
}

build() {
  cd $pkgname

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  ./autogen.sh --prefix=/usr --disable-vivante --disable-etnaviv --with-etnaviv-source="${srcdir}/etna_viv"
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
  install -Dm644 conf/xorg-sample.conf "${pkgdir}/etc/X11/xorg.conf.d/99-armada.conf"
}
