# Xorg video driver for Marvell Armada DRM and Freescale i.MX
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=xf86-video-armada
pkgver=396.fc05021
pkgrel=1
pkgdesc='X.org graphics driver for Marvell Armada DRM and Freescale i.MX'
arch=('armv7h')
url="http://git.arm.linux.org.uk/cgit/xf86-video-armada.git/"
license=('GPL2')
depends=('libdrm-armada')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23' 'git' 'libdrm')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
options=('!libtool')
backup=('etc/X11/xorg.conf.d/99-armada.conf')
source=("$pkgname::git://git.arm.linux.org.uk/xf86-video-armada.git#branch=unstable-devel"
        'git+https://github.com/laanwj/etna_viv.git'
        '0001-fix-drm_fourcc.h-include-path.patch')
md5sums=('SKIP'
         'SKIP'
         '7660a23a5204189f166e1b521eb584e7')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

prepare() {
  cd $pkgname
  patch -p1 -i ../0001-fix-drm_fourcc.h-include-path.patch
}

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --disable-vivante --disable-etnaviv --with-etnaviv-source="${srcdir}/etna_viv"
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
  install -Dm644 conf/xorg-sample.conf "${pkgdir}/etc/X11/xorg.conf.d/99-armada.conf"
}
