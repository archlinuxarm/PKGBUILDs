#Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
#Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python-dateutil-2.1
pkgname=('python-dateutil-2.1' 'python2-dateutil-2.1')
pkgver=2.1
pkgrel=8
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('custom:PYTHON')
url="http://labix.org/python-dateutil"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=(http://pypi.python.org/packages/source/p/python-dateutil/python-dateutil-$pkgver.tar.gz{,.asc} python-dateutil-2.1-open-utf-8.patch)
md5sums=('1534bb15cf311f07afaa3aacba1c028b'
         '4aa0ba908299d983781c58ec0640ef2b'
         '4b780c62fc03be161629ee08e35eba6a')

build() {
  cd $srcdir
  cp -r  python-dateutil-$pkgver python2-dateutil-$pkgver

  cd python-dateutil-$pkgver
  patch -Np0 -i $srcdir/python-dateutil-2.1-open-utf-8.patch

}

package_python-dateutil-2.1()
{
depends=('python' 'python-six')
conflicts=('python-dateutil')
  cd $srcdir/python-dateutil-$pkgver
  python3 setup.py install --root=$pkgdir --optimize=1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  chmod -R a+r $pkgdir/usr/lib/python3.3/

  # Remove data files
  rm $pkgdir/usr/lib/python3.3/site-packages/dateutil/zoneinfo/zoneinfo--latest.tar.gz

}

package_python2-dateutil-2.1()
{
depends=('python2' 'python2-six')
conflicts=('python2-dateutil')
  cd $srcdir/python2-dateutil-$pkgver
  python2 setup.py install --root=$pkgdir --optimize=1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  chmod -R a+r $pkgdir/usr/lib/python2.7/
  rm $pkgdir/usr/lib/python2.7/site-packages/dateutil/zoneinfo/zoneinfo--latest.tar.gz

}

check() {
  cd $srcdir/python-dateutil-$pkgver
  python3 test.py

  cd $srcdir/python2-dateutil-$pkgver
  python2 test.py
}
