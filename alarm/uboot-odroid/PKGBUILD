# U-Boot: ODROID-X/X2/U2/U3
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-odroid
pkgname=('uboot-odroid' 'uboot-odroid-x')
pkgver=2016.07
pkgrel=2
arch=('armv7h')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
install=$pkgname.install
backup=('boot/extlinux/extlinux.conf')
makedepends=('bc' 'dtc' 'git')
_commit=dd9a970aa4accf5d266d334c0f319c674e933027
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl1.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl2.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/tzsw.HardKernel"
        "sd_fusing.sh"
        '0001-arch-linux-arm-modifications.patch'
        '0002-odroid-x-support.patch'
        'extlinux.conf')
md5sums=('425a3fa610a7d972e5092a0e92276c70'
         '3ab6d3cc2061bc2590d60320254017c6'
         '841502de02bd42f2898e36c89c260b0f'
         'c38faafa02a6a1ae834457f378c82113'
         '17909ae93a0ae7b4c155234040105035'
         '55eda80baf90fd36bfd568bfc4db8462'
         'f4f8c8b6217e6646b9659de642264a3f'
         '1380273afdb6303b0c9cb58440076a26')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  # build for X2/U2/U3
  make distclean 
  make odroid_config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-dtb.bin ../u-boot-dtb.bin

  # patch and build for X
  git apply ../0002-odroid-x-support.patch
  make distclean
  make odroid_config
  make EXTRAVERSION=-${pkgrel}
}

package_uboot-odroid() {
  pkgdesc="U-Boot for ODROID-X2/U2/U3"

  cd u-boot-${pkgver}
  mkdir -p ${pkgdir}/boot
  cp ../u-boot-dtb.bin ${pkgdir}/boot/u-boot.bin
  cp ../{{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} ${pkgdir}/boot

  mkdir -p ${pkgdir}/boot/extlinux
  cp ../extlinux.conf ${pkgdir}/boot/extlinux
}

package_uboot-odroid-x() {
  pkgdesc="U-Boot for ODROID-X"

  cd u-boot-${pkgver}
  mkdir -p ${pkgdir}/boot
  cp u-boot-dtb.bin ${pkgdir}/boot/u-boot.bin
  cp ../{{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} ${pkgdir}/boot

  mkdir -p ${pkgdir}/boot/extlinux
  cp ../extlinux.conf ${pkgdir}/boot/extlinux
}
