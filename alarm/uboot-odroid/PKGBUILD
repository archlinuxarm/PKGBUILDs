# U-Boot: ODROID-X/X2/U2/U3
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-odroid
pkgver=2014.07
pkgrel=3
pkgdesc="U-Boot for ODROID-X/X2/U2/U3"
arch=('armv7h')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('bc' 'dtc')
_commit=dd9a970aa4accf5d266d334c0f319c674e933027
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl1.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl2.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/tzsw.HardKernel"
        "sd_fusing.sh"
        '0001-support-for-odroid-4412-machines.patch'
        '0002-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('36d4bad687edcafa396fee607e505d4e'
         '3ab6d3cc2061bc2590d60320254017c6'
         '841502de02bd42f2898e36c89c260b0f'
         'c38faafa02a6a1ae834457f378c82113'
         '17909ae93a0ae7b4c155234040105035'
         'b527876d561cc88cc0d6e077e831918b'
         '70e0122f419416062e434c295670946c'
         'db13b6e82ae36b916c826383b04fb9c2')

prepare() {
  cd u-boot-${pkgver}

  patch -p1 -i ../0001-support-for-odroid-4412-machines.patch
  patch -p1 -i ../0002-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS
  unset CXXFLAGS

  make distclean 
  make odroid_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p ${pkgdir}/boot
  cp u-boot-dtb.bin ${pkgdir}/boot/u-boot.bin
  cp ../uEnv.txt ${pkgdir}/boot
  cp ../{{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} ${pkgdir}/boot
}
