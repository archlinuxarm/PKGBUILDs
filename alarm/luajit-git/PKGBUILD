# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - temporary git version for AArch64

buildarch=8

pkgname=luajit-git
pkgver=v2.1.0.beta3.r13.gcf2dfaf3
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('i686' 'x86_64')
url='http://luajit.org/'
license=('MIT')
depends=('gcc-libs')
makedepends=('git')
provides=('luajit')
source=("git+https://github.com/LuaJIT/LuaJIT.git#branch=v2.1")
md5sums=('SKIP')

pkgver() {
  cd LuaJIT
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
  )
}

build() {
  cd LuaJIT
  make amalg PREFIX=/usr
}

package() {
  cd LuaJIT

  make install DESTDIR="$pkgdir" PREFIX=/usr
  cp ${pkgdir}/usr/bin/luajit-* ${pkgdir}/usr/bin/luajit
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
