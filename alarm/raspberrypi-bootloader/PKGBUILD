# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Maintainer: graysky <graysky@archlinux.us>

buildarch=28

pkgbase=raspberrypi-bootloader
pkgname=('raspberrypi-bootloader'
         'raspberrypi-bootloader-x')
pkgver=20201129
pkgrel=1
_commit=e15ef4e4fe8be99cd816cec901d977224a1eb07e
#ok:
# e15ef4e4fe8be99cd816cec901d977224a1eb07e
# 08ed7a0c9ad4d9db559aaec462520ab435c7ce1c
#bad:
# 919aee0ed75f7db48a38b8b96c13228a7584cfd7
# cfb5b76811f853612bc89c76da77d3d74d5f22ca
arch=('any')
url="https://github.com/raspberrypi/firmware"
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz")
md5sums=('4b33cd845ce9d42a13abf9a32f802243')

package_raspberrypi-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"
  replaces=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start{_,4}x.elf,fixup{_,4}x.dat}
}

package_raspberrypi-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-bootloader')
  replaces=('raspberrypi-firmware-bootloader-x')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start{_,4}x.elf,fixup{_,4}x.dat} "${pkgdir}"/boot
}
