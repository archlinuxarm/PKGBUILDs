# U-Boot: Rock64
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=uboot-rock64
pkgver=2019.07
pkgrel=1
pkgdesc="U-Boot for Rock64"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'git' 'rockchip-tools')
install=${pkgname}.install
_commit_rkbin=0b8a4e66958b5d3d87fc61fc4bd8e069c79e931a
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "git+https://github.com/rockchip-linux/rkbin.git#commit=$_commit_rkbin"
        'boot.txt'
        'mkscr')
md5sums=('73434338536c7500b4302bd0a02921ed'
         'SKIP'
         'c926f318d8fa7a5c89108331cbd3f8e2'
         '021623a04afd29ac3f368977140cfbfd')

build() {
  cd u-boot-${pkgver/rc/-rc}

  unset CLFAGS CXXFLAGS CPPFLAGS LDFLAGS

  make evb-rk3328_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}/boot"

  source <(grep -E "^Flash(Data|Boot)" ../rkbin/RKBOOT/RK3328MINIALL.ini)
  tools/mkimage -n rk3328 -T rksd -d ../rkbin/${FlashData} "${pkgdir}/boot/idbloader.img"
  cat ../rkbin/${FlashBoot} >> "${pkgdir}/boot/idbloader.img"

  loaderimage --pack --uboot u-boot-dtb.bin "${pkgdir}/boot/uboot.img" 0x200000
  cp u-boot-dtb.bin "${pkgdir}/boot"

  (
    cd ../rkbin
    trust_merger RKTRUST/RK3328TRUST.ini
    cp trust.img "${pkgdir}/boot"
  )

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}/boot/boot.scr"
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
