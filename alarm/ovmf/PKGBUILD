_edk_ver=201911
pkgbase=ovmf
pkgname=(ovmf-armvirtqemu64)
pkgver=$_edk_ver
pkgrel=1
arch=('any')
pkgdesc="Tianocore UEFI firmware for qemu."
url="https://github.com/tianocore/tianocore.github.io/wiki/EDK-II"
license=('custom')
makedepends=('python' 'iasl' 'git')
_edk2_tag=edk2-stable${_edk_ver}
source=(git+https://github.com/tianocore/edk2.git#tag=${_edk2_tag}
        git+https://git.linaro.org/uefi/uefi-tools.git#commit=a47d7f8f4cc4a0ce63588ee3a89a3918f0f0751d)
sha256sums=('SKIP'
            'SKIP')
options=(!makeflags)

prepare() {
  cd "${srcdir}"/edk2
  git submodule update --init
}

build() {
  cd "${srcdir}"/uefi-tools
  ./edk2-build.sh -e "${srcdir}"/edk2 -b RELEASE armvirtqemu64
}

package_ovmf-armvirtqemu64() {
  install -m644 "${srcdir}"/uefi-tools/Build/ArmVirtQemu-AARCH64/RELEASE_GCC5/FV/*.fd -Dt "${pkgdir}"/usr/share/ovmf/aa64-qemu/
}
