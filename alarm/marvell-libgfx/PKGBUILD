# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# Armada 510/Dove graphics libraries

buildarch=4

pkgname="marvell-libgfx"
pkgver=0.2.0
pkgrel=4
arch=('armv7h')
url="http://archlinuxarm.org"
license=('Proprietary')
source=("http://archlinuxarm.org/builder/src/marvell-libgfx-0.2.0.tar.gz"
        "http://archlinuxarm.org/builder/src/gc3184-gcc-4.6-armv7-hard_release.zip")
provides=('libegl' 'libgles' 'khrplatform-devel')
conflicts=('libegl' 'libgles' 'khrplatform-devel')

package() {
  pkgdesc="Armada 510/Dove graphics Libraries"

  cd "${srcdir}/${pkgname}-${pkgver}"
  find include -type f -not -path "include/KHR*" -exec \
    install -Dm644 {} "${pkgdir}/usr/{}" \;

  install -Dm644 etc/udev/rules.d/99-galcore.rules \
    "${pkgdir}/usr/lib/udev/rules.d/99-galcore.rules"

  cd bin
  for _lib in $(find -type f); do
    install -m755 ${_lib} "${pkgdir}/usr/lib/"
    ln -s /usr/lib/${_lib#./} "${pkgdir}/usr/lib/${_lib%.1}"
  done
  ln -s /usr/lib/libGLES_CM.so.1 "${pkgdir}/usr/lib/libGLESv1_CM.so"
  ln -s /usr/lib/libGLESv2x.so.1 "${pkgdir}/usr/lib/libGLESv2.so"

  cp "${srcdir}"/gc3184-gcc-4.6-armv7-hard_release/sdk/drivers/{libVIVANTE,libGLES_CL}.so "${pkgdir}/usr/lib"
  cp -r "${srcdir}/gc3184-gcc-4.6-armv7-hard_release/sdk/include/KHR" "${pkgdir}/usr/include"
}

md5sums=('6cd584e6feb6a8605662bb2280676f65'
         '0d1a842ebb9d9bd1c198950bfe5c04f5')
