# Xorg video driver for Adreno mobile GPUs
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=12

pkgname=xf86-video-freedreno
pkgver=1.4.0.r4.gccba8f8
pkgrel=2
pkgdesc='X.org graphics driver for Adreno mobile GPUs'
arch=('armv7h')
url="https://cgit.freedesktop.org/xorg/driver/xf86-video-freedreno/"
license=('custom')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'git')
conflicts=('xorg-server<1.20' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25')
backup=('etc/X11/xorg.conf.d/42-freedreno.conf')
source=('git+https://anongit.freedesktop.org/git/xorg/driver/xf86-video-freedreno.git'
        '42-freedreno.conf')
md5sums=('SKIP'
         'b6703241877849a98da4e85f9dc77191')

pkgver() {
  cd $pkgname
  git describe --long | sed "s/${pkgname}-//;s/\([^-]*-g\)/r\1/;s/-/./g"
}

build() {
  cd $pkgname

  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 ../42-freedreno.conf "$pkgdir/etc/X11/xorg.conf.d/42-freedreno.conf"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
