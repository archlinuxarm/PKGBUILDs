pkgname=xf86-video-fbdev-damage
pkgver=0.4.2
pkgrel=1
pkgdesc="X.org framebuffer video driver with DAMAGE support"
arch=(arm)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server-devel' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.11.99.903'
           'xf86-video-fbdev')
options=('!libtool')
source=("${url}/releases/individual/driver/xf86-video-fbdev-${pkgver}.tar.bz2"
        'damage.patch')
sha1sums=('c8562f997d56c9fec50df6ca9892f39f43ff4c2c'
          '3d51830e043f5986138a1ca241ce3300dd8530b4')

build() {
  cd "${srcdir}/xf86-video-fbdev-${pkgver}"

  # add Plugable's DAMAGE patch
  patch -Np1 -i "${srcdir}/damage.patch"
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
  chmod +x autogen.sh
  ./autogen.sh
  make
}

package() {
  cd "${srcdir}/xf86-video-fbdev-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
