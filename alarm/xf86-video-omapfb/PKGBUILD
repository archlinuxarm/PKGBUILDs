# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# OMAP FrameBuffer driver for Xorg

buildarch=4

pkgname="xf86-video-omapfb"
pkgver=0.1.1.1
pkgrel=10
pkgdesc="OMAP3+ framebuffer driver for X"
arch=('armv7h')
license=('GPL2')
url="http://anonscm.debian.org/cgit/collab-maint/xf86-video-omapfb.git/"
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23')
conflicts=('xorg-server<1.19' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
source=("http://archlinuxarm.org/builder/src/xf86-video-omapfb_0.1.1.1.tar.gz"
        "0001-Revert-Set-a-large-CRTC-upper-limit-to-not-prune-lar.patch"
        "0002-Revert-Set-virtual-size-when-configuring-framebuffer.patch"
        "0003-force-plain-mode.patch"
        "0004-blacklist-tv-out.patch"
        "0005-Attempt-to-fix-VRFB.patch"
        "0006-omapfb-port-to-new-xserver-video-API.patch"
        "0007-compat-for-newer-xorg.patch")
md5sums=('a9bca5424bd76d53b78826f9d5fa4abe'
         '890884c69a2334d8c7e812c729af5b13'
         '723efe8327d43795e9a5b0efc6d7010d'
         '1cc90e32023eb989451ba0360becb96e'
         '8f322ad500ca6d73cbffa03dc8069db5'
         '66887007c4341ba693b46f90dc93e300'
         '198a41675ba379bb6545104270126f29'
         '2d7bb40047ebcb3997afa345d80f9e67')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../0001-Revert-Set-a-large-CRTC-upper-limit-to-not-prune-lar.patch
  patch -p1 -i ../0002-Revert-Set-virtual-size-when-configuring-framebuffer.patch
  patch -p1 -i ../0003-force-plain-mode.patch
  patch -p1 -i ../0004-blacklist-tv-out.patch
  patch -p1 -i ../0005-Attempt-to-fix-VRFB.patch
  patch -p1 -i ../0006-omapfb-port-to-new-xserver-video-API.patch
  patch -p1 -i ../0007-compat-for-newer-xorg.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
