# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

# OMAP FrameBuffer driver for Xorg

buildarch=4

pkgname="xf86-video-omapfb"
pkgver=0.1.1.1
pkgrel=8
arch=('armv7h')
url="http://gitweb.pingu.fi/?p=xf86-video-omapfb.git"
license=('GPL2')
makedepends=('pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
source=("http://archlinuxarm.org/builder/src/xf86-video-omapfb_0.1.1.1.tar.gz"
        "0001-Revert-Set-a-large-CRTC-upper-limit-to-not-prune-lar.patch"
        "0002-Revert-Set-virtual-size-when-configuring-framebuffer.patch"
        "0003-force-plain-mode.patch"
        "0004-blacklist-tv-out.patch"
        "0005-Attempt-to-fix-VRFB.patch"
        "0006-omapfb-port-to-new-xserver-video-API.patch")
md5sums=('a9bca5424bd76d53b78826f9d5fa4abe'
         'f52826ee0feb5aefc92431df973a6ed2'
         '22d822068b570115a4613b73cb719ffd'
         '8e04b609ba12d094edecc2ada8335951'
         '1abeb90f138fc86015960a9269612e91'
         '2acd1c0448e4628353d084f0374c48b6'
         '4094ad2bb96ecd5c6faf51f09a7ce65b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../0001-Revert-Set-a-large-CRTC-upper-limit-to-not-prune-lar.patch
  patch -p1 -i ../0002-Revert-Set-virtual-size-when-configuring-framebuffer.patch
  patch -p1 -i ../0003-force-plain-mode.patch
  patch -p1 -i ../0004-blacklist-tv-out.patch
  patch -p1 -i ../0005-Attempt-to-fix-VRFB.patch
  patch -p1 -i ../0006-omapfb-port-to-new-xserver-video-API.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  pkgdesc="OMAP3+ framebuffer driver for X"

  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
