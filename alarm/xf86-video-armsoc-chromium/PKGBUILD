# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Chromium.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>
# Contributor: Mauro Ribeiro <mauro.ribeiro@hardkernel.com>

buildarch=4

pkgname=xf86-video-armsoc-chromium
pkgver=r228.1ccf708
pkgrel=1
epoch=1
arch=('armv7h')
url="https://chromium.googlesource.com/chromiumos/third_party/xf86-video-armsoc"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=(xf86-video-armsoc)
provides=(xf86-video-armsoc)
source=("$pkgname::git+https://chromium.googlesource.com/chromiumos/third_party/xf86-video-armsoc"
        '0001-compat-for-newer-xorg.patch')
md5sums=('SKIP'
         '429bc16ad0c7fe308d90c701c1aa97d1')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
  git apply ../0001-compat-for-newer-xorg.patch
}

build() {
  cd $pkgname

  CFLAGS=`echo $CFLAGS | sed -e 's/vfpv3-d16/neon/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}
