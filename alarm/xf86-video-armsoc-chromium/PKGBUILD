# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Chromium.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>
# Contributor: Mauro Ribeiro <mauro.ribeiro@hardkernel.com>

# Official upstream is at 
# http://git.linaro.org/gitweb?p=arm/xorg/driver/xf86-video-armsoc.git;a=summary
# https://chromium.googlesource.com/chromiumos/third_party/xf86-video-armsoc

buildarch=4

pkgname=xf86-video-armsoc-chromium
pkgver=r180.f6d255c
pkgrel=2
epoch=1
arch=('armv7h')
url="https://chromium.googlesource.com/chromiumos/third_party/xf86-video-armsoc"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=(xf86-video-armsoc)
replaces=(xf86-video-armsoc)
provides=(xf86-video-armsoc)
source=("$pkgname::git+https://chromium.googlesource.com/chromiumos/third_party/xf86-video-armsoc"
        '0001-compat-for-newer-xorg.patch')
md5sums=('SKIP'
         '12361ef14f940d10c471f55104ada39d')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"
  git apply "$srcdir/0001-compat-for-newer-xorg.patch"
}

build() {
  cd "$srcdir/$pkgname"

  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  pkgdesc="armsoc framebuffer driver for X from chromium os git"

  make -C "$srcdir/$pkgname" DESTDIR="${pkgdir}" install
}

