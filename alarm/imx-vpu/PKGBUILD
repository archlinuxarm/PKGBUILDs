# Maintainer: CruX <crux@project-insanity.org>
# Contributor: pepedog at archlinuxarm dot com
# Contributor: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>

buildarch=4
pkgname=imx-vpu
pkgver=5.4.39.3
pkgrel=1
pkgdesc="Freescale proprietary extensions for i.MX6 SoC"
url="https://community.freescale.com/docs/DOC-95560"
arch=('armv7h')
license=('proprietary')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${pkgver}.bin"
        '99-mxc-vpu.rules')
md5sums=('6d6302189a6704874375afe62a65def0'
         'b749d82b96a323d8f914c63c65de9f92')

prepare() {
  cd "${srcdir}"
  # extract the sources
  sh ${pkgname}-${pkgver}.bin --force --auto-accept
}

build() {
  cd "${pkgname}-${pkgver}"
  # disable -mno-omit-leaf-frame-pointer
  export CFLAGS="${CFLAGS//-mno-omit-leaf-frame-pointer}"
  export PLATFORM="IMX6Q"
  make all
}

package() {
  cd "${pkgname}-${pkgver}"
  make DEST_DIR="${pkgdir}" install
  cd "${pkgdir}"
  mkdir -p opt/fsl/include opt/fsl/lib
  mv usr/include/* opt/fsl/include/
  mv usr/lib/* opt/fsl/lib
  rm -r usr

  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/fsl/lib" > "${pkgdir}/etc/ld.so.conf.d/$pkgname.conf"

  install -D -m0644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "$pkgdir/opt/fsl/licenses/LICENSE.${pkgname}"
  install -D -m0644 "${srcdir}/99-mxc-vpu.rules" "${pkgdir}/usr/lib/udev/rules.d/99-mxc-vpu.rules"
}
