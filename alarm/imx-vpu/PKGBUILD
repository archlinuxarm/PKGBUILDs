# Maintainer: CruX <crux@project-insanity.org>
# Contributor: pepedog at archlinuxarm dot com

buildarch=4
pkgname=imx-vpu
pkgver=3.10.17_1.0.0
_pkgver=${pkgver/_/-}
pkgrel=2
pkgdesc="Freescale proprietary extensions for i.MX6 SoC"
url="https://community.freescale.com/docs/DOC-95560"
arch=('armv7h')
license=('proprietary')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${_pkgver}.bin" '99-mxc-vpu.rules')
md5sums=('71ea1b803864101ebf88a1bab45514d2'
          'b749d82b96a323d8f914c63c65de9f92')


prepare() {
  cd "${srcdir}"
  #chmod for execution, library is packed as binary to accept EULA
#  chmod +x ${pkgname}-${_pkgver}.bin
  sh ${pkgname}-${_pkgver}.bin --force --auto-accept
  sed -n '/EOEULA/,/EOEULA/p' ${pkgname}-${_pkgver}.bin | grep -v EOEULA > LICENSE.$pkgname
}

build() {
  cd "${pkgname}-${_pkgver}"
  export PLATFORM="IMX6Q"
  make all
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DEST_DIR="${pkgdir}" install
  cd "${pkgdir}"
  mkdir -p opt/fsl/include opt/fsl/lib
  mv usr/include/* opt/fsl/include/
  mv usr/lib/* opt/fsl/lib
  rm -r usr

  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/fsl/lib" > "${pkgdir}/etc/ld.so.conf.d/$pkgname.conf"

  install -D -m0644 "${srcdir}/99-mxc-vpu.rules" "${pkgdir}/usr/lib/udev/rules.d/99-mxc-vpu.rules"
}

# vim:set ts=2 sw=2 et:
