# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=12

pkgname=wiring-odroid
pkgver=216.9666c1a
pkgrel=1
pkgdesc="An implementation of most of the Arduino Wiring functions for ODROID SBCs"
arch=('armv7h' 'aarch64')
makedepends=('git')
replaces=('wiringc1')
license=('LGPL')
url="https://github.com/hardkernel/wiringPi"
source=("git+https://github.com/hardkernel/wiringPi.git")
md5sums=('SKIP')

pkgver() {
  cd wiringPi
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd wiringPi

  VERSION=$(cat VERSION)

  make -C wiringPi all
  make -C devLib all INCLUDE+="-I${srcdir}/wiringPi/wiringPi"

  ln -s "${srcdir}/wiringPi/wiringPi/libwiringPi.so.${VERSION}" wiringPi/libwiringPi.so
  ln -s "${srcdir}/wiringPi/devLib/libwiringPiDev.so.${VERSION}" devLib/libwiringPiDev.so

  make -C gpio INCLUDE+="-I${srcdir}/wiringPi/wiringPi -I${srcdir}/wiringPi/devLib" LDFLAGS+="-L${srcdir}/wiringPi/wiringPi -L${srcdir}/wiringPi/devLib"
}

package(){
  cd wiringPi

  make LDCONFIG= PREFIX= DESTDIR="${pkgdir}/usr" -C wiringPi install
  make LDCONFIG= PREFIX= DESTDIR="${pkgdir}/usr" -C devLib install
  install -d "${pkgdir}/usr/bin"
  make PREFIX= DESTDIR="${pkgdir}/usr" WIRINGPI_SUID=1 -C gpio install
  install -d "${pkgdir}/usr/share"
  mv "${pkgdir}/usr/man" "${pkgdir}/usr/share"
  install -Dm644 COPYING.LESSER "$pkgdir/usr/share/licenses/$pkgname/COPYING.LESSER"
}
