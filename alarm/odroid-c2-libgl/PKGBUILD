# ODROID-C2 Mali GL Driver
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgbase=odroid-c2-libgl
pkgname=("${pkgbase}-fb")
pkgver=r5p1
pkgrel=2
_commit=228b2e75bea3426661437d5642fbae3a376895fd
arch=('aarch64')
url="http://www.hardkernel.com/"
license=('Proprietary')
makedepends=('git')
source=("git+https://github.com/mdrjr/c2_mali.git#commit=${_commit}"
        'mali-c2.conf')
md5sums=('SKIP'
         '0b2d7cfb2a6e03ba26799c0a574e4e92')

package_odroid-c2-libgl-fb() {
  pkgdesc="ODROID-C2 Mali driver (framebuffer)"
  conflicts=('odroid-c2-libgl' 'mesa-libgl')
  provides=('odroid-c2-libgl' 'libgl')

  install -d "${pkgdir}"/usr/lib/mali-egl
  install -d "${pkgdir}"/etc/ld.so.conf.d
  cp -a c2_mali/fbdev/mali_libs/lib* "${pkgdir}"/usr/lib/mali-egl
  cp "${srcdir}"/mali-c2.conf "${pkgdir}"/etc/ld.so.conf.d
}
