# Maintainer: CruX <crux@project-insanity.org>
# Contributor: pepedog at archlinuxarm dot com

buildarch=4
pkgname=firmware-imx
pkgver=3.5.7_1.0.0
_pkgver=${pkgver/_/-}
pkgrel=2
pkgdesc="Freescale proprietary firmware for i.MX6 SoC"
url="https://community.freescale.com/docs/DOC-95560"
arch=('armv7h')
license=('proprietay')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${_pkgver}.bin")
md5sums=('7285345d99e1064bdbad113e3d9b8f40')

prepare() {
  cd "${srcdir}"
  #extract the firmware, its packed as a script
  sh ${pkgname}-${_pkgver}.bin --force --auto-accept
  sed -n '/EOEULA/,/EOEULA/p' ${pkgname}-${_pkgver}.bin | grep -v EOEULA > LICENSE.$pkgname
}

package() {
  mkdir -p "${pkgdir}/usr/lib/firmware/vpu"
  cp -rv "${srcdir}/${pkgname}-${_pkgver}/firmware/vpu/"* "${pkgdir}/usr/lib/firmware/vpu"

  mkdir -p "${pkgdir}/opt/fsl/licenses"
  cp "${srcdir}/LICENSE.$pkgname" "$pkgdir/opt/fsl/licenses"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/fsl/lib" > "${pkgdir}/etc/ld.so.conf.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
