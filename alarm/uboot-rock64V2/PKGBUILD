# U-Boot: Rock64
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=uboot-rock64V2
pkgver=2025.10
pkgrel=1
pkgdesc="U-Boot for Rock64 V2"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'git' 'python' 'python-pyelftools' 'python-setuptools' 'swig' 'dtc')
conflicts=('uboot-rock64' 'uboot-rock64-spi' 'uboot-rock64V2-spi')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://github.com/ARM-software/arm-trusted-firmware/archive/v2.14.tar.gz"
        'boot.txt'
        'mkscr'
        '0000-rock64V2-lpddr3-1333.patch')
sha256sums=('b4f032848e56cc8f213ad59f9132c084dbbb632bc29176d024e58220e0efdf4a'
            '3c87f39851eb3f26a90fe894b1838edecc5349eaf9c88754e62716735d71ae0d'
            '28d98741349f0898d6c1f76fcd20b7536cf107ad3bdaf19bd4952bb62ffbf812'
            'a4fc8b6b92bc364d6542670d294aa618a8501fb8729f415cc0a3eed776ef0c8e'
            'f3b0da1c910865da896d8a66673cdb93b8787797be3cf168f9442f4d9e10cf75')

prepare() {
  export CC=cc
  export LD=cc
  export AS=cc
  export OC=objcopy
  export OD=objdump

  cd ${srcdir}/arm-trusted-firmware-2.14
  make PLAT=rk3328 all
  export BL31=${srcdir}/arm-trusted-firmware-2.14/build/rk3328/release/bl31/bl31.elf

  cd ${srcdir}/u-boot-${pkgver}
  patch -p1 < ../0000-rock64V2-lpddr3-1333.patch

  cd ${srcdir}/u-boot-${pkgver}/configs
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> rock64-rk3328_defconfig

}

build() {
  cd ${srcdir}/u-boot-${pkgver}

  unset CLFAGS CXXFLAGS CPPFLAGS LDFLAGS

  make rock64-rk3328_defconfig
  make EXTRAVERSION=-${pkgrel}
  make EXTRAVERSION=-${pkgrel} u-boot.itb
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}/boot"

  tools/mkimage -n rk3328 -T rksd -d ./tpl/u-boot-tpl.bin "${pkgdir}/boot/rksd_loader.img"
  cat ./spl/u-boot-spl.bin >> "${pkgdir}/boot/rksd_loader.img"

  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}/boot/boot.scr"
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
