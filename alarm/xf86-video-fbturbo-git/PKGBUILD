# Maintainer: smotocel69 <smotocel69@gmail.com>
# Contribuitor: Georges Discry <georges at discry dot be>

pkgname=xf86-video-fbturbo-git
_gitname=xf86-video-fbturbo
pkgver=199.f9a6ed7
pkgrel=4
pkgdesc="X.org MALI video driver"
arch=('armv7h')
url="https://github.com/ssvb/xf86-video-fbturbo"
license=('MIT')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'git')
conflicts=('xorg-server<1.20' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25')
options=('!libtool')
provides=('xf86-video-fbturbo')
source=('git+https://github.com/ssvb/xf86-video-fbturbo'
        '0001-Use-own-thunk-functions-instead-of-fbdevHW-Weak.patch'
        '0002-GCC-8-fix.patch')
md5sums=('SKIP'
         'c6bf86bda174e0d37c75294518909b76'
         '36ff3a90c4c17bbfb982d53bfd411936')

pkgver() {
  cd "${SRCDEST}/${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${srcdir}/${_gitname}"
  patch -p1 -i ../0001-Use-own-thunk-functions-instead-of-fbdevHW-Weak.patch
  patch -p1 -i ../0002-GCC-8-fix.patch
}

build() {
  cd "${srcdir}/${_gitname}"

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  mkdir -p "${pkgdir}/etc/X11/xorg.conf.d/"
  cp "xorg.conf" "${pkgdir}/etc/X11/xorg.conf.d/99-fbturbo.conf"
  make DESTDIR="${pkgdir}/" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
