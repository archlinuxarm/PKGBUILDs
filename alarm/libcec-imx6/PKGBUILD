# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>
# vim: ft=sh:

# ALARM: Oleg Rakhmanov <oleg@archlinuxarm.org>
#  - IMX support, no Exynos
#  - IMX patches are a courtesy of openbricks:
#    https://github.com/OpenBricks/openbricks/tree/master/packages/libs/libcec3/patches 
buildarch=4

pkgname=libcec-imx6
_pkgname=libcec
pkgver=3.0.1
pkgrel=3
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter (IMX6)"
arch=('armv7h')
url="http://libcec.pulse-eight.com/"
license=('GPL')
makedepends=('cmake')
depends=('udev' 'lockdev' 'libplatform' 'libxrandr')
provides=('libcec')
conflicts=('libcec')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/Pulse-Eight/$_pkgname/archive/$_pkgname-$pkgver.tar.gz"
        '0001-add-imx-support-1.patch'
        '0002-add-imx-support-2.patch'
        'https://github.com/Pulse-Eight/libcec/commit/2f32a9debc1f148b5dfcfc463480f1432bb71725.patch')

sha256sums=('7e3670c8949a1964d6e5481f56dfff838857da10bdc60b506f6e9b7f117e253e'
            '1105f457f49c4caa0791de11a321dde11df086c06017c6a4a23c8a17502f01ef'
            '5faf57652d8c0c83ca37fecc961c50b268f0f2b4092da31c56cc6874040593d9'
            '83ca3b742831aa2e05297d11e238a0b0d1035a6404f60a6610acc8381d1c17ea')

prepare() {
    cd "$_pkgname-$_pkgname-$pkgver"

    patch -Np1 -i ../0001-add-imx-support-1.patch
    patch -Np1 -i ../0002-add-imx-support-2.patch
    patch -Np1 -i ../2f32a9debc1f148b5dfcfc463480f1432bb71725.patch
}

build() {
    cd "$_pkgname-$_pkgname-$pkgver"

    unset LDFLAGS

    mkdir build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBDIR_NOARCH=/usr/lib \
        -DHAVE_EXYNOS_API=0 \
        -DHAVE_IMX6_API=1 \
        -DRPI_INCLUDE_DIR=/opt/vc/include \
        -DRPI_LIB_DIR=/opt/vc/lib
    make

}

package() {
    cd "$_pkgname-$_pkgname-$pkgver/build"
    make DESTDIR="$pkgdir" install
}
