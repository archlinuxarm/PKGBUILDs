# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>
# vim: ft=sh:

# ALARM: Oleg Rakhmanov <oleg@archlinuxarm.org>
#  - IMX support, no Exynos
#  - IMX patches are a courtesy of OpenELEC:
#    https://github.com/OpenELEC/OpenELEC.tv/blob/master/packages/devel/libcec/patches/ 
buildarch=4

pkgname=libcec-imx6
_pkgname=libcec
pkgver=4.0.0
pkgrel=2
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter (IMX6)"
arch=('armv7h')
url="http://libcec.pulse-eight.com/"
license=('GPL')
makedepends=('cmake')
depends=('udev' 'lockdev' 'p8-platform' 'libxrandr')
provides=('libcec')
conflicts=('libcec')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/Pulse-Eight/$_pkgname/archive/$_pkgname-$pkgver.tar.gz"
        'libcec-00-imx6-support.patch')

sha256sums=('4c6ee8e44f2e6b28e3dfbee6c77df8d18605150ef69ab0564728b79a92a646ed'
            '49c2519e7d3230b84fd050762444bf6d424c69a892a20e98b924ae73ff4845e8')

prepare() {
    cd "$_pkgname-$_pkgname-$pkgver"

    patch -Np1 -i ../libcec-00-imx6-support.patch
}

build() {
    cd "$_pkgname-$_pkgname-$pkgver"

    unset LDFLAGS

    mkdir build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBDIR_NOARCH=/usr/lib \
        -DHAVE_EXYNOS_API=0 \
        -DHAVE_IMX_API=1 \
        -DRPI_INCLUDE_DIR=/opt/vc/include \
        -DRPI_LIB_DIR=/opt/vc/lib
    make

}

package() {
    cd "$_pkgname-$_pkgname-$pkgver/build"
    make DESTDIR="$pkgdir" install
}
