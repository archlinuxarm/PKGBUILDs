# U-Boot for i.MX233-OLinuXino
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Kiril Zyapkov <kiril.zyapkov@gmail.com>

buildarch=2

pkgname=uboot-olinuxino
pkgdesc="U-Boot for i.MX233-OLinuXino"
pkgver=2015.10
pkgrel=1
arch=('arm')
license=('GPL')
makedepends=('git' 'bc')
install=$pkgname.install
options=('!strip')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('7c203b0fc3390a122d8e8b75f147eac5'
         '6b5f319d0b043e223629d001579c3a2e'
         'd41d8cd98f00b204e9800998ecf8427e')

prepare() {
  cd "$srcdir/u-boot-${pkgver}"
  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd "$srcdir/u-boot-${pkgver}"

  unset LDFLAGS CFLAGS CXXFLAGS

  make distclean
  make mx23_olinuxino_config
  make u-boot.sb
}

package() {
  mkdir -p "${pkgdir}/boot"
  cp "${srcdir}/u-boot-${pkgver}"/u-boot.sb "${srcdir}"/uEnv.txt "${pkgdir}/boot"
}
