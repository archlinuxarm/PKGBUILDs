# Maintainer: CruX <crux@project-insanity.org>
# Contributor: pepedog at archlinuxarm dot com
# Contributor: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>

buildarch=4
pkgname=imx-vpuwrap
pkgver=1.0.65
pkgrel=1
pkgdesc="Wrapper library for the freescale proprietary VPU extensions"
url="https://community.freescale.com/docs/DOC-95560"
arch=('armv7h')
license=('proprietary')
depends=('imx-vpu')
replaces=('libfslvpuwrap')
conflicts=('libfslvpuwrap')
provides=('libfslvpuwrap')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${pkgver}.bin")
md5sums=('0bbcd6b9c9906fb85f98160eba94827b')

prepare() {
  cd "${srcdir}"
  #extract the sources
  sh ${pkgname}-${pkgver}.bin --force --auto-accept
}

build() {
  cd "${pkgname}-${pkgver}"
  export CFLAGS="$CFLAGS -O2 -I/opt/fsl/include"
  export CPPFLAGS="$CPPFLAGS -O2 -I/opt/fsl/include"
  export LDFLAGS="$LDFLAGS -L/opt/fsl/lib"
  ./autogen.sh --prefix=/opt/fsl
  make all
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -D -m0644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "$pkgdir/opt/fsl/licenses/LICENSE.${pkgname}"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/fsl/lib" > "${pkgdir}/etc/ld.so.conf.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
