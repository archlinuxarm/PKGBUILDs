# ODROID-C2 Amlogic libraries
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Adrian Fedoreanu <adrian.fedoreanu@gmail.com>

buildarch=8

pkgname=aml-libs-c2
 _commit=296f39bc6fc47ddf5d88b9fd3cfb82a5b39048ca
 pkgver=r9.296f39b
pkgrel=1
arch=('aarch64')
pkgdesc="ODROID-C2 Amlogic Libraries"
url="http://www.hardkernel.com/"
license=('Proprietary')
depends=('alsa-lib')
makedepends=('git')
source=("https://github.com/mdrjr/c2_aml_libs/archive/${_commit}.tar.gz"
        '99-amlogic.rules')
sha256sums=('2c9812154b6dfd6a6c9c43aa334d0d03f299379fdf83c20121ad3f3d3f97c666'
            'd484525d55062b03648554b5a1b15aea7338fd0c8a9bb50e7f17650dddf5b162')

build() {
  cd c2_aml_libs-${_commit}

  CFLAGS=`echo "$CFLAGS -O2 -mcpu=cortex-a53+crc -mtune=cortex-a53 -mabi=lp64"` && CXXFLAGS="$CFLAGS"

  make -C amadec all
  make -C amavutils all
  make -C amcodec all
}

package() {
  cd c2_aml_libs-${_commit}

  install -d "${pkgdir}"/usr/include/{,amports,cutils,ppmgr,amcodec,amadec,amavutils}
  install -d "${pkgdir}/etc/ld.so.conf.d"
  install -d "${pkgdir}/usr/lib/aml_libs"

  install -m644 aml.conf "${pkgdir}/etc/ld.so.conf.d"

  install -m755 amadec/libamadec.so amavutils/libamavutils.so amcodec/libamcodec.so "${pkgdir}/usr/lib/aml_libs"
  install -m644 amadec/{,include/}*.h "${pkgdir}/usr/include/amadec"
  install -m644 amavutils/include/*.h "${pkgdir}/usr/include/amavutils"
  install -m644 amavutils/include/cutils/*.h "${pkgdir}/usr/include/cutils"
  install -m644 amcodec/include/*.h "${pkgdir}/usr/include/amcodec"
  install -m644 amcodec/include/*.h "${pkgdir}/usr/include"
  install -m644 amcodec/include/amports/*.h "${pkgdir}/usr/include/amports"
  install -m644 amcodec/include/ppmgr/*.h "${pkgdir}/usr/include/ppmgr"

  # install /dev/am* permission fix
  install -Dm644 "${srcdir}/99-amlogic.rules" "${pkgdir}/usr/lib/udev/rules.d/99-amlogic.rules"
}
