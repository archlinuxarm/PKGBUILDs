# ODROID-C2 Amlogic libraries
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=aml-libs-c2
pkgver=r9.296f39b
pkgrel=1
arch=('armv7h')
pkgdesc="ODROID-C2 Amlogic Libraries"
url="http://www.hardkernel.com/"
license=('Proprietary')
depends=('alsa-lib')
makedepends=('git')
source=("git+https://github.com/mdrjr/c2_aml_libs.git"
        '99-amlogic.rules')
md5sums=('SKIP'
         '42c9f74fc92755a975ed524c4735db42')

pkgver() {
  cd c2_aml_libs
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd c2_aml_libs

  make -C amadec all
  make -C amavutils all
  make -C amcodec all
}

package() {
  cd c2_aml_libs
  install -d "${pkgdir}"/usr/include/{,amports,cutils,ppmgr,amcodec,amadec,amavutils}
  install -d "${pkgdir}/etc/ld.so.conf.d"
  install -d "${pkgdir}/usr/lib/aml_libs"

  install -m644 aml.conf "${pkgdir}/etc/ld.so.conf.d"

  install -m755 amadec/libamadec.so amavutils/libamavutils.so amcodec/libamcodec.so "${pkgdir}/usr/lib/aml_libs"
  install -m644 amadec/{,include/}*.h "${pkgdir}/usr/include/amadec"
  install -m644 amavutils/include/*.h "${pkgdir}/usr/include/amavutils"
  install -m644 amavutils/include/cutils/*.h "${pkgdir}/usr/include/cutils"
  install -m644 amcodec/include/*.h "${pkgdir}/usr/include/amcodec"
  install -m644 amcodec/include/*.h "${pkgdir}/usr/include"
  install -m644 amcodec/include/amports/*.h "${pkgdir}/usr/include/amports"
  install -m644 amcodec/include/ppmgr/*.h "${pkgdir}/usr/include/ppmgr"

  # install /dev/am* permission fix
  install -Dm644 "${srcdir}/99-amlogic.rules" "${pkgdir}/usr/lib/udev/rules.d/99-amlogic.rules"
}
