# U-Boot: Beaglebone and Beaglebone Black
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-beaglebone
pkgver=2013.07
pkgrel=1
pkgdesc="U-Boot for Beaglebone and Beaglebone Black"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git')
source=("git://git.denx.de/u-boot.git#tag=v${pkgver}"
        'https://raw.github.com/eewiki/u-boot-patches/master/v2013.07/0001-am335x_evm-uEnv.txt-bootz-n-fixes.patch'
        'uEnv.txt')
md5sums=('SKIP'
         'dbc20ae3e48ae69fc2bbd35b96a6663e'
         'a280b41fc634331ae4e497bcf985fa46')

prepare() {
  cd u-boot
  patch -p1 -i "${srcdir}"/0001-am335x_evm-uEnv.txt-bootz-n-fixes.patch
  rm -rf .git
}

build() {
  cd u-boot

  unset CFLAGS
  unset CXXFLAGS

  make distclean 
  make am335x_evm_config
  make
}

package() {
  cd u-boot
  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
