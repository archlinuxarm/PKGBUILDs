# Contributor Arno Rehn <arno@arnorehn.de>

buildarch=16

pkgname=qt5-eglfs-rpi
pkgver=5.4.0
pkgrel=1
pkgdesc="Qt5 eglfs plugin built against Broadcom's GL libs."
url="http://www.qt-project.org"
arch=('armv6h')
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('qt5-base' 'mtdev')
optdepends=()
makedepends=()
conflicts=()
replaces=()
backup=()
install=
source=("http://download.qt-project.org/official_releases/qt/${pkgver%.*}/${pkgver}/submodules/qtbase-opensource-src-${pkgver}.tar.xz"
        "eglfs-rpi-qmake.conf"
        "eglfs-rpi.patch")
md5sums=('eaaa72a5cb25713ca8d17f3a8d149765'
         'bf626b48b7960b0a558dd475b19a8dd2'
         '5b7ef681deb4b923ca3bbf3fe3820e69')

prepare() {
  cd "${srcdir}"
  rm -rf "${pkgname}-${pkgver}"
  cp -r qtbase-opensource-src-${pkgver}/src/plugins/platforms/eglfs "${pkgname}-${pkgver}"
  cp qtbase-opensource-src-${pkgver}/mkspecs/devices/linux-rasp-pi-g++/qeglfshooks_pi.cpp "${pkgname}-${pkgver}/"
  cp eglfs-rpi-qmake.conf "${pkgname}-${pkgver}"/.qmake.conf
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../eglfs-rpi.patch
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install

  cd "${srcdir}/qtbase-opensource-src-${pkgver}"
  install -D -m644 LGPL_EXCEPTION.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LGPL_EXCEPTION.txt
}

# vim:set ts=2 sw=2 et:
