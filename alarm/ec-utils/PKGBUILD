# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=12

pkgname=ec-utils
pkgver=6748.aeeeafb192
pkgrel=1
pkgdesc="Chromium OS Embedded Controller utilities"
arch=('armv7h' 'aarch64')
makedepends=('git' 'inetutils')
license=('custom:chromiumos')
source=("git+https://chromium.googlesource.com/chromiumos/platform/ec")
md5sums=('SKIP')

pkgver() {
  cd ec
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ec
  CCACHE= HOST_CROSS_COMPILE= make utils-host
}

package() {
  cd ec
  install -dm755 ${pkgdir}/usr/bin
  install -m755 build/bds/util/{ectool,lbplay,stm32mon,ec_sb_firmware_update,lbcc} ${pkgdir}/usr/bin
}
