# Maintainer: Alexander Foremny <alexanderforemny@gmail.com>

pkgname=bash-native
_patchlevel=007
pkgver=4.1.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
depends=('readline-native' 'glibc-native' "bash>=${pkgver}")
makedepends=(gzip)
provides=('sh')
source=(http://ftp.gnu.org/gnu/bash/bash-4.1.tar.gz)
for p in $(seq -w 001 $_patchlevel); do
    source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-4.1-patches/bash41-$p)
done

md5sums=('9800d8724815fd84994d9be65ab5e7b8'
	 '582dea5671b557f783e18629c2f77b68'
	 '118d465095d4a4706eb1d34696a2666a'
	 '120f7cf039a40d35fe375e59d6f17adc'
	 '336ee037fc2cc1e2350b05097fbdc87c'
	 '9471e666797f0b03eb2175ed752a9550'
	 'fb80ccd58cb1e34940f3adf4ce6e4a1e'
	 '192a8b161d419a1d0d211169f1d1046e')

_PREFIX=/usr/arm-softfloat-linux-gnueabi
_LIBDIR=/usr/lib/$CARCH
_LINKER=$_LIBDIR/ld-linux.so.2

build() {
  cd ${startdir}/src/bash-4.1

  msg "Applying pachtes ..."
  for p in ../bash41-*; do
    patch -Np0 -i ${p} || return 1
  done

  ./configure --prefix=$_PREFIX \
    --build=${CHOST} \
    --host=${CHOST} \
    --with-curses \
    --enable-readline \
    --without-bash-malloc \
    --with-installed-readline || return 1
  make LDFLAGS="-Wl,-dynamic-linker,$_LINKER" || return 1
  make DESTDIR="$pkgdir" install || return 1

  # Use bash as sh
  ln -s "$_PREFIX/bin/bash" "$pkgdir$_PREFIX/usr/$CHOST/bin/sh"

  for dir in info man; do
    rm -rf "$pkgdir$_PREFIX/$dir"
  done
}
