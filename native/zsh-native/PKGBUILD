# Maintainer: Alexander Foremny <alexanderforemny@gmail.com>

pkgname=zsh-native
pkgver=4.3.10
pkgrel=1
pkgdesc="A very advanced and programmable command interpreter (shell) for UNIX"
arch=('i686' 'x86_64')
url="http://www.zsh.org/"
license=('custom')
depends=('ncurses-native' 'gdbm-native' 'pcre-native')
install="zsh.install"
source=(ftp://ftp.zsh.org/pub/zsh-$pkgver.tar.bz2)

_PREFIX=/usr/arm-softfloat-linux-gnueabi
_LIBDIR=/usr/lib/$CARCH
_LINKER=$_LIBDIR/ld-linux.so.2

build() {
  cd "$srcdir/zsh-$pkgver"

  ./configure --prefix=$_PREFIX \
    --libdir=$_LIBDIR \
    --enable-maildir-support \
    --with-term-lib="ncursesw" \
    --enable-multibyte \
    --enable-function-subdirs \
    --enable-zsh-mem \
    --with-tcsetpgrp \
    --enable-pcre \
    --enable-zsh-secure-free
  make LDFLAGS="-Wl,-dynamic-linker,$_LINKER" || return 1
  make DESTDIR="$pkgdir/" install

  install -D -m644 LICENCE "$pkgdir$_PREFIX/share/licenses/zsh/LICENSE"

  # Remove duplicated binary.
  rm -f "$pkgdir$_PREFIX/bin/zsh"
  ln -s "$_PREFIX/bin/zsh-$pkgver" "$pkgdir$_PREFIX/bin/zsh"

  # Remove man pages.
  rm -rf "$pkgdir$_PREFIX/share/man"
}

# vim: set ft=sh ts=2 sw=2 et:
