# U-Boot: TBS2910
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-tbs2910
pkgver=2015.04
pkgrel=1
pkgdesc="U-Boot for TBS2910"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc')
options=(!strip !upx)
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'alarm.patch')
sha1sums=('21db842ba5c3633a5f673babdd91afaa4a190bd5'
          '3b138dfceda42bf8d2a3427432a24a887095501a')
install=${pkgname}.install

prepare() {
  cd u-boot-${pkgver}
  make distclean
  patch -p1 -i "${srcdir}"/alarm.patch
  make tbs2910_defconfig
}

build() {
  cd u-boot-${pkgver}
  
  make 
}

package() {
  install -D -m 644 u-boot-${pkgver}/u-boot.imx ${pkgdir}/boot/u-boot.imx
}
