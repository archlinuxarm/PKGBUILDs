# $Id: PKGBUILD 52495 2009-09-21 02:38:38Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gpm
pkgver=1.20.6
pkgrel=3
pkgdesc="A mouse server for the console and xterm"
arch=('i686' 'x86_64')
url="http://unix.schottelius.org/gpm/"
license=('GPL')
depends=('ncurses' 'texinfo')
backup=('etc/conf.d/gpm')
options=('!makeflags')
install=gpm.install
source=(http://unix.schottelius.org/gpm/archives/${pkgname}-${pkgver}.tar.bz2 \
        gpm gpm.conf.d)
md5sums=('6b534da16dc1b28ba828dea89e520f6f' '945ecde0836687e05c13ea837092ea6c'\
         '63e599950946e5f5a649432510aacf4c')
sha1sums=('f3bb9272878b7934968381c8b25edab359114150' '19e1feb1493373512a77801699df012d186336ea'\
         'e483f893b4e6b9e1221a208527d728ffefe5438e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m755 ../gpm "${pkgdir}/etc/rc.d/gpm" || return 1
  install -D -m644 ../gpm.conf.d "${pkgdir}/etc/conf.d/gpm" || return 1

# library fixes
  cd "${pkgdir}/usr/lib/"
  ln -s libgpm.so.2.* libgpm.so || return 1
  chmod 755 "${pkgdir}"/usr/lib/libgpm.so.* || return 1
}
