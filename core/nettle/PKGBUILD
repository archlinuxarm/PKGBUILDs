# $Id: PKGBUILD 183668 2013-04-26 12:45:09Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 at gmx dot com

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon, assembler

pkgname=nettle
pkgver=3.2
pkgrel=1
pkgdesc="A low-level cryptographic library"
arch=('i686' 'x86_64')
url="http://www.lysator.liu.se/~nisse/nettle/"
license=('GPL2')
install=$pkgname.install
depends=('gmp')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('afb15b4764ebf1b4e6d06c62bd4d29e4'
         'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
	--disable-static --disable-arm-neon --disable-assembler
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
