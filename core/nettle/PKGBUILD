# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 at gmx dot com

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon, assembler

pkgname=nettle
pkgver=3.6
pkgrel=1
pkgdesc="A low-level cryptographic library"
arch=('x86_64')
url="https://www.lysator.liu.se/~nisse/nettle"
license=('GPL2')
depends=('gmp')
provides=('libnettle.so' 'libhogweed.so')
checkdepends=('valgrind')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('d24c0d0f2abffbc8f4f34dcf114b0f131ec3774895f3555922fe2f40f3d5e3f1'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static --disable-arm-neon --disable-assembler
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
