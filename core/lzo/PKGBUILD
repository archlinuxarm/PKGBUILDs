# $Id$
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>
# Maintainer: dorphell <dorphell@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable unaligned access on v5 with -DLZO_CFG_NO_UNALIGNED

pkgname=lzo
pkgver=2.10
pkgrel=1
pkgdesc="Portable lossless data compression library"
arch=('i686' 'x86_64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
source=(http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz)
sha1sums=('4924676a9bae5db58ef129dc1cebce3baa3c4b5d')

build() {
  cd lzo-${pkgver}

  [[ $CARCH == "arm" ]] && CPPFLAGS+=" -DLZO_CFG_NO_UNALIGNED"

  ./configure --prefix=/usr --enable-shared
  make

  # build minilzo
  gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
  gcc $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

check() {
  cd lzo-${pkgver}
  make test # Larger test
  make check
}

package() {
  cd lzo-${pkgver}
  make DESTDIR=${pkgdir} install

  # install minilzo
  install -m 755 libminilzo.so.0 ${pkgdir}/usr/lib
  install -p -m 644 minilzo/minilzo.h ${pkgdir}/usr/include/lzo
  cd ${pkgdir}/usr/lib
  ln -s libminilzo.so.0 libminilzo.so
}
