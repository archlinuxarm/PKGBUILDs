# $Id: PKGBUILD 87235 2010-08-11 12:10:19Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: ezzetabi <ezzetabi at gawab dot com>
# PlugApps: Kevin Mihelich <kevin@plugapps.com>

noautobuild=1 # prevent plugbuild from autobuilding this package

pkgname=cloog-ppl
pkgver=0.15.9
pkgrel=2
pkgdesc="Library that generates loops for scanning polyhedra"
arch=('i686' 'x86_64')
url="http://www.cloog.org/"
license=('GPL')
depends=('ppl>=0.11')
options=('!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/infrastructure/${pkgname}-${pkgver}.tar.gz)
md5sums=('806e001d1b1a6b130069ff6274900af5')

build() {
  cd $srcdir/$pkgname-$pkgver

  # "Add" ppl-0.11 compatibility
  sed -i "/ppl_minor_version=/s#10#11#" configure

  ./configure --prefix=/usr --infodir=/usr/share/info \
    --with-bits=gmp --with-ppl=/usr 
  make
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir/ install
}
