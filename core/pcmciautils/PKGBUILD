# $Id: PKGBUILD 12902 2008-09-24 14:14:21Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pcmciautils
pkgver=015
pkgrel=2
pkgdesc="Utilities for inserting and removing PCMCIA cards"
arch=(i686 x86_64)
url="http://kernel.org/pub/linux/utils/kernel/pcmcia/pcmcia.html"
license=('GPL')
groups=('base')
depends=('glibc' 'sysfsutils' 'module-init-tools>=3.2pre9')
conflicts=('pcmcia-cs')
source=(http://kernel.org/pub/linux/utils/kernel/pcmcia/pcmciautils-$pkgver.tar.bz2 
	60-pcmcia.rules)
options=(!makeflags)

build() {
  cd $startdir/src/$pkgname-$pkgver
  sed -i -e 's,/usr/bin/install,/bin/install,g' Makefile
  make || return 1
  make DESTDIR=$startdir/pkg/ install
  # fix lspcmcia symlink
  ln -sf pccardctl $startdir/pkg/sbin/lspcmcia
  # adding static binaries for initrd setup
  make clean
  sed -i -e 's/STATIC\ =\ false/STATIC\ =\ true/g' Makefile
  make || return 1
  install -D -m755 pcmcia-check-broken-cis $startdir/pkg/sbin/pcmcia-check-broken-cis.static
  install -D -m755 pcmcia-socket-startup $startdir/pkg/sbin/pcmcia-socket-startup.static
  # add fixed rules file
  install -D -m644 $startdir/src/60-pcmcia.rules $startdir/pkg/etc/udev/rules.d/
}
md5sums=('9e12435c8b6cf7bf59894e90e480b4aa'
         '683150da64dd81cf9f7884b6fce06980')
