# Maintainer: Philipp Scholl <pscholl@bawue.de>
# Modified by OpenPogo

pkgname=tcl
pkgver=8.5.7
pkgrel=1
pkgdesc="The Tcl scripting language"
arch=('arm')
license=('custom')
depends=()
groups=('base-devel')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
url="http://tcl.sourceforge.net/"
md5sums=('f70ad8f78b5e4a9f792fe101f22b125f')

build() {
  cd ${srcdir}/tcl${pkgver}/unix
  
  if [ "$CARCH" = "x86_64" ]; then
    ./configure --prefix=/opt --mandir=/opt/share/man --enable-threads --enable-64bit
  else
    ./configure --prefix=/opt --mandir=/opt/share/man --enable-threads --disable-64bit
  fi
  
  make || return 1
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf tclsh8.5 ${pkgdir}/opt/bin/tclsh
  # install license
  install -Dm644 ../license.terms ${pkgdir}/opt/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces / fixes #3602
  sed -i \
  -e "s,^TCL_BUILD_LIB_SPEC='-L.*/unix,TCL_BUILD_LIB_SPEC='-L/opt/lib," \
  -e "s,^TCL_SRC_DIR='.*',TCL_SRC_DIR='/opt/include'," \
  -e "s,^TCL_BUILD_STUB_LIB_SPEC='-L.*/unix,TCL_BUILD_STUB_LIB_SPEC='-L/opt/lib," \
  -e "s,^TCL_BUILD_STUB_LIB_PATH='.*/unix,TCL_BUILD_STUB_LIB_PATH='/opt/lib," \
  -e "s,^TCL_LIB_FILE='libtcl8.5..TCL_DBGX..so',TCL_LIB_FILE=\"libtcl8.5\$\{TCL_DBGX\}.so\"," \
  -e "s,^TCL_CC_SEARCH_FLAGS='\(.*\)',TCL_CC_SEARCH_FLAGS='\1:/opt/lib'," \
  -e "s,^TCL_LD_SEARCH_FLAGS='\(.*\)',TCL_LD_SEARCH_FLAGS='\1:/opt/lib'," \
  ${pkgdir}/opt/lib/tclConfig.sh
}
