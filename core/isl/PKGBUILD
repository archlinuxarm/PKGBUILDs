# $Id: PKGBUILD 162067 2012-06-19 12:27:31Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - no changes, just version hold

noautobuild=1

pkgname=isl
pkgver=0.11.2
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i686' 'x86_64')
url="http://www.kotnet.org/~skimo/isl/"
depends=('gmp')
license=('MIT')
options=('!libtool')
source=(http://www.kotnet.org/~skimo/isl/$pkgname-$pkgver.tar.bz2)
md5sums=('c40daa17d2995d1c98a0c1aca607541f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
  mv $pkgdir/{,/usr/share/gdb/auto-load/}usr/lib/libisl.so.10.1.2-gdb.py

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}
