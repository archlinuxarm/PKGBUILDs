# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Darshit Shah <darnir@gmail.com>

# no changes, hold for soname bumps

pkgname=libpsl
pkgver=0.21.1
pkgrel=3
pkgdesc='Public Suffix List library'
url='https://github.com/rockdaboot/libpsl'
arch=('x86_64')
license=('MIT')
depends=('libidn2' 'libunistring')
makedepends=('libxslt' 'python' 'publicsuffix-list' 'gtk-doc')
provides=('libpsl.so')
source=(https://github.com/rockdaboot/libpsl/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a5084b9df4ff2a0b1f5074b20972efe0da846473396d27b57967c7f6aa190ab3c910b4bfc4f8f03802f08decbbad5820d850c36ad59610262ae37fe77de0c7f5')
b2sums=('87c72651303a7870fd3f742e9ce8028e0dd48d3ded78ae7dea317ce54b98975f372429399b6a2d6746fd8e074d6a3ed13772b6c173f96c3a86aa7d726fd75586')

prepare() {
  cd ${pkgname}-${pkgver}
  rm -frv list
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-dependency-tracking \
    --enable-man \
    --enable-gtk-doc \
    --enable-{builtin,runtime}=libidn2 \
    --with-psl-file=/usr/share/publicsuffix/effective_tld_names.dat \
    --with-psl-testfile=/usr/share/publicsuffix/test_psl.txt
  LC_CTYPE=en_US.UTF-8 make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  depends+=('libidn2.so' 'libunistring.so')

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
