# Maintainer: Mike Staszel <mikestaszel@plugboxlinux.org>

pkgname=zlib
pkgver=1.2.5
pkgrel=2
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('i686' 'x86_64' 'arm')
license=('custom')
url="http://www.zlib.net/"
depends=('glibc')
options=('!makeflags')
source=("http://zlib.net/zlib-${pkgver}.tar.gz"
        'zlib-1.2.5-lfs-decls.patch')
md5sums=('c735eab2d659a96e5a594c9e8541ad63'
         '4cb279ea3beab621f3526bf7b7ab99e5')

build() {
	cd ${srcdir}/zlib-$pkgver

	# see http://bugs.archlinux.org/task/19280
	patch -p1 -i ${srcdir}/zlib-1.2.5-lfs-decls.patch || return 1

	./configure --prefix=/usr --shared

	make || return 1

	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir} || return 1
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
