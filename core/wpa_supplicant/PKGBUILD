# Maintainer: Thomas Baechler <thomas@archlinux.org>
# Modified by OpenPogo

pkgname=wpa_supplicant
pkgver=0.6.9
#_madwifi_ver=0.9.4
pkgrel=1
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=(arm)
depends=('openssl' 'dbus-core>=1.2.4' 'readline' 'libnl')
optdepends=('wpa_supplicant_gui: wpa_gui program')
license=('GPL')
groups=('base')
backup=('opt/etc/wpa_supplicant.conf')
url="http://hostap.epitest.fi/wpa_supplicant"
source=(http://hostap.epitest.fi/releases/wpa_supplicant-${pkgver}.tar.gz
        config
        Makefile)
md5sums=('0efb8fcedf0a8acf6f423dfdb0658fdd'
         '7b74567825df411fbc2f43b2642e547d'
         'd94c65954602146834b92a7a354f75eb')

build() {
  cd "$srcdir/$pkgname-$pkgver/$pkgname"
  cp $srcdir/config ./.config
  rm Makefile
  cp $srcdir/Makefile .
  sed -i 's@/opt/local@$(PREFIX)@g' Makefile
  make || return 1
  make PREFIX=/opt DESTDIR="$pkgdir" install || return 1
  install -m755 -d "$pkgdir/opt/etc"
  install -m644 wpa_supplicant.conf "$pkgdir/opt/etc/wpa_supplicant.conf" || return 1
  install -d -m755 $pkgdir/opt/share/man/man{5,8} || return 1
  install -m644 doc/docbook/wpa_supplicant.conf.5 "$pkgdir/opt/share/man/man5/" || return 1
  install -m644 doc/docbook/{wpa_background,wpa_cli,wpa_passphrase,wpa_supplicant}.8 "$pkgdir/opt/share/man/man8/" || return 1

  install -m755 -d "$pkgdir/opt/share/dbus-1/system-services"
  install -m644 dbus-wpa_supplicant.service "$pkgdir/opt/share/dbus-1/system-services/fi.epitest.hostap.WPASupplicant.service" || return 1
  sed -e 's/sbin/opt\/sbin/' -i "$pkgdir/opt/share/dbus-1/system-services/fi.epitest.hostap.WPASupplicant.service" || return 1
  install -m755 -d "$pkgdir/opt/etc/dbus-1/system.d"
  install -m644 dbus-wpa_supplicant.conf "$pkgdir/opt/etc/dbus-1/system.d/fi.epitest.hostap.WPASupplicant.conf" || return 1
}
