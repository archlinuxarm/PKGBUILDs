# Maintainer: Mike Staszel <mikestaszel@openpogo.com>

pkgname=coreutils
pkgver=7.4
pkgrel=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=(i686 x86_64 arm)
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('glibc>=2.9' 'shadow>=4.1.2.1' 'pam>=1.0.3' 'acl>=2.2.47' 'gmp>=4.2.4')
provides=('mktemp')
conflicts=('mktemp')
replaces=('sh-utils' 'fileutils' 'textutils' 'mktemp')
backup=('etc/pam.d/su')
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz
		su)
md5sums=('c52f4f64dda9a245c38e74c09fdd86d2'
	 	 'fa85e5cce5d723275b14365ba71a8aad')

build() {
  cd "$srcdir/coreutils-$pkgver"

  ./configure --prefix=/usr \
      --enable-install-program=su \
      --enable-pam ac_cv_func_openat=no || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # Remove binaries provided by other packages.
  for f in hostname uptime groups kill; do
    rm -f $pkgdir/usr/bin/$f
  done

  cd $pkgdir/usr/bin
  install -dm755 $pkgdir/{bin,sbin,usr/sbin}
  mv su date echo false pwd stty true uname cat tr cut readlink $pkgdir/bin
  mv dd cp df du ln ls mv rm dir sync vdir chgrp chmod chown $pkgdir/bin
  mv mkdir mknod rmdir shred touch mkfifo dircolors install sleep $pkgdir/bin
  mv chroot $pkgdir/usr/sbin
  ln -sf test [
  ln -sf /bin/sleep $pkgdir/usr/bin/sleep
  install -Dm644 $srcdir/su $pkgdir/etc/pam.d/su

  chmod -v 4555 $pkgdir/bin/su
}
