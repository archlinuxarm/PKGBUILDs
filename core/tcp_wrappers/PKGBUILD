# Maintainer: judd <jvinet@zeroflux.org>

# Plugbox changes: Add "sshd: ALL" to hosts.allow
# NOTE: hosts.allow is only backed up if the user has made any changes

pkgname=tcp_wrappers
pkgver=7.6
pkgrel=13
pkgdesc="Monitors and Controls incoming TCP connections"
arch=(i686 x86_64)
url="ftp://ftp.porcupine.org/pub/security/index.html"
license=('custom')
groups=('base')
backup=(etc/hosts.allow etc/hosts.deny)
depends=('bash' 'glibc')
source=(ftp://ftp.porcupine.org/pub/security/${pkgname}_$pkgver.tar.gz
        hosts.allow 
        hosts.deny
        try-from.8
        safe_finger.8
        shared_lib_plus_plus-1.patch
        01_all_redhat-bug11881.patch
        02_all_redhat-bug17795.patch
        03_all_wildcard.patch
        04_all_fixgethostbyname.patch
        07_all_sig.patch
        08_all_strerror.patch
        09_all_gcc-3.4.patch
        10_all_more-headers.patch
        11_inet6_fixes.patch
        tcp-wrappers-7.6-ipv6-1.14.patch)
md5sums=('e6fa25f71226d090f34de3f6b122fb5a'
         '3d1d035ff0addf3faf0dee5995d246a1'
         'e4743ca604a1749c7312f9283b4bb0d1'
         '4a8f40f9a69f0848df92b232072e8561'
         '1a6d7b11abb1fd69ace775d02a1c72cf'
         '99345104130b91cb151af9d87eee1449'
         'e7ac7ae271703eacf175d597d329e11a'
         '2790301fbf1b4711e75d5b799b6d4ac8'
         'd286da9fca993f8afa89631133312151'
         '97d4d81faaecf0958eeb52c45df71e34'
         '374b8179b8d5c71979008c6a775d658e'
         '801292cacf14a92e2784b925e72a1db1'
         '0978932f49aae33834a46e189ace7d77'
         '68b1c7f82fed60b446b00f6de27c3b9f'
         'bd0af9dedcacd594e0715de800fc1b57'
         '9154c38e3fb69d12b5bfb2fc5284314f')

build() {
  cd $srcdir/${pkgname}_$pkgver
  # add gentoo / fedora / redhat patches
  patch -Np1 -i ../shared_lib_plus_plus-1.patch 
  patch -Np1 -i ../01_all_redhat-bug11881.patch
  patch -Np0 -i ../02_all_redhat-bug17795.patch
  patch -Np0 -i ../03_all_wildcard.patch
  patch -Np1 -i ../04_all_fixgethostbyname.patch
  patch -Np1 -i ../07_all_sig.patch
  patch -Np1 -i ../09_all_gcc-3.4.patch
  patch -Np1 -i ../10_all_more-headers.patch
  patch -Np0 -i ../11_inet6_fixes.patch
  patch -Np2 -i ../tcp-wrappers-7.6-ipv6-1.14.patch

  make REAL_DAEMON_DIR=/usr/sbin STYLE=-DPROCESS_OPTIONS linux
  # dumb makefile
  mkdir -p $pkgdir/usr/{include,lib,sbin}
  mkdir -p $pkgdir/usr/share/man/man{3,5,8}
  make DESTDIR=$pkgdir install
  install -D -m644 ../hosts.allow $pkgdir/etc/hosts.allow
  install -D -m644 ../hosts.deny $pkgdir/etc/hosts.deny
  # add manpage symlinks for hosts.deny and hosts.allow
  cd $pkgdir/usr/share/man/man5
  ln -s hosts_access.5.gz hosts.allow.5.gz
  ln -s hosts_access.5.gz hosts.deny.5.gz
  # install license
  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  install -m644 $srcdir/${pkgname}_$pkgver/DISCLAIMER $pkgdir/usr/share/licenses/$pkgname/license.txt
}
