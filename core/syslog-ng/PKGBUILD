# Maintainer: Manuel Rotter <rotter.manuel@gmail.com>
# Modified by OpenPogo

pkgname=syslog-ng
pkgver=3.0.3
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=(arm)
license=('GPL2')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'tcp_wrappers' 'libcap>=2.16' 'openssl')
makedepends=('flex' 'pkgconfig') 
optdepends=('logrotate')
provides=('logger')
backup=('opt/etc/syslog-ng.conf' 'opt/etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/$pkgname\_$pkgver.tar.gz
        syslog-ng.conf
        syslog-ng.logrotate
        syslog-ng.rc)
install=syslog-ng.install
md5sums=('b8b3c9af28696bce48ceca0de6444426'
         '951798397619884393ba7f886124a519'
         'f9f99dae78fdd9d2f965bb4806657168'
         'cbfecd299aed3cf967dbc9e64a7a57da')

build() {
    cd $srcdir/$pkgname-$pkgver

    LDFLAGS="-Wl,--as-needed -Wl,--no-undefined" ./configure \
                --prefix=/opt \
                --sysconfdir=/opt/etc \
                --localstatedir=/opt/var/lib/syslog-ng \
                --enable-tcp-wrapper \
                --with-pidfile-dir=/opt/var/run \
                --disable-spoof-source
    make || return 1
    make DESTDIR=$pkgdir install || return 1
    install -dm755 $pkgdir/opt/var/lib/syslog-ng
    install -Dm644 $srcdir/syslog-ng.conf $pkgdir/opt/etc/syslog-ng.conf
    install -Dm644 $srcdir/syslog-ng.logrotate $pkgdir/opt/etc/logrotate.d/syslog-ng
    install -Dm755 $srcdir/syslog-ng.rc $pkgdir/opt/etc/rc.d/syslog-ng
    # remove libexec
    rm -rf $pkgdir/opt/libexec
}
