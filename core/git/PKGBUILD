# Maintainer: Alexander Foremny <alexanderforemny@gmail.com>

pkgname=git
pkgver=1.6.4.1
pkgrel=1
pkgdesc="GIT - the stupid content tracker"
groups=('base-devel')
arch=('arm')
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl>=5.10.0')
source=("http://kernel.org/pub/software/scm/git/${pkgname}-${pkgver}.tar.bz2")
md5sums=('bdc9b8be7d56a3beabad2d228f2e8e9a')

build() {
  cd "$srcdir/git-$pkgver"

  make prefix=/usr gitexecdir=/usr/lib/git-core || return 1
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    INSTALLDIRS=vendor DESTDIR="$pkgdir" install || return 1
  mkdir -p "$pkgdir/usr/share/"
  cp -dR ./gitweb "$pkgdir/usr/share/gitweb" || return 1

  mkdir -p $pkgdir/etc/bash_completion.d/
  install -m644 ./contrib/completion/git-completion.bash \
                "$pkgdir/etc/bash_completion.d/git" || return 1
  rm -rf $pkgdir/usr/lib/perl5
}
