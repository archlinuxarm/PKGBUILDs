# $Id: PKGBUILD 53088 2009-09-26 02:08:25Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsasl
pkgver=2.1.23
pkgrel=2
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i686' 'x86_64')
url="http://cyrusimap.web.cmu.edu/downloads.html#sasl"
license=('custom')
depends=('db>=4.8')
install=libsasl.install
source=(ftp://ftp.andrew.cmu.edu/pub/cyrus-mail/cyrus-sasl-${pkgver}.tar.gz
	cyrus-sasl-2.1.19-checkpw.c.patch db-4.7.patch)
options=('!makeflags')
md5sums=('2eb0e48106f0e9cd8001e654f267ecbc'
         'e27ddff076342e7a3041c4759817d04b'
         '71a3b7454f4d7cc2966b347bdf03f2fc')

build() {
  cd ${srcdir}/cyrus-sasl-${pkgver}
  patch -Np0 -i ${srcdir}/cyrus-sasl-2.1.19-checkpw.c.patch || return 1
  patch -Np0 -i ${srcdir}/db-4.7.patch || return 1
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
  	--localstatedir=/var \
	--disable-anon \
	--disable-cram \
	--disable-digest \
	--disable-gssapi \
	--enable-login \
	--disable-otp \
	--enable-plain \
        --mandir=/usr/share/man
  for dir in include lib sasldb plugins utils; do
    pushd ${dir} || return 1
    make || return 1
    make DESTDIR=${pkgdir} install || return 1
    popd
  done

  # install license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING || return 1
}
