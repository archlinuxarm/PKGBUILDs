# $Id: PKGBUILD 162068 2012-06-19 12:27:34Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - no changes, just version hold

noautobuild=1

pkgname=cloog
pkgver=0.18.0
pkgrel=2
pkgdesc="Library that generates loops for scanning polyhedra"
arch=('i686' 'x86_64')
url="http://www.bastoul.net/cloog/"
license=('GPL')
depends=('isl' 'gmp')
options=('!libtool')
source=(http://www.bastoul.net/cloog/pages/download/$pkgname-$pkgver.tar.gz
        cloog-0.18.0-isl-0.11.2.patch)
md5sums=('be78a47bd82523250eb3e91646db5b3d'
         '1bec9a7f1c3e9e0f3f10f23898f7148d')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  
  # test-suite fix - commit fc8b0627
  patch -p1 -i $srcdir/cloog-0.18.0-isl-0.11.2.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-isl=system
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
