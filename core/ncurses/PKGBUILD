# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# remove when bumped upstream

pkgname=ncurses
_pkgver=6.0-20170902
pkgver=${_pkgver/-/+}
pkgrel=2.1
pkgdesc='System V Release 4.0 curses emulation library'
arch=(i686 x86_64)
url='http://invisible-island.net/ncurses/ncurses.html'
license=(MIT)
depends=(glibc gcc-libs)
provides=(libncurses++w.so libformw.so libmenuw.so libpanelw.so libncursesw.so)
source=(http://invisible-mirror.net/archives/ncurses/current/ncurses-${_pkgver}.tgz{,.asc})
md5sums=('b7b1cedc484172434855b00831183458'
         'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')  # Thomas Dickey

build() {
  cd $pkgname-${_pkgver}

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-pkg-config-libdir=/usr/lib/pkgconfig \
    --with-shared --with-normal --without-debug --without-ada \
    --enable-widec --enable-pc-files --with-cxx-binding --with-cxx-shared \
    --with-termlib=tinfo --with-ticlib=tic
  make
}

package() {
  cd $pkgname-${_pkgver}
  make DESTDIR="$pkgdir" install

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    echo "INPUT(-l${lib}w)" > "$pkgdir/usr/lib/lib${lib}.so"
    ln -s ${lib}w.pc "$pkgdir/usr/lib/pkgconfig/${lib}.pc"
  done

  # don't require -ltinfo when linking with --no-add-needed
  rm -f "$pkgdir/usr/lib/libncursesw.so"
  echo "INPUT(libncursesw.so.${pkgver:0:1} -ltinfo)" > "$pkgdir/usr/lib/libncursesw.so"

  # some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" > "$pkgdir/usr/lib/libcursesw.so"
  ln -s libncurses.so "$pkgdir/usr/lib/libcurses.so"

  # install license, rip it from the readme
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  grep -B 100 '$Id' README > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
