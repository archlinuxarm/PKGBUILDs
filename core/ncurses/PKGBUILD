# Maintainer: Philipp Scholl <pscholl@bawue.de>
# Modified by OpenPogo

pkgname=ncurses
pkgver=5.7
pkgrel=3
pkgdesc="System V Release 4.0 curses emulation library"
arch=(arm)
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
groups=('base')
depends=()
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('cce05daf61a64501ef6cd8da1f727ec6')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/opt --mandir=/opt/share/man \
     --with-shared --with-normal --without-debug --without-ada \
     --with-install-prefix=${pkgdir} --enable-widec 
  make || return 1
  make install

  # move libraries needed for boot to /opt/lib (we call tput in initscripts)
  install -dm755 ${pkgdir}/opt/lib
#  mv ${pkgdir}/opt/lib/libncursesw.so.5* ${pkgdir}/lib
  ln -sf ${pkgdir}/opt/lib/libncursesw.so.5 ${pkgdir}/opt/lib/libncursesw.so

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in curses ncurses form panel menu ; do \
    rm -f ${pkgdir}/opt/lib/lib${lib}.so ; \
    echo "INPUT(-l${lib}w)" >${pkgdir}/opt/lib/lib${lib}.so ; \
    ln -sf lib${lib}w.a ${pkgdir}/opt/lib/lib${lib}.a ; \
  done
  ln -sf libncurses++w.a ${pkgdir}/opt/lib/libncurses++.a

  # install tput to /opt/bin
  install -dm755 ${pkgdir}/opt/bin/
#  mv ${pkgdir}/opt/bin/tput ${pkgdir}/bin/tput

  # Some packages look for -lcurses during build
  rm -f ${pkgdir}/opt/lib/libcursesw.so
  echo "INPUT(-lncursesw)" >${pkgdir}/opt/lib/libcursesw.so
  ln -sf libncurses.so ${pkgdir}/opt/lib/libcurses.so
  ln -sf libncursesw.a ${pkgdir}/opt/lib/libcursesw.a
  ln -sf libncurses.a ${pkgdir}/opt/lib/libcurses.a

  # Install libncurses.so.5 for external binary support 
  [ $CARCH = "x86_64" ] && CONFIGFLAG="--with-chtype=long"
  ./configure --prefix=/opt \
    --with-shared --with-normal --without-debug --without-ada \
    --with-install-prefix=${pkgdir} $CONFIGFLAG
  make || return 1
  
  install -Dm755 lib/libncurses.so.${pkgver} ${pkgdir}/opt/lib/libncurses.so.${pkgver}
  ln -sf libncurses.so.${pkgver} ${pkgdir}/opt/lib/libncurses.so.5

  # install license, rip it from the readme
  cd ${srcdir}/${pkgname}-${pkgver}
  install -dm755 ${pkgdir}/opt/share/licenses/$pkgname
  grep -B 100 '$Id' README > ${pkgdir}/opt/share/licenses/${pkgname}/license.txt
}
