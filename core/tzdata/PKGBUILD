# $Id: PKGBUILD 59199 2009-11-21 11:27:19Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2009s
pkgrel=1
_tzcode=2009r
_tzdata=2009s
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64' 'arm')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
groups=('base')
depends=()
makedepends=()
optdepends=('sh: required by tzselect')
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch)
md5sums=('3ea46872dfb400b9141021b975eea83c'
         '3cd8038d7bbfb3a4ac544aceef724853'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz  || return 1
  tar -xf ${pkgname}${_tzdata}.tar.gz || return 1

  patch -Np1 -i "${srcdir}/Makefile.patch" || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
