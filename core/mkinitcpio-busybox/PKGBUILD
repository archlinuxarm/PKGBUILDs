# $Id: PKGBUILD 107680 2011-01-26 22:30:45Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - removed -mtune, changed -march to armv5te from i686

plugrel=1

pkgname=mkinitcpio-busybox
pkgver=1.18.2
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
source=(http://busybox.net/downloads/busybox-${pkgver}.tar.bz2
        config)
sha256sums=('aa7e1cec8cd9c7f4e56098b9e4bb2ab5d593d5a35f766ad9e6a312289bf57080'
            'dd6dca08fbc335f1394e0f1b619df6895349c759d0c6e43456aed411e16d4194')

build() {
  cd "${srcdir}/busybox-${pkgver}"

  unset CFLAGS CXXFLAGS
  if [ ${CARCH} = "x86_64" ]; then
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=x86-64 -mtune=generic -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  else
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=armv5te -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  fi
  make
}

package() {
  install -D -m755 "${srcdir}/busybox-${pkgver}"/busybox "${pkgdir}/lib/initcpio/busybox"
}
# vim:set ts=2 sw=2 et:
