# $Id: PKGBUILD 123921 2011-05-14 11:29:02Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - removed -mtune, changed -march to v7/v5 compat

plugrel=1

pkgname=mkinitcpio-busybox
pkgver=1.18.4
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
source=(http://busybox.net/downloads/busybox-${pkgver}.tar.bz2
        config)
sha256sums=('4d24d37bd6f1bd153e8cf9a984ec2f32f18464f73ca535e2cc2e8be9694097fa'
            'dd6dca08fbc335f1394e0f1b619df6895349c759d0c6e43456aed411e16d4194')

build() {
  cd "${srcdir}/busybox-${pkgver}"

  unset CFLAGS CXXFLAGS
  if [ ${CARCH} = "armv7h" ]; then
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=armv7-a -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  else
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=armv5te -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  fi
  make
}

package() {
  install -D -m755 "${srcdir}/busybox-${pkgver}"/busybox "${pkgdir}/lib/initcpio/busybox"
}
# vim:set ts=2 sw=2 et:
