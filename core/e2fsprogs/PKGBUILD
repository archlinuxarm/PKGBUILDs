# Maintainer: Ronald van Haren <ronald.archlinux.org> 
# Contributor: judd <jvinet@zeroflux.org>

# No Plugbox changes, just a version bump. If ABS goes above 1.41.12-2,
# delete this from git.

pkgname=e2fsprogs
pkgver=1.41.12
pkgrel=2
plugrel=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'MIT')
url="http://e2fsprogs.sourceforge.net"
groups=('base')
depends=('sh' 'util-linux-ng')
makedepends=('bc')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'MIT-LICENSE')
backup=('etc/mke2fs.conf')
install=${pkgname}.install
sha1sums=('62b002fe507b2ddafc24b89532e472fe4b55a14f'
          'f4a0d5b0cdb980e3fedd6f5e7dde0b0ffb7bbdfb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in || return 1

  ./configure --prefix=/usr --with-root-prefix="" --enable-elf-shlibs \
      --disable-fsck --disable-uuidd \
      --disable-libuuid --disable-libblkid || return 1


  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-libs || return 1

  sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et" || return 1

  # install MIT license
  install -Dm644 "${srcdir}/MIT-LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE" || return 1

}
