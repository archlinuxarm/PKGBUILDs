# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=man-pages
pkgver=3.23
_posixver=2003-a
pkgrel=1
pkgdesc="Linux man pages"
arch=('arm')
license=('GPL')
url="http://www.kernel.org/doc/man-pages/"
groups=('base')
depends=()
source=(http://www.kernel.org/pub/linux/docs/man-pages/$pkgname-$pkgver.tar.gz
		http://www.kernel.org/pub/linux/docs/man-pages/man-pages-posix/$pkgname-posix-${_posixver}.tar.bz2)
md5sums=('4d0263bded9ad02a123ce11114ef76ac'
         '7c78aff03c0a6767ba483d34f19e4b09')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  
  # move the posix pages
  mkdir -p ${srcdir}/$pkgname-$pkgver/man0
  for sect in 0 1 3; do
    sed -i "/^\.so /s/man${sect}p/man$sect/" "${srcdir}/$pkgname-posix-${_posixver}/man${sect}p"/*
    mv -iv "${srcdir}/$pkgname-posix-${_posixver}/man${sect}p"/* "${srcdir}/$pkgname-$pkgver/man$sect/"
  done

  make prefix=${pkgdir}/usr install || return 1

  cd ${pkgdir}/usr/share/man
  # these are included in coreutils || return 1
  rm -f man1/{chgrp,chmod,chown,cp,dir,dd}.1 || return 1
  rm -f man1/{df,dircolors,du,install,ln,ls}.1 || return 1
  rm -f man1/{mkdir,mkfifo,mknod,mv,rm,rmdir}.1 || return 1
  rm -f man1/{touch,vdir}.1 || return 1
  # this is included in shadow
  rm -f man5/passwd.5 || return 1
  rm -f man3/getspnam.3 || return 1
  # this is included in diffutils
  rm -f man1/diff.1 || return 1
  # this is included in xf86-input-mouse
  rm -f man4/mouse.4 || return 1
  # these are included in module-init-tools
  rm -f man2/{create_module,delete_module,get_kernel_syms}.2 || return 1
  rm -f man2/{init_module,query_module}.2 || return 1
}
