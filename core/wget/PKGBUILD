# $Id: PKGBUILD 122950 2011-05-07 12:59:07Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

# Plugbox changes: ONLY added libidn and libsigsegv to depends,

plugrel=1

pkgname=wget
pkgver=1.12
pkgrel=7
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base')
depends=('libidn' 'libsigsegv' 'glibc' 'openssl')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        wget-1.12-CVE-2010-2252.patch
        wget-1.12-subjectAltName.patch)
md5sums=('141461b9c04e454dc8933c9d1f2abf83'
         '2c8bc23eff98fd4efc3f96394fc8e61e'
         'bd589403b7bb4967a6f41b0f43b1c8aa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix arbitrary file overwrite via 3xx redirect (CVE-2010-2252)
  patch -Np1 -i $srcdir/wget-1.12-CVE-2010-2252.patch

  # https://savannah.gnu.org/bugs/index.php?20421
  patch -Np0 -i $srcdir/wget-1.12-subjectAltName.patch

  # Note : We do not build with --enable-nls, because there is a bug in wget causing
  # international domain names to be not properly converted to punycode if
  # the current locale is a UTF-8 one
  # See : http://lists.gnu.org/archive/html/bug-wget/2011-02/msg00026.html

  ./configure -with-ssl --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cat >> "$pkgdir/etc/wgetrc" <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF

  # remove IRI option from wgetrc as it does not work (see above)
  sed -i '118,120d' $pkgdir/etc/wgetrc

}
