# $Id: PKGBUILD 110501 2011-02-19 17:08:52Z angvp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Maintainer: Mike Staszel <mikestaszel@plugboxlinux.org>

# Plugbox changes: ONLY added libidn and libsigsegv to depends,

plugrel=1

pkgname=wget
pkgver=1.12
pkgrel=4
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'openssl' 'libidn' 'libsigsegv')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        wget-1.12-CVE-2010-2252.patch)
md5sums=('141461b9c04e454dc8933c9d1f2abf83'
         '2c8bc23eff98fd4efc3f96394fc8e61e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # Fix arbitrary file overwrite via 3xx redirect (CVE-2010-2252)
  patch -Np1 -i ../wget-1.12-CVE-2010-2252.patch

  ./configure -with-ssl --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  cat > "$pkgdir/etc/wgetrc" <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF

}
