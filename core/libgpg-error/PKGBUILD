# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM

pkgname=libgpg-error
pkgver=1.28
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="https://www.gnupg.org"
license=('LGPL')
depends=('glibc' 'sh')
source=(ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2{,.sig}
	#ftp://ftp.franken.de/pub/crypt/mirror/ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2
	0001-Fix-compile-on-ARM.patch)
# https://www.gnupg.org/download/integrity_check.html
sha1sums=('2b9baae264f3e82ebe00dcd10bae3f2d64232c10'
          'SKIP'
          '05b14d377bf73093029ebdb009f8f6842aca2743')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch
              '031EC2536E580D8EA286A9F22071B08A33BD3F06') # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../0001-Fix-compile-on-ARM.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
}
