# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - replace CFLAGS setting with just a removal of -fstack-protector

pkgname=libaio
pkgver=0.3.110
pkgrel=1
pkgdesc="The Linux-native asynchronous I/O facility (aio) library"
arch=('i686' 'x86_64')
url="http://lse.sourceforge.net/io/aio.html"
license=('LGPL')
source=(http://ftp.de.debian.org/debian/pool/main/liba/$pkgname/${pkgname}_${pkgver}.orig.tar.gz)
md5sums=('2a35602e43778383e2f4907a4ca39ab8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # AIO library is a thin wrapper around kernel syscalls, it does not use stdlib
  # and other helpers like stack protection libraries
  CFLAGS=`echo $CFLAGS | sed -e 's/-fstack-protector//'` && CXXFLAGS="$CFLAGS"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make partcheck
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir/usr" install
}

