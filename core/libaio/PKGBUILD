# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - replace simplified CFLAGS with ARM versions

pkgname=libaio
pkgver=0.3.113
pkgrel=2
pkgdesc="The Linux-native asynchronous I/O facility (aio) library"
arch=(x86_64)
url="https://pagure.io/libaio"
license=(LGPL2.1)
depends=(glibc)
provides=(libaio.so)
options=(debug)
source=(https://pagure.io/libaio/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.gz)
sha512sums=('8058c927de0b5f7079fc232d2be23272537694bf271488af1dc0330b58afc307931792ab138512c5e00aa3ea921935a6d862f575fb0cc2bf323de63d8df208cd')
b2sums=('e06c1c1cc118c15f6bed1abfa97368d7ccc0d2e716a233cd4141936fb382e40909c340a6c882b18ee9461aaa23dd77a6644a38bdb5a00c1444538a2e0ab816d4')

prepare() {
  # -Werror, not even once
  sed 's/-Werror//' -i $pkgname-$pkgname-$pkgver/harness/Makefile
}

build() {
  # libaio is a thin wrapper around kernel syscalls, it does not use stdlib and
  # other helpers like stack protection libraries
  [[ $CARCH == "aarch64" ]] && CFLAGS="-march=armv8-a -O2 -pipe" || CFLAGS="-march=armv7-a -mfloat-abi=hard -mfpu=neon -O2 -pipe"
  make -C $pkgname-$pkgname-$pkgver
}

check() {
  make partcheck -k -C $pkgname-$pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgname-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/man/*.3 -t "$pkgdir/usr/share/man/man3/"
}

