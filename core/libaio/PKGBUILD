# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - replace CFLAGS setting with just a removal of -fstack-protector

pkgname=libaio
pkgver=0.3.111
pkgrel=2
pkgdesc="The Linux-native asynchronous I/O facility (aio) library"
arch=('x86_64')
url="http://lse.sourceforge.net/io/aio.html"
license=('LGPL')
source=(http://ftp.de.debian.org/debian/pool/main/liba/$pkgname/${pkgname}_${pkgver}.orig.tar.gz)
md5sums=('abb8f46d64b9bd3d0c6097e3a36639be')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # AIO library is a thin wrapper around kernel syscalls, it does not use stdlib
  # and other helpers like stack protection libraries
  CFLAGS=`echo $CFLAGS | sed -e 's/-fstack-protector-strong//'` && CXXFLAGS="$CFLAGS"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  # work around gcc warning (-Werror) in test suite
  sed -i '/strncpy/s#sizeof(TEMPLATE)#sizeof(template)#' harness/cases/19.t
  sed -i '/strncpy/s#sizeof(TEMPLATE)#sizeof(temp_file)#' harness/cases/21.t

  make partcheck
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir/usr" install
}

