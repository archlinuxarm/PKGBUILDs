# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - replace simplified CFLAGS with ARM versions

pkgname=libaio
pkgver=0.3.112
pkgrel=3
pkgdesc="The Linux-native asynchronous I/O facility (aio) library"
arch=(x86_64)
url="https://pagure.io/libaio"
license=(LGPL2.1)
depends=(glibc)
provides=(libaio.so)
options=(debug)
source=(https://releases.pagure.org/libaio/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5f984529c9f747a6c82f1e4457fc0832bb1fc299ae6e700f2ac5a8ea7b9bfc6ea1e75809728cc115a020cff6685ed1f4e38c6aeacc1ea98dfccce04dd19dafaa')
b2sums=('088f3b195a65bdc97ae2318e47af17c65259ed3208dca7bfef93c81a800602085e5b2078dbd436c740be316d0ebd923a1b3b7c0808257e2e7c7fb0f7ae1e0dba')

prepare() {
  # -Werror, not even once
  sed 's/-Werror//' -i $pkgname-$pkgver/harness/Makefile
}

build() {
  # libaio is a thin wrapper around kernel syscalls, it does not use stdlib and
  # other helpers like stack protection libraries
  [[ $CARCH == "aarch64" ]] && CFLAGS="-march=armv8-a -O2 -pipe" || CFLAGS="-march=armv7-a -mfloat-abi=hard -mfpu=neon -O2 -pipe"
  make -C $pkgname-$pkgver
}

check() {
  make partcheck -k -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/man/*.3 -t "$pkgdir/usr/share/man/man3/"
}

