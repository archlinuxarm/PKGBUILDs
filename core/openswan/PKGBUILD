# Maintainer: Tom Killian <tom@arclinux.org>

pkgname=openswan
pkgver=2.4.14
pkgrel=1
pkgdesc="Open Source implementation of IPsec for the Linux operating system"
url="http://www.openswan.org"
license=('GPL' 'custom')
arch=('arm')
depends=('iproute' 'gmp' 'perl')
backup=(etc/ipsec.conf etc/ipsec.d/examples/no_oe.conf \
        etc/ipsec.d/policies/{block,clear,clear-or-private,private,private-or-clear})
source=(http://www.openswan.org/download/openswan-$pkgver.tar.gz
        openswan.rc.d
        compile.patch)
md5sums=('9a6201ffacbf881a760058ab84ef6579'
         '543d84162761b9cc9ec319e938c4dd2a'
         '83c4728750ee2cce89bc659a170ae14c')

build() {
  # Create /etc/rc.d for init script, and license directory
  mkdir -p $pkgdir/{etc/rc.d,usr/share/licenses/openswan}

  cd $srcdir/openswan-$pkgver
  patch -Np1 -i ../compile.patch || return 1

  # Change install paths to Arch defaults
  sed -i 's|/usr/local|/usr|;s|libexec/ipsec|lib/openswan|' Makefile.inc

  make programs || return 1
  make DESTDIR=$pkgdir install

  # Remove SysV rc*.d directories
  rm -r $pkgdir/etc/rc[0-6].d

  # Change permissions in /var
  chmod 755 $pkgdir/var/run/pluto
  
  # Copy License
  cp LICENSE $pkgdir/usr/share/licenses/openswan
  
  # Install init script
  install -Dm755 ../openswan.rc.d $pkgdir/etc/rc.d/openswan
  # fix manpages
  mv $pkgdir/usr/man $pkgdir/usr/share/
}
