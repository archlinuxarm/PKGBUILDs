# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
# Modified by OpenPogo

pkgname=dbus-core
pkgver=1.2.14
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(arm)
license=('GPL' 'custom')
depends=('expat>=2.0' 'coreutils')
conflicts=('dbus<1.2.3-2')
options=(!libtool)
groups=('base')
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz
	    dbus)
md5sums=('2c267ccd45d0b18db8c9edacad63ec98'
         'ae21f22ea7579ac0d70227eebc04e1f8')

build() {
  cd "${srcdir}/dbus-${pkgver}"
  ./configure --prefix=/opt --sysconfdir=/opt/etc --localstatedir=/opt/var \
  	      --libexecdir=/opt/lib/dbus-1.0 --with-dbus-user=81 \
              --with-system-pid-file=/opt/var/run/dbus.pid \
	      --enable-inotify --disable-dnotify \
              --disable-verbose-mode --disable-static \
	      --disable-tests --disable-asserts --without-x || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -f "${pkgdir}/opt/bin/dbus-launch"
  rm -f "${pkgdir}/opt/share/man/man1/dbus-launch.1"

  chown 81:81 "${pkgdir}/opt/var/run/dbus" || return 1

  install -m755 -d "${pkgdir}/opt/etc/rc.d" || return 1
  install -m755 "${srcdir}/dbus" "${pkgdir}/opt/etc/rc.d/" || return 1

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "${pkgdir}/opt/etc/dbus-1/system.conf" || return 1

  #install .keep files so pacman doesn't delete empty dirs
  touch "${pkgdir}/opt/share/dbus-1/services/.keep" || return 1
  touch "${pkgdir}/opt/share/dbus-1/system-services/.keep" || return 1
  touch "${pkgdir}/opt/etc/dbus-1/session.d/.keep" || return 1
  touch "${pkgdir}/opt/etc/dbus-1/system.d/.keep" || return 1

  rmdir "${pkgdir}/opt/lib/dbus-1.0/dbus-1" || return 1

  install -d -m755 "${pkgdir}/opt/share/licenses/dbus-core"
  install -m644 COPYING "${pkgdir}/opt/share/licenses/dbus-core/" || return 1
}
