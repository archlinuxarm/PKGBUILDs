# $Id: PKGBUILD 42668 2009-06-18 15:45:42Z aaron $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=inetutils
pkgver=1.6
pkgrel=4
pkgdesc="A collection of common network programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/inetutils/"
license=('GPL')
depends=('readline' 'ncurses' 'pam' 'tcp_wrappers')
provides=('netkit-ftp' 'netkit-rsh' 'netkit-telnet' 'netkit-ntalk')
conflicts=('netkit-ftp' 'netkit-rsh' 'netkit-telnet' 'netkit-ntalk')
replaces=('netkit-ftp' 'netkit-rsh' 'netkit-telnet' 'netkit-ntalk')
backup=('etc/conf.d/ftpd' 'etc/xinetd.d/telnet' 'etc/xinetd.d/talk' \
        'etc/xinetd.d/rexec' 'etc/xinetd.d/rlogin' 'etc/xinetd.d/rsh')
options=('!emptydirs')
install=inetutils.install
source=(http://ftp.gnu.org/gnu/inetutils/${pkgname}-${pkgver}.tar.gz iu-1.6.escape.diff \
        ftp://ftp.uk.linux.org/pub/linux/Networking/netkit/netkit-rsh-0.17.tar.gz \
        ftpd.rc ftpd.conf telnet.xinetd talk.xinetd rexec.xinetd rlogin.xinetd rsh.xinetd)
md5sums=('23cc24bc77751bf77d50a07a7395f9b3' '3675a46bd42cc39e6a2f2f3b96064e28'\
         '65f5f28e2fe22d9ad8b17bb9a10df096' 'c167200f6faeb64659e69ced2a9d672a'\
         'e6e6e5990a2e8159a276fef8fbf54c04' '8596208d119045543c5fb644e1d713d5'\
         '2c3655985cf122591d71d031af84ff90' 'c490ea36751ac5f1d61d2c014959a3d1'\
         'c25d8382a994de84d9c6962d804f689b' 'c3aa508a9d1293a787341108cd49c1b7')
sha1sums=('481060bd8b93d0efa0a0f2ab5ac5c491773a21af' '2223f8e7c191a4edd163b7fb6cbd549ff31ffd6b'\
          '022185bf31af23abbb6d4f34621f94d9d0c4b6ea' '84dc802b5e57b5e04c847572225a3b9612017155'\
          '68a590083b45997dfdb80e666b2de762f494ba74' 'bfad98a4a62f1fff8779f076c6019ed07f9111af'\
          '27d99b910eec0fc26bd79ccc2c1de26608330298' '87aa4f38ebee9dac5dcaa04cbc3f2f0906bec605'\
          '81f10b3b688e3952f793b35bcef63b5bf257a92b' '81f4a8da823cf0bb14284fc71ee6108849691eda')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 < ../iu-1.6.escape.diff || return 1
  ./configure --prefix=/usr --libexec=/usr/sbin --mandir=/usr/share/man --infodir=/usr/share/info \
    --with-wrap --with-pam \
    --enable-ftp --enable-ftpd \
    --enable-telnet --enable-telnetd \
    --enable-talk --enable-talkd \
    --enable-rlogin --enable-rlogind \
    --enable-rsh --enable-rshd \
    --enable-rcp --enable-rexecd \
    --disable-tftp --disable-tftpd \
    --disable-ping --disable-ping6 \
    --disable-logger --disable-syslogd \
    --disable-inetd --disable-whois \
    --disable-uucpd --disable-hostname \
    --disable-ifconfig --disable-traceroute || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

# Building and adding netkit's rexec client to package
  cd ${srcdir}/netkit-rsh-0.17
  ./configure --prefix=/usr || return 1
  cd rexec
  make || return 1
  make INSTALLROOT=${pkgdir} MANDIR=/usr/share/man install || return 1

  install -D -m755 ${srcdir}/ftpd.rc ${pkgdir}/etc/rc.d/ftpd || return 1
  install -D -m644 ${srcdir}/ftpd.conf ${pkgdir}/etc/conf.d/ftpd || return 1
  install -D -m644 ${srcdir}/telnet.xinetd ${pkgdir}/etc/xinetd.d/telnet || return 1
  install -D -m644 ${srcdir}/talk.xinetd ${pkgdir}/etc/xinetd.d/talk || return 1
  install -D -m644 ${srcdir}/rexec.xinetd ${pkgdir}/etc/xinetd.d/rexec || return 1
  install -D -m644 ${srcdir}/rlogin.xinetd ${pkgdir}/etc/xinetd.d/rlogin || return 1
  install -D -m644 ${srcdir}/rsh.xinetd ${pkgdir}/etc/xinetd.d/rsh || return 1

  rm ${pkgdir}/usr/share/man/man1/{logger.1,tftp.1}
  rm ${pkgdir}/usr/share/man/man5/syslog.conf.5
  rm ${pkgdir}/usr/share/man/man8/{inetd.8,ping.8,syslogd.8,tftpd.8}

  rm ${pkgdir}/usr/share/info/dir
  gzip ${pkgdir}/usr/share/info/*
}
