# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=dmraid
pkgver=1.0.0.rc15
pkgrel=8
pkgdesc="Device mapper RAID interface"
url="http://people.redhat.com/~heinzm/sw/dmraid/"
depends=(device-mapper)
arch=('arm')
license=('GPL')
source=(http://people.redhat.com/~heinzm/sw/dmraid/src/$pkgname-$pkgver.tar.bz2
        dmraid-1.0.0.rc15-isw-raid10.patch
        dmraid-1.0.0.rc15-isw-serial.patch
        dmraid-1.0.0.rc15-rm_partitions.patch
        dmraid-1.0.0.rc15-whitespace.patch
        dmraid_install
        dmraid_hook)
install=dmraid.install
md5sums=('2602887205a35f89b59eeba3a868150f'
         '5a422c82e63321e65fbccb41dda70ea2'
         '499864f61c36874c9cef69600da31b59'
         '3d76f99972123b6efd7796bf51d543fa'
         '0a3ec92f071a25a01753c6c34b4cb0df'
         'db9a72cbf07560059d407c874f646240'
         'afdb3cd655ea0145410e2aae247c7bcf')
build() {
  cd $srcdir/$pkgname/$pkgver
  # add fedora patches
  patch -Np1 -i ../../dmraid-1.0.0.rc15-isw-raid10.patch || return 1
  patch -Np1 -i ../../dmraid-1.0.0.rc15-isw-serial.patch || return 1
  patch -Np1 -i ../../dmraid-1.0.0.rc15-rm_partitions.patch || return 1
  patch -Np1 -i ../../dmraid-1.0.0.rc15-whitespace.patch || return 1
  # create static bin
  ./configure --enable-static_link --prefix=/usr 
  make || return 1
  make DESTDIR=$pkgdir exec_prefix=$pkgdir/usr install
  mv $pkgdir/usr/sbin/dmraid $pkgdir/usr/sbin/dmraid.static

  # create dynamic linked bin
  ./configure --prefix=/usr 
  make || return 1
  make DESTDIR=$pkgdir exec_prefix=$pkgdir/usr install
  mkdir -p $pkgdir/var/lock/dmraid
  chmod 1777 $pkgdir/var/lock/
  install -D -m644 $srcdir/dmraid_install $pkgdir/lib/initcpio/install/dmraid
  install -D -m644 $srcdir/dmraid_hook $pkgdir/lib/initcpio/hooks/dmraid
  # fix .a file
  chmod 644 $pkgdir/usr/lib/libdmraid.a
  # fix manpage
  #mkdir -p $pkgdir/usr/share/man
  #mv $pkgdir/usr/man/man8 $pkgdir/usr/share/man/
  #rm -rf $pkgdir/usr/man
}
