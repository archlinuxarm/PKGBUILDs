# $Id: PKGBUILD 42609 2009-06-17 01:51:42Z dan $
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman-mirrorlist
pkgver=20090616
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
backup=(etc/pacman.d/mirrorlist)
source=(mirrorlist)

# NOTE on building this package:
# * Go to the trunk/ directory
# * Run bash -c ". PKGBUILD; updatelist"
# * Update the checksums, update pkgver
# * Build the package

updatelist() {
  wget -O mirrorlist http://www.archlinux.org/mirrorlist/i686/all/
  sed -i 's/i686/@carch@/' mirrorlist
}

build() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/mirrorlist $pkgdir/etc/pacman.d/
  # customize mirrorlist to architecture
  sed -i -e "s/@carch@/${CARCH}/g" $pkgdir/etc/pacman.d/mirrorlist
}

md5sums=('aa8909d007a64a77cac0d6a56b6cc4d7')
sha256sums=('d85613b4a5014c525f93bacfa9c8797cd000a9cb61f01130600c316018649831')
