# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sqlite3
pkgver=3.6.20
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('arm')
license=('custom')
url="http://www.sqlite.org/"
depends=('readline>=6.0.00')
makedepends=('tcl')
conflicts=('sqlite')
replaces=('sqlite')
source=(http://www.sqlite.org/sqlite-$pkgver.tar.gz
	license.txt)
options=(!libtool)
md5sums=('0faf8fc8ccff5297513c6532b2b4ce23'
         'c1cdbc5544034d9012e421e75a5e4890')

build() {
  cd ${srcdir}/sqlite-${pkgver}
  export LTLINK_EXTRAS="-ldl"
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1"
  ./configure --prefix=/usr \
	--enable-threadsafe \
	--enable-threads-override-locks \
	--enable-cross-thread-connections \
	--disable-static \
	--enable-load-extension || return 1

  # rpath removal
  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool

  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -Dm0644 sqlite3.1 ${pkgdir}/usr/share/man/man1/sqlite3.1

  # license
  install -Dm644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
