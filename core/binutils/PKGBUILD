# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Added a CONFIGFLAG for ARM, defining our build hosts
#  - --disable-sim, files provided by gdb
#  - Disabled make check, too processor/ram intensive, blows up
#  - upstream patch for AArch64

noautobuild=1

pkgname=binutils
pkgver=2.30
pkgrel=5
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=(x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
groups=(base-devel)
depends=(glibc zlib)
checkdepends=(dejagnu bc)
options=(staticlibs !distcc !ccache)
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig}
        0001-PR22741-objcopy-segfault-on-fuzzed-COFF-object.patch
        0002-PR22829-objcopy-strip-removes-PT_GNU_RELRO-from-lld-.patch
        0003-PR22836-r-s-doesnt-work-with-g3-using-GCC-7.patch
        0004-PR22868-Fix-case-where-IR-file-provides-symbol-visibility.patch
        0001-PR22764-LD-AARCH64-Allow-R_AARCH64_ABS16-and-R_AARCH.patch)
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)
md5sums=('ffc476dd46c96f932875d1b2e27e929f'
         'SKIP'
         '469164f3c93a0e92a697537b60c9806c'
         '0c679b37e90fb23de60a4d28329b956a'
         '53b5682e09c0a27e9994c3efdfe01d29'
         '0fac94f7fa54a95354454677d3e43994'
         '2b1d13379e401e8209e3d05016914d3b')

prepare() {
  mkdir -p binutils-build

  #cd binutils-gdb
  cd binutils-$pkgver

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # https://sourceware.org/bugzilla/show_bug.cgi?id=22741
  patch -p1 -i "$srcdir/0001-PR22741-objcopy-segfault-on-fuzzed-COFF-object.patch"

  # https://sourceware.org/bugzilla/show_bug.cgi?id=22829
  patch -p1 -i "$srcdir/0002-PR22829-objcopy-strip-removes-PT_GNU_RELRO-from-lld-.patch"

  # https://sourceware.org/bugzilla/show_bug.cgi?id=22836
  patch -p1 -i "$srcdir/0003-PR22836-r-s-doesnt-work-with-g3-using-GCC-7.patch"

  # https://sourceware.org/bugzilla/show_bug.cgi?id=22868
  patch -p1 -i "$srcdir/0004-PR22868-Fix-case-where-IR-file-provides-symbol-visibility.patch"

  # https://sourceware.org/bugzilla/show_bug.cgi?id=22764
  patch -p1 -i "$srcdir/0001-PR22764-LD-AARCH64-Allow-R_AARCH64_ABS16-and-R_AARCH.patch"
}

build() {
  cd binutils-build

  # ALARM: Specify build host types
  [[ $CARCH == "arm" ]] && CONFIGFLAG="--host=armv5tel-unknown-linux-gnueabi --build=armv5tel-unknown-linux-gnueabi"
  [[ $CARCH == "armv6h" ]] && CONFIGFLAG="--host=armv6l-unknown-linux-gnueabihf --build=armv6l-unknown-linux-gnueabihf"
  [[ $CARCH == "armv7h" ]] && CONFIGFLAG="--host=armv7l-unknown-linux-gnueabihf --build=armv7l-unknown-linux-gnueabihf"
  [[ $CARCH == "aarch64" ]] && CONFIGFLAG="--host=aarch64-unknown-linux-gnu --build=aarch64-unknown-linux-gnu"

  "$srcdir/binutils-$pkgver/configure" \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://github.com/archlinuxarm/PKGBUILDs/issues \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-ld=default \
    --enable-lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-threads \
    --disable-gdb \
    --disable-werror \
    --with-pic \
    --with-system-zlib \
    --disable-sim \
    $CONFIGFLAG

  make configure-host
  make tooldir=/usr
}

check() {
  cd binutils-build
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # ignore failures in gold testsuite...
  make -k LDFLAGS="" check || true
}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"
}
