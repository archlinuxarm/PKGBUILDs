# $Id: PKGBUILD 145910 2012-01-04 01:24:50Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
# - Added a CONFIGFLAG for ARM, defining our build hosts
# - Disabled make check, too processor/ram intensive, blows up

noautobuild=1

pkgname=binutils
pkgver=2.24
pkgrel=8
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('glibc>=2.20' 'zlib')
checkdepends=('dejagnu')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        binutils-2.24-lto-testsuite.patch
        binutils-2.24-gold-testsuite-plugin.patch
        binutils-2.24-shared-pie.patch
        binutils-2.24-CVE-2014-8484.patch
        binutils-2.24-CVE-2014-8485.patch
        binutils-2.24-CVE-2014-8501.patch
        binutils-2.24-CVE-2014-8502.patch
        binutils-2.24-CVE-2014-8503.patch
        binutils-2.24-CVE-2014-8504.patch
        binutils-2.24-CVE-2014-8737.patch
        binutils-2.24-CVE-2014-8738.patch
        binutils-2.24-strings-all-default.patch)
md5sums=('e0f71a7b2ddab0f8612336ac81d9636b'
         'SKIP'
         'e68a1f221545f082baa5bd417cd077af'
         'e46bbb03abaa5db58c12247644dc4857'
         'ec59461c4febab9cfe1c1ef33326bc50'
         '125d8ce675ced84814d5f068106dbaa3'
         '6923e5279a84cb7134b07e2cf6686434'
         '4c590dc70829d8f4fc190f0fe7e3add8'
         '64efddd24d5628437cc457b468df8de3'
         '70540e2c6418a54ad88ab9c6b988d075'
         'acce1f1d28db6bdb84b430a05f331366'
         '12fb3e860203a06b5d1f4b023c06dcec'
         'd35b0ccb79d565757821b3e0206f3873'
         '99445d528110543d88b59a1ec7677592')

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # fix lto testsuite with gcc-4.9 - commits b35d44f1 and 37a6ac44
  patch -p1 -i $srcdir/binutils-2.24-lto-testsuite.patch

  # fix gold testsuite with gcc-4.9 - commit ae447ddd
  patch -p1 -i $srcdir/binutils-2.24-gold-testsuite-plugin.patch

  # fix issues with -static -fPIE -pie - commit 4199e3b8
  patch -p1 -i $srcdir/binutils-2.24-shared-pie.patch

  # CVE-2014-8484 - backport of commit bd25671c
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8484.patch

  # CVE-2014-8485 - commit 493a3386
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8485.patch
  
  # CVE-2014-8501 - commit 7e1e1988
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8501.patch

  # CVE-2014-8502 - commits bf67003b and 5a4b0ccc
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8502.patch

  # CVE-2014-8503 - commit 0102ea8c
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8503.patch

  # CVE-2014-8504 - commit 708d7d0d
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8504.patch

  # CVE-2014-8737 - commit dd9b91de
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8737.patch

  # CVE-2014-8738 - commit bb0d8671
  patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8738.patch

  # strings -a default - commit 7fac9594 (fedora backport)
  patch -p0 -i $srcdir/binutils-2.24-strings-all-default.patch

  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  [[ $CARCH == "x86_64" ]] && CONFIGFLAG="--enable-64-bit-bfd --disable-multilib"

  # ALARM: Specify build host types
  [[ $CARCH == "arm" ]] && CONFIGFLAG="--host=armv5tel-unknown-linux-gnueabi --build=armv5tel-unknown-linux-gnueabi"
  [[ $CARCH == "armv6h" ]] && CONFIGFLAG="--host=armv6l-unknown-linux-gnueabihf --build=armv6l-unknown-linux-gnueabihf"
  [[ $CARCH == "armv7h" ]] && CONFIGFLAG="--host=armv7l-unknown-linux-gnueabihf --build=armv7l-unknown-linux-gnueabihf"

  ${srcdir}/binutils-${pkgver}/configure --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://github.com/archlinuxarm/PKGBUILDs/issues \
    --enable-threads --enable-shared --with-pic \
    --enable-ld=default --enable-gold --enable-plugins \
    --disable-werror $CONFIGFLAG

  # check the host environment and makes sure all the necessary tools are available
  make configure-host

  make tooldir=/usr
}

check() {
  cd ${srcdir}/binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # ignore failures in gold testsuite...
  make -k LDFLAGS="" check || true
}

package() {
  cd ${srcdir}/binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
  
  # Remove unwanted files
  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*
  rm ${pkgdir}/usr/share/info/{configure,standards}.info

  # No shared linking to these files outside binutils
  rm -f ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
}
