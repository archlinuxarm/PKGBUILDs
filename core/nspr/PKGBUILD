# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - --enable-64bit for AArch64

pkgname=nspr
pkgver=4.38.2
pkgrel=1
pkgdesc="Netscape Portable Runtime"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSPR"
arch=(x86_64)
license=(MPL-2.0)
depends=(
  glibc
  sh
)
makedepends=(
  mercurial
  zip
)
source=(
  "hg+https://hg.mozilla.org/projects/nspr#tag=NSPR_${pkgver//./_}_RTM"
  0001-linux-prefer-GCC-provided-atomics-to-asssembly-imple.patch
  0002-configure.in-Remove-assembly-files-from-build.patch
)
b2sums=('96a15b7d2ff97e6b7ad6ea42c3e2494e504c7c9a34bd8206a246e77f9010d33bbad77699450f563773e81c4a9267e79e42dd1b275f7db767f0267c4349fc74d5'
        '50a9d787e3a5c569472a47efbde1a0ceb3f1be97b2b2f29148f010c3eca9c1f94e7045c58d49213b61e3677550bc0af01245a8daf9ca4580b94f29658e0b8e75'
        '337f76eafca696b25e7c00076837a0971f3eaed1e15f009eac34a077e5c5f1d56664db6dd4921151ed97f40149b7b922eb396ef2443bcaee72e3e9aa46fe9221')

prepare() {
  cd nspr

  # https://bugzilla.mozilla.org/show_bug.cgi?id=1496426
  # https://gitlab.archlinux.org/archlinux/packaging/packages/nspr/-/merge_requests/1
  patch -Np1 -i ../0001-linux-prefer-GCC-provided-atomics-to-asssembly-imple.patch
  patch -Np1 -i ../0002-configure.in-Remove-assembly-files-from-build.patch

  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --includedir=/usr/include/nspr
    --enable-optimize
    --disable-debug \
    $([[ $CARCH == aarch64 ]] && echo --enable-64bit)
  )

  cd nspr
  ./configure "${configure_options[@]}"
  make ${SOURCE_DATE_EPOCH:+
    SH_NOW="${SOURCE_DATE_EPOCH}000000"
    SH_DATE="$(date --utc --date="@$SOURCE_DATE_EPOCH" '+%Y-%m-%d %T')"
  }
}

package() {
  cd nspr
  make DESTDIR="$pkgdir" install
  ln -s nspr.pc "$pkgdir/usr/lib/pkgconfig/mozilla-nspr.pc"

  rm -r "$pkgdir"/usr/include/nspr/md
  rm "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties}
}

# vim:set sw=2 sts=-1 et:
