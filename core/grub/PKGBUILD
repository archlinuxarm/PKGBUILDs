# Maintainer : Christian Hesse <mail@eworm.de>
# Maintainer : Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# ALARM: Vasily Khoruzhick <anarsoul@gmail.com>
#  - Removed call to _build_grub-common_and_bios() since
#    it's not supported on aarch64
#  - Drop everything but packaging /etc/grub/default in
#    _package_grub-common_and_bios()
#  - patch to add /boot/Image to kernel path

buildarch=8

## "1" to enable IA32-EFI build in Arch x86_64, "0" to disable
_IA32_EFI_IN_ARCH_X64="1"

## "1" to enable EMU build, "0" to disable
_GRUB_EMU_BUILD="0"

[[ "${CARCH}" == 'x86_64' ]] && _EFI_ARCH='x86_64'
[[ "${CARCH}" == 'i686' ]] && _EFI_ARCH='i386'
[[ "${CARCH}" == 'aarch64' ]] && _EFI_ARCH='aarch64'

[[ "${CARCH}" == 'x86_64' ]] && _EMU_ARCH='x86_64'
[[ "${CARCH}" == 'i686' ]] && _EMU_ARCH='i386'
[[ "${CARCH}" == 'aarch64' ]] && _EMU_ARCH='aarch64'

pkgname='grub'
pkgdesc='GNU GRand Unified Bootloader (2)'
epoch=2
_pkgver=2.12
_unifont_ver='16.0.02'
#pkgver=${_pkgver/-/}
pkgver='2.12.r212.g4dc616657'
pkgrel=2
url='https://www.gnu.org/software/grub/'
arch=('x86_64')
license=('GPL-3.0-or-later')
backup=(
  etc/default/grub
  etc/grub.d/40_custom
)
install="${pkgname}.install"
options=('!makeflags')
conflicts=(
  grub-bios
  grub-common
  grub-efi-${_EFI_ARCH}
  grub-emu
  grub-legacy
)
replaces=(
  grub-common
  grub-bios
  grub-emu 
  grub-efi-${_EFI_ARCH}
)
provides=(
  grub-bios
  grub-common
  grub-efi-${_EFI_ARCH}
  grub-emu
)
makedepends=(
  autogen
  device-mapper
  freetype2
  fuse3
  gettext
  git
  help2man
  python
  rsync
  texinfo
  ttf-dejavu
  xz
)
depends=(
  device-mapper
  gettext
  sh
  xz
)
optdepends=(
  'dosfstools: For grub-mkrescue FAT FS and EFI support'
  'efibootmgr: For grub-install EFI support'
  'freetype2: For grub-mkfont usage'
  'fuse3: For grub-mount usage'
  'libisoburn: Provides xorriso for generating grub rescue iso using grub-mkrescue'
  'lzop: For grub-mkrescue LZO support'
  'mtools: For grub-mkrescue FAT FS support'
  'os-prober: To detect other OSes when generating grub.cfg in BIOS systems'
)

if [[ "${_GRUB_EMU_BUILD}" == "1" ]]; then
  makedepends+=(
  libusbx
  sdl
  )
  optdepends+=(
  'libusbx: For grub-emu USB support'
  'sdl: For grub-emu SDL support'
  )
fi

validpgpkeys=(
  'E53D497F3FA42AD8C9B4D1E835A93B74E82E4209'  # Vladimir 'phcoder' Serbinenko <phcoder@gmail.com>
  'BE5C23209ACDDACEB20DB0A28C8189F1988C2166'  # Daniel Kiper <dkiper@net-space.pl>
  '95D2E9AB8740D8046387FD151A09227B1F435A33') # Paul Hardy <unifoundry@unifoundry.com>

source=(
  "git+https://git.savannah.gnu.org/git/grub.git#tag=grub-${_pkgver}?signed"
  'git+https://git.savannah.gnu.org/git/gnulib.git'
  "https://ftp.gnu.org/gnu/unifont/unifont-${_unifont_ver}/unifont-${_unifont_ver}.bdf.gz"{,.sig}
  '0001-00_header-add-GRUB_COLOR_-variables.patch'
  '0002-10_linux-detect-archlinux-initramfs.patch'
  '0003-support-dropins-for-default-configuration.patch'
  '1001-loopback-ext.patch'
  '0004-10_linux-add-archlinuxarm-s-default-kernel-path.patch'
  'grub.default'
  'sbat.csv'
)
b2sums=('SKIP'
        'SKIP'
        '03d2c64d795e0dc904d3648ff0d74e49ca4003964cd1c784dc82e61b3d070cc35c295a3291163c3c990db61a8590df0f718fb212ea96e4a4b4734e02d005bc2c'
        'SKIP'
        '992c71790785304c28fbaf0dba21dab3e283b199509f0e7e1aa0df08126da75e15b6626c3638279ff2ecaa59b925096d7dbd67d6a53cebd0ce4326ff3719d25b'
        'b4cd9ac976a579eca19d54c0b31c8d6324525fe5a0b9f5405deb63845367ac1adaa80ece4c166dfd5304608c41aa44b4f64efe235c03f437523b993be06e06e3'
        'b8316136261563a411d30fd44b71c20c8c8c65b94fd576ac4122ac40a15c8b948547c31b17cc31757f224dd2bb79fb8936aa0d20315e988ed1e9f5b55f649044'
        'a7820bfe9bddc34af49de63222b3d2a9788367083e29db13b33120269adbfa1619ac421d8597f662f756592889f5cc5538544a17d9936d1420bd5742282c710c'
        '879f50775080fd189b7a7f0a96e72196d0be31b292cde092ad941691e42a427f5bc48a497c5ca991daa285e9dea1d913695cff667e74f96da2520a49648c1cdd'
        '5e42db2161e8f594b82005b26e590a20a0e8d32b01119bdd7b1a7f7c4b0f3360e8730a3ecdd5912a4dc7af5bd9aed1c3e780965ad6747d831b470158da19388d'
        'ce9b3904ce4bd00463c226ab1c97d3af94c6151c408bcd6483a0dd07ae4c343ee135907ad887c1d93525ec95d87409ef703ac7278698d49e99342332e483c436')

_backports=(
  # current git master for loads of security fixes
  # https://lists.gnu.org/archive/html/grub-devel/2025-02/msg00024.html
  "grub-${_pkgver}..4dc6166571645780c459dde2cdc1b001a5ec844c"
)

_reverts=(
)

_configure_options=(
  PACKAGE_VERSION="${epoch}:${pkgver}-${pkgrel}"
  FREETYPE="pkg-config freetype2"
  BUILD_FREETYPE="pkg-config freetype2"
  --enable-nls
  --enable-device-mapper
  --enable-cache-stats
  --enable-grub-mkfont
  --enable-grub-mount
  --prefix="/usr"
  --bindir="/usr/bin"
  --sbindir="/usr/bin"
  --mandir="/usr/share/man"
  --infodir="/usr/share/info"
  --datarootdir="/usr/share"
  --sysconfdir="/etc"
  --program-prefix=""
  --with-bootdir="/boot"
  --with-grubdir="grub"
  --disable-silent-rules
  --disable-werror
)

prepare() {
  cd "${srcdir}/grub/"

  echo "Apply backports..."
  local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done

  echo "Apply reverts..."
  local _c _l
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  echo "Patch to enable GRUB_COLOR_* variables in grub-mkconfig..."
  ## Based on http://lists.gnu.org/archive/html/grub-devel/2012-02/msg00021.html
  patch -Np1 -i "${srcdir}/0001-00_header-add-GRUB_COLOR_-variables.patch"

  echo "Patch to detect of Arch Linux initramfs images by grub-mkconfig..."
  patch -Np1 -i "${srcdir}/0002-10_linux-detect-archlinux-initramfs.patch"

  echo "Patch to support dropins for default configuration..."
  patch -Np1 -i "${srcdir}/0003-support-dropins-for-default-configuration.patch"

  echo "Patch to detect of Arch Linux ARM kernel images by grub-mkconfig..."
  patch -Np1 -i "${srcdir}/0004-10_linux-add-archlinuxarm-s-default-kernel-path.patch"

  echo "Patch: fs/ext2: Rework out-of-bounds read for inline and external exten"
  patch -Np1 -i "${srcdir}/1001-loopback-ext.patch"

  echo "Fix DejaVuSans.ttf location so that grub-mkfont can create *.pf2 files for starfield theme..."
  sed 's|/usr/share/fonts/dejavu|/usr/share/fonts/dejavu /usr/share/fonts/TTF|g' -i "configure.ac"

  echo "Fix mkinitcpio 'rw' FS#36275..."
  sed 's| ro | rw |g' -i "util/grub.d/10_linux.in"

  echo "Fix OS naming FS#33393..."
  sed 's|GNU/Linux|Linux|' -i "util/grub.d/10_linux.in"

  echo "Pull in latest language files..."
  ./linguas.sh

  echo "Avoid problem with unifont during compile of grub..."
  # http://savannah.gnu.org/bugs/?40330 and https://bugs.archlinux.org/task/37847
  gzip -cd "${srcdir}/unifont-${_unifont_ver}.bdf.gz" > "unifont.bdf"

  echo "Run bootstrap..."
  ./bootstrap \
    --gnulib-srcdir="${srcdir}/gnulib/" \
    --no-git

  echo "Make translations reproducible..."
  sed -i '1i /^PO-Revision-Date:/ d' po/*.sed
}

_build_grub-common_and_bios() {
  echo "Set ARCH dependent variables for bios build..."
  if [[ "${CARCH}" == 'x86_64' ]]; then
    _EFIEMU="--enable-efiemu"
  else
    _EFIEMU="--disable-efiemu"
  fi

  echo "Copy the source for building the bios part..."
  cp -r "${srcdir}/grub/" "${srcdir}/grub-bios/"
  cd "${srcdir}/grub-bios/"

  echo "Unset all compiler FLAGS for bios build..."
  unset CFLAGS
  unset CPPFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset MAKEFLAGS

  echo "Run ./configure for bios build..."
  ./configure \
    --with-platform="pc" \
    --target="i386" \
    "${_EFIEMU}" \
    --enable-boot-time \
    "${_configure_options[@]}"

  if [ ! -z "${SOURCE_DATE_EPOCH}" ]; then
    echo "Make info pages reproducible..."
    touch -d "@${SOURCE_DATE_EPOCH}" $(find -name '*.texi')
  fi

  echo "Run make for bios build..."
  make
}

_build_grub-efi() {
  echo "Copy the source for building the ${_EFI_ARCH} efi part..."
  cp -r "${srcdir}/grub/" "${srcdir}/grub-efi-${_EFI_ARCH}/"
  cd "${srcdir}/grub-efi-${_EFI_ARCH}/"

  echo "Unset all compiler FLAGS for ${_EFI_ARCH} efi build..."
  unset CFLAGS
  unset CPPFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset MAKEFLAGS

  echo "Run ./configure for ${_EFI_ARCH} efi build..."
  ./configure \
    --with-platform="efi" \
    --target="${_EFI_ARCH}" \
    --disable-efiemu \
    --enable-boot-time \
    "${_configure_options[@]}"

  echo "Run make for ${_EFI_ARCH} efi build..."
  make
}

_build_grub-emu() {
  echo "Copy the source for building the emu part..."
  cp -r "${srcdir}/grub/" "${srcdir}/grub-emu/"
  cd "${srcdir}/grub-emu/"

  echo "Unset all compiler FLAGS for emu build..."
  unset CFLAGS
  unset CPPFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset MAKEFLAGS

  echo "Run ./configure for emu build..."
  ./configure \
    --with-platform="emu" \
    --target="${_EMU_ARCH}" \
    --enable-grub-emu-usb=no \
    --enable-grub-emu-sdl=no \
    --disable-grub-emu-pci \
    "${_configure_options[@]}"

  echo "Run make for emu build..."
  make
}

build() {
  cd "${srcdir}/grub/"

  echo "Build grub ${_EFI_ARCH} efi stuff..."
  _build_grub-efi

  if [[ "${CARCH}" == "x86_64" ]] && [[ "${_IA32_EFI_IN_ARCH_X64}" == "1" ]]; then
    echo "Build grub i386 efi stuff..."
    _EFI_ARCH="i386" _build_grub-efi
  fi

  if [[ "${_GRUB_EMU_BUILD}" == "1" ]]; then
    echo "Build grub emu stuff..."
    _build_grub-emu
  fi
}

_package_grub-common_and_bios() {
  echo "Install /etc/default/grub (used by grub-mkconfig)..."
  install -D -m0644 "${srcdir}/grub.default" "${pkgdir}/etc/default/grub"
}

_package_grub-efi() {
  cd "${srcdir}/grub-efi-${_EFI_ARCH}/"

  echo "Run make install for ${_EFI_ARCH} efi build..."
  make DESTDIR="${pkgdir}/" bashcompletiondir="/usr/share/bash-completion/completions" install

  echo "Remove gdb debugging related files for ${_EFI_ARCH} efi build..."
  rm -f "${pkgdir}/usr/lib/grub/${_EFI_ARCH}-efi"/*.module || true
  rm -f "${pkgdir}/usr/lib/grub/${_EFI_ARCH}-efi"/*.image || true
  rm -f "${pkgdir}/usr/lib/grub/${_EFI_ARCH}-efi"/{kernel.exec,gdb_grub,gmodule.pl} || true

  sed -e "s/%PKGVER%/${epoch}:${pkgver}-${pkgrel}/" < "${srcdir}/sbat.csv" > "${pkgdir}/usr/share/grub/sbat.csv"
}

_package_grub-emu() {
  cd "${srcdir}/grub-emu/"

  echo "Run make install for emu build..."
  make DESTDIR="${pkgdir}/" bashcompletiondir="/usr/share/bash-completion/completions" install

  echo "Remove gdb debugging related files for emu build..."
  rm -f "${pkgdir}/usr/lib/grub/${_EMU_ARCH}-emu"/*.module || true
  rm -f "${pkgdir}/usr/lib/grub/${_EMU_ARCH}-emu"/*.image || true
  rm -f "${pkgdir}/usr/lib/grub/${_EMU_ARCH}-emu"/{kernel.exec,gdb_grub,gmodule.pl} || true
}

package() {
  cd "${srcdir}/grub/"

  echo "Package grub ${_EFI_ARCH} efi stuff..."
  _package_grub-efi

  if [[ "${CARCH}" == "x86_64" ]] && [[ "${_IA32_EFI_IN_ARCH_X64}" == "1" ]]; then
    echo "Package grub i386 efi stuff..."
    _EFI_ARCH="i386" _package_grub-efi
  fi

  if [[ "${_GRUB_EMU_BUILD}" == "1" ]]; then
    echo "Package grub emu stuff..."
    _package_grub-emu
  fi

  echo "Package grub bios stuff..."
  _package_grub-common_and_bios
}
