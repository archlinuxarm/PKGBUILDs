# $Id: PKGBUILD 91020 2010-09-21 03:26:36Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# PlugApps patch: Kevin Mihelich <kevin@plugaps.com>

pkgname=grep
pkgver=2.7
pkgrel=1
plugrel=1
pkgdesc="A string search utility"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/grep/grep.html"
groups=('base')
depends=('glibc' 'pcre' 'sh')
makedepends=('texinfo')
install=${pkgname}.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz
        grep.patch)
md5sums=('6dd9931a52501519d7779a27cf953326'
         '79d26bec3335edc85add876f297ef961')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/grep.patch
  ./configure --prefix=/usr --bindir=/bin --without-included-regex
  make
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
