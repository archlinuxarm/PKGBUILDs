# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Mantas M. <grawity@gmail.com>

# remove when bumped upstream

pkgname=ding-libs
pkgver=0.6.2
pkgrel=1
pkgdesc="\"DING is not GNU\" helper libraries for SSSD and FreeIPA"
arch=('x86_64')
url="https://github.com/SSSD/ding-libs"
license=('GPL3' 'LGPL3')
depends=('glibc')
makedepends=('doxygen')
checkdepends=('check')
# https://github.com/SSSD/ding-libs/releases/download/0.6.2/ding-libs-0.6.2.tar.gz.sha256sum
source=(https://github.com/SSSD/ding-libs/releases/download/$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('e5f07f34f5921bcb5ccccfe3751c28497879a6451cd7b395e99e24d9b5728e8d'
            'SKIP')
validpgpkeys=('930201AAB42DD1947210B7838D7326351A726211') # Alexey Tikhonov <atikhono@redhat.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make all docs
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
} 
