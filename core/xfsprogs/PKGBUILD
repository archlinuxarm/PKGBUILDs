# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - original OPTIMIZER export does not play nice with our system
#  - configure with --enable-lto=no
#  - 32-bit cflags addition to fix FTBFS with liburcu

# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=xfsprogs
pkgver=6.8.0
pkgrel=2
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=('GPL-2.0-only' 'LGPL-2.1-only')
url="https://xfs.wiki.kernel.org"
depends=('sh' 'util-linux-libs' 'device-mapper' 'libinih' 'liburcu')
optdepends=('icu: for xfs_scrub'
	    'python: for xfs_scrub_all script'
            'smtp-forwarder: for xfs_scrub_fail script')
makedepends=('git' 'icu')
options=('!makeflags')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git?signed#tag=v${pkgver}")
b2sums=('0797e57b0c6a7512233d431832e966a2a03659c2ea5f80255e13f26ea2d0f61cfecaf4603fc1636b008b77ff9c946b06fbf77a1cd23e9583c7f782d098772099')
validpgpkeys=(
  '2B8185919E8D248981869DED20AE1692E13DDEE0' # Eric Sandeen <sandeen@sandeen.net>
  '4020459E58C1A52511F5399113F703E6C11CF6F0' # Carlos Maiolino <cem@kernel.org>
)

build() {
  cd ${pkgname}-dev
  make configure
  #export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export OPTIMIZER="-O1"
  export DEBUG=-DNDEBUG
  [[ $CARCH != "aarch64" ]] && CFLAGS+=" -DUATOMIC_NO_LINK_ERROR"
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib --with-systemd-unit-dir=/usr/lib/systemd/system --enable-lto=no
  make
}

package() {
  cd ${pkgname}-dev
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root "$pkgdir"
  chgrp -R root "$pkgdir"
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
