#

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - original OPTIMIZER export does not play nice with our system
#  - configure with --enable-lto=no

pkgname=xfsprogs
pkgver=4.18.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=('LGPL')
url="http://xfs.org"
groups=('base')
depends=('sh' 'libutil-linux' 'readline')
makedepends=('git')
options=('!makeflags')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git#tag=v${pkgver}")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}-dev"
  make configure
  #export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export OPTIMIZER="-O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-readline --enable-lto=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-dev"
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
