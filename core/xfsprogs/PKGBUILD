# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - original OPTIMIZER export does not play nice with our system
#  - configure with --enable-lto=no
#  - 32-bit cflags addition to fix FTBFS with liburcu

# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=xfsprogs
pkgver=6.12.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=(
  GPL-2.0-only
  LGPL-2.1-only
)
url="https://xfs.wiki.kernel.org"
depends=(
  device-mapper
  libedit
  libinih
  liburcu
  sh
  util-linux-libs
)
optdepends=(
  'icu: for xfs_scrub'
  'python: for xfs_scrub_all script'
  'smtp-forwarder: for xfs_scrub_fail script'
)
makedepends=(
  git 
  icu
)
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git?signed#tag=v${pkgver}")
b2sums=('f34492ed498c38b68c3a30eb028d5221e8a45ac0fe0cdf721744d77a2cf03a16a70fa7d6ebd50face5e05c25fcbfc1c671b66f1761387deddbdb14af5e9d8a53')
validpgpkeys=(
  'AE1B2A9562721A6FC4307C1F46A7EA18AC33E108' #  Andrey Albershteyn <aalbersh@redhat.com>
  '2B8185919E8D248981869DED20AE1692E13DDEE0' # Eric Sandeen <sandeen@sandeen.net>
  '4020459E58C1A52511F5399113F703E6C11CF6F0' # Carlos Maiolino <cem@kernel.org>
)

build() {
  cd ${pkgname}-dev
  make configure
  [[ $CARCH != "aarch64" ]] && CFLAGS+=" -DUATOMIC_NO_LINK_ERROR"
  # Package is honoring CFLAGS; No need to use OPTIMIZER anymore.
  # However, we have to provide an empty value to avoid default
  # flags.
  export OPTIMIZER=" "
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib \
              --with-systemd-unit-dir=/usr/lib/systemd/system \
              --localstatedir=/var --enable-editline --enable-lto=no
  make
}

package() {
  cd ${pkgname}-dev
  make PKG_USER=root PKG_GROUP=root DIST_ROOT="${pkgdir}" \
       PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
}
