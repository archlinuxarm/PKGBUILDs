# $Id: PKGBUILD 142967 2011-11-19 08:45:35Z tpowa $
# Maintainer: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - original OPTIMIZER export does not play nice with our system

pkgname=xfsprogs
pkgver=4.10.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://oss.sgi.com/projects/xfs/"
groups=('base')
depends=('util-linux')
makedepends=('git')
options=('!makeflags')
source=("git://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git#tag=v${pkgver}")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}-dev"
  make configure
  #export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export OPTIMIZER="-O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-dev"
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
