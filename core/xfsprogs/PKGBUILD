# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - original OPTIMIZER export does not play nice with our system
#  - configure with --enable-lto=no
#  - 32-bit cflags addition to fix FTBFS with liburcu

# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=xfsprogs
pkgver=6.13.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=(
  GPL-2.0-only
  LGPL-2.1-only
)
url="https://xfs.wiki.kernel.org"
depends=(
  device-mapper
  libedit
  libinih
  liburcu
  sh
  util-linux-libs
)
optdepends=(
  'icu: for xfs_scrub'
  'python: for xfs_scrub_all script'
  'smtp-forwarder: for xfs_scrub_fail script'
)
makedepends=(
  git 
  icu
)
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git?signed#tag=v${pkgver}")
b2sums=('27a21cb9d926f67df3d32d97a9b51028964e8c358d8170ddc4c6b22e92c97ec524d706ca1ea3446e3185bae2dedc17ce14fd77b11680cdf3f4a4ceef00bdcdcd')
validpgpkeys=(
  'AE1B2A9562721A6FC4307C1F46A7EA18AC33E108' #  Andrey Albershteyn <aalbersh@redhat.com>
  '2B8185919E8D248981869DED20AE1692E13DDEE0' # Eric Sandeen <sandeen@sandeen.net>
  '4020459E58C1A52511F5399113F703E6C11CF6F0' # Carlos Maiolino <cem@kernel.org>
)

build() {
  cd ${pkgname}-dev
  make configure

  # Inject libicuuc to fix link error:
  # /usr/bin/ld: /tmp/ccRHx17I.ltrans1.ltrans.o: undefined reference to symbol 'uiter_setString_76'
  # /usr/bin/ld: /usr/lib64/libicuuc.so.76: error adding symbols: DSO missing from command line
  sed -r -i 's/\$\(LIBICU_LIBS\)/\0 -licuuc/' scrub/Makefile

  [[ $CARCH != "aarch64" ]] && CFLAGS+=" -DUATOMIC_NO_LINK_ERROR"
  # Package is honoring CFLAGS; No need to use OPTIMIZER anymore.
  # However, we have to provide an empty value to avoid default
  # flags.
  export OPTIMIZER=" "
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib \
              --with-systemd-unit-dir=/usr/lib/systemd/system \
              --localstatedir=/var --enable-editline --enable-lto=no
  make
}

package() {
  cd ${pkgname}-dev
  make PKG_USER=root PKG_GROUP=root DIST_ROOT="${pkgdir}" \
       PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
}
