# Maintainer: Mike Staszel <mikestaszel@plugboxlinux.org>

pkgname=pacman
pkgver=3.3.3
pkgrel=5
pkgdesc="A library-based package manager with dependency support"
arch=('arm')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'libarchive>=2.7.1' 'libfetch>=2.25' 'pacman-mirrorlist')
optdepends=('fakeroot: for makepkg usage as normal user'
            'python: for rankmirrors script usage')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf
        makepkg.conf
        vercmp.patch
        completion.patch)
md5sums=('a8cef73d68e2a4c3a46fb46c33210719'
         '8a1eea46c9af940a8812dfb4bbbd159d'
         'ce00f190714c02ef26117ecfdf5fa296'
         '84129116c61c8a7cad03a5ae5d80aee5'
         '7eddda58bf55809e6bde14b446e41a15')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
  cd $srcdir/$pkgname-$pkgver
  # avoid linking vercmp against libalpm as this might be broken during a transaction
  patch -p0 -i $srcdir/vercmp.patch || return 1
  # fix completion for .pkg.tar.xz pacakges (included in next upstream release)
  patch -p1 -i $srcdir/completion.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install || return 1

  # install Arch specific stuff
  mkdir -p $pkgdir/etc
  install -m644 $srcdir/pacman.conf $pkgdir/etc/
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/

  # install completion files
  mkdir -p $pkgdir/etc/bash_completion.d/
  install -m644 contrib/bash_completion $pkgdir/etc/bash_completion.d/pacman
  mkdir -p $pkgdir/usr/share/zsh/site-functions/
  install -m644 contrib/zsh_completion $pkgdir/usr/share/zsh/site-functions/_pacman
}
