# Maintainer: Alexander Foremny <alexanderforemny@gmail.com>
# Modified by OpenPogo

pkgname=pacman
pkgver=3.3.0
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('arm')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('libarchive>=2.7.0' 'libfetch>=2.20')
optdepends=('fakeroot: for makepkg usage as normal user'
            'python: for rankmirrors script usage')
backup=(opt/etc/pacman.conf opt/etc/makepkg.conf)
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf
        makepkg.conf)
md5sums=('945b95633cc7340efb4d4564b463c6b1'
         'f70cbef20e50d54f99eb41fad81a2f99'
         'aeba1a7e602f0b8e956dea540f309965')

build() {
  cd "$srcdir/pacman-$pkgver"
  
  ./configure --prefix=/opt --sysconfdir=/opt/etc --localstatedir=/opt/var
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # Install Arch specific stuff.
  install -Dm644 "$srcdir/pacman.conf" "$pkgdir/opt/etc/"
  install -Dm644 "$srcdir/makepkg.conf" "$pkgdir/opt/etc/"

  # install completion files
  mkdir -p "$pkgdir/opt/etc/bash_completion.d/"
  install -m644 contrib/bash_completion "$pkgdir/opt/etc/bash_completion.d/pacman"
  mkdir -p "$pkgdir/opt/share/zsh/site-functions/"
  install -m644 contrib/zsh_completion \
                "$pkgdir/opt/share/zsh/site-functions/_pacman"
}
