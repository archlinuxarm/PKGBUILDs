# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lucy <lucy@luz.lu>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

# remove when bumped upstream

pkgname=pkgconf
pkgver=1.5.3
pkgrel=1.1
pkgdesc="Package compiler and linker metadata toolkit"
url="https://github.com/pkgconf/pkgconf"
license=(custom:ISC)
arch=(x86_64)
depends=(glibc sh)
makedepends=(git)
provides=(pkg-config pkgconfig)
conflicts=(pkg-config)
replaces=(pkg-config)
groups=(base-devel)
_commit=101b99b5b59d58fe6c6dc46ae22edb049cc0bd3a  # tags/pkgconf-1.5.3
source=("git+https://github.com/pkgconf/pkgconf#commit=$_commit" platform-pkg-config.in)
sha256sums=('SKIP'
            '7c61338fbd83f9783d805c2f2f97b426977895a2f4b79e0ae5bc8e9d7996edaa')

_pcdirs=/usr/lib/pkgconfig:/usr/share/pkgconfig
_libdir=/usr/lib
_includedir=/usr/include

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^pkgconf-//;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  ./autogen.sh
}

build() {
  cd build
  ../$pkgname/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pkg-config-dir="$_pcdirs" \
    --with-system-libdir="$_libdir" \
    --with-system-includedir="$_includedir" \
    --disable-static
  make
}

package() {
  DESTDIR="$pkgdir" make -C build install

  # From https://src.fedoraproject.org/rpms/pkgconf/
  sed -e "s|@TARGET_PLATFORM@|$CHOST|g" \
      -e "s|@PKGCONF_LIBDIRS@|$_pcdirs|g" \
      -e "s|@PKGCONF_SYSLIBDIR@|$_libdir|g" \
      -e "s|@PKGCONF_SYSINCDIR@|$_includedir|g" \
      platform-pkg-config.in |
    install -D /dev/stdin "$pkgdir/usr/bin/$CHOST-pkg-config"
  ln -s $CHOST-pkg-config "$pkgdir/usr/bin/pkg-config"

  # Multilib
  if [[ $CARCH = x86_64 ]]; then
    _host32=${CHOST/x86_64/i686}
    sed -e "s|@TARGET_PLATFORM@|$_host32|g" \
        -e "s|@PKGCONF_LIBDIRS@|${_pcdirs/lib/lib32}|g" \
        -e "s|@PKGCONF_SYSLIBDIR@|${_libdir/lib/lib32}|g" \
        -e "s|@PKGCONF_SYSINCDIR@|$_includedir|g" \
        platform-pkg-config.in |
      install -D /dev/stdin "$pkgdir/usr/bin/$_host32-pkg-config"
    ln -s $_host32-pkg-config "$pkgdir/usr/bin/pkg-config-32"
  fi

  ln -s pkgconf.1 "$pkgdir/usr/share/man/man1/pkg-config.1"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 et:
