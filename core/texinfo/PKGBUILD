# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - perl patch to fix a2ps FTBFS

pkgname=texinfo
pkgver=7.0.2
pkgrel=2
pkgdesc='GNU documentation system for on-line information and printed output'
arch=(x86_64)
url='https://www.gnu.org/software/texinfo/'
license=(GPL3)
depends=(ncurses gzip perl sh)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-install.hook
        texinfo-remove.hook
        perl.patch)
sha256sums=('f211ec3261383e1a89e4555a93b9d017fe807b9c3992fb2dff4871dae6da54ad'
            'SKIP'
            '5409fc3aa8a35e8f3d901e96bc50c986f204d90ce6bface0a7d79d99b487f801'
            '913ca8aac84386399b0a83ed0f6b04b5e6322da62f5c1d7ed31e1050ed37c1a9'
            'fbd35efe4a3000612ca120ba95af555dd2244c40280b2ef78f55d786bb6131cd')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../perl.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 texinfo-{install,remove}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
