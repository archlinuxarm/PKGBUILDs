# $Id: PKGBUILD 41787 2009-06-07 11:37:55Z thomas $
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname=klibc-module-init-tools
pkgver=3.8
pkgrel=1
pkgdesc="Utilities for inserting and removing modules from the Linux kernel"
arch=(arm)
url="http://www.kernel.org"
license=('GPL')
groups=('base')
depends=('klibc' $(basename /lib/klibc-*.so .so))
source=(http://www.kernel.org/pub/linux/utils/kernel/module-init-tools/module-init-tools-$pkgver.tar.bz2)
md5sums=('470d7830fd263d29a92149da6ae9f122')

build() {
  cd $srcdir/module-init-tools-$pkgver
  CFLAGS="-DCONFIG_NO_BACKWARDS_COMPAT" CC=klcc LD=klcc ./configure --prefix=/usr/lib/klibc
  # klibc lacks getc_unlocked, so use getc instead
  sed 's|getc_unlocked|getc|g' -i *.c
  make || return 1
  INSTALL=/bin/install make DESTDIR=$pkgdir install || return 1
  rm -f $pkgdir/usr/lib/klibc/sbin/{generate-modprobe.conf,insmod.static}
  rm -rf $pkgdir/usr/lib/klibc/{share,man}
}
