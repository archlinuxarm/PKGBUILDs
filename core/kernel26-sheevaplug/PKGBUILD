# Maintainer: Dominik Schemmel <dominik.schemmel@googlemail.com>
# Package is built natively on sheevaplug
# based on the kernel on http://sheeva.with-linux.com/sheeva/
# PKGBUILD based on archlinux core/kernel26

pkgname=kernel26-sheevaplug
pkgver=2.6.31.5
pkgrel=1
_basekernel='2.6.31.5'
_localversion='-sheevaplug'
_kernver="${_basekernel}${_localversion}"
pkgdesc="The Linux Kernel and modules"
arch=('arm')
license=('GPL2')
url="http://sheeva.with-linux.com/sheeva/"
depends=('coreutils' 'module-init-tools')
makedepends=('patch' 'gcc' 'uboot-mkimage')
conflicts=('kernel26')
provides=('kernel26')
source=('kernel26-sheevaplug.install'
        'config'
        'http://sheeva.with-linux.com/sheeva/2.6.31.5/patches/UdlFb-0.2.3-2.6.30.2.patch'
        'http://sheeva.with-linux.com/sheeva/2.6.31.5/patches/cpuidle-irq.patch'
	'http://sheeva.with-linux.com/sheeva/2.6.31.5/patches/linux-2.6.29-usb-serial-evdo.patch'
	'http://sheeva.with-linux.com/sheeva/2.6.31.5/patches/mvsdio-1-2.6.30.patch'
	'http://sheeva.with-linux.com/sheeva/2.6.31.5/patches/mvsdio-2.patch'
        'http://www.kernel.org/pub/linux/kernel/v2.6/linux-2.6.31.5.tar.bz2')

md5sums=('296e8dd4a28110928b9df3df4032e110'
         'ad5d3f9f66cab0ae40656c877fcc0c34'
	 'e75754b44c800cddfb06990485da183c'
	 '4a459ff49af0d154ccfb7f5569d0c596'
	 '7bb47c46c7365ddab592024549501ea3'
	 'e655da31381a7ca7347e610912d9c610'
	 '59664af0589cba9d3e18d6d1aac59833'
         '926bff46d24e2f303e4ee92234e394d8')

build() {
  KARCH=arm
  cd ${srcdir}/linux-${_basekernel}

  # apply patches
  patch -p0 < ${srcdir}/UdlFb-0.2.3-2.6.30.2.patch
  patch -p0 < ${srcdir}/cpuidle-irq.patch
  patch -p0 < ${srcdir}/linux-2.6.29-usb-serial-evdo.patch
  patch -p0 < ${srcdir}/mvsdio-1-2.6.30.patch
  patch -p1 < ${srcdir}/mvsdio-2.patch

  # compile
  cp ${srcdir}/config .config
  make ARCH=$KARCH modules uImage

  #install files
  mkdir -p ${pkgdir}/boot
  install -D -m644 arch/$KARCH/boot/uImage ${pkgdir}/boot/uImage${_localversion}
  install -D -m644 System.map ${pkgdir}/boot/System.map${_localversion}
  make INSTALL_MOD_PATH=${pkgdir} ARCH=$KARCH modules_install
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}
  make INSTALL_HDR_PATH=${pkgdir}/usr/src/linux-${_kernver} ARCH=$KARCH headers_install
  
  install -D -m644 Makefile ${pkgdir}/usr/src/linux-${_kernver}/Makefile
  install -D -m644 kernel/Makefile ${pkgdir}/usr/src/linux-${_kernver}/kernel/Makefile
  install -D -m644 .config ${pkgdir}/usr/src/linux-${_kernver}/.config

  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include
  for i in acpi asm-{generic,$KARCH} config crypto drm keys linux math-emu media mtd net pcmcia rdma rxrpc scsi sound trace video xen; do
    cp -a include/$i ${pkgdir}/usr/src/linux-${_kernver}/include/
  done

  # copy arch includes for external modules
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH
  cp -a arch/$KARCH/include ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/

  # copy files necessary for later builds, like nvidia and vmware
  cp -a Module.symvers ${pkgdir}/usr/src/linux-${_kernver}
  cp -a scripts ${pkgdir}/usr/src/linux-${_kernver}
  # fix permissions on scripts dir
  chmod og-w -R ${pkgdir}/usr/src/linux-${_kernver}/scripts
  #mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/.tmp_versions

  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel
  cp -a arch/$KARCH/Makefile ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  cp arch/$KARCH/kernel/asm-offsets.s ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel/

  # add headers for lirc package
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video
  cp drivers/media/video/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/
  for i in bt8xx cpia2 cx25840 cx88 em28xx et61x251 pwc saa7134 sn9c102 usbvideo zc0301; do
   mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
   cp -a drivers/media/video/$i/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
  done
  # add docbook makefile
  install -D -m644 Documentation/DocBook/Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/Documentation/DocBook/Makefile
  # add dm headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  cp drivers/md/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  # add inotify.h
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/linux
  cp include/linux/inotify.h ${pkgdir}/usr/src/linux-${_kernver}/include/linux/
  # add wireless headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  cp net/mac80211/*.h ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/9912
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core
  cp drivers/media/dvb/dvb-core/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/11194
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  cp include/config/dvb/*.h ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  # add dvb headers for http://mcentral.de/hg/~mrec/em28xx-new
  # in reference to:
  # http://bugs.archlinux.org/task/13146
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/dvb/frontends/lgdt330x.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/video/msp3400-driver.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  # add xfs and shmem for aufs building
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/mm
  cp fs/xfs/xfs_sb.h ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs/xfs_sb.h
  # add headers vor virtualbox
  # in reference to:
  # http://bugs.archlinux.org/task/14568
  cp -a include/drm $pkgdir/usr/src/linux-${_kernver}/include/
  # add headers for broadcom wl
  # in reference to:
  # http://bugs.archlinux.org/task/14568
  cp -a include/trace $pkgdir/usr/src/linux-${_kernver}/include/
  # add vmlinux
  cp vmlinux ${pkgdir}/usr/src/linux-${_kernver}
  # copy in Kconfig files
  for i in `find . -name "Kconfig*"`; do 
    mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/`echo $i | sed 's|/Kconfig.*||'`
    cp $i ${pkgdir}/usr/src/linux-${_kernver}/$i
  done

  cd ${pkgdir}/usr/src/linux-${_kernver}/include && ln -s asm-$KARCH asm
  # add header for aufs2-util
  cp -a ${srcdir}/linux-$_basekernel/include/asm-generic/bitsperlong.h ${pkgdir}/usr/src/linux-${_kernver}/include/asm/

  chown -R root.root ${pkgdir}/usr/src/linux-${_kernver}
  find ${pkgdir}/usr/src/linux-${_kernver} -type d -exec chmod 755 {} \;
  cd ${pkgdir}/lib/modules/${_kernver} && \
    (rm -f source build; ln -sf ../../../usr/src/linux-${_kernver} build)

  # set correct depmod command for install
  sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" $startdir/kernel26${_localversion}.install

  # remove unneeded architectures
  rm -rf ${pkgdir}/usr/src/linux-${_kernver}/arch/{alpha,avr32,blackfin,cris,frv,h8300,ia64,m32r,m68k,m68knommu,mips,microblaze,mn10300,parisc,powerpc,s390,sh,sparc,um,x86,xtensa}
  # remove the firmware
  rm -rf ${pkgdir}/lib/firmware
}
