# Maintainer: Mike Staszel <mikestaszel@plugapps.com>

pkgname=filesystem
pkgver=2011.1
pkgrel=1
pkgdesc="Base filesystem"
arch=('arm')
license=('GPL')
url="http://plugboxlinux.org"
groups=('base')
install=filesystem.install
depends=('coreutils' 'plugapps-scripts')
backup=(etc/fstab etc/crypttab etc/group etc/hosts etc/ld.so.conf etc/passwd
        etc/shadow etc/gshadow etc/resolv.conf etc/motd etc/nsswitch.conf
        etc/shells etc/host.conf etc/securetty etc/profile etc/issue)
source=(group issue nsswitch.conf securetty host.conf ld.so.conf
        passwd shadow fstab crypttab hosts motd resolv.conf shells
        gshadow	services protocols profile modprobe.d.usb-load-ehci-first)
md5sums=('78a40317b8ea1d6810835fb12501fec7'
         '336bdaeeff0ccc84a6b84f1d8c4e2127'
         '13753e4e0964f3652b0cc60a28528bdf'
         '2f890aefb8eee68b5b9d91f4777d0cd9'
         'f28150d4c0b22a017be51b9f7f9977ed'
         '6e488ffecc8ba142c0cf7e2d7aeb832e'
         '8a9042a2cedf6b6b47eb8973f14289cb'
         '46bc1c4b941f19c1505102bb5bbddb8c'
         'ef40305da5803ca69d22e428ffc2ab9b'
         'e5d8323a4dbee7a6d0d2a19cbf4b819f'
         '81b3cb42a6ddabc2ed2310511ee9c859'
         'd41d8cd98f00b204e9800998ecf8427e'
         '6f48288b6fcaf0065fcb7b0e525413e0'
         '40dac0de4c6b99c8ca97effbd7527c84'
         '5340b9287b71ffe9d4d99510bffe933f'
         'f436d2e0ed02b7b73bd10c6693e95ac3'
         '65d78e621ed69eed69f854c3ee2e5942'
         'f7ba22b78c5d2b53f86227aa2447e7ea'
         '8098ffd9fbf890468d3198277596b85a')

build()
{
  cd $pkgdir
  mkdir -p bin boot dev etc home lib mnt proc root sbin tmp usr var opt srv sys
  chmod 555 proc
  mkdir -p usr/{bin,include,lib,sbin,share/misc,src}
  mkdir -p media
  touch media/.empty

  mkdir -p usr/share/man/man{1,2,3,4,5,6,7,8}
  ln -s man3 $pkgdir/usr/share/man/man3x

  # fhs compliance
  mkdir -p usr/local/{bin,games,include,lib,man,sbin,share,src}
  ln -s ../man $startdir/pkg/usr/local/share/man
  mkdir -p var/{cache/man,local,lock,opt,run,spool/mail,tmp,games}
  chmod 1777 var/lock
  mkdir -p var/log/old
  mkdir -p etc/{skel,profile.d}
  mkdir -p lib/modules
  mkdir -p var/lib/misc
  (cd $startdir/pkg/var; ln -s spool/mail mail)

  # vsftpd won't run with write perms on /srv/ftp
  mkdir -p srv/ftp
  chown root.ftp srv/ftp
  chmod 555 srv/ftp

  install -d -o root -g root -m 755 srv/http

  chmod 1777 var/spool/mail tmp var/tmp
  chmod 0750 root

  # Allow setgid games to write scores:
  chmod 775 ${startdir}/pkg/var/games
  chown root:50 ${startdir}/pkg/var/games

  cd $srcdir
  cp fstab crypttab group host.conf hosts issue ld.so.conf motd nsswitch.conf \
    passwd protocols resolv.conf securetty services shadow shells profile \
    $pkgdir/etc/
  install -m 600 $startdir/src/gshadow $pkgdir/etc/gshadow
  chmod 600 $startdir/pkg/etc/shadow
  chmod 600 $startdir/pkg/etc/crypttab

  # re-add /etc/arch-release, some software uses it
  # to check whether arch is running  
  touch $pkgdir/etc/arch-release
  
  # Add /etc/modprobe.d/
  install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first \
    ${pkgdir}/etc/modprobe.d/usb-load-ehci-first.conf

  # Add ld.so.conf.d
  mkdir -p $pkgdir/etc/ld.so.conf.d

  # Prevent pacman from removing directory (FS#16886)
  mkdir ${pkgdir}/var/empty
  touch ${pkgdir}/var/empty/.keep
}
