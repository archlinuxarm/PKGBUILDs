# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use linux-armv4 target for our architectures
#  - cryptodev-enabled version
#
#	moonman <moonman [dot] ca [at] gmail [dot] com>
#  - get cryptodev.h from cryptodev tarball instead of the kernel headers
#  - patches from cryptodev developers for openssl

buildarch=6

pkgname=openssl-cryptodev
_pkgname=openssl
_ver=1.0.1h
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('arm' 'armv7h')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('perl')
optdepends=('ca-certificates')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
conflicts=('openssl')
provides=("openssl=${pkgver}")
_cryptover=1.6

source=("https://www.openssl.org/source/${_pkgname}-${_ver}.tar.gz"
        'no-rpath.patch'
        'ca-dir.patch'
        "http://download.gna.org/cryptodev-linux/cryptodev-linux-${_cryptover}.tar.gz"
	"https://github.com/openssl/openssl/commit/be2c4d9bd9e81030c547a34216ae2d8e5c888190.patch"
	"https://github.com/openssl/openssl/commit/6ecbc2bb62835a401ad6efe240d469a23b21755b.patch"
        '0001-cryptodev-Fix-issue-with-signature-generation.patch'
        '0002-cryptodev-allow-copying-EVP-contexts.patch')

md5sums=('8d6d684a9430d5cc98a62a5d8fbda8cf'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90'
         'eade38998313c25fd7934719cdf8a2ea'
         'a1761273d79bc5c62eb3bd4cb91f66b4'
         '310a63a4ea3e948eee54c953d49a6bf1'
         '440f706c63735f32274e13325557a331'
         'e8ec495c49ae2494acbfb47d98c363b6')

prepare() {
        cd $srcdir/${_pkgname}-$_ver

        # remove rpath: http://bugs.archlinux.org/task/14367
        patch -p0 -i $srcdir/no-rpath.patch
        # set ca dir to /etc/ssl by default
        patch -p0 -i $srcdir/ca-dir.patch

	msg "Bring eng_cryptodev.c up-to-date for the following 2 patches"
	patch -Np1 -i $srcdir/be2c4d9bd9e81030c547a34216ae2d8e5c888190.patch
	patch -Np1 -i $srcdir/6ecbc2bb62835a401ad6efe240d469a23b21755b.patch

        msg2 "Patch eng_cryptodev.c"
        patch -p1 -i $srcdir/0001-cryptodev-Fix-issue-with-signature-generation.patch
        patch -p1 -i $srcdir/0002-cryptodev-allow-copying-EVP-contexts.patch

        # Copy the header file
        cp -u ${srcdir}/cryptodev-linux-${_cryptover}/crypto/cryptodev.h ${srcdir}/openssl-${_ver}/crypto/

}

build() {
	cd $srcdir/${_pkgname}-$_ver

	if [ "${CARCH}" == 'x86_64' ]; then
		openssltarget='linux-x86_64'
	elif [ "${CARCH}" == 'i686' ]; then
		openssltarget='linux-elf'
	elif [ "${CARCH}" == 'arm' -o "${CARCH}" == 'armv7h' ]; then
		openssltarget='linux-armv4'
	fi

	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
	./Configure --prefix=/usr --openssldir=/etc/ssl --libdir=lib \
		-DHAVE_CRYPTODEV -DUSE_CRYPTODEV_DIGESTS -DHASH_MAX_LEN=64 \
		shared threads zlib \
		"${openssltarget}" \
		"-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

	make depend
	make
}

check() {
	cd $srcdir/$_pkgname-$_ver
	# the test fails due to missing write permissions in /etc/ssl
	# revert this patch for make test
	patch -p0 -R -i $srcdir/ca-dir.patch
	make test
	patch -p0 -i $srcdir/ca-dir.patch
}

package() {
	depends=('cryptodev_friendly')
	cd $srcdir/$_pkgname-$_ver
	make INSTALL_PREFIX=$pkgdir MANDIR=/usr/share/man MANSUFFIX=ssl install
	install -D -m644 LICENSE $pkgdir/usr/share/licenses/$_pkgname/LICENSE
}
