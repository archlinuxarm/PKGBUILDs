# Maintainer: Alexander Foremny <alexanderforemny@gmail.com>

pkgname=pcre
pkgver=7.9
pkgrel=3
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('arm')
url="http://pcre.sourceforge.net"
license=('BSD')
groups=('base')
depends=('gcc-libs')
options=('!libtool' '!makeflags')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b6a9669d1863423f01ea46cdf00f93dc')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
      --enable-utf8 --enable-unicode-properties || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # grep uses pcre, so we need the libs in /lib
  mkdir -p ${pkgdir}/lib
  mv ${pkgdir}/usr/lib/libpcre.so.* ${pkgdir}/lib/ || return 1
  ln -sf ../../lib/libpcre.so.0 ${pkgdir}/usr/lib/libpcre.so || return 1
  install -D -m644 LICENCE \
                   ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
