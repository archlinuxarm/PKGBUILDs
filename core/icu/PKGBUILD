# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Art Gramlich <art@gramlich-net.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix icudata library linking on ARM

pkgname=icu
pkgver=59.1
pkgrel=2
pkgdesc="International Components for Unicode library"
arch=(i686 x86_64)
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('gcc-libs' 'sh')
#makedepends=('clang')
# no https available
source=(#http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver/./_}-src.tgz
	http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
	icudata-stdlibs.patch)
# upstream offers md5sum checks, only asc file for md5sum check
sha256sums=('7132fdaf9379429d004005217f10e00b7d2319d0fea22bdfddef8991c45b75fe'
            '13444e40ec8da75a3c4140448b25bdf51887a2691c9afdb0c63134ddd33d915b')

prepare() {
  cd icu/source
  sed -i 's/xlocale/locale/' i18n/digitlst.cpp
  patch -p2 -i ${srcdir}/icudata-stdlibs.patch
}

build() {
  cd icu/source
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--mandir=/usr/share/man \
	--sbindir=/usr/bin
  make
}

check() {
  cd icu/source
  # some tests are broken when crossbuilding i686
  if [ "$CARCH" = "x86_64" ]; then
    make -k check
	else
    make -k check || /bin/true
  fi
}

package() {
  cd icu/source
  make -j1 DESTDIR=${pkgdir} install

  # Install license
  install -Dm644 ${srcdir}/icu/license.html ${pkgdir}/usr/share/licenses/icu/license.html
}
