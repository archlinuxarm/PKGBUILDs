# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable NEON and ARMv8 crypto support on !AArch64

pkgname=libgcrypt
pkgver=1.9.0
pkgrel=2
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=(x86_64)
url="https://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error')
options=('!emptydirs')
# https://www.gnupg.org/download/integrity_check.html
source=(https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        libgcrypt-1.9.0-fix-ed25519.patch)
sha1sums=('459383a8b6200673cfc31f7b265c4961c0850031'
          'SKIP'
          '7721be79a4de70be2def86f3e97416c380acd9cb')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # "Werner Koch (dist signing 2020)"

prepare() {
  cd "${pkgname}"-${pkgver}

  # fix gpg-agent crash using Ed25519 keys - FS#69389
  # https://lists.gnupg.org/pipermail/gcrypt-devel/2021-January/005076.html
  patch -Np1 -i ../libgcrypt-1.9.0-fix-ed25519.patch

  # tests fail due to systemd+libseccomp preventing memory syscalls when building in chroots
  #  t-secmem: line 176: gcry_control (GCRYCTL_INIT_SECMEM, pool_size, 0) failed: General error
  #  FAIL: t-secmem
  #  t-sexp: line 1174: gcry_control (GCRYCTL_INIT_SECMEM, 16384, 0) failed: General error
  #  FAIL: t-sexp
  sed -i "s:t-secmem::" tests/Makefile.am
  sed -i "s:t-sexp::" tests/Makefile.am
  autoreconf -vfi
}

build() {
  cd "${pkgname}"-${pkgver}

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon-support --disable-arm-crypto-support --disable-asm"

  ./configure --prefix=/usr \
	--disable-static \
	--disable-padlock-support \
	$CONFIG

  make
}

check() {
  cd "${pkgname}"-${pkgver}
  make check
}

package() {
  cd "${pkgname}"-${pkgver}
  make DESTDIR="${pkgdir}" install
}
