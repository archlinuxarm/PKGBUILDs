# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - noautobuild, !distcc
#  - removed multilib stuff
#  - patch for armv7h tests

noautobuild=1

pkgname=libtool
pkgver=2.5.2+r1+g2fe3a3e2
_commit=2fe3a3e2a954adcbfb1139d780bec47b85ef6077
pkgrel=1
_gccver=14.2.1
pkgdesc='A generic library support script'
arch=(x86_64)
url='https://www.gnu.org/software/libtool'
license=('LGPL-2.0-or-later WITH Libtool-exception')
depends=(sh tar glibc)
makedepends=("gcc>=$_gccver" git help2man)
checkdepends=(gcc-fortran)
provides=("libltdl=$pkgver")
conflicts=(libltdl)
replaces=(libltdl)
options=(!distcc)
source=(
  git+https://git.savannah.gnu.org/git/libtool.git#commit=$_commit
  git+https://git.savannah.gnu.org/git/gnulib.git
  gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git
  disable-lto-link-order2.patch
  armv7h-nopic.patch
)
b2sums=('191bd0dde07c5dc39c7e1cc8ea7169f2a37ad89ac83b549c5ef5437395e9121052c0a32ac43b6876bd4c978e742a326d6c33e8d930674ec109caa7fe5335cc1d'
        'SKIP'
        'SKIP'
        '9d1e0460b9ef56ba33ac498814b409d1b1d7c3e8ed41a3aed2a86d86341ed7051ca88a5adfa92bd87da968460514230058c3d490b58537d95722e68d7d1687ff'
        '4a65a2525c8709faf7fe39dfada893e3a21d021f83c25b0798fe0584386c25755877e8b7c220fee6f252e4f2a42b47e4ba3bec36297d01e654b040232b0a711b')

pkgver() {
  cd libtool
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libtool

  # test 67 is broken with lto
  # this patch removes the -flto flag for this very test
  # adapt when -ffat-lto-objects is enabled by Arch
  patch -Np1 -i "${srcdir}"/disable-lto-link-order2.patch

  git submodule init
  git config --local submodule.gnulib.url "${srcdir}"/gnulib
  git config --local submodule.gl-mod/bootstrap.url "${srcdir}"/gnulib-bootstrap
  git -c protocol.file.allow=always submodule update

  patch -p1 -i ../armv7h-nopic.patch
  ./bootstrap
}

build() {
  cd libtool
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make
}

check() {
  cd libtool
  make check gl_public_submodule_commit=
}

package() {
  cd libtool
  make DESTDIR="$pkgdir" install
}
