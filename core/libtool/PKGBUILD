# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - noautobuild, !distcc
#  - removed multilib and lib32 stuff
#  - patch for armv7h tests

noautobuild=1

pkgbase=libtool
pkgname=(libtool)
pkgver=2.6.0
_commit=e40fdc22cf727fb885f9df7d9affa827e3253d1c
pkgrel=3
_gccver=15.2.1
pkgdesc='A generic library support script'
arch=(x86_64)
url='https://www.gnu.org/software/libtool'
license=('LGPL-2.0-or-later WITH Libtool-exception')
depends=(
  sh
  tar
)
makedepends=(
  "gcc>=$_gccver"
  git
  glibc
  help2man
)
checkdepends=(
  gcc-fortran
)
source=(
  git+https://git.savannah.gnu.org/git/libtool.git#commit=$_commit
  git+https://git.savannah.gnu.org/git/gnulib.git
  gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git
  armv7h-nopic.patch
)
b2sums=('944513327da07374709975c44ad8a80e953727ec11a68bdf6dd3a376acb02339000a0d5982011db9eeb863be0362991cbab1e2fe6c56d7710c1d527ea40bbcde'
        'SKIP'
        'SKIP'
        'ee0607b4f1b8bbf314c0665cffc74d05473672e40189e9b5734d9c42be86d0557161faa58fb2cd500ff2260e7a0cb8ce42df1165eb5798b078f990072e1d6420')

pkgver() {
  cd libtool
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libtool

  git submodule init
  git config --local submodule.gnulib.url "${srcdir}"/gnulib
  git config --local submodule.gl-mod/bootstrap.url "${srcdir}"/gnulib-bootstrap
  git -c protocol.file.allow=always submodule update

  patch -p1 -i ../armv7h-nopic.patch
  ./bootstrap
}

build() {
  cd libtool
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make
}

check() {
  cd libtool
  make check gl_public_submodule_commit=
}

package_libtool() {
  depends+=(
    glibc
  )
  provides=(
    "libltdl=$pkgver"
    "libtool-multilib=$pkgver"
  )
  conflicts=(
    libltdl
    libtool-multilib
  )
  replaces=(
    libltdl
    libtool-multilib
  )

  cd libtool
  make DESTDIR="$pkgdir" install
}
