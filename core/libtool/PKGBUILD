# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - noautobuild, !distcc
#  - removed multilib stuff
#  - patch for armv7h tests

noautobuild=1

pkgname=libtool
pkgver=2.4.6+40+g6ca5e224
pkgrel=1
_gccver=7.2.0
pkgdesc='A generic library support script'
arch=(i686 x86_64)
url='http://www.gnu.org/software/libtool"'
license=(GPL)
groups=(base-devel)
depends=(sh tar glibc)
makedepends=("gcc=$_gccver" git help2man)
provides=("libltdl=$pkgver")
conflicts=('libltdl')
replaces=('libltdl')
options=('!distcc')
_commit=6ca5e224bc7bcc114a9ba2cf5dcf0fbf0ec40c9f
source=(git+https://git.savannah.gnu.org/git/libtool.git#commit=$_commit
        armv7h-nopic.patch)
md5sums=('SKIP'
         '8a1fdc77814fa5db3fc4d6a888d0cd89')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
  cd $pkgname
  patch -p1 -i ../armv7h-nopic.patch
  ./bootstrap
}

build() {
  cd $pkgname
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
