# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on valgrind

pkgname=libsecret
pkgver=0.20.3
pkgrel=1
pkgdesc="Library for storing and retrieving passwords and other secrets"
url="https://wiki.gnome.org/Projects/Libsecret"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libgcrypt)
checkdepends=(python-dbus dbus-glib python-gobject gjs)
makedepends=(docbook-xsl gobject-introspection vala git gtk-doc meson)
optdepends=('org.freedesktop.secrets: secret storage backend')
provides=(libsecret-1.so)
_commit=fb456a3853a080996f044496b11f3001af4a2659  # tags/0.20.3^0
source=("git+https://gitlab.gnome.org/GNOME/libsecret.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
