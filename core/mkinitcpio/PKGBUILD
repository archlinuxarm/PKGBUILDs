# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=40
pkgrel=3
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL-2.0-only')
depends=('awk' 'mkinitcpio-busybox' 'kmod' 'util-linux' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'gzip' 'filesystem' 'zstd' 'systemd')
checkdepends=('bats' 'bats-assert' 'lzop')
makedepends=('meson')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS'
            'systemd-ukify: alternative UKI generator')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        '0001-trigger.patch'
        '0002-systemd-deps.patch'
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch')
sha512sums=('f74a1cee5ea24b8f57d84952e92048548b6cba2138c19d9a05e76051f4765994cd5b1e41363d90b3a9718e0844268cc4046cd02dcecb32e38948ba90ecceb17f'
            'SKIP'
            'b21e3961294e80bedd89a7e332ab11fc3b83eebfaf58d8f658e30f7d9caf2f84f4934224173c70f111932de8538fa327f5f6bfe9576b11bcbaf84d2d5ad8e85d'
            '203fd0dacf9d1792921744047fe6089ba898f16ff35fe50569b886d1765b796acd31b2dccffa68c80701edc30b75ebb9d761c1549e0b7e8c0e49d0a4600b8da8'
            '6ad59b4c380c77c9711be072043ef1683763589ff40bd40b1714541da88d364178eeed184f626f6cc2a2ee35b453a54f352f0c8eab1f13ebe20f51ccac5925c7'
            '8e02b47bf0526cff336a080d12a073005ff1a3edb6a7e510d9cb9b8192915477f419c168755c039af62e371cf6f573f36638b8bd818e3f68e395165cb794d13d'
            '82301ea1e8f0d7d27359e68cc01db674cd4e0cf790b0eb50e949dc3bea039d00881903c7337315e57421a5884f91a9e2e8b13fcdee57cab3f30b252ddcd2910e')
b2sums=('c683e795e0d058567d86905e0fe14086a675e0156d11f965cc335da9361ce1809659906b0d263c2bb368f4049fc94715cfb8981414d57cfd3f877816a75ac14d'
        'SKIP'
        '3b8e08d56e209ad11827d65595ab245bb680e72fb81139ba946e7610d16214c2a9022f1a1794e6797ef07fb0a43c5239167729225daf89396a8920f39f75e34b'
        '2d75ce180e68b3254d29c2bf5a9756d7adb09a88fe6ab640bc1ba9ee1966a118ba6109134604b4b821df711b13c465cbb7b0f595603280f0ff94b1760efd7e67'
        '7b63fb19330a8cbf475e19a922f2ca80050ba6ea0a25748e8f67d7a06ec7461f6dcc69556a525ba4612f68e20c354c73990d17faa10ae04cc1e192cead82a270'
        'dcd9b99ef988f9e35b1e23cd76b114092f5730b957d030d0ead84a95ff248a7e67c04f32272e0550b995801e731340aeaa758bcb53b2840f8988cc5e349a3ad4'
        'bd051bbc00300e1cfaafa129943a96bf3cf0ab36edc3ab127fb2911a030b20331e224a2c682425afec111a4e51e2f2afa28925a6fdaa61b0a33f1b7b5c32fdf8')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 < ../0001-trigger.patch
  patch -Np1 < ../0002-systemd-deps.patch
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
}

build(){
  cd $pkgname-$pkgver
  arch-meson \
    -Dsystemd_hooks=true \
    ../build
  meson compile -C ../build
}

check() {
  cd $pkgname-$pkgver
  meson test -C ../build
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" meson install -C ../build
}
