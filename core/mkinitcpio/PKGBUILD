# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARMM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - backport patch to fix finding modules/built-ins

pkgname=mkinitcpio
pkgver=23
pkgrel=2.1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="https://projects.archlinux.org/mkinitcpio.git/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive'
         'coreutils' 'bash' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-make-ldd-parsing-compatible-with-upstream-glibc-chan.patch'
        '0002-avoid-erroring-when-a-builtin-module-can-t-be-found.patch')
install=mkinitcpio.install
sha256sums=('80f12a07f0dceef81dfe87200f099bd2149e0990391dda6defebaa5697f8a35a'
            'SKIP'
            '2cd9cfc5a88d32e03be12c788f4ee892405e900e1ba00ec95b8ae00a84e9df14'
            'f3d58896195012a8def7a7651a6bffb49968438fe92d10bae891450c7091cbcf')
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53'   # Dave Reisner
              '86CFFCA918CF3AF47147588051E8B148A9999C34')  # Evangelos Foutras

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../0001-make-ldd-parsing-compatible-with-upstream-glibc-chan.patch
  patch -p1 -i ../0002-avoid-erroring-when-a-builtin-module-can-t-be-found.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
