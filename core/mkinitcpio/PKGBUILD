# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=38
pkgrel=4
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
conflicts=(
  'systemd<255.4-2'
  'cryptsetup<2.7.0-3'
  'mdadm<4.3-2'
  'lvm2<2.03.23-3'
)
checkdepends=('bats' 'bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        "$pkgname-fix-path.patch::https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio/-/commit/aced07e06b9b274439eeb90e235edf42780b4a7a.patch"
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch')
install=mkinitcpio.install
sha512sums=('ad1a4895e5cc3a01637f71d96ddb79d7f45708ec7305ffdb874403a1eb3c1743d121f28d93273b91792298eb21bcc0c5d9ef1ab3a3773083d60da5bdaee59d6e'
            'SKIP'
            'b1387deab0199fdb4e4a417b009467cb6f95b2a3f8074858f8a85b46492ebaf420ac028f13d81acb5a675382cccb896cac62f42e26edd82dc1c3a7f48d4d822b'
            '52f9913b8c297919db34844439df0799d8699aa75f9d2adbdaf2bb3e68eee54ec9d27946ecd9e2053fef4057ae1859dedc7d99d8da7110b31ff9ac6970e970c1'
            '7c0ea267567c23c37dc5b5aac587e64885ff36ec9818a250687b40ad4903ea74d50b033d1b1a1dd46f968cab53f70f3da96af171d8f2fdf1aacaca328fbd4e2d'
            '674729df541f852153fb634b391df2b2af037efc9f2561c9ed1c211ce8b05e21f30f603a44c552283b449534b3f2ab95150fc0adc088069c86f91dfede5f7f3c')
b2sums=('4bc50da7196a69dc0ab7e7de345684baebbb655f9a07def9ac36a7f1c9aec752cf41c62134d6bbf240d8f49c6492a211f152bab062ec09457791d7ab030f1bc5'
        'SKIP'
        '4530828b559c08aa14f3663b8a078c71bb739adcbd1dfbf21947cfe837ec51fc8ed7e7586c53a4e0ee4b7d86784acab5edf916c387e7385a78fe98c825177fa9'
        '3c8ca4927c9eb39c8923e1000cf301605e2ded6d18d5d551941fdd8fc2c4a4b24ceff6593cf7afa02391f8559fafce5c80cfffd854612323905a41c90f55bb43'
        'c089ac622e6550c0ad9dc361c552e524b547f1124c127e8f32d7bd25ad45be7cbfe6afe1e0152d104f7c9c6a7e0d54f850b90b579bee33f9466a71c4cd40fa22'
        'dc5cd0fee2dc44cc76000e4875672b4e8b6cd53a09e7e98df2f648a8706dd276a06836bbe74903ad16d3633f104ac33a641d08e15a035cb379d92cb0fd0f84f3')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 < "$srcdir/mkinitcpio-fix-path.patch"
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
