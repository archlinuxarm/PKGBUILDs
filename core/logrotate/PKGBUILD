# $Id: PKGBUILD 35410 2009-04-13 11:02:40Z allan $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=logrotate
pkgver=3.7.8
pkgrel=1
pkgdesc="Rotates system logs automatically"
url='http://cvs.fedora.redhat.com'
arch=('i686' 'x86_64')
license=('GPL')
groups=('base')
depends=('popt' 'cron' 'gzip')
backup=('etc/logrotate.conf')
#WTF are you doing, Fedora?
_pkgmd5=b3589bea6d8d5afc8a84134fddaae973
source=( http://cvs.fedora.redhat.com/repo/pkgs/$pkgname/$pkgname-$pkgver.tar.gz/$_pkgmd5/$pkgname-$pkgver.tar.gz
        logrotate.conf logrotate.cron.daily)
md5sums=($_pkgmd5
         '462a5f364717461537eb2ae6394ad23e'
         '8e23d5d4cc29b1e055b24df87e355cdc')

build() {
  cd $srcdir/${pkgname}-${pkgver}

  sed -i 's|#define DEFAULT_MAIL_COMMAND .*|#define DEFAULT_MAIL_COMMAND "/usr/bin/mail"|'\
    config.h
  sed -i "s|CFLAGS = -Wall|CFLAGS = -Wall $CFLAGS|" Makefile
  sed -i 's|$(BASEDIR)/man|$(BASEDIR)/share/man|' Makefile
  make || return 1
  make PREFIX=$pkgdir install
  install -Dm644 $srcdir/logrotate.conf $pkgdir/etc/logrotate.conf
  install -Dm744 $srcdir/logrotate.cron.daily $pkgdir/etc/cron.daily/logrotate
}
