# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build aarch64 with -fPIC

pkgname=swi-prolog
pkgver=7.4.1
pkgrel=1
pkgdesc='Prolog environment'
arch=('x86_64' 'i686')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
depends=('gmp' 'readline' 'openssl' 'libarchive')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library'
            'libjpeg: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft: for using the pl2xpce library')

# no https with valid certificates available
source=("http://www.swi-prolog.org/download/stable/src/swipl-${pkgver/_/-}.tar.gz")
sha256sums=('891e314e8f5d856ef71d8bbce5d255a18b0c8f227628748bb0e1e19473273cc1')

build() {
  [[ $CARCH == "aarch64" ]] && CPPFLAGS+=" -fPIC"

  cd "swipl-${pkgver/_/-}"
  ./configure --prefix=/usr --with-world

  cd src
  ./configure --enable-readline --prefix=/usr

  make -C ..
}

check() {
  make -C "swipl-${pkgver/_/-}" check || true
}

package() {
  make -C "swipl-${pkgver/_/-}" DESTDIR="$pkgdir" install

  # Fix for FS#20873
  chmod +x "$pkgdir/usr/lib/swipl-${pkgver/_/-}/library/dialect/sicstus/swipl-lfr.pl"
}

# getver: swi-prolog.org/download/stable
# vim:set ts=2 sw=2 et:
