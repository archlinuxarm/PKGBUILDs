# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: drakkan <nicola.murino@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - compiler flags adjustment to build correctly
#  - --cpu in configure set to correct architectures, autodetect fails

pkgname=botan1.10
pkgver=1.10.17
pkgrel=1
pkgdesc='Crypto library written in C++ - old stable branch'
license=('BSD')
arch=('x86_64')
url='http://botan.randombit.net/'
depends=('gcc-libs' 'sh')
makedepends=('python2')
source=("http://botan.randombit.net/releases/Botan-${pkgver}.tgz")
sha512sums=('a47cab3af113652247c8efc8b0f043eb62175eaa8554833d5fc3016ea94dbdd8aa722ab9b5226cc5f133afbcc088d54362111630eaa4594812c39925cc3c8649')

build() {
  cd "Botan-$pkgver"

  if [[ $CARCH == 'aarch64' ]]; then
    CPU='aarch64'
  elif [[ $CARCH == 'armv7h' ]]; then
    sed -i 's/lang_flags "/lang_flags "-mfpu=vfpv3-d16 -mfloat-abi=hard /' src/build-data/cc/gcc.txt
    CPU='arm/armv7-a'
  elif [[ $CARCH == 'armv6h' ]]; then
    sed -i 's/lang_flags "/lang_flags "-mfpu=vfp -mfloat-abi=hard /' src/build-data/cc/gcc.txt
    CPU='arm/armv6'
  elif [[ $CARCH == 'arm' ]]; then
    CPU='arm/armv5te'
  fi

  python2 configure.py --prefix=/usr --enable-modules=cvc --cpu=$CPU
  make 
}

package() {
  cd "Botan-$pkgver"

  make DESTDIR="$pkgdir/usr" install
  find "$pkgdir/usr/share/doc" -type f -exec chmod 0644 {} \;
  install -Dm644 doc/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
