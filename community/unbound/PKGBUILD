# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Hisato Tatekura <hisato_tatekura@excentrics.net>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO (--disable-flto)

pkgname=unbound
pkgver=1.12.0
pkgrel=1
pkgdesc="Validating, recursive, and caching DNS resolver"
url="https://unbound.net/"
license=('BSD')
arch=('x86_64')
depends=('dnssec-anchors' 'fstrm' 'hiredis' 'openssl' 'ldns' 'libevent'
'libnghttp2' 'libsodium')
makedepends=('expat' 'protobuf-c' 'systemd')
optdepends=('expat: unbound-anchor')
provides=('libunbound.so')
backup=("etc/${pkgname}/${pkgname}.conf")
source=("https://unbound.net/downloads/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "${pkgname}-service.patch::https://github.com/NLnetLabs/unbound/pull/351/commits/c48f01445e22686a0cb83451ab055cb405940b58.patch"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf"
        "${pkgname}-trusted-key.hook")
sha512sums=('90d99bc65e9ba62e50a7809dbf1e98889d0fc9fd50cf3cc99b726c67bcaeda0c2bc176d09f84771adb9796833b595591462f96e949d6969a47d6898d8fae3479'
            'SKIP'
            '7461e6cc588dbb955d88fc369a8203249b4c52c53a6ec804113e9311a511630bb10437fe522d99a107683d3b662087870c6be2475a02293e159dac2362248725'
            'ef71d4e9b0eb0cc602d66bd0573d9424578fe33ef28a852c582d56f0fd34fdd63046c365ef7aed8b84a461b81254240af7ad3fd539da72f9587817d21bd6c585'
            '6b1849ae9d7cf427f6fa6cd0590e8f8c3f06210d2d6795e543b0f325a9e866db0f5db2275a29fa90f688783c0dd16f19c8a49a9817d5f5444e13f8f2df3ff712'
            '613826cdf5ab6e77f2805fa2aa65272508dcd11090add1961b3df6dfac3b67db016bc9f45fbcf0ef0de82b2d602c153d5263a488027a6cf13a72680b581b266d')
b2sums=('b3dccecb1fd019f36e47c62583348a375f5eed7c84094c48def1ff9e73dc283911a2a9b318c8a4be45f5cd4bfb17f26722a9b900aa1980f49790cc55f6412d1d'
        'SKIP'
        'ce23a5529c31d7be34392fc65b4c691d28bae5aef40d9c8627c1d3fef4d500bf92b760a13121bd1f38e1a875f4f514e38967ed06060fe330afa6d3c1f6950495'
        '292a3c2e5fde292a03b6c9b2ddabd5089f52e73b50a404c3d9f54c1a43184924b661a21eea61cc521c594c1005a3b40b630fa585a38195c61298f9b24b248b92'
        'd3951006b43068be904c6b91a9e0563d56228225854e12b40abbdd4ba9b47338e97265837297a6de879acbc8051bb749163f9457683f5e12fc29ac2e7b687fd3'
        'd28785390eb6c125bd26ca11f097fe8864b080482157deeb7c70e9bee47ff2844abaed574db59a7c152ed3ec0acba05cfee4c3751f7a9f553320b064578f86c7')
validpgpkeys=('EDFAA3F2CA4E6EB05681AF8E9F6F1C2D7E045F8D') # W.C.A. Wijngaards <wouter@nlnetlabs.nl>

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix systemd service:
  # https://github.com/NLnetLabs/unbound/issues/350
  patch -Np1 -i ../"${pkgname}-service.patch"
  # set default location of trusted-key.key
  sed '/# trust-anchor-file:/c\\ttrust-anchor-file: /etc/unbound/trusted-key.key' \
    -i doc/example.conf.in

  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --sbindir=/usr/bin \
              --disable-rpath \
              --enable-dnscrypt \
              --enable-dnstap \
              --enable-pie \
              --enable-relro-now \
              --enable-subnet \
              --enable-systemd \
              --enable-tfo-client \
              --enable-tfo-server \
              --enable-cachedb \
              --with-libhiredis \
              --with-conf-file=/etc/unbound/unbound.conf \
              --with-pidfile=/run/unbound.pid \
              --with-rootkey-file=/etc/trusted-key.key \
              --with-libevent \
              --with-libnghttp2 \
              --disable-flto
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make -k check
}

package() {
  depends+=('libprotobuf-c.so' 'libsystemd.so')

  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 "contrib/${pkgname}.service" \
    -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # sysusers.d
  install -vDm 644 "../${pkgname}-sysusers.conf" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # tmpfiles.d
  install -vDm 644 "../${pkgname}-tmpfiles.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # libalpm hook to copy the dnssec-anchors provided key to /etc/unbound
  install -vDm 644 ../${pkgname}-trusted-key.hook \
    -t "${pkgdir}/usr/share/libalpm/hooks/"
}
