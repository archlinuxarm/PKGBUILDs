# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Frank Ickstadt (frank dot ickstadt at gmail dot com)

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't unset CFLAGS, use -fPIC with armv7h and aarch64

pkgname=jbigkit
pkgver=2.1
pkgrel=3
pkgdesc="Data compression library/utilities for bi-level high-resolution images"
arch=('i686' 'x86_64')
url="http://www.cl.cam.ac.uk/~mgk25/jbigkit/"
license=('GPL')
depends=()
options=('staticlibs')
source=(http://www.cl.cam.ac.uk/~mgk25/download/jbigkit-$pkgver.tar.gz)
md5sums=('ebcf09bed9f14d7fa188d3bd57349522')

build() {
  cd $srcdir/$pkgname-$pkgver
  unset CPPFLAGS LDFLAGS
  [ "$CARCH" == "x86_64" ] && export CFLAGS="$CFLAGS -fPIC"
  [ "$CARCH" == "armv7h" ] && export CFLAGS="$CFLAGS -fPIC"
  [ "$CARCH" == "aarch64" ] && export CFLAGS="$CFLAGS -fPIC"
  make CFLAGS="$CFLAGS"
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -D -m644 libjbig/libjbig.a $pkgdir/usr/lib/libjbig.a
  install -D -m644 libjbig/libjbig85.a $pkgdir/usr/lib/libjbig85.a
  install -D -m644 libjbig/jbig.h $pkgdir/usr/include/jbig.h
  install -D -m644 libjbig/jbig_ar.h $pkgdir/usr/include/jbig_ar.h
  install -D -m644 libjbig/jbig85.h $pkgdir/usr/include/jbig85.h

  install -d -m755 $pkgdir/usr/share/man/man1
#  install -d -m755 $pkgdir/usr/share/man/man5
  install -m644 pbmtools/*.1* $pkgdir/usr/share/man/man1
#  install -m644 pbmtools/*.5* $pkgdir/usr/share/man/man5

  install -D -m755 pbmtools/jbgtopbm $pkgdir/usr/bin/jbgtopbm
  install -D -m755 pbmtools/pbmtojbg $pkgdir/usr/bin/pbmtojbg
  install -D -m755 pbmtools/jbgtopbm85 $pkgdir/usr/bin/jbgtopbm85
  install -D -m755 pbmtools/pbmtojbg85 $pkgdir/usr/bin/pbmtojbg85

  rm -f $pkgdir/usr/share/man/man5/p{b,g}m.5*
}
