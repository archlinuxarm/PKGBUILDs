# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO to fix FTBFS on other lxqt packages

pkgname=lxqt-build-tools
pkgver=0.5.0
pkgrel=1
pkgdesc="Various packaging tools and scripts for LXQt applications"
arch=("any")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("cmake" "qt5-tools" "git")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	'0001-disable-LTO.patch'
)
sha256sums=(
	"a66d080d4cf075007fcdd9eab1689559728418dd8f85f7e8e79679738de8658d"
	"SKIP"
	"7e9cd88a6e28f16204ff4facf4b29e53ecf4f25eb166e53ba9052bba901bad53"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


prepare() {
	cd $pkgname-$pkgver
	if [[ $CARCH != "aarch64" ]]; then
		patch -p1 -i ../0001-disable-LTO.patch
	fi
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
