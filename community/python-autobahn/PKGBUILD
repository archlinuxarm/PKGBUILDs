# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Maintainer: Anatol Pomozov

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed AUTOBAHN_USE_NVX=1 from python build/install commands

pkgname=python-autobahn
pkgver=19.7.1
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things'
arch=(x86_64)
url='https://crossbar.io/autobahn/'
license=(MIT)
depends=(python-cffi python-twisted python-six python-txaio python-wsaccel
         python-setuptools python-cryptography)
makedepends=(python-argon2_cffi python-cbor python-flatbuffers
             python-msgpack python-passlib python-pynacl python-ubjson)
checkdepends=(python-mock python-pytest python-pytest-asyncio)
optdepends=(
  'python-cbor: CBOR serializer support'
  'python-flatbuffers: FlatBuffers serializer support'
  'python-msgpack: MsgPack serializer support'
  'python-ubjson: UBJSON serializer support'
  'python-argon2_cffi: WAMP-SCRAM authentication support'
  'python-passlib: WAMP-SCRAM authentication support'
  'python-pynacl: WAMP-cryptosign support'
)

source=(https://files.pythonhosted.org/packages/source/a/autobahn/autobahn-$pkgver.tar.gz)
sha256sums=('89f94a1535673b1655df28ef91e96b7f34faea76da04a5e56441c9ac779a2f9f')

build() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/autobahn-$pkgver"
  USE_TWISTED=1 PYTHONPATH=.:build/lib.linux-$CARCH-3.7 pytest -v autobahn
  USE_ASYNCIO=1 PYTHONPATH=.:build/lib.linux-$CARCH-3.7 pytest -v autobahn
}

package() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
