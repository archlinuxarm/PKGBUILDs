# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Maintainer: Anatol Pomozov

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed AUTOBAHN_USE_NVX=1 from python build/install commands

pkgname=python-autobahn
pkgver=19.11.2
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things'
arch=(x86_64)
url='https://crossbar.io/autobahn/'
license=(MIT)
depends=(python-cffi python-twisted python-six python-txaio python-wsaccel
         python-setuptools python-cryptography)
makedepends=(python-argon2_cffi python-cbor python-flatbuffers
             python-msgpack python-passlib python-pynacl python-pytrie
             python-ubjson)
checkdepends=(python-mock python-pytest python-pytest-asyncio)
optdepends=(
  'python-cbor: CBOR serializer support'
  'python-flatbuffers: FlatBuffers serializer support'
  'python-msgpack: MsgPack serializer support'
  'python-ubjson: UBJSON serializer support'
  'python-argon2_cffi: WAMP-SCRAM authentication support'
  'python-passlib: WAMP-SCRAM authentication support'
  'python-pynacl: WAMP-cryptosign and WAMP-cryptobox support'
  'python-pytrie: WAMP-cryptobox support'
)

source=(https://files.pythonhosted.org/packages/source/a/autobahn/autobahn-$pkgver.tar.gz
        pytest-compatibility.diff)
sha256sums=('64fa063b3a1ab16588037d4713f13f66167f7ad2a2e95fd675decbc3bc85c089'
            '07af16d4f41f5cccb269ccce0c887d347f9158e25002a638071b178130add8c4')

prepare() {
  cd "$srcdir/autobahn-$pkgver"
  patch -Np1 -i ../pytest-compatibility.diff
}

build() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/autobahn-$pkgver"
  pyver=$(python -c "import sys; print('{}.{}'.format(*sys.version_info[:2]))")
  # test_depleting checks whether the entropy on the running machine can be depleted or not.
  # Unrelated to autobahn and makes the test suite flaky
  # Now test_non_depleting is also flaky :(
  # https://github.com/crossbario/autobahn-python/issues/1278
  USE_TWISTED=1 PYTHONPATH=.:build/lib.linux-$CARCH-$pyver pytest -v autobahn --ignore=autobahn/test/test_rng.py
  USE_ASYNCIO=1 PYTHONPATH=.:build/lib.linux-$CARCH-$pyver pytest -v autobahn --ignore=autobahn/test/test_rng.py
}

package() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
