# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Maintainer: Anatol Pomozov

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed AUTOBAHN_USE_NVX=1 from python build/install commands

pkgname=python-autobahn
pkgver=19.3.3
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things'
arch=(x86_64)
url='https://crossbar.io/autobahn/'
license=(MIT)
depends=(python-cffi python-twisted python-six python-txaio python-wsaccel)
makedepends=(python-setuptools python-argon2_cffi python-msgpack python-passlib python-pynacl)
checkdepends=(python-mock python-pytest python-pytest-asyncio)
optdepends=(
  'python-msgpack: MsgPack serializer support'
  'python-argon2_cffi: WAMP-SCRAM authentication support'
  'python-passlib: WAMP-SCRAM authentication support'
  'python-pynacl: WAMP-cryptosign support'
)

source=(https://pypi.io/packages/source/a/autobahn/autobahn-$pkgver.tar.gz
        skip-test-missing-serializers.patch)
sha256sums=('e92f40ab26fb51672c25cd301ae79a549c6ff7748effe6abdea2ef31d5363a4f'
            '2d4ec4300f98cddd13c3a4d70e6ae4934a98f17b04628cadfd18654172d04f92')

prepare() {
  cd "$srcdir/autobahn-$pkgver"
  patch -Np1 -i ../skip-test-missing-serializers.patch
}

build() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/autobahn-$pkgver"
  USE_TWISTED=1 PYTHONPATH=.:build/lib.linux-$CARCH-3.7 pytest -v autobahn
  USE_ASYNCIO=1 PYTHONPATH=.:build/lib.linux-$CARCH-3.7 pytest -v autobahn
}

package() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
