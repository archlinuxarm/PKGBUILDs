# $Id: PKGBUILD 85342 2013-03-01 10:29:55Z andrea $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to cast qReal's to double's to work with ARM

pkgname=fbreader
pkgver=0.99.4
pkgrel=5
pkgdesc="An e-book reader for Linux"
arch=('i686' 'x86_64')
url="http://www.fbreader.org/"
license=('GPL')
depends=('fribidi' 'bzip2' 'curl' 'qt4' 'libunibreak' 'sqlite')
makedepends=('ccache')
source=(http://www.fbreader.org/files/desktop/fbreader-sources-$pkgver.tgz
	"fbreader-0.99.4.patch::http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/app-text/fbreader/files/fbreader-0.99.4.patch?revision=1.1"
        arm.patch)
md5sums=('ed13a9c3aeb68aaa98a1dd6f7947a31d'
         'bc8975fc8235f393ef011b564addf300'
         '9e6f7c9437c193be6c7fb18978bdf876')

prepare() {
  cd $srcdir/fbreader-$pkgver
  patch -p1 <$srcdir/fbreader-0.99.4.patch
  patch -p1 -i ../arm.patch
}

build() {
  export TARGET_ARCH=desktop
  export UI_TYPE=qt4
  export TARGET_STATUS=release
  cd $srcdir/fbreader-$pkgver
  make ZLSHARED=no MOC=/usr/bin/moc-qt4
}

package() {
  cd $srcdir/fbreader-$pkgver
  make ZLSHARED=no DESTDIR=$pkgdir install
}
