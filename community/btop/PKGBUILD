# Maintainer: Caleb Maclennan <caleb@alerque.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto

pkgname=btop
pkgver=1.0.19
pkgrel=1
pkgdesc='A monitor of system resourecs, bpytop ported to C++'
arch=(x86_64 aarch64)
url="https://github.com/aristocratos/$pkgname"
license=(Apache)
depends=(gcc-libs)
_archive="$pkgname-$pkgver"
source=("$_archive.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('2983247a804ced692385b61a7e8f98536cb1bbffeacb9ca985c68f810658db5e')

build() {
	cd "$_archive"
	sed -i 's/-flto\S*//' Makefile
	make all
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" PREFIX=/usr install
}

