# Maintainer: Caleb Maclennan <caleb@alerque.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto

pkgname=btop
pkgver=1.2.9
pkgrel=1
pkgdesc='A monitor of system resources, bpytop ported to C++'
arch=(x86_64)
url="https://github.com/aristocratos/$pkgname"
license=(Apache)
depends=(gcc-libs)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('0f8c3434a9c4c132a34415a9cc4f048595b8a4d1a94e94223ac3a795e1c16531')

build() {
	cd "$_archive"
	sed -i 's/-flto\S*//' Makefile
	make all
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" PREFIX=/usr install
}

