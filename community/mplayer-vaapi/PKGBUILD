# $Id: PKGBUILD 73508 2012-07-08 19:34:25Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-runtime-cpudetection from configure

pkgname=mplayer-vaapi
pkgver=36265
pkgrel=14
pkgdesc="A movie player, compiled with vaapi support"
arch=('i686' 'x86_64')
url="http://gitorious.org/vaapi/mplayer"
license=('GPL')
depends=('libxxf86dga' 'libxxf86vm' 'libmad' 'libcdio-paranoia' 'libxinerama' 'sdl'
         'lame' 'fontconfig' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'glu'
         'sdl' 'smbclient' 'aalib' 'jack' 'libcaca' 'libx264' 'faac' 'faad2'
         'lirc' 'ttf-dejavu' 'libxvmc' 'enca' 'opencore-amr' 'libdca'
         'a52dec' 'schroedinger' 'mpg123' 'libvpx' 'libpulse' 'fribidi' 'opus'
         'libbluray' 'libva' 'libass' 'rtmpdump' 'gsm' 'desktop-file-utils')
depends+=('libx264.so')
makedepends=('unzip' 'live-media' 'yasm' 'ladspa' 'git' 'mesa' 'x264')
provides=("mplayer=$pkgver")
conflicts=('mplayer')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(http://pkgbuild.com/~foutrelis/sources/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        cdio-includes.patch
        tweak-desktop-file.patch
        subreader-fix-srt-parsing.patch
        include-samba-4.0.patch)
options=('!buildflags' '!emptydirs')
install=mplayer-vaapi.install
sha256sums=('5747c28c30c15d1000fb655a8abaa4b22483746d0e82775b27466948ae0c549a'
            'SKIP'
            '72e6c654f9733953ad2466d0ea1a52f23e753791d8232d90f13293eb1b358720'
            '5a09fb462729a4e573568f9e8c1f57dbe7f69c0b68cfa4f6d70b3e52c450d93b'
            '8b6cd325d89ff8bce3662c6aaa9b61b8e6163c6574e09b575426a1eed02b8ad3'
            '169eb47b3b838ea95e50c871bdbbfb6fe0b9349b054da830f55f3b4d5055e4f3')
validpgpkeys=('86CFFCA918CF3AF47147588051E8B148A9999C34')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/cdio-includes.patch"
  patch -d etc -Np0 -i "$srcdir/tweak-desktop-file.patch"

  # http://bugzilla.mplayerhq.hu/show_bug.cgi?id=2139
  patch -Np0 -i "$srcdir/subreader-fix-srt-parsing.patch"

  # https://bugs.archlinux.org/task/41588
  patch -Np1 -i "$srcdir/include-samba-4.0.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --disable-gui \
    --disable-arts \
    --disable-liblzo \
    --disable-speex \
    --disable-cdparanoia \
    --disable-openal \
    --disable-libdv \
    --disable-musepack \
    --disable-esd \
    --disable-mga \
    --disable-ass-internal \
    --enable-xvmc \
    --enable-radio \
    --enable-radio-capture \
    --enable-smb \
    --disable-vdpau \
    --enable-vaapi \
    --language=all \
    --confdir=/etc/mplayer

  [[ $CARCH == i686 ]] && sed -i 's|-march=i486|-march=i686|g' config.mak

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install-mplayer install-mplayer-man
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} "$pkgdir/etc/mplayer/"
  install -Dm644 etc/mplayer.desktop "$pkgdir/usr/share/applications/mplayer.desktop"
  install -d "$pkgdir/usr/share/mplayer/"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "$pkgdir/usr/share/mplayer/subfont.ttf"
  rm -rf "$pkgdir/usr/share/mplayer/font"
  install -Dm644 etc/mplayer256x256.png "$pkgdir/usr/share/pixmaps/mplayer.png"
}

# vim:set ts=2 sw=2 et:
