# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Matthias Maennich <arch@maennich.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with -fsigned-char

pkgname=garmindev
pkgver=0.3.4
pkgrel=9
pkgdesc="Garmin Device Drivers for QlandkarteGT"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org/"
license=('GPL')
depends=('libusb-compat')
makedepends=('cmake')
changelog=$pkgname.changelog
install=$pkgname.install
source=(http://downloads.sourceforge.net/qlandkartegt/$pkgname-$pkgver.tar.gz \
        51-garmin.rules)
sha256sums=('86c38f65ba1a05c5d5eb076107094cb789db6ffc5d80bfd7bb209d645bcf87d5'
            '0196ae234c0daf97afb9f4af6fdc04c2af77bb742233731870ba8cfc6ae5a7d7')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  install -d ${srcdir}/$pkgname-$pkgver/build
  cd ${srcdir}/$pkgname-$pkgver/build

  CXXFLAGS+=" -fsigned-char"

  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package(){
  cd ${srcdir}/$pkgname-$pkgver/build

  make DESTDIR=${pkgdir} install

#ChangeLog and example udev rule
  install -D -m644 ../changelog.txt ${pkgdir}/usr/share/$pkgname/CHANGELOG
  install -D -m644 ${srcdir}/51-garmin.rules ${pkgdir}/usr/share/$pkgname/51-garmin.rules
}
