# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jorge Mokross <mokross@gmail.com>

pkgname=notify-sharp-svn
pkgver=3032
pkgrel=1
pkgdesc="C# bindings for dekstop notification"
url="http://www.galago-project.org"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gtk-sharp-2' 'ndesk-dbus-glib') 
makedepends=('subversion' 'pkgconfig')
provides=('notify-sharp')
conflicts=('notify-sharp')
source=()
md5sums=()

_svntrunk='http://svn.galago-project.org/trunk/notify-sharp'
_svnmod='notify-sharp'

build() {
  export MONO_SHARED_DIR=${startdir}/src/.wabi
  mkdir -p "${MONO_SHARED_DIR}"

  cd $srcdir

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  
  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  aclocal
  autoconf
  automake -a -c
  ./configure --prefix=/usr --disable-docs --disable-static
  make || return 1
  make DESTDIR=$pkgdir install

  rm -fr "${MONO_SHARED_DIR}"
}
