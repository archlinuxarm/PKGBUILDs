# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DZ3_LINK_TIME_OPTIMIZATION=OFF

pkgbase=z3
pkgname=('z3' 'python-z3')
pkgver=4.9.0
pkgrel=1
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=('x86_64')
license=('MIT')
 # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib'
makedepends=('glibc' 'cmake' 'ninja' 'python' 'python-setuptools' 'gcc-libs')
source=(https://github.com/Z3Prover/z3/archive/z3-$pkgver.tar.gz)
sha512sums=('ddfe9901ea9fb4d23761cb6bbd534ffadaa4ba8264c8e2893b08d65f4e2e4b8aa7d824dc54e00480e9121be9b27ac42db7abcce778d86eb23465046405cc8594')
b2sums=('97ee7f7ec6fbeddd01de0200149af6095bb9a7ba841b79320bf49fe6d34e7288a396db1fa4d4045b5fbaf33aba3046f736e8ca367ac7dade8a440302801b2ed6')

build() {
  cd $pkgbase-z3-$pkgver
  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZ3_LINK_TIME_OPTIMIZATION=OFF \
    -DZ3_BUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  ninja -C build
}

package_z3() {
  depends=('glibc' 'gcc-libs')
  provides=('libz3' 'libz3.so')
  cd $pkgbase-z3-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  rm -r "$pkgdir"/usr/lib/python*
}

package_python-z3() {
  depends=('python' 'z3')
  cd $pkgbase-z3-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  cd "$pkgdir"
  rm -r usr/include usr/bin usr/lib/{cmake,pkgconfig,lib*}
  python -m compileall .
  python -O -m compileall .
}

# vim: ts=2 sw=2 et:
