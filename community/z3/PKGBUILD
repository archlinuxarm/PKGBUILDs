# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DZ3_LINK_TIME_OPTIMIZATION=OFF

pkgbase=z3
pkgname=('z3' 'python-z3')
pkgver=4.9.1
pkgrel=1
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=('x86_64')
license=('MIT')
 # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib'
makedepends=('glibc' 'cmake' 'ninja' 'python' 'python-setuptools' 'gcc-libs')
source=(https://github.com/Z3Prover/z3/archive/z3-$pkgver.tar.gz)
sha512sums=('0c44e10d039c3bf16591a7b94e3a3209bc334635106ac7ae4afda95541d13d4fc39214646662683c26b4874846e0d83813215e189ce5422d13f8ce7c2ac4db51')
b2sums=('b707084424b615a43dcefbc395d6cf0519f3d6ea3ab1cc8b34b1810d5eb241afa22eae694e4dfc756fff42179a5dabe10052f88aab02953154ada8334f7e0b64')

build() {
  cd $pkgbase-z3-$pkgver
  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZ3_LINK_TIME_OPTIMIZATION=OFF \
    -DZ3_BUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  ninja -C build
}

package_z3() {
  depends=('glibc' 'gcc-libs')
  provides=('libz3' 'libz3.so')
  cd $pkgbase-z3-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  rm -r "$pkgdir"/usr/lib/python*
}

package_python-z3() {
  depends=('python' 'z3')
  cd $pkgbase-z3-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  cd "$pkgdir"
  rm -r usr/include usr/bin usr/lib/{cmake,pkgconfig,lib*}
  python -m compileall .
  python -O -m compileall .
}

# vim: ts=2 sw=2 et:
