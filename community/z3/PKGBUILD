# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DZ3_LINK_TIME_OPTIMIZATION=OFF

pkgbase=z3
pkgname=('z3' 'python-z3-solver')
pkgver=4.11.2
pkgrel=1
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=('x86_64')
license=('MIT')
makedepends=('glibc' 'cmake' 'ninja' 'python' 'python-setuptools' 'gcc-libs'
             'python-build' 'python-installer' 'python-wheel')
source=(https://github.com/Z3Prover/z3/archive/z3-$pkgver.tar.gz)
sha512sums=('be2573d38c0e50b46fcb025d50335e016769fdeab3c26f5dc2a13102fae889d23039258ea8d38de3f53daa4cf073704d8639ac291e781a74633194adedaae21a')
b2sums=('85fe5b94c5bfaeaca5b1dfe42fe9763a278c414955a9aed45abbb3e60c366a86bcdcd7c3bdcf27cc67bc10c76700069d548cb1e27ccf3286c8c6eccfa9d492d1')

build() {
  cd $pkgbase-z3-$pkgver
  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZ3_LINK_TIME_OPTIMIZATION=OFF \
    -DZ3_BUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  ninja -C build
}

package_z3() {
  depends=('glibc' 'gcc-libs')
  provides=('libz3' 'libz3.so')

  cd $pkgbase-z3-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  rm -r "$pkgdir"/usr/lib/python*
}

package_python-z3-solver() {
  depends=('python' 'python-setuptools' 'z3')
  replaces=('python-z3')
  conflicts=('python-z3')

  cd $pkgbase-z3-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname

  cd "$pkgdir"
  rm -r usr/include usr/bin usr/lib/{cmake,pkgconfig,lib*}
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim: ts=2 sw=2 et:
