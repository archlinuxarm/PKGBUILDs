# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Adam Griffiths

pkgname=zziplib
pkgver=0.13.49
pkgrel=2
pkgdesc="The zziplib library is intentionally lightweight, it offers the ability to easily extract data from files archived in a single zip file."
arch=('i686' 'x86_64')
url="http://zziplib.sourceforge.net/zzip-index.html"
license=("LGPL" "MPL")
depends=('zlib')
source=(http://downloads.sourceforge.net/sourceforge/zziplib/$pkgname-$pkgver.tar.bz2
        datarootdir.patch)
md5sums=('5f7b88ebb2bcd7e8044328482d079661'
         '142e2d01c10655720a34125d64143c49')
options=('!libtool')

build() {
  cd $startdir/src/$pkgname-$pkgver

  # fix stupid version check prohibiting from using pkg-config
  sed -i 's/am__api_version="1.9"/am__api_version="1.10"/' configure
  sed -i 's/am__api_version="1.9"/am__api_version="1.10"/' aclocal.m4

  ./configure --prefix=/usr
  make || return 1
  make prefix=$startdir/pkg/usr install

  # add missing datarootdir to *.pc files
  cd $startdir/pkg/usr/lib/pkgconfig
  patch -Np0 -i $startdir/src/datarootdir.patch
}
