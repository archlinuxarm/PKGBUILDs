# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define -DPNG_ARM_NEON_OPT=0 in CPPFLAGS to disable NEON in bundled libpng

pkgname=htmldoc
pkgver=1.9.6
pkgrel=1.1
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/index.html'
license=('GPL2')
depends=('libxpm' 'gnutls' 'fltk' 'shared-mime-info')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('4f49385a55c14de2b432b737593b67a68b40415bf5cc276e0a14ca0ce2e00ef3'
            'SKIP')
validpgpkeys=('845464660B686AAB36540B6F999559A027815955') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    
    CPPFLAGS+=" -DPNG_ARM_NEON_OPT=0"
    ./configure \
        --prefix='/usr' \
        --enable-largefile \
        --enable-ssl \
        --enable-gnutls \
        --enable-cdsassl \
        --enable-localjpeg \
        --enable-localzlib \
        --enable-localpng \
        --with-gui
    
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
