# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc
#  - patch to remove manual generation that uses pandoc
#  - comment manual installation

pkgname=geoipupdate
pkgver=4.10.0
pkgrel=2
pkgdesc="Update GeoIP2 and GeoIP Legacy binary databases from MaxMind"
license=('Apache' 'MIT')
arch=('x86_64')
url="https://dev.maxmind.com/geoip/geoipupdate"
depends=(glibc)
makedepends=(go)
backup=(etc/GeoIP.conf)
provides=(geoip2-database)
replaces=(geoip2-database)

source=(
    "https://github.com/maxmind/geoipupdate/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
    "geoipupdate.service"
    "geoipupdate.timer"
    "0001-remove-manuals.patch"
)

sha256sums=('12e0083eb56eb39f4304fc21ef07af566702a0d5b843f3a19d3e5e5e33099230'
            'ab2899197ea4c889b6da6e2b71bd7e50bc5aca976414aa79526d0008184e0307'
            'ba9039ae9cc3dea4fe48480527b515cab2ad3a2f69aea5bf55f551e6895779e3'
            'bac4deced8219f56e1b394986f5125a6b36864bb5e9ebf0499ec0edcba55f9ed')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    sed -i 's/EditionIDs .*/EditionIDs GeoLite2-ASN GeoLite2-City GeoLite2-Country/' conf/GeoIP.conf.default
    patch -p1 -i ../0001-remove-manuals.patch
}

build() {
    cd "$srcdir/$pkgname-$pkgver"

    export CONFFILE=/etc/GeoIP.conf
    export DATADIR=/var/lib/GeoIP

    make BUILDDIR="$srcdir/build" data

    cd cmd/geoipupdate
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-extldflags \"$LDFLAGS\" -X main.defaultConfigFile=$CONFFILE -X main.defaultDatabaseDirectory=$DATADIR -X main.version=$pkgver" \
        -o "$srcdir/build/geoipupdate" \
        .
}

package() {
    install -dm755 "$pkgdir/var/lib/GeoIP"

    cd "$srcdir"
    install -Dm644 geoipupdate.service "$pkgdir/usr/lib/systemd/system/geoipupdate.service"
    install -Dm644 geoipupdate.timer "$pkgdir/usr/lib/systemd/system/geoipupdate.timer"

    cd "$srcdir/build"
    install -Dm755 geoipupdate "$pkgdir/usr/bin/geoipupdate"
    install -Dm600 GeoIP.conf "$pkgdir/etc/GeoIP.conf"
    install -Dm644 geoipupdate.md "$pkgdir/usr/share/doc/$pkgname/geoipupdate.md"
    install -Dm644 GeoIP.conf.md "$pkgdir/usr/share/doc/$pkgname/GeoIP.conf.md"
    #install -Dm644 geoipupdate.1 "$pkgdir/usr/share/man/man1/geoipupdate.1"
    #install -Dm644 GeoIP.conf.5 "$pkgdir/usr/share/man/man5/GeoIP.conf.5"

    cd "$srcdir/$pkgname-$pkgver"
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
