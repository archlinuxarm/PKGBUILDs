# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: SmackleFunky <smacklefunky@optusnet.com.au>

pkgname=ltris
pkgver=1.0.12
pkgrel=2
pkgdesc="LTris as a tetris clone which means you have a bowl with blocks falling down."
arch=('i686' 'x86_64')
url="http://lgames.sourceforge.net/index.php?project=LTris"
license=('GPL')
depends=('sdl_mixer')
install=ltris.install
source=(http://downloads.sourceforge.net/lgames/${pkgname}-${pkgver}.tar.gz)
md5sums=('53b00c17a05c438602a52009e65ebde2')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --localstatedir=/var/games || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -d ${pkgdir}/usr/share/pixmaps
  install -m644 icons/ltris{16,32,48}.xpm ${pkgdir}/usr/share/pixmaps

  chown root:games ${pkgdir}/usr/bin/ltris
  chmod 2755 ${pkgdir}/usr/bin/ltris
  chown root:games ${pkgdir}/var/games
  chmod 775 ${pkgdir}/var/games
  rm ${pkgdir}/var/games/ltris.hscr
}
