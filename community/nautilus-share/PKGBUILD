# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=nautilus-share
pkgver=0.7.2
pkgrel=4
pkgdesc="Share files through Samba with Nautilus"
arch=('i686' 'x86_64')
url="http://gentoo.ovibes.net/nautilus-share"
license=('GPL')
depends=('nautilus' 'libglade' 'samba')
makedepends=('intltool')
backup=('etc/samba/usershare.conf')
options=('!libtool')
install=$pkgname.install
source=(http://gentoo.ovibes.net/nautilus-share/$pkgname-$pkgver.tar.gz
        usershare.conf 05_dependencies.patch)
md5sums=('0def7d5ee308cbc78774fffa93a0bbcf'
         '475ff9531f5fa114028fd7e285d58fe0'
	 '5129d0a6ecc7e42230c82bc70ced1a11')

build() {
  cd "$srcdir/$pkgname-$pkgver"
 
  #patch for fixing dependency check 
  patch -Np1 -i $srcdir/05_dependencies.patch || return 1

  sed -i 's|extensions-1.0|extensions-2.0|' src/Makefile.in src/Makefile.am || return 1

  #patch for use correct icon
  sed -i 's|stock_shared-by-me|gnome-fs-share|' src/nautilus-share.c

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make || return 1
  make DESTDIR="$pkgdir" install
  install -Dm644 ../usershare.conf "$pkgdir/etc/samba/usershare.conf"
  install -d -gsamba -m01770 "$pkgdir/var/lib/samba/usershare"
}
