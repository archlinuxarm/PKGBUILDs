# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Sven Kauber <celeon@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=partimage
pkgver=0.6.7
pkgrel=4
pkgdesc="Partition Image saves partitions in many formats to an image file."
arch=('i686' 'x86_64')
url="http://www.partimage.org/"
license=('GPL')
depends=('libnewt' 'lzo')
optdepends=('openssl: for SSL support')
backup=('etc/partimaged/partimagedusers')
install=partimage.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2 \
	'partimaged' 'partimaged-gencrt' 'gcc4.3.patch')
md5sums=('1c13530b43ea5c368c1c2fd0ab36bddb'
         'f3b517a62de5540f3b1a8b7edf65b7ec'
         '2316b116227b07efb48266e660ca1ba1'
	 'e15c3d64111228a6743a749d599558c8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-pam

  patch -Np1 -i "$srcdir/gcc4.3.patch"

  make || return 1
  make pamfile || return 1
  make DESTDIR="$pkgdir" install || return 1

  install -D -m 644 partimaged.pam "$pkgdir/etc/pam.d/partimaged" || return 1
  install -D -m 755 "$srcdir/partimaged" "$pkgdir/etc/rc.d/partimaged" || return 1
  install -D -m 755 "$srcdir/partimaged-gencrt" \
    "$pkgdir/usr/bin/partimaged-gencrt" || return 1
  chmod 644 "$pkgdir/etc/partimaged/partimagedusers"
}
