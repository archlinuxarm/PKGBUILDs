# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --without-dav1d, FTBFS

pkgname=xine-lib
pkgver=1.2.12
pkgrel=3
pkgdesc='Multimedia playback engine'
arch=('x86_64')
url='https://www.xine-project.org'
license=('GPL' 'LGPL')
depends=('ffmpeg' 'libjpeg-turbo' 'libnsl' 'libpng' 'libx11' 'libxcb'
         'libxext' 'libxinerama' 'libxv' 'libxvmc' 'zlib')
optdepends=('libdvdnav: for dvd plugin'
            'libdvdread: for spu and dxr3 plugins'
            'vcdimager: for vcd plugin'
            'glu: for opengl and vaapi plugins'
            'sdl: for sdl plugin'
            'wayland: for egl_wl plugin'
            'aalib: for aalib plugin'
            'a52dec: for a52 plugin'
            'faad2: for faad plugin'
            'flac: for flac plugin'
            'libdca: for dts (dca) plugin'
            'libmad: for mad (mp3) plugin'
            'libmpcdec: for mpc (musepack) plugin'
            'wavpack: for wavpack plugin'
            'gdk-pixbuf2: for gdk_pixbuf plugin'
            'imagemagick: for imagemagick plugin'
            'libcaca: for caca plugin'
            'libmng: for mng plugin'
            'libnfs: for nfs plugin'
            'libssh2: for ssh plugin'
            'smbclient: for samba plugin')
makedepends=('mesa' 'libdvdnav' 'libdvdread' 'vcdimager' 'glu' 'sdl'
             'wayland' 'aalib' 'a52dec' 'faad2' 'flac' 'libdca' 'libmad'
             'libmpcdec' 'wavpack' 'gdk-pixbuf2' 'imagemagick' 'libcaca'
             'libmng' 'libnfs' 'smbclient')
source=("https://downloads.sourceforge.net/project/xine/xine-lib/${pkgver}/xine-lib-${pkgver}.tar.xz"
        '010-xine-lib-dav1d-1.0.0-support.patch'
        '020-xine-lib-ffmpeg-5.1-fix.patch')
sha256sums=('d606270468e1540c2a89c0d7f5fdf11e17ecc0c2698cc0bcb1065ff26abee098'
            '9e41a7e49bc81d04d8f5014d5a4fcb4961eeb735eb6329a76ac24425fdafae06'
            '36930d0973c71027175530e6911a9e9ad824107eedd519b01f375800f3f98822')

prepare() {
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-xine-lib-dav1d-1.0.0-support.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/020-xine-lib-ffmpeg-5.1-fix.patch"
}

build() {
    cd "${pkgname}-${pkgver}"
    ./autogen.sh \
        --prefix='/usr' \
        --enable-antialiasing \
        --with-external-dvdnav \
        --with-wavpack \
        --without-dav1d
    sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
