# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --without-dav1d, FTBFS
#  - fix source directory name, - separator instead of .

pkgname=xine-lib
pkgver=1.2.13
pkgrel=2
pkgdesc='Multimedia playback engine'
arch=('x86_64')
url='https://www.xine-project.org'
license=('GPL' 'LGPL')
depends=('ffmpeg' 'libjpeg-turbo' 'libnsl' 'libpng' 'libx11' 'libxcb'
         'libxext' 'libxinerama' 'libxv' 'libxvmc' 'zlib')
optdepends=('libdvdnav: for dvd plugin'
            'libdvdread: for spu and dxr3 plugins'
            'vcdimager: for vcd plugin'
            'glu: for opengl and vaapi plugins'
            'sdl: for sdl plugin'
            'wayland: for egl_wl plugin'
            'aalib: for aalib plugin'
            'a52dec: for a52 plugin'
            'faad2: for faad plugin'
            'flac: for flac plugin'
            'libdca: for dts (dca) plugin'
            'libmad: for mad (mp3) plugin'
            'libmpcdec: for mpc (musepack) plugin'
            'wavpack: for wavpack plugin'
            'gdk-pixbuf2: for gdk_pixbuf plugin'
            'imagemagick: for imagemagick plugin'
            'libcaca: for caca plugin'
            'libmng: for mng plugin'
            'libnfs: for nfs plugin'
            'libssh2: for ssh plugin'
            'smbclient: for samba plugin')
makedepends=('mesa' 'libdvdnav' 'libdvdread' 'vcdimager' 'glu' 'sdl'
             'wayland' 'aalib' 'a52dec' 'faad2' 'flac' 'libdca' 'libmad'
             'libmpcdec' 'wavpack' 'gdk-pixbuf2' 'imagemagick' 'libcaca'
             'libmng' 'libnfs' 'smbclient')
source=("https://downloads.sourceforge.net/project/xine/xine-lib/${pkgver}/xine-lib-${pkgver}.tar.xz")
sha256sums=('3def43d3c0597f0f23cbf73e9d4b1dcd328852f2100fcff89fe925ad7cdd0251')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix='/usr' \
        --enable-antialiasing \
        --with-external-dvdnav \
        --with-wavpack \
        --without-dav1d
    sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
