# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=libfbclient
pkgver=2.1.2.18118
pkgdesc="Client library for Firebird."
pkgrel=5
arch=('i686' 'x86_64')
url="http://www.firebirdsql.org/"
license=('custom')
depends=("gcc-libs" "icu")
makedepends=("gcc" "autoconf" "automake")
source=(http://downloads.sourceforge.net/firebird/Firebird-$pkgver-0.tar.bz2
LICENSE firebird-gcc-icu.patch)

build() {
    cd $srcdir/Firebird-$pkgver-0
    
    # Fix gcc 4.4.0 and icu compiling problems
    patch -Np0 < $srcdir/firebird-gcc-icu.patch

    msg "Configuring to use superserver, compiling, making and instaling."
    msg "Dont worry about the errors below. It will compile, then we will"
    msg "move the libs to the right dir."
    autoreconf
    automake --add-missing
    ./configure --prefix=/opt/firebird --with-system-icu
    make -j1 # bug in firebird makefiles (don't allow parallel build)

    msg "Now moving to the right place."
    mkdir -p $pkgdir/opt/firebird $pkgdir/usr/bin
    cp -R gen/firebird/{lib,include} $pkgdir/usr
    install -m644 gen/firebird/*.msg $pkgdir/opt/firebird
    install -m755 gen/firebird/bin/fb_config $pkgdir/usr/bin
    rm $pkgdir/usr/lib/libeditline.a # remove to avoid conflict with heimdal
    sed -i -e "s/fb_root=.*/fb_root=\/usr/" \
    -e "s/fb_bindir=.*/fb_bindir=\/opt\/firebird\/bin/" \
    $pkgdir/usr/bin/fb_config

    mkdir -p $srcdir/usr/share/licenses/$pkgname/

    msg "Installing the LICENSE"
    install -D -m644 ${srcdir}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
md5sums=('063b3825a06d328f726b733fd74614b1'
         '0d61798861e159c85dbdf0f30fe2f2ce'
         '9572494efc022f8c728553d37448e2ed')
