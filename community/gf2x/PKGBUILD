# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed sse2 configure/cflags options

pkgname=gf2x
pkgver=1.3.0
pkgrel=1
pkgdesc="A library for multiplying polynomials over the binary field"
arch=(x86_64)
url="https://gforge.inria.fr/projects/gf2x/"
license=(GPL LGPL)
depends=(glibc)
source=("https://gforge.inria.fr/frs/download.php/38243/gf2x-$pkgver.tar.gz")
sha256sums=('9472cd651972a1de38e3c4c47697a86e0ecf19d7d33454d4bc2a62bc85841b59')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-pclmul
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir"/usr install
}
