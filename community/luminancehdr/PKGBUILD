# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to remove sse and sse2 flags

pkgname=luminancehdr
pkgver=2.5.0
pkgrel=1
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'lcms2' 'libraw' 'openexr' 'qt5-webengine' 'qt5-svg'
         'qt5-webkit' 'ccfits' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake' 'boost' 'gtest' 'qt5-tools' 'patch')
optdepends=('hugin: align image stack functionality')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://sourceforge.net/projects/qtpfsgui/files/luminance/$pkgver/luminance-hdr-$pkgver.tar.bz2
        strip-sse.patch)
sha256sums=('f5caf3316d1763058b1b0f2a6963df34465fbf918b7abeee5245a51fc14a7942'
            'f74a1d27684a7a8fa3223e1bf8e04bd2158925b8f1d0b32ef167910920511974')

prepare() {
  cd "${srcdir}"/luminance-hdr-$pkgver

  patch -p0 -i ../strip-sse.patch
}

build() {
  cd "${srcdir}"/luminance-hdr-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/luminance-hdr-$pkgver

  make DESTDIR="${pkgdir}" install
}
