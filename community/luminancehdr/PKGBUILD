# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to remove sse and sse2 flags

pkgname=luminancehdr
pkgver=2.4.0
pkgrel=2
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'lcms2' 'libraw' 'openexr' 'qt5-webkit' 'ccfits')
makedepends=('cmake' 'boost' 'gtest' 'qt5-tools')
optdepends=('hugin: align image stack functionality')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://sourceforge.net/projects/qtpfsgui/files/luminance/${pkgver}/luminance-hdr-${pkgver}.tar.bz2
        strip-sse.patch)
sha256sums=('14014eadab099944f051dd99447f6f47d455ffb0e4e0272d93a8e71fd96ad401'
            'f74a1d27684a7a8fa3223e1bf8e04bd2158925b8f1d0b32ef167910920511974')

build() {
  cd "${srcdir}/luminance-hdr-${pkgver}"

  patch -p0 -i ../strip-sse.patch

  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/luminance-hdr-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
