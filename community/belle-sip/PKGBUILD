# $Id: PKGBUILD 99895 2013-10-31 02:20:19Z allan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - increase maximum allocation pool to 512MB to fix AArch64 build

pkgname=belle-sip
pkgver=1.5.0
pkgrel=2
pkgdesc="A Voice-over-IP phone"
arch=('i686' 'x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('libantlr3c' 'gcc-libs' 'mbedtls' 'bctoolbox')
makedepends=('java-runtime')
options=('!emptydirs')
#source=("http://download-mirror.savannah.gnu.org/releases/linphone/belle-sip/belle-sip-$pkgver.tar.gz"
source=("$pkgname-$pkgver.tar.gz::https://github.com/BelledonneCommunications/belle-sip/archive/$pkgver.tar.gz"
	"antlr.jar::https://github.com/antlr/website-antlr3/blob/gh-pages/download/antlr-3.4-complete.jar?raw=true")
noextract=('antlr.jar')
md5sums=('b1587edb9c082d0cc5a1299a489a4259'
         '1b91dea1c7d480b3223f7c8a9aa0e172')

prepare() {
  cd $pkgname-$pkgver
  sed -i \
    -e "s#antlr_java_prefixes=.*#antlr_java_prefixes="$srcdir"#" \
    -e "s|-Werror||g" \
    configure.ac
  sed -i \
    -e 's|bctbx_list_delete_link|bctbx_list_erase_link|g' \
    -e 's|bctbx_list_remove_link|bctbx_list_unlink|g' \
    include/belle-sip/list.h
  sed -i 's|, super->base.peer_cname ? super->base.peer_cname : super->base.peer_name ||' src/transports/tls_channel.c
  sed -i 's/Xmx256m/Xmx512m/' configure.ac
}

build() {
  cd $pkgname-$pkgver
  [ -x ./configure ] || ./autogen.sh
  ./configure --prefix=/usr  --libexecdir=/usr/lib/$pkgname \
    --disable-tests --disable-static --enable-tls
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
