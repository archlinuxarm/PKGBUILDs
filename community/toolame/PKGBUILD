# $Id: PKGBUILD 65411 2012-02-21 03:29:11Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Victor Martinez Romanos <vmromanos@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Makefile replacements for ARM

pkgname=toolame
pkgver=02l
pkgrel=11
pkgdesc="An optimized mpeg 1/2 layer 2 audio encoder"
arch=('x86_64')
url="http://sourceforge.net/projects/toolame"
license=('LGPL')
depends=('glibc')
source=(https://downloads.sourceforge.net/sourceforge/toolame/toolame-02l.tgz
	toolame-02l.patch)
sha256sums=('3b95cd2504bceac4aab2fca03bb3b5974d1d44b5afdd8eb4cc0f0899521bffc4'
            'ec5932294146af74e8861537f42621a5df6e78d46ff45e41f1b4e214401f3bde')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 < ../toolame-02l.patch
  [ "$CARCH" = "x86_64" ] && sed -i "s|-march=i686|-march=x86-64|g" Makefile
  [ "$CARCH" = "arm" ] && sed -i "s|-march=i686|-march=armv5te|g" Makefile
  [ "$CARCH" = "armv6h" ] && sed -i "s|-march=i686|-march=armv6 -mfloat-abi=hard -mfpu=vfp|g" Makefile
  [ "$CARCH" = "armv7h" ] && sed -i "s|-march=i686|-march=armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16|g" Makefile
  [ "$CARCH" = "aarch64" ] && sed -i "s|-march=i686|-march=armv8-a|g" Makefile
  sed -e 's|INLINE void putbits|void putbits|' -i bitstream.h # Fix build
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -D -m755 toolame "$pkgdir"/usr/bin/toolame
}
