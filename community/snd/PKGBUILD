# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with pulseaudio as sound server, not jack (x86 asm)

pkgname=snd
pkgver=19.6
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('pulseaudio' 'fftw' 'gsl' 'gtk3' 'libmpc' 'ruby')
makedepends=('flac' 'gendesk' 'gmp' 'glu' 'ladspa' 'libpulse' 'libxpm' 'mpg123'
'openmotif' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('13dccf3fc41bf4822dbb6b0d164a576dfd19427d6eddc396d38b366922df6cfb853ed69be92528d04556fe11b14ec5bbd101d18dd30c74a6de74ea9eb24d9903')

prepare() {
  cd "${pkgname}-${pkgver}"
  # create XDG desktop file, as the default is broken
  gendesk -f -n \
          --pkgname "${pkgname}" \
          --name "${pkgname}" \
          --exec "${pkgname}" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gmp \
              --with-gtk \
              --with-ladspa \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 *.html -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 pix/*.png -t "${pkgdir}/usr/share/doc/${pkgname}/html/pix"
}

# vim:set ts=2 sw=2 et:
