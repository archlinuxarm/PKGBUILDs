# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with pulseaudio as sound server, not jack (x86 asm)

pkgname=snd
pkgver=18.1
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
depends=('pulseaudio' 'fftw' 'gsl' 'gtk3' 'libmpc' 'ruby')
makedepends=('flac' 'gmp' 'glu' 'ladspa' 'libpulse' 'libxpm' 'mpg123'
'openmotif' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4843e64a34a531c3d95ad6f9ff65a15c5698631e028bae4aa193bb85f0039b740568d00fd23eea29e0dce1906113dc948f8751b7c443814bc5d51f88c8c182a2')

build() {
  cd "${pkgname}-${pkgver}"
#  LDFLAGS+=" -Wl,-z,noexecstack"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gmp \
              --with-gtk \
              --with-ladspa \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -vDm644 HISTORY.Snd  "${pkgdir}/usr/share/doc/${pkgname}/HISTORY"
  install -vDm644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
  install -vDm644 README.Snd  "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
