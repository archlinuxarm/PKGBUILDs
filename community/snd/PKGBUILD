# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with pulseaudio as sound server, not jack (x86 asm)

pkgname=snd
pkgver=20.6
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('cairo' 'gdk-pixbuf2' 'glibc' 'gmp' 'gsl' 'libmpc' 'mpfr' 'ruby' 'pulseaudio')
# TODO: maybe replace mpg123 with mpg321
makedepends=('alsa-lib' 'fftw' 'flac' 'gendesk' 'glib2' 'glu' 'gtk3'
'ladspa' 'libpulse' 'libsamplerate' 'libxpm' 'mpg123' 'openmotif' 'pango'
'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'pulseaudio: For pulseaudio support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b54b9cbcc37e410127612ceb0f7d435c7baae9cb4d767f9e81d91c622f2686dcc7b7225a3ed132e89655a67e0cf22f65fc8fa9dee675583c5d50f6c563bb900e')
b2sums=('f20c070dc3784a0d764fab36791d9cfd61e483eda4b62310822547246cb6d6bae84f24212ddb6ae173018801cf71ef9e2d611745362e492578605e325584ae17')

prepare() {
  cd "${pkgname}-${pkgver}"
  # create XDG desktop file, as the default is broken
  gendesk -f -n \
          --pkgname "${pkgname}" \
          --name "${pkgname}" \
          --exec "${pkgname}" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gmp \
              --with-gtk \
              --with-ladspa \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  depends+=('libasound.so' 'libfftw3.so' 'libgdk-3.so' 'libgio-2.0.so'
  'libglib-2.0.so' 'libgobject-2.0.so' 'libgtk-3.so'
  'libpangocairo-1.0.so' 'libpango-1.0.so' 'libsamplerate.so')
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 *.html -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 pix/*.png -t "${pkgdir}/usr/share/doc/${pkgname}/html/pix"
}

# vim:set ts=2 sw=2 et:
