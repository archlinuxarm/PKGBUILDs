# Contributor: Vasco Costa <vasco dot costa at geekslot dot com>
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>

pkgname=pbzip2
pkgver=1.0.5
pkgrel=2
pkgdesc="Parallel implementation of the bzip2 block-sorting file compressor"
arch=(i686 x86_64)
url="http://compression.ca/pbzip2/"
license=('BSD')
depends=('bzip2' 'gcc-libs')
source=(http://compression.ca/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('e2448d22ee29d1e6549ac58b98df11ab')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make || return 1
  make PREFIX="$pkgdir/usr" install

  # Fix location of man pages
  install -d "$pkgdir/usr/share"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share"

  # Symlinks for useful shortcuts
  ln -sf pbzip2 "$pkgdir/usr/bin/pbunzip2"
  ln -sf pbzip2 "$pkgdir/usr/bin/pbzcat"

  # License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
