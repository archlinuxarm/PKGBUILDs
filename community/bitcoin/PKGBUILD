# Maintainer: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: shahid <helllamer@gmail.com>
# Contributor: James Harvey

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build aarch64 with crc+crypto

#_dbver=4.8.30
pkgbase=bitcoin
pkgname=('bitcoin-daemon' 'bitcoin-cli' 'bitcoin-qt' 'bitcoin-tx')
pkgver=24.0
pkgrel=2
arch=('x86_64')
url="https://bitcoin.org/en/wallets/desktop/linux/bitcoincore/"
makedepends=('boost' 'libevent' 'qt5-base' 'qt5-tools' 'qrencode' 'miniupnpc' 'protobuf' 'zeromq')
license=('MIT')
source=("$pkgbase-$pkgver.tar.gz::https://bitcoincore.org/bin/bitcoin-core-$pkgver/bitcoin-$pkgver.tar.gz"
        "$pkgbase-$pkgver.SHA256SUMS::https://bitcoincore.org/bin/bitcoin-core-$pkgver/SHA256SUMS"
        "$pkgbase-$pkgver.SHA256SUMS.asc::https://bitcoincore.org/bin/bitcoin-core-$pkgver/SHA256SUMS.asc"
        "bitcoin.sysusers"
        "bitcoin.tmpfiles"
        "bitcoin-qt.desktop"
        "bitcoin-qt.appdata.xml")
sha256sums=('9cfa4a9f4acb5093e85b8b528392f0f05067f3f8fafacd4dcfe8a396158fd9f4'
            '508fe601aabf22670a59406ec7bb88878da453956be4c1ff284ebaf007b1875d'
            'SKIP'
            '7a77112fb094b9b2e6dd873e24c9a6bacb1f1c453d811a024f271f4b78f2a704'
            'ba83570b4671b18c230f23d8d93cd8faa73179a546655448c14d1644d9b49f35'
            'a020897651c03690eae3411fe401100fd1a2ffaa799247165eb71124609b7959'
            '71b125555451e9bc60b54a6658d886bad63bd18c58b5d6e3bd8977b82a70da7a')
b2sums=('2348e6e7ea9daaf801ed3555caa6f72f25b3a5d7924f3eaf06a450ccda932fd622196acd2c545e40f5a505216796a67c3db06441ac3cee43bb6adfc62344f740'
        'ca46b3def0b158607315f2e4303c663ab4de2db02a601e3a22dfd06935fb749131ecbac192ad544474f0f0ea3bedf1e11566ceb093882d4aee06df1ed69301bd'
        'SKIP'
        '25166f5726b43c531b7ba5f58aaf1c0242d26b96d2057da156024a8f9fc53702ea9ef0c67290cf8d302b9fb473cb16435dc2fe4489ebc5d0eae516635fd81c0e'
        '45d4703b78476cb1c5748f62863914e5397858c835e43cee2f4895608dbabf383993b840bb3ec2d4d2c64ca02152881ad4b495b28f94451f706687b83e99d2af'
        'f14315183b5ac6b6a9f3e4ff269be37eeb6a4529bb4c9d0cd3e1d54e3ec8052f87ba1e6591080a0d1c78daf7af40cb2ecbd11d9702e217b9c1e50d5011a8e048'
        '236a841446b905f4cf154184109451300b984694ace96bee3ec75872563313279f9c543daaceed2dd712bf56115deee538b862aa73f15dee5447d235299389c7')
validpgpkeys=(71A3B16735405025D447E8F274810B012346C9A6
              28E72909F1717FE9607754F8A7BEB2621678D37D)
changelog=Changelog

prepare() {
  sha256sum -c --ignore-missing "$pkgbase-$pkgver.SHA256SUMS"
  cd "$pkgbase-$pkgver"
  autoreconf -fi
}

build() {
  cd $pkgbase-$pkgver
  [[ $CARCH == "aarch64" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/-march=armv8-a/-march=armv8-a+crc+crypto/'` && CXXFLAGS="$CFLAGS"
  ./configure --prefix=/usr --with-gui=qt5 --with-incompatible-bdb #LDFLAGS="-L$srcdir/db4/lib/ $LDFLAGS" CPPFLAGS="-I$srcdir/db4/include/ $CPPFLAGS"
  make
}

package_bitcoin-qt() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - Qt"
  depends=(boost-libs libevent qt5-base miniupnpc libminiupnpc.so qrencode protobuf zeromq hicolor-icon-theme db)

  cd $pkgbase-$pkgver
  install -Dm755 src/qt/bitcoin-qt "$pkgdir"/usr/bin/bitcoin-qt
  install -Dm644 doc/man/bitcoin-qt.1 \
    "$pkgdir"/usr/share/man/man1/bitcoin-qt.1
  install -Dm644 ../bitcoin-qt.desktop \
    "$pkgdir"/usr/share/applications/bitcoin-qt.desktop
  install -Dm644 ../bitcoin-qt.appdata.xml \
    "$pkgdir"/usr/share/metainfo/bitcoin-qt.appdata.xml
  install -Dm644 src/qt/res/src/bitcoin.svg \
    "$pkgdir"/usr/share/icons/hicolor/scalable/apps/bitcoin-qt.svg

  for i in 16 32 64 128 256; do
    install -Dm644 share/pixmaps/bitcoin${i}.png \
      "$pkgdir"/usr/share/icons/hicolor/${i}x${i}/apps/bitcoin-qt.png
  done

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-daemon() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - daemon"
  depends=(boost-libs db libevent miniupnpc libminiupnpc.so sqlite zeromq)
  backup=('etc/bitcoin/bitcoin.conf')

  cd $pkgbase-$pkgver
  install -Dm755 src/bitcoind "$pkgdir"/usr/bin/bitcoind
  install -Dm644 contrib/bitcoind.bash-completion \
    "$pkgdir"/usr/share/bash-completion/completions/bitcoind
  install -Dm644 doc/man/bitcoind.1 \
    "$pkgdir"/usr/share/man/man1/bitcoind.1
  install -Dm644 contrib/init/bitcoind.service \
    "$pkgdir/usr/lib/systemd/system/bitcoind.service"
  install -Dm644 "$srcdir/bitcoin.sysusers" \
    "$pkgdir/usr/lib/sysusers.d/bitcoin.conf"
  install -Dm644 "$srcdir/bitcoin.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/bitcoin.conf"
  install -Dm644 share/examples/bitcoin.conf \
    "$pkgdir/etc/bitcoin/bitcoin.conf"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-cli() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - RPC client"
  depends=(boost-libs libevent)

  cd $pkgbase-$pkgver
  install -Dm755 src/bitcoin-cli "$pkgdir"/usr/bin/bitcoin-cli
  install -Dm644 contrib/bitcoin-cli.bash-completion \
    "$pkgdir"/usr/share/bash-completion/completions/bitcoin-cli
  install -Dm644 doc/man/bitcoin-cli.1 \
    "$pkgdir"/usr/share/man/man1/bitcoin-cli.1

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_bitcoin-tx() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - Transaction tool"
  depends=(boost-libs openssl)

  cd $pkgbase-$pkgver
  install -Dm755 src/bitcoin-tx "$pkgdir"/usr/bin/bitcoin-tx
  install -Dm644 contrib/bitcoin-tx.bash-completion \
    "$pkgdir"/usr/share/bash-completion/completions/bitcoin-tx
  install -Dm644 doc/man/bitcoin-tx.1 \
    "$pkgdir"/usr/share/man/man1/bitcoin-tx.1

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
