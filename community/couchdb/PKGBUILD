# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix aarch64 js check, disable LTO

pkgname=couchdb
pkgver=3.2.2
pkgrel=5
pkgdesc="Document-oriented database that can be queried and indexed in a MapReduce fashion using JSON"
arch=(x86_64)
url="https://couchdb.apache.org"
license=(Apache)
depends=(icu js91 zlib)
optdepends=('erlang-nox: for weatherreport')
makedepends=(erlang-nox)
backup=(etc/couchdb/local.ini
        etc/couchdb/vm.args
        etc/default/couchdb)
options=(debug)
source=("https://archive.apache.org/dist/${pkgname}/source/${pkgver}/apache-${pkgname}-${pkgver}.tar.gz"{,.asc}
        https://github.com/davisp/jiffy/archive/1.1.1/jiffy-1.1.1.tar.gz
        erlang-25.patch
        erlang-25-snappy.patch
        couchdb.default
        couchdb.service
        couchdb.sysusers
        couchdb.tmpfiles
        arm.patch)
sha256sums=('69c9fd6f80133557f68a02e92dda72a4fd646d646f429f45bb8329a30f82f20e'
            'SKIP'
            'a999351d956d3ed10c33a6dd7e9435e674e519b5b82a46d60361afdb83062fd8'
            '59a8a92200321f670f20746848fcadae35d1818f6bdacbedaf49012866dcfc27'
            '8a74ae8cc5c5a54cd0a65f7aeed55af257eef8912630b953bfeb8d9329228b4b'
            '0d21fdd7851b3a6fa099b030cab0e96705b1d5a3a83a4ff1c871814d8742cae9'
            '6ab3d362f3fcd9079036c883dc09dc597584cb627831460f2e0c68a31aa052ae'
            '3ed1ad2a37a068ce194b03fb72eb35285d60fa7faf2d2c2bb710703d229108a8'
            '7331b9675e3668c5f0d632bee5d154c061cc3e60d451bef3dc0d0ab2dadfa006'
            '5d97fe00ad7a05169988817ed0727452ab8a0c0582c5399aa06c90e76bf75754')

# https://downloads.apache.org/couchdb/KEYS
validpgpkeys=(2EC788AE3F239FA13E82D215CDE711289384AE37  # Joan Touzet (CODE SIGNING KEY) <wohali@apache.org>
              D2B17F9DA23C0A10991AF2E3D9EE01E47852AEE4  # Jan Lehnardt <jan@apache.org>
              0BD7A98499C4AB41C910EE65FC04DFBC9657A78E) # Nick Vatamaniuc <vatamane@gmail.com>

prepare() {
  cd apache-couchdb-${pkgver}
  sed -i 's|./data|/var/lib/couchdb|' configure

  # force recompilation of "rebar" with current erlang
  rm bin/rebar
  # use newer jiffy which doesn't ship an "enc" binary
  rm -r src/jiffy
  mv ../jiffy-1.1.1 src/jiffy

  patch -Np1 <../erlang-25.patch
  patch -Np1 -d src/snappy <../erlang-25-snappy.patch

  patch -p1 -i ../arm.patch
}

build() {
  cd apache-couchdb-${pkgver}
  ./configure --spidermonkey-version 91
  make release
}

package() {
  cd apache-couchdb-${pkgver}
  install -dm755 "${pkgdir}"/usr/lib
  install -dm755 "${pkgdir}"/etc/couchdb

  cp -r rel/couchdb "${pkgdir}"/usr/lib/couchdb
  mv "${pkgdir}"/usr/lib/couchdb/etc/{default.ini,local.ini,vm.args} "${pkgdir}"/etc/couchdb/

  cd "${srcdir}"
  install -Dm644 ${pkgname}.default "${pkgdir}"/etc/default/${pkgname}
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf

  # Remove some cruft
  rm -r "${pkgdir}"/usr/lib/couchdb/erts-13.1.1/{doc,include,lib,man,src}
  rm -rv "${pkgdir}"/usr/lib/couchdb/etc/
  rm -rv "${pkgdir}"/usr/lib/couchdb/lib/couch-${pkgver}/priv/couch_{ejson_compare,js}
}
