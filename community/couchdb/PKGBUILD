# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch and rebuild rebar on AArch64

pkgname=couchdb
pkgver=2.1.0
pkgrel=1
pkgdesc="A document-oriented database that can be queried and indexed in a MapReduce fashion using JSON"
arch=('i686' 'x86_64')
url="http://couchdb.apache.org"
license=('APACHE')
depends=('icu' 'js185' 'openssl' 'zlib')
makedepends=('erlang19-nox')
install=${pkgname}.install
backup=('etc/couchdb/local.ini'
        'etc/couchdb/vm.args')
validpgpkeys=('29E4F38113DF707D722A6EF91FE9AF73118F1A7C')
source=("http://www-eu.apache.org/dist/couchdb/source/${pkgver}/apache-couchdb-${pkgver}.tar.gz"{,.asc}
        "https://github.com/rebar/rebar/commit/f9b850c428dae6cf6673ff076d863d5b6c343569.patch"
        'couchdb.service'
        'couchdb.sysusers'
        'couchdb.tmpfiles'
        'datadirs.ini')
sha256sums=('eb1fbf746cd4bce2322389983e4d174c78e1520435578bff1507bb51c96a8825'
            'SKIP'
            'f9465e279b40adde6db8008c4c0366a700a04602bfa7f189c9ad6eac13754efa'
            'b658af33dcd5e7f90b42ba1b65b61905b818103f0a9fa85f15f3bfec216c7435'
            '3ed1ad2a37a068ce194b03fb72eb35285d60fa7faf2d2c2bb710703d229108a8'
            '0ce806cbc5e18e60b17be9fd2cdbd4c7f12cc84ca95b079efdede16ddb5f3efd'
            '937ca3498aab47b3f2226d027fa8a1a95de55cbb463373099e28cb9a6c7046ac')

prepare() {
  cd apache-couchdb-${pkgver}
  sed -i 's|$ROOTDIR/etc/vm.args|/etc/couchdb/vm.args|' rel/overlay/bin/couchdb

  if [[ $CARCH == aarch64 ]]; then
    cd src/rebar
    patch -p1 -i "$srcdir/f9b850c428dae6cf6673ff076d863d5b6c343569.patch"
    ./bootstrap
    cp rebar ../../bin
  fi
}

build() {
  cd apache-couchdb-${pkgver}
  ./configure
  make release
}

package() {
  cd apache-couchdb-${pkgver}
  install -dm755 "${pkgdir}"/usr/lib/
  install -dm755 "${pkgdir}"/etc/couchdb/

  cp -r rel/couchdb "${pkgdir}"/usr/lib/couchdb
  mv "${pkgdir}"/usr/lib/couchdb/etc/local.ini "${pkgdir}"/etc/couchdb/local.ini
  mv "${pkgdir}"/usr/lib/couchdb/etc/vm.args "${pkgdir}"/etc/couchdb/vm.args

  cd "${srcdir}"
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
  install -Dm644 datadirs.ini -t "${pkgdir}"/usr/lib/couchdb/etc/
}
