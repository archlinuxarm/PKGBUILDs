# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: grimsock <lord.grimsock at gmail dot com>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Henry Tang <henryykt@gmail.com>

pkgname=phantomjs
pkgver=1.9.1
pkgrel=2
pkgdesc="Headless WebKit with JavaScript API"
url="http://www.phantomjs.org/"
license=('BSD' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
depends=('gstreamer0.10-base' 'fontconfig' 'freetype2')
makedepends=('unzip' 'qtwebkit')
source=("http://phantomjs.googlecode.com/files/${pkgname}-${pkgver}-source.zip")
noextract=("${pkgname}-${pkgver}-source.zip")
md5sums=('93a0043d4ff8cd83e23e9d261c93830d')

build() {
  # workaround for https://code.google.com/p/libarchive/issues/detail?id=271
  # cd $srcdir/$pkgname-$pkgver
  unzip ${pkgname}-${pkgver}-source.zip

  cd $pkgname-$pkgver
  # workaround for http://code.google.com/p/phantomjs/issues/detail?id=635
  sed -i 's/QMAKE_LFLAGS+=-fuse-ld=gold/#QMAKE_LFLAGS+=-fuse-ld=gold/' src/qt/src/3rdparty/webkit/Source/common.pri

  ./build.sh --confirm --qt-config "-no-rpath"
}

package() {
  install -Dm755 "$srcdir/$pkgname-$pkgver/bin/phantomjs" "$pkgdir/usr/bin/phantomjs"

  mkdir -p "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/$pkgname-$pkgver/examples" "$pkgdir/usr/share/$pkgname"/

  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE.BSD" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.BSD"
  install -Dm644 "$srcdir/$pkgname-$pkgver/third-party.txt" "$pkgdir/usr/share/licenses/$pkgname/third-party.txt"
}

