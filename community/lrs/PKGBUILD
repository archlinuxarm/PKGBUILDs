# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix up makefile for 32-bit builds

pkgname=lrs
pkgver=071
_pkgver=${pkgver/./}
pkgrel=3
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$_pkgver.tar.gz"
        lrs-system-cflags.patch
        lrs-missing-include.patch
        32-bit-support.patch)
sha256sums=('d3ea5636bfde3011d43c835773fabe131d9251197b6cc666a52d8caa3e1c7816'
            '8e43f9f6c583c653e28b75907f27f616f45f36d6fc677a4aae6a7004d60424c0'
            '61da0543e92ab242df1d5adcb6bb842a3c489c011c858705278de1c6cf580649'
            '1981f63153f9fbf3730cea68ff68be0645cecbaa385aa63912e3ab22c978c59c')

prepare() {
  cd lrslib-${pkgver%.*}
  patch -p1 -i ../lrs-system-cflags.patch # Use system CFLAGS/LDFLAGS
  patch -p1 -i ../lrs-missing-include.patch # Install missing header
  [[ $CARCH != "aarch64" ]] && patch -p1 -i ../32-bit-support.patch || true
}

build() {
  cd lrslib-${pkgver%.*}
  make
}

package() {
  cd lrslib-${pkgver%.*}
  make DESTDIR="$pkgdir" prefix=/usr install
}
