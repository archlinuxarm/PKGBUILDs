# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from debian, export LDLIBS to explicitly link v5/v6 with libatomic

pkgname=cmus
pkgver=2.9.1
pkgrel=1
pkgdesc='Feature-rich ncurses-based music player'
arch=('x86_64')
url='https://cmus.github.io/'
license=('GPL')
depends=('libdiscid' 'ncurses')
makedepends=(
  'faad2'
  'ffmpeg'
  'flac'
  'jack'
  'libao'
  'libcdio-paranoia'
  'libmad'
  'libmodplug'
  'libmp4v2'
  'libmpcdec'
  'libpulse'
  'libsamplerate'
  'libvorbis'
  'opusfile>=0.12'
  'wavpack'
)
optdepends=('alsa-lib: for ALSA output plugin support'
            'libao: for AO output plugin support'
            'libpulse: for PulseAudio output plugin support'
            'faad2: for AAC input plugin support'
            'ffmpeg: for ffmpeg input plugin support'
            'flac: for flac input plugin support'
            'jack: for jack plugin support'
            'libmad: for mp3 input plugin support'
            'libmodplug: for modplug input plugin support'
            'libmp4v2: for mp4 input plugin support'
            'libmpcdec: for musepack input plugin support'
            'libsamplerate: for sampe rate converter support'
            'libvorbis: for vorbis input plugin support'
            'libcdio-paranoia: for cdio support'
            'opusfile: for opus input plugin support'
            'wavpack: for wavpack input plugin support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/cmus/cmus/archive/v${pkgver}.tar.gz"
         '13-atomic_ld.patch')
sha256sums=('6fb799cae60db9324f03922bbb2e322107fd386ab429c0271996985294e2ef44'
            '8526dc4649a90513b66ed46cf675e2bc5d0d553f0e3eb438f2dcd317858c64e3')

prepare() {
  if [[ $CARCH == "arm" || $CARCH == "armv6h" ]]; then
    cd "$pkgname-$pkgver"
    patch -p1 -i ../13-atomic_ld.patch
    export LDLIBS="-latomic"
  fi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 contrib/_cmus "$pkgdir"/usr/share/zsh/site-functions/_cmus
}
