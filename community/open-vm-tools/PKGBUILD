# $Id: PKGBUILD 3006 2009-09-25 12:18:31Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
pkgver=2009.09.18
_pkgsubver=193784
pkgrel=1
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools."
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('LGPL')
depends=('open-vm-tools-modules' 'libdnet' 'icu' 'procps' 'glib2')
makedepends=('chrpath' 'doxygen')
optdepends=('gtkmm' 'libnotify' 'libxtst')
options=('docs')
install=$pkgname.install
source=(http://switch.dl.sourceforge.net/$pkgname/$pkgname-$pkgver-${_pkgsubver}.tar.gz
	suspend-vm-default.patch
	resume-vm-default.patch)
md5sums=('90c79c24756f0b6cc3d42faed10f5c1b'
         '61c3a8d8428b557f53e7dfbfb9db809e'
         'ef77692f3d60730daeccbb6f53752850')

build() {
  cd "$srcdir/$pkgname-$pkgver-${_pkgsubver}"

  sed -i 's#-lproc-3.2.7#-lproc-3.2.8#' configure

  ./configure --prefix=/usr --disable-unity --without-kernel-modules
  make || return 1
  make install DESTDIR=$pkgdir || return 1

  cd $pkgdir && \
  patch -Np1 -i $srcdir/suspend-vm-default.patch etc/vmware-tools/suspend-vm-default && \
  patch -Np1 -i $srcdir/resume-vm-default.patch etc/vmware-tools/resume-vm-default || return 1

  ln -fs /usr/sbin/mount.vmhgfs $pkgdir/sbin/mount.vmhgfs || return 1

  install -D -m 755 $startdir/open-vm-tools.rc.d $pkgdir/etc/rc.d/open-vm-tools && \
  install -D -m 644 $startdir/open-vm-tools.conf.d $pkgdir/etc/conf.d/open-vm-tools && \
  install -D -m 644 $startdir/tools.conf $pkgdir/etc/vmware-tools/tools.conf && \
  install -D -m 644 $startdir/xautostart.conf $pkgdir/etc/vmware-tools/xautostart.conf && \
  install -D -m 644 $startdir/vmware-guestd $pkgdir/etc/pam.d/vmware-guestd && \
  rm -rf $pkgdir/usr/etc || return 1

  cd $pkgdir && find -type f -exec sh -c "file {} | grep ELF >/dev/null && echo {} && chrpath -d {}" \;
}
