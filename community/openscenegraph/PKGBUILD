# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Hans Janssen <janserv@gmail.com>
# Contributor: my64 <packages@obordes.com>
# Contributor: Colin Pitrat <colin.pitrat@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch for signed char

pkgname=openscenegraph
pkgver=3.4.1
pkgrel=1
pkgdesc="An Open Source, high performance real-time graphics toolkit"
arch=('i686' 'x86_64')
license=('custom:OSGPL')
url="http://www.openscenegraph.org"
depends=('giflib' 'jasper' 'librsvg' 'xine-lib' 'curl' 'pth')
makedepends=('cmake' 'libvncserver' 'qt5-base' 'ffmpeg' 'mesa')
optdepends=('libvncserver' 'gdal' 'openexr' 'poppler-glib' 'qt5-base' 'ffmpeg')
conflicts=('openthreads')
provides=('openthreads')
source=(https://github.com/openscenegraph/OpenSceneGraph/archive/OpenSceneGraph-$pkgver.tar.gz
	openscenegraph-ffmpeg3.patch
	https://github.com/openscenegraph/OpenSceneGraph/commit/01c712e85195803993acb1d7712796cf494943c6.patch)
sha256sums=('930eb46f05781a76883ec16c5f49cfb29a059421db131005d75bec4d78401fd5'
            'a65fc1ac0538fddb8d11ef1ab2322a782715f8d2b42e0bb01a191212a0612477'
            'b58fd51b1934a4bc00ece9de6737bff70b10fb0155879ac75fc0e8116b47077b')

prepare() {
  cd OpenSceneGraph-OpenSceneGraph-$pkgver
  # Fix build with ffmpeg 3.0 (Debian)
#  patch -p2 -i ../openscenegraph-ffmpeg3.patch
  patch -p1 -i ../01c712e85195803993acb1d7712796cf494943c6.patch
}

build() {
  cd OpenSceneGraph-OpenSceneGraph-$pkgver
#  CFLAGS="$CFLAGS -D__STDC_CONSTANT_MACROS"
#  CXXFLAGS="$CXXFLAGS -D__STDC_CONSTANT_MACROS"
  [ $NOEXTRACT -eq 1 ] || cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd OpenSceneGraph-OpenSceneGraph-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  [ -d "$pkgdir/usr/lib64" ] && mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib" || true
}
