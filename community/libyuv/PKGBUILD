# Maintainer: Bruno Pagani <archange@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable NEON, broken assembly

pkgname=libyuv
pkgver=r2266+eb6e7bb6
pkgrel=1.1
pkgdesc="Library for YUV scaling"
arch=(x86_64)
url="https://chromium.googlesource.com/libyuv/libyuv/"
license=(custom)
depends=(gcc-libs libjpeg)
makedepends=(cmake git)
# Latest commit in https://chromium.googlesource.com/libyuv/libyuv/+log/refs/heads/stable
_commit=eb6e7bb63738e29efd82ea3cf2a115238a89fa51
source=(git+${url}#commit=${_commit})
sha512sums=(SKIP)

pkgver() {
  cd ${pkgname}
  printf "r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  sed -i 's|yuvconvert ${JPEG_LIBRARY}|${ly_lib_shared} ${JPEG_LIBRARY}|g' ${pkgname}/CMakeLists.txt
}

build() {
  CPPFLAGS+=" -DLIBYUV_DISABLE_NEON"
  cmake -B build -S ${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
