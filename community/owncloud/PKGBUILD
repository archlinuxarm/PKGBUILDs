# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Based on owncloud-git PKGBUILD by Alexander Ovsyannikov

# Remove when bumped upstream.

pkgname=owncloud
pkgver=6.0.0.a
_pkgver=6.0.0a
pkgrel=2
pkgdesc="A cloud server to store your files centrally on a hardware controlled by you"
arch=('any')
url="http://owncloud.org/"
license=('GPL')
depends=('php-gd' 'php-intl')
optdepends=('php-apache: to use the Apache web server'
	    'php-sqlite: to use the SQLite database backend'
	    'php-pgsql: to use the PostgreSQL database backend'
	    'mariadb: to use the MySQL database backend'
	    'smbclient: to mount SAMBA shares'
	    'php-mcrypt'
	    'php-imagick: file preview'
	    'ffmpeg: file preview'
	    'libreoffice-common: file preview')
makedepends=()
options=('!strip')
source=("http://download.owncloud.org/community/$pkgname-$_pkgver.tar.bz2"
	'apache.example.conf')
backup=('etc/webapps/owncloud/apache.example.conf')
md5sums=('63c2913aa8382f695d7ade5ad11e51b2'
         'c1d49a3c0d8433bed6bffcd21ebb0064')

package() {
  # install license
  install -d $pkgdir/usr/share/licenses/$pkgname
  cp $srcdir/$pkgname/COPYING-* $pkgdir/usr/share/licenses/$pkgname

  # install project
  install -d $pkgdir/usr/share/webapps/
  cp -a $srcdir/$pkgname $pkgdir/usr/share/webapps/
  chown -R http:http $pkgdir/usr/share/webapps/owncloud/{apps,config}

  # install apache config file
  install -d  $pkgdir/etc/webapps/$pkgname
  install -m 644 $srcdir/apache.example.conf  $pkgdir/etc/webapps/$pkgname
}
