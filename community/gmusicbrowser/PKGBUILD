# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>

pkgname=gmusicbrowser
pkgver=1.0.1
pkgrel=2
pkgdesc="An open-source jukebox for large collections of mp3/ogg/flac files, written in perl."
arch=('i686' 'x86_64')
url="http://squentin.free.fr/gmusicbrowser/gmusicbrowser.html"
license=('GPL')
depends=('gtk2-perl')
optdepends=('alsa-utils: enables the ALSA backend'
            'flac123: flac support for the ALSA backend'
            'mpg321: mp3 support for the ALSA backend'
            'vorbis-tools: vorbis support for the ALSA backend'
            'mplayer: enables the mplayer backend'
            'perl-gstreamer: enables the gstreamer backend'
            'gstreamer0.10-base: vorbis supportfor the gstreamer backend' 
            'gstreamer0.10-good-plugins: flac support for the gstreamer backend'
            'gstreamer0.10-ugly-plugins: mp3 support for the gstreamer backend'
            'perl-gtk2-trayicon: tray icon support'
            'perl-gtk2-mozembed: web-based plugins support'
            'perl-gtk2-webkit: alternative web-based plugin suppor'
            'perl-locale-gettext: localization support'
            'perl-net-dbus: control gmusicbrowser through dbus and gnome hotkeys')
options=('!libtool')
source=(http://squentin.free.fr/gmusicbrowser/${pkgname}-${pkgver}.tar.gz
        gmusicbrowser.desktop)
md5sums=('f088bc27f4c1ddfb46e441b879560398' 'b6d641a139675344983e358bb54affa0')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -d ${pkgdir}/usr/share/gmusicbrowser/{pix/{tango,gnome-classic},plugins}
  install -Dm755 gmusicbrowser.pl ${pkgdir}/usr/bin/gmusicbrowser
  install -Dm644 gmusicbrowser.man ${pkgdir}/usr/share/man/man1/gmusicbrowser.1
  install -m755 iceserver.pl ${pkgdir}/usr/share/gmusicbrowser/iceserver.pl
  install -m644 *.pm layouts ${pkgdir}/usr/share/gmusicbrowser/
  install -m644 pix/*.png ${pkgdir}/usr/share/gmusicbrowser/pix/
  install -m644 pix/gnome-classic/*.png ${pkgdir}/usr/share/gmusicbrowser/pix/gnome-classic/
  install -m644 pix/tango/*.png ${pkgdir}/usr/share/gmusicbrowser/pix/tango/
  install -m644 plugins/*.pm ${pkgdir}/usr/share/gmusicbrowser/plugins/

  # Locales
  for locale in $(ls locale) ; do
    install -Dm644 locale/${locale}/LC_MESSAGES/gmusicbrowser.mo \
      ${pkgdir}/usr/share/locale/${locale}/LC_MESSAGES/gmusicbrowser.mo
  done

  # Desktop file
  install -Dm644 ${srcdir}/gmusicbrowser.desktop \
    ${pkgdir}/usr/share/applications/gmusicbrowser.desktop

  # Pixmap files
  install -Dm644 pix/trayicon.png \
    ${pkgdir}/usr/share/icons/hicolor/16x16/apps/gmusicbrowser.png
  install -Dm644 pix/gmusicbrowser32x32.png \
    ${pkgdir}/usr/share/icons/hicolor/32x32/apps/gmusicbrowser.png
  install -Dm644 pix/gmusicbrowser.png \
    ${pkgdir}/usr/share/icons/hicolor/48x48/apps/gmusicbrowser.png
  install -Dm644 pix/gmusicbrowser.png \
    ${pkgdir}/usr/share/pixmaps/gmusicbrowser.png
}

# vim:set ts=2 sw=2 et:
