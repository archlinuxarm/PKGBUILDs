# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to drop forced -msse flag

pkgbase=python-pytables
pkgname=('python2-pytables' 'python-pytables')
pkgver=3.1.0
pkgrel=1
arch=("i686" "x86_64")
pkgdesc="A package for managing hierarchical datasets and designed to efficiently and easily cope with extremely large amounts of data"
url="http://www.pytables.org"
license=("BSD")
makedepends=('lzo2' 'hdf5' 'python2-numexpr' 'cython2' 'python-numexpr' 'cython')
source=("http://pypi.python.org/packages/source/t/tables/tables-$pkgver.tar.gz"
        'arm.patch')
md5sums=('22e048ef20883269c4a2a3f2026c38ee'
         'ead28de59a99ef97aceec17cc1a9132d')

prepare() {
  cd "$srcdir/tables-$pkgver"
  patch -p1 -i ../arm.patch
}

build() {
  cd "$srcdir"
  cp -a tables-$pkgver tables-py2-$pkgver

  msg "Building Python2"
  cd "$srcdir"/tables-py2-$pkgver
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/tables-$pkgver
  python setup.py build
}

package_python2-pytables() {
  depends=('lzo2' 'hdf5' 'python2-numexpr' 'cython2')

  cd "$srcdir"/tables-py2-${pkgver}
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # see FS#36015
  mv "$pkgdir"/usr/bin/pt2to3{,-2.7}
  mv "$pkgdir"/usr/bin/ptdump{,-2.7}
  mv "$pkgdir"/usr/bin/ptrepack{,-2.7}
}

package_python-pytables() {
  depends=('lzo2' 'hdf5' 'python-numexpr' 'cython')

  cd "$srcdir"/tables-${pkgver}
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
