# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add LDFLAGS and CXXOPT to lower RAM usage
#  - make -j2 due to RAM constraints

highmem=1

pkgname=polymake
pkgver=4.6
pkgrel=5
_perlver=`perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]).".".($v->[2]);'`
pkgdesc='Open source software for research in polyhedral geometry'
arch=(x86_64)
url='https://polymake.org/'
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt
         perl-term-readline-gnu perl-term-readkey perl-json sympol perl=$_perlver)
makedepends=(ninja cddlib ant boost bliss libnsl jdk8-openjdk permlib nauty)
optdepends=('java-runtime: java module')
source=(https://polymake.org/lib/exe/fetch.php/download/polymake-${pkgver/.r/r}-minimal.tar.bz2
        perl-5.36.patch)
sha256sums=('423a44ddef11eaea8457ab15dd5d86ddb7a8bcc6c9bb11776f9e2958d7dcf9d0'
            '497d447f3371858382e29b3b1149d09560d7be4847c5709b34cc83c043077c21')

prepare() {
  cd polymake-$pkgver
  patch -Np1 -i ../perl-5.36.patch
}

build() {
  cd polymake-$pkgver
  ./configure \
    --prefix=/usr \
    --with-cdd=/usr \
    --with-lrs=/usr \
    --with-permlib=/usr \
    --with-sympol=/usr CXXOPT="-O1"
  LDFLAGS+=" -Wl,--no-keep-memory -Wl,--reduce-memory-overheads"
  make -j2
}

package() {
  cd polymake-$pkgver
  make DESTDIR="$pkgdir" install
}
