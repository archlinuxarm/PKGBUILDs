# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add LDFLAGS and CXXOPT to lower RAM usage
#  - make -j2 due to RAM constraints

highmem=1

pkgname=polymake
pkgver=3.2
pkgrel=1
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(mpfr normaliz perl-xml-writer perl-xml-libxml perl-xml-libxslt perl-term-readline-gnu perl-term-readkey)
makedepends=(ninja cddlib lrs ppl apache-ant boost singular bliss-graphs)
optdepends=('singular: ideal library' 'ppl: polytope library' 'java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-$pkgver-minimal.tar.bz2")
sha256sums=('fc714cec2e6902b982fe6dc420e3e5a6e871efedbc402070812679c2faa83b2b')

prepare() {
  cd polymake-$pkgver
# fix lrs headers location
  sed -e 's|lrslib.h|lrslib/lrslib.h|g' -i bundled/lrs/apps/polytope/src/lrs_interface.cc \
      -i bundled/sympol/external/sympol/sympol/raycomputationlrs.cpp
  sed -e 's|<lrslib.h>|<lrslib/lrslib.h>|g' -e 's|\$lrs_path\/include|$lrs_path/include/lrslib|g' -i bundled/lrs/support/configure.pl
}

build() {
  cd polymake-$pkgver
  LDFLAGS+=" -Wl,--no-keep-memory -Wl,--reduce-memory-overheads"
  ./configure --prefix=/usr CXXOPT="-O1"
  make -j2
}

package() {
  cd polymake-$pkgver
  make DESTDIR="$pkgdir" install
}
