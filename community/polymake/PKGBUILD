# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add LDFLAGS and CXXOPT to lower RAM usage
#  - make -j2 due to RAM constraints

highmem=1

pkgname=polymake
_pkgver=3.2
_patchver=2
pkgver=$_pkgver.r$_patchver
pkgrel=2
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(mpfr normaliz singular ppl perl-xml-writer perl-xml-libxml perl-xml-libxslt perl-term-readline-gnu perl-term-readkey)
makedepends=(ninja cddlib lrs ppl apache-ant boost singular bliss-graphs)
optdepends=('java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-${_pkgver}r${_patchver}-minimal.tar.bz2")
sha256sums=('415e101482d92dd73cdae0af8b43907e4195d3052491aea49e0563a04f015a6b')

prepare() {
  cd polymake-$_pkgver
# fix lrs headers location
  sed -e 's|lrslib.h|lrslib/lrslib.h|g' -i bundled/lrs/apps/polytope/src/lrs_interface.cc \
      -i bundled/sympol/external/sympol/sympol/raycomputationlrs.cpp
  sed -e 's|<lrslib.h>|<lrslib/lrslib.h>|g' -e 's|\$lrs_path\/include|$lrs_path/include/lrslib|g' -i bundled/lrs/support/configure.pl
}

build() {
  cd polymake-$_pkgver
  LDFLAGS+=" -Wl,--no-keep-memory -Wl,--reduce-memory-overheads"
  ./configure --prefix=/usr CXXOPT="-O1"
  make -j2
}

package() {
  cd polymake-$_pkgver
  make DESTDIR="$pkgdir" install
}
