# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add LDFLAGS and CXXOPT to lower RAM usage
#  - make -j2 due to RAM constraints

highmem=1

pkgname=polymake
pkgver=4.9
pkgrel=2
_perlver=`perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]).".".($v->[2]);'`
pkgdesc='Open source software for research in polyhedral geometry'
arch=(x86_64)
url='https://polymake.org/'
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt
         perl-term-readline-gnu perl-term-readkey perl-json sympol scip perl=$_perlver)
makedepends=(ninja cddlib ant boost bliss libnsl jdk-openjdk permlib nauty soplex)
optdepends=('java-runtime: java module')
source=(https://polymake.org/lib/exe/fetch.php/download/polymake-${pkgver/.r/r}-minimal.tar.bz2)
sha256sums=('04c90b826b3a26c5a63e18be319bff12a89ef0128bf8a68fb64df10594b259f3')

prepare() {
  sed -e 's|-lgmp|-lgmp -lmpfr|g' -i polymake-$pkgver/bundled/soplex/support/configure.pl # Fix soplex linking
}

build() {
  cd polymake-$pkgver
  CXXOPT="" \
  ./configure \
    --prefix=/usr \
    --without-native \
    --with-cdd=/usr \
    --with-lrs=/usr \
    --with-permlib=/usr \
    --with-soplex=/usr \
    --with-sympol=/usr CXXOPT="-O1"
  LDFLAGS+=" -Wl,--no-keep-memory -Wl,--reduce-memory-overheads"
  make -j2
}

package() {
  cd polymake-$pkgver
  make DESTDIR="$pkgdir" install

# Executables are installed in mode 555, which breaks strip
  find "$pkgdir" -exec chmod u+w {} +
}
