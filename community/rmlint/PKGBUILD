# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Blisfull <narthana.epa@gmail.com>
# Contributor: SahibBommelig <sahib@online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove line appending -march=native to CFLAGS in SConstruct

pkgbase=rmlint
pkgname=(rmlint rmlint-shredder)
pkgver=2.4.2
pkgrel=1
url="https://github.com/sahib/rmlint"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('scons' 'python-sphinx' 'gettext' 'sqlite' 'libutil-linux'
             'binutils' 'json-glib' 'libelf' 'python-gobject' 'python-cairo'
             'dconf' 'gtksourceview3' 'librsvg' 'xdg-utils')
source=("https://github.com/sahib/rmlint/archive/v$pkgver/$pkgbase-$pkgver.tar.gz")
sha256sums=('8e59bffc0389ba15902251014bc00080e25d259665a1d7e5313f219eabfacf65')

prepare() {
    cd "$srcdir"/$pkgbase-$pkgver
    sed 's/python4/python/' -i gui/SConscript
    sed -i '/march=native/d' SConstruct
}

build() {
    cd "$srcdir"/$pkgbase-$pkgver
    scons config
    scons -j4 DEBUG=1 --prefix="$pkgdir"/usr --actual-prefix=/usr
}

package_rmlint() {
    pkgdesc="Tool to remove duplicates and other lint, being much faster than fdupes"
    depends=('sqlite' 'libutil-linux' 'binutils' 'json-glib' 'libelf')

    cd "$srcdir"/$pkgbase-$pkgver
    scons DEBUG=1 --prefix="$pkgdir"/usr install --actual-prefix=/usr

    rm -rf "$pkgdir"/usr/share/{glib-2.0,icons,applications}
    rm -rf "$pkgdir"/usr/lib
}

package_rmlint-shredder() {
    pkgdesc="Graphical user interface for rmlint"
    depends=('rmlint' 'python-gobject' 'python-cairo' 'dconf' 'librsvg'
             'gtksourceview3' 'xdg-utils')
    install=rmlint-shredder.install

    cd "$srcdir"/$pkgbase-$pkgver
    scons DEBUG=1 --prefix="$pkgdir"/usr install --actual-prefix=/usr

    rm -rf "$pkgdir"/usr/{bin,share/locale,share/man}
    rm "$pkgdir"/usr/share/glib-2.0/schemas/gschemas.compiled
}
