# Maintainer: Blisfull <narthana.epa@gmail.com>
# Contributer: SahibBommelig <sahib@online.de>
# rmlint PKBUILD for ArchLinux

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove line appending -march=native to CFLAGS in SConstruct

pkgname=rmlint
pkgver=2.2.0
pkgrel=1.1
pkgdesc="Tool to remove duplicates and other lint, being much faster than fdupes"
arch=('i686' 'x86_64')
url="https://github.com/sahib/rmlint"
license=('GPL3')
depends=('sqlite' 'libutil-linux' 'binutils' 'json-glib' 'libelf')
makedepends=('scons' 'python-sphinx' 'gettext')
source=("https://github.com/sahib/rmlint/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('b684aa73ec80423e1cc5e9cee9ae466f0ed0c6a2e5a88482a07d3306465ee6f1')

prepare() {
    cd "$srcdir"/$pkgname-$pkgver
    sed -i '/march=native/d' SConstruct
}

build() {
    cd "$srcdir"/$pkgname-$pkgver
    scons config
    scons -j4 DEBUG=1 --prefix="$pkgdir"/usr --actual-prefix=/usr
}

package() {
    cd "$srcdir"/$pkgname-$pkgver
    scons DEBUG=1 --prefix="$pkgdir"/usr install --actual-prefix=/usr
}
