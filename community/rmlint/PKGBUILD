# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Blisfull <narthana.epa@gmail.com>
# Contributor: SahibBommelig <sahib@online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove line appending -march=native to CFLAGS in SConstruct

pkgname=rmlint
pkgver=2.2.1
pkgrel=1
pkgdesc="Tool to remove duplicates and other lint, being much faster than fdupes"
arch=('i686' 'x86_64')
url="https://github.com/sahib/rmlint"
license=('GPL3')
depends=('sqlite' 'libutil-linux' 'binutils' 'json-glib' 'libelf')
makedepends=('scons' 'python-sphinx' 'gettext')
source=("https://github.com/sahib/rmlint/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('28b4e2447c2ff303bc31e6d57b99c34662eda22e138daa985aa8d00726b4f886')

prepare() {
    cd "$srcdir"/$pkgname-$pkgver
    sed -i '/march=native/d' SConstruct
}

build() {
    cd "$srcdir"/$pkgname-$pkgver
    scons config
    scons -j4 DEBUG=1 --prefix="$pkgdir"/usr --actual-prefix=/usr
}

package() {
    cd "$srcdir"/$pkgname-$pkgver
    scons DEBUG=1 --prefix="$pkgdir"/usr install --actual-prefix=/usr
}
