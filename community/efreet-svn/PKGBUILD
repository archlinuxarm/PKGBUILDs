# $Id: PKGBUILD 3321 2009-10-02 11:39:21Z rvanharen $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=efreet-svn
pkgver=42759
pkgrel=1
pkgdesc="freedesktop.org specifications for e17"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('ecore-svn' 'gnome-menus' 'gnome-icon-theme')
# gnome-menus is needed for /etc/xdg/menus/applications.menu
# gnome-icon-theme is needed for e17 to use a backup icon theme
makedepends=('svn')
conflicts=('efreet')
provides=('efreet')
replaces=('efreet-cvs')
options=('!libtool')
source=()
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/efreet"
_svnmod="efreet"

build() {
   cd $srcdir

msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./autogen.sh --prefix=/usr
  make || return 1
  make DESTDIR=$pkgdir install || return 1

# install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING
      
  rm -r $srcdir/$_svnmod-build
}
