# $Id$
# Maintainer : Ray Rashif <schiv@archlinux.org>
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM

pkgname=qtractor
pkgver=0.6.0
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('i686' 'x86_64')
url="http://qtractor.sourceforge.net/"
license=('GPL')
depends=('qt4' 'suil' 'lilv' 'libmad' 'liblo>=0.28'
         'rubberband' 'shared-mime-info')
makedepends=('ladspa' 'dssi')
optdepends=('dssi-vst: win32 VST support')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        '04-q_atomic_ftbfs.patch')
md5sums=('95eeb8a1f6d08645d8f9f606f0c04f15'
         'da44b00b6bcf808a1b840959361ba19f')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../04-q_atomic_ftbfs.patch

  # do not look for /usr/lib64/lv2
  # prevent to load lv2 plugins twice
  sed -i '/__x86_64__/,/#endif/d' src/qtractorPlugin.cpp

  # force qt4
  export ac_qmake=/usr/bin/qmake-qt4 \
         ac_moc=/usr/bin/moc-qt4 \
         ac_uic=/usr/bin/uic-qt4 \
         ac_lupdate=/usr/bin/lupdate-qt4 \
         ac_lrelease=/usr/bin/lrelease-qt4 \
         QMAKE=qmake-qt4
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
