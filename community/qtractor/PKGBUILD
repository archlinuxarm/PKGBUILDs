# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=0.9.25
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(glibc gcc-libs hicolor-icon-theme libmad libxcb qt6-base zlib)
makedepends=(alsa-lib aubio cmake dssi jack ladspa liblo libogg libsamplerate
libsndfile libvorbis lilv lv2 qt6-tools rubberband suil vst3sdk)
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=(dssi-host ladspa-host lv2-host vst-host vst3-host)
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('057add530356bd81c4afc9e5527fbf2f6ec21b2f0455f0f8739efb82730ea834a5d2397683e3622071fa75dde31e9d0a1f36d9381618d4a2604100f1b8dd5aa4')
b2sums=('94e653724582ebb6be417574e4bc357b58fe79deca686745607dcdf75f14b813cfccdb1d976dc98f89a19c23450ff52ef6060417ce986882374ba36c8f724a7f')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -DCONFIG_SSE=OFF \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libaubio.so libjack.so liblilv-0.so liblo.so libogg.so
  librubberband.so libsamplerate.so libsndfile.so libvorbisenc.so
  libvorbisfile.so libvorbis.so )

  make DESTDIR="${pkgdir}" install -C build
  # docs
  install -vDm 644 "${pkgname}-${pkgver}/"{README,README.VST} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
