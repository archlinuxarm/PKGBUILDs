# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=0.9.30
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(glibc gcc-libs hicolor-icon-theme libmad libxcb qt6-base qt6-svg zlib)
makedepends=(alsa-lib aubio clap cmake dssi jack ladspa liblo libogg
libsamplerate libsndfile libvorbis lilv lv2 qt6-tools rubberband suil vst3sdk)
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=(clap-host dssi-host ladspa-host lv2-host vst-host vst3-host)
options=(debug)
source=(https://github.com/rncbc/$pkgname/archive/refs/tags/${pkgname}_${pkgver//./_}.tar.gz)
sha512sums=('2fd59282a0cd7c41924e80ed3302766c06f182f9a2106fb91d8b24e81a2d46e962294490be6ff2e4b263d626c04f3efdd57d635162152fdfd4638c946ab87562')
b2sums=('119c5deb7a804a9e88b3171b339830fec94b6ae691a47d0ea129791e88444dca5ff8f9b1dc17ca558b65c739c1a8638c353bdaacd1e9e9cd075b7a97bf27d1ca')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -DCONFIG_SSE=OFF \
        -Wno-dev \
        -B build \
        -S $pkgname-${pkgname}_${pkgver//./_}
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libaubio.so libjack.so liblilv-0.so liblo.so libogg.so
  librubberband.so libsamplerate.so libsndfile.so libvorbisenc.so
  libvorbisfile.so libvorbis.so )

  make DESTDIR="$pkgdir" install -C build
  # docs
  install -vDm 644 $pkgname-${pkgname}_${pkgver//./_}/{README,README.VST} -t "$pkgdir/usr/share/doc/$pkgname/"
}
