# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=0.9.20
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="https://qtractor.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'gcc-libs' 'hicolor-icon-theme' 'libmad' 'qt5-base'
'qt5-x11extras' 'zlib')
makedepends=('alsa-lib' 'aubio' 'cmake' 'dssi' 'jack' 'ladspa' 'liblo' 'libogg'
'libsamplerate' 'libsndfile' 'libvorbis' 'lilv' 'lv2' 'qt5-tools' 'rubberband'
'suil' 'vst3sdk')
optdepends=('new-session-manager: for session management')
provides=('dssi-host' 'ladspa-host' 'lv2-host' 'vst-host' 'vst3-host')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-0.9.20-qwindow.patch"
        '04-q_atomic_ftbfs.patch')
sha512sums=('663d43f0ca2a2ee8deb260c29c7dc39ac36d2a4f4f3b1b6e3074e66fe5bb60c37a93fde89c09ceedfd6848ba1519de8d38d613d68b2693356fbda97a5d1c2c00'
            '09a4eb8666fed7099ef5ba24b2069b8c0edb12ff26e9d8c8fbbae46350c4f7243ebac68de813a7b6986e9e0653bce6893f42955385049592e618ac049aa6967c'
            'fe0e4d6adcd0eb64c39a194f16c026e6505d5f3b0537b7724ed9060443340559cc12a57665aec3e2a4d4c71ea05b1e575508cae334a8ca3a16543b5fdf55b4bf')
b2sums=('7989e827bdfb72cf8402a87a2e219fc2767fa971ce8f958cd8fb5aa0536aa12d486fc854b2d3a407de9e2b0ac3d6737420163d39b194bcd1d08e831f26cfab0c'
        '7baf13deb5b37eb4389d98269fea65056e96a4a491a7336eff57439dce4ec99a83bf12347d928ed0fcf5cb138cfae7d792a6689bab32709a5991c08a651d75f0'
        'ab86331b8cdc583cbe740c23e2a47280205cd20e80f23de1d8e189d6f2ea9a651caad14188656e5f85357cb444f9a51544871b2603ad54748db555ab6a9f4124')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix the build by making sure QWindow is included:
  # https://github.com/rncbc/qtractor/issues/297
  patch -Np1 -i ../"${pkgname}-0.9.20-qwindow.patch"

  patch -p1 -i ../04-q_atomic_ftbfs.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_SSE=OFF \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libaubio.so' 'libjack.so' 'liblilv-0.so' 'liblo.so'
  'libogg.so' 'librubberband.so' 'libsamplerate.so' 'libsndfile.so'
  'libsuil-0.so' 'libvorbisenc.so' 'libvorbisfile.so' 'libvorbis.so' )
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  # docs
  install -vDm 644 {AUTHORS,README,README.VST,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
