# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM

pkgname=qtractor
pkgver=0.9.4
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.org/"
license=('GPL')
groups=('pro-audio')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'liblo' 'libmad' 'lilv'
'qt5-x11extras' 'rubberband' 'suil')
makedepends=('qt5-tools' 'ladspa' 'dssi')
optdepends=('dssi-vst: win32 VST support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '04-q_atomic_ftbfs.patch')
sha512sums=('697e8f0b02658823bad2f75557340250349779c3eb1ce0efd1c30895524f9807b030d44149d9d25fd92a92f0822441d3c3236953d909b6ecfc9a18fa3ec1fbab'
            'fe0e4d6adcd0eb64c39a194f16c026e6505d5f3b0537b7724ed9060443340559cc12a57665aec3e2a4d4c71ea05b1e575508cae334a8ca3a16543b5fdf55b4bf')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  patch -p1 -i ../04-q_atomic_ftbfs.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README,README.VST,TODO}
}
# vim:set ts=2 sw=2 et:
