# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM

pkgname=qtractor
pkgver=0.9.0
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.org/"
license=('GPL')
groups=('pro-audio')
depends=('qt5-x11extras' 'suil' 'lilv' 'libmad' 'liblo'
         'rubberband' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'ladspa' 'dssi')
optdepends=('dssi-vst: win32 VST support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '04-q_atomic_ftbfs.patch')
sha512sums=('56beb82b3c98a40ceaac1aa4db51497ea9200666621ec2647f0543727df8fb9fbf8626103c1dac1d95358e8e325ad8166e985ccc609db4e4172092a324acf926'
            'fe0e4d6adcd0eb64c39a194f16c026e6505d5f3b0537b7724ed9060443340559cc12a57665aec3e2a4d4c71ea05b1e575508cae334a8ca3a16543b5fdf55b4bf')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ../04-q_atomic_ftbfs.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,README,README.VST,TODO}
}
# vim:set ts=2 sw=2 et:
