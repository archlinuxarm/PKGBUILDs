# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=0.9.27
pkgrel=2
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(glibc gcc-libs hicolor-icon-theme libmad libxcb qt6-base qt6-svg zlib)
makedepends=(alsa-lib aubio cmake dssi jack ladspa liblo libogg libsamplerate
libsndfile libvorbis lilv lv2 qt6-tools rubberband suil vst3sdk)
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=(dssi-host ladspa-host lv2-host vst-host vst3-host)
# NOTE: debug package contains include files of vst3sdk
# options=(debug)
source=(https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('2bcd4c5676fda58c7cf9ad9a9dcc139eda11c69b1c608812ff912ff63da56fbd0ba6b76828b326038365c9d84519bc752c54572e8edbf573e6cd0d068a17faf6')
b2sums=('08b3a18d37eb700960e24741178079deb728f909f80fc00491abbe832c1d1966022ec6bd2c24469047b53b168a7e084a7d76da96e587afff78662f2f1c9b1d26')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -DCONFIG_SSE=OFF \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libaubio.so libjack.so liblilv-0.so liblo.so libogg.so
  librubberband.so libsamplerate.so libsndfile.so libvorbisenc.so
  libvorbisfile.so libvorbis.so )

  make DESTDIR="$pkgdir" install -C build
  # docs
  install -vDm 644 $pkgname-$pkgver/{README,README.VST} -t "$pkgdir/usr/share/doc/$pkgname/"
}
