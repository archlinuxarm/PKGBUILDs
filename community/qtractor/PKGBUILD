# $Id$
# Maintainer : Ray Rashif <schiv@archlinux.org>
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM

pkgname=qtractor
pkgver=0.8.1
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('i686' 'x86_64')
url="http://qtractor.org/"
license=('GPL')
depends=('qt5-x11extras' 'suil' 'lilv' 'libmad' 'liblo>=0.28'
         'rubberband' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'ladspa' 'dssi')
optdepends=('dssi-vst: win32 VST support')
source=("https://sourceforge.net/projects/qtractor/files/qtractor/$pkgver/qtractor-$pkgver.tar.gz"
        '04-q_atomic_ftbfs.patch')
md5sums=('89949777599c68208f120a6af93f8a12'
         'da44b00b6bcf808a1b840959361ba19f')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../04-q_atomic_ftbfs.patch

  # do not look for /usr/lib64/lv2
  # prevent to load lv2 plugins twice
  sed -i '/__x86_64__/,/#endif/d' src/qtractorPluginFactory.cpp

  # force qt4
  #export ac_qmake=/usr/bin/qmake-qt4 \
  #       ac_moc=/usr/bin/moc-qt4 \
  #       ac_uic=/usr/bin/uic-qt4 \
  #       ac_lupdate=/usr/bin/lupdate-qt4 \
  #       ac_lrelease=/usr/bin/lrelease-qt4 \
  #       QMAKE=qmake-qt4

  export ac_qmake=/usr/bin/qmake-qt5 \
         ac_moc=/usr/bin/moc-qt5 \
         ac_uic=/usr/bin/uic-qt5 \
         ac_lupdate=/usr/bin/lupdate-qt5 \
         ac_lrelease=/usr/bin/lrelease-qt5 \
         QMAKE=qmake-qt5

 # Fix build with GCC 6 (Fedora)
 #sed -i.ac_with_paths -e "s|^ac_with_paths=.*|ac_with_paths=|g" configure configure.ac
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
