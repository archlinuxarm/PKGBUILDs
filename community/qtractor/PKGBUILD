# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=0.9.23
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="https://qtractor.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'gcc-libs' 'hicolor-icon-theme' 'libmad' 'libxcb' 'qt6-base' 'zlib')
makedepends=('alsa-lib' 'aubio' 'cmake' 'dssi' 'jack' 'ladspa' 'liblo' 'libogg'
'libsamplerate' 'libsndfile' 'libvorbis' 'lilv' 'lv2' 'qt6-tools' 'rubberband'
'suil' 'vst3sdk')
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=('dssi-host' 'ladspa-host' 'lv2-host' 'vst-host' 'vst3-host')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('fe57cc82d072ed49569441bb9f25f5233c0d7a6e7f0958a84247c8896741f2e4b83067e69d8e7999ccd9e68002b0e6ba4dc084674286952c17d472ae9f507bbd')
b2sums=('6ccf22ccc21ad36aecff63a16de9e3bb2ae19ba6fd7475432c3cd1e37ce2468f837a68c160ceec0f9c7ec9931e1fc1614c3a84cd0dd34078d1e227868338c021')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -DCONFIG_SSE=OFF \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libaubio.so' 'libjack.so' 'liblilv-0.so' 'liblo.so'
  'libogg.so' 'librubberband.so' 'libsamplerate.so' 'libsndfile.so'
  'libsuil-0.so' 'libvorbisenc.so' 'libvorbisfile.so' 'libvorbis.so' )
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  # docs
  install -vDm 644 {AUTHORS,README,README.VST,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
