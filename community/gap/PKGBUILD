# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - rm -fr "$pkgdir"/usr/lib/gap/pkg/digraphs-*/bin/lib/ for aarch64

pkgbase=gap
pkgname=(gap gap-doc gap-packages)
pkgver=4.11.1
pkgrel=10
pkgdesc='Groups, Algorithms, Programming: a system for computational discrete algebra'
arch=(x86_64)
url='https://www.gap-system.org/'
license=(GPL)
_semigroupsver=4.0.1
_normalizinterfacever=1.3.2
source=(https://github.com/gap-system/gap/releases/download/v$pkgver/$pkgbase-$pkgver.tar.gz
        gap.sh
        https://github.com/semigroups/Semigroups/archive/v$_semigroupsver/Semigroups-$_semigroupsver.tar.gz
        https://github.com/gap-packages/NormalizInterface/releases/download/v$_normalizinterfacever/NormalizInterface-$_normalizinterfacever.tar.gz)
sha256sums=('6635c5da7d82755f8339486b9cac33766f58712f297e8234fba40818902ea304'
            '143fb8a79a52c007903cce13407850df309ef803a9b00398d05169355917de46'
            '09f3f59506e6b273d44954eff91b9af7ffb91263d70e43faf676bcdb51878974'
            '05f8ec10bd9975cc3f84249057b96e4865c284d9aaf5d94ac0c3e96377c3645e')
makedepends=(libxaw givaro mpfi cddlib boost libsemigroups c-xsc zeromq fplll wget chrpath fmt eigen normaliz)

prepare() {
  cd gap-$pkgver

# Use system libsemigroups
  sed -e 's|test "$with_external_libsemigroups" = yes|true|' -i pkg/semigroups-*/configure
# Use system normaliz
  sed -e 's|.\/build-normaliz.sh|true|' -i bin/BuildPackages.sh
# Fix https://bugs.archlinux.org/task/55174
  sed -e '/xgap/d' -i pkg/sonata-*/PackageInfo.g
  sed -e '/XGAP/d' -i pkg/cryst/PackageInfo.g

# Update semigroups to support libsemigroups 2
  rm -r pkg/semigroups-* 
  mv ../Semigroups-$_semigroupsver pkg/semigroups-$_semigroupsver
  cd pkg/semigroups-$_semigroupsver
  rm prerequisites.sh
  mkdir libsemigroups
  ./autogen.sh
  sed -e 's|test "$with_external_libsemigroups" = yes|true|' -i configure

# Update normalizinterface to support normaliz 3.9
  cd ..
  rm -r NormalizInterface*
  mv ../../NormalizInterface-$_normalizinterfacever .
  sed -e 's|\$PWD/NormalizInstallDir|/usr|' -i NormalizInterface-$_normalizinterfacever/configure

  cd CddInterface-*
  sed -e 's|/usr/include/cdd|/usr/include/cddlib|' -i configure.ac # Fix build with cddlib 0.94k
  rm configure
}

build() {
  cd gap-$pkgver
  ./configure --prefix=/usr --with-gmp=system

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
  make libgap.la

  # Install libgap so we can link packages against it
  mkdir -p tmp-install
  libtool --mode=install install libgap.la "$srcdir"/gap-$pkgver/tmp-install
  rm "$srcdir"/gap-$pkgver/tmp-install/libgap.la

  cd pkg
  export CFLAGS+=" -fcommon" # Fix anupq and cohomolo build with GCC 10
  export CXXFLAGS+=" -I/usr/include/cxsc" # Find c-xsc headers
  export CXXFLAGS+=" -I/usr/include/eigen3" # Fix build of semigroups
  export LDFLAGS+=" -L$srcdir/gap-$pkgver/tmp-install -lgap" # See https://trac.sagemath.org/ticket/27372
  export LD_LIBRARY_PATH="$srcdir"/gap-$pkgver/tmp-install
  ../bin/BuildPackages.sh
}

_standardpkgs=(GAPDoc-* primgrp-* SmallGrp-* transgrp atlasrep autpgrp-* alnuth-* crisp-* ctbllib-* FactInt-* fga irredsol-* laguna-*
               polenta-* polycyclic-* resclasses-* sophus-* tomlib-*)

package_gap() {
  depends=(gmp zlib)
  optdepends=('gap-packages: extra packages' 'gap-doc: documentation')
  conflicts=(libgap)
  replaces=(gap-data libgap gap-4.8 gap-4.8-data)
  cd gap-$pkgver

  install -Dm644 src/*.h -t "$pkgdir"/usr/include/gap
  install -Dm644 gen/config.h -t "$pkgdir"/usr/include/gap
  install -Dm644 src/hpc/*.h -t "$pkgdir"/usr/include/gap/hpc

  install -d "$pkgdir"/usr/lib
  libtool --mode=install install libgap.la "$pkgdir"/usr/lib

  mkdir -p "$pkgdir"/usr/{bin,lib/gap/pkg}
  cp -r grp lib "$pkgdir"/usr/lib/gap
  for _pkg in ${_standardpkgs[@]}; do
    cp -r pkg/$_pkg "$pkgdir"/usr/lib/gap/pkg
  done
  install -Dm755 gap -t "$pkgdir"/usr/lib/gap
  sed -e "s|$PWD|/usr/lib/gap|g" -e "s|-I/usr/lib|-I/usr/include|g" -i sysinfo.gap
  cp -ar sysinfo.gap* "$pkgdir"/usr/lib/gap
# Install launcher script
  install -Dm755 "$srcdir"/gap.sh "$pkgdir"/usr/bin/gap

  mkdir -p "$pkgdir"/usr/share
  ln -s /usr/lib/gap -t "$pkgdir"/usr/share # expected by sagemath

# Add provides for bundled packages
  for _f in $(ls "$pkgdir"/usr/lib/gap/pkg); do
    _pkg=$(basename $_f | cut -d- -f1 | tr '[:upper:]' '[:lower:]')
    _prov=gap-$_pkg
    if [[ $_f == *-* ]]; then
      _prov+="=$(basename $_f | cut -d- -f2)"
    fi
    provides+=($_prov)
  done
}

package_gap-doc() {
  depends=(gap)
  replaces=(gap-4.8-doc)
  pkgdesc="Documentation for GAP"
  cd gap-$pkgver

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r doc "$pkgdir"/usr/lib/gap
}

package_gap-packages() {
  depends=(gap)
  replaces=(gap-4.8-packages)
  optdepends=('libxaw: xgap package' 'c-xsc: float package' 'mpfi: float package'
              'libmpc: float package' 'fplll: float package' 'zeromq: ZeroMQ interface package' 'planarity: digraph package'
              'curl: curl interface package' 'libsemigroups: semigroups package' 'cddlib: CddLib interface package'
              'normaliz: NormalizInterface package')
  pkgdesc="Extra packages for GAP"
  cd gap-$pkgver

  mkdir -p "$pkgdir"/usr/{bin,lib/gap}
  cp -r pkg "$pkgdir"/usr/lib/gap

# fix xgap launch script
  sed -e "s|/build/gap/src/gap-$pkgver|/usr/lib/gap|g" -e 's|^GAP=.*|GAP=/usr/lib/gap/gap|g' \
    "$pkgdir"/usr/lib/gap/pkg/xgap-*/xgap.sh > "$pkgdir"/usr/bin/xgap
  chmod 755 "$pkgdir"/usr/bin/xgap
  rm "$pkgdir"/usr/lib/gap/pkg/xgap-*/xgap.sh*

# provided by main gap package
  for _pkg in ${_standardpkgs[@]}; do
    rm -r "$pkgdir"/usr/lib/gap/pkg/$_pkg
  done

# fix RPATH
  find "$pkgdir"/usr/lib/gap/pkg/ -name '*.so' | xargs chrpath -d

# remove bundled planarity
  rm -fr "$pkgdir"/usr/lib/gap/pkg/digraphs-*/bin/lib/

# remove leftover binaries and source files
  find "$pkgdir"/usr/lib/gap/pkg -name .libs -o -name '*.o' | xargs rm -fr
  find "$pkgdir"/usr/lib/gap/pkg -type d -name src | xargs rm -fr
  rm -r "$pkgdir"/usr/lib/gap/pkg/digraphs*/extern
  rm -r "$pkgdir"/usr/lib/gap/pkg/semigroups*/libsemigroups
  rm -r "$pkgdir"/usr/lib/gap/pkg/CaratInterface/carat*
  rm -r "$pkgdir"/usr/lib/gap/pkg/kbmag-1.5.9/standalone
  rm -r "$pkgdir"/usr/lib/gap/pkg/log

# Add provides for bundled packages
  for _f in $(ls "$pkgdir"/usr/lib/gap/pkg); do
    _pkg=$(basename $_f | cut -d- -f1 | tr '[:upper:]' '[:lower:]')
    _prov=gap-$_pkg
    if [[ $_f == *-* ]]; then
      _prov+="=$(basename $_f | cut -d- -f2)"
    fi
    provides+=($_prov)
  done
}
