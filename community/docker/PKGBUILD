# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer

# Add patch for ARM being a supported platform.

pkgname=docker
_truever=0.9.0
pkgver=${_truever%.*}
pkgrel=1
epoch=1
pkgdesc='Pack, ship and run any application as a lightweight container'
arch=('x86_64')
url='http://www.docker.io/'
license=('Apache')
depends=('bridge-utils' 'iproute2' 'device-mapper' 'sqlite' 'systemd')
makedepends=('git' 'go' 'btrfs-progs')
optdepends=('btrfs-progs: btrfs backend support'
            'lxc: lxc driver support')
# don't strip binaries! A sha1 is used to check binary consistency.
options=('!strip')
install=$pkgname.install
source=("git+https://github.com/dotcloud/docker.git#tag=v$_truever"
        'docker-arm.patch'
        'btrfs-arm.patch')
md5sums=('SKIP'
         '47cc1cdf900af66660bfa3402012ea71'
         '1ec4b4b42eb80f2692cee757324370b0')

# magic harcoded path
_magic=src/github.com/dotcloud

prepare() {
  mkdir -p "$_magic"
  ln -sfn "../../../docker" "$_magic/docker"
  cd "${srcdir}/docker"
  patch -p1 <"${srcdir}/docker-arm.patch"
  patch -p1 <"${srcdir}/btrfs-arm.patch"
  find . -name '*.go' | xargs sed --in-place= -e 's/\/\/ +build amd64/\/\/ +build arm/'
  find . -name '*.go' | xargs sed --in-place= -e 's/\/\/ +build !linux !amd64/\/\/ +build !linux !arm/'
  find . -name '*.go' | xargs sed --in-place= -e 's/\/\/ +build linux,amd64/\/\/ +build linux,arm/'
}

build() {
  cd "$_magic/docker"
  export GOPATH="$srcdir:$srcdir/$_magic/docker/vendor"
  ./hack/make.sh dynbinary
}

check() {
  cd "$_magic/docker"
  # Will be added upstream soon
  #./hack/make.sh dyntest
}

package() {
  cd "$_magic/docker"
  install -Dm755 "bundles/$_truever/dynbinary/docker-$_truever" "$pkgdir/usr/bin/docker"
  install -Dm755 "bundles/$_truever/dynbinary/dockerinit-$_truever" "$pkgdir/usr/lib/docker/dockerinit"
  # completion
  install -Dm644 "contrib/completion/bash/docker" "$pkgdir/usr/share/bash-completion/completions/docker"
  install -Dm644 "contrib/completion/zsh/_docker" "$pkgdir/usr/share/zsh/site-functions/_docker"
  # systemd
  install -Dm644 "contrib/init/systemd/docker.service" "$pkgdir/usr/lib/systemd/system/docker.service"
}

# vim:set ts=2 sw=2 et:
