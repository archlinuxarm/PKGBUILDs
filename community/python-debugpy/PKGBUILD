# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: mark.blakeney at bullet-systems dot net

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - comment out extra compile, x86 and doesn't appear to be necessary for ARM

_pipname=debugpy
pkgname=python-$_pipname
pkgver=1.6.1
pkgrel=1
pkgdesc='An implementation of the Debug Adapter Protocol for Python'
arch=(x86_64)
url='https://aka.ms/debugpy'
license=(EPL MIT)
depends=(python)
makedepends=(python-setuptools)
source=(https://github.com/microsoft/debugpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        fix-build.patch)
sha256sums=('1c8cea3de71ff3cce5887a8d459579d6b6c4d455b3dbb70e02c7f2e463bf869e'
            'a86e1e1a5a4d7a585c8185721863f563233b680c36fa35d65bfbff11abd519bc')

prepare() {
  cd $_pipname-$pkgver
# remove precompiled libs
  rm src/debugpy/_vendored/pydevd/pydevd_attach_to_process/*.{so,dll,dylib,exe,pdb}

  patch -p1 -i ../fix-build.patch
}

build() {
  cd $_pipname-$pkgver
  python setup.py build
# Compile attach libraries
  #cd build/lib*/debugpy/_vendored/pydevd/pydevd_attach_to_process
  #g++ ${CXXFLAGS} -m64 -shared -o attach_linux_amd64.so -fPIC -nostartfiles linux_and_mac/attach.cpp ${LDFLAGS}
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
