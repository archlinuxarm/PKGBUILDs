# $Id: PKGBUILD 90017 2013-05-06 19:36:41Z foutrelis $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable mesa, add rules for our architectures

pkgname=directfb
pkgver=1.7.1
pkgrel=2
pkgdesc="A thin library that provides hardware graphics acceleration, input device handling and abstraction, integrated windowing system on top of the Linux Framebuffer Device"
arch=('i686' 'x86_64')
url="http://www.directfb.org"
license=('LGPL')
depends=('gcc-libs' 'libjpeg' 'sdl' 'libpng' 'freetype2')
source=(http://www.directfb.org/downloads/Core/DirectFB-${pkgver%.*}/DirectFB-${pkgver}.tar.gz)
sha1sums=('ff3dc6885f2875586849d88ee9f87fca866259b0')

build() {
  cd DirectFB-${pkgver}

  if [[ $CARCH == 'armv7h' ]]; then
    DRIVERS="omap,pvr2d"
  elif [[ $CARCH == 'armv6h' ]]; then
    export EGL_CFLAGS="-I/opt/vc/include -I/opt/vc/include/interface/vcos/pthreads -I/opt/vc/include/interface/vmcs_host/linux"
    export EGL_LIBS="-lEGL -lGLESv2 -lbcm_host -lvcos -L/opt/vc/lib"
    DRIVERS="gles2 --enable-egl"
    sed -i 's/-Werror-implicit-function-declaration//g' configure
  elif [[ $CARCH == 'arm' ]]; then
    DRIVERS="pxa3xx"
  fi

  ./configure --prefix=/usr --sysconfdir=/etc --enable-static --disable-silent-rules \
    --enable-zlib --enable-x11 --enable-sdl --disable-vnc --disable-osx \
    --enable-video4linux2 --enable-fbdev --disable-mesa --with-gfxdrivers=$DRIVERS
  make
} 

package() {
  cd DirectFB-${pkgver}
  make DESTDIR="${pkgdir}" install
}
