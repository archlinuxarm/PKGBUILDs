# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define WITH_NEON and ARM_FP_ABI

_name=FreeRDP
pkgname=freerdp
epoch=1
pkgver=2.0.0_rc4
pkgrel=7
pkgdesc="Free implementation of the Remote Desktop Protocol (RDP)"
arch=('x86_64')
url="http://www.freerdp.com/"
license=('Apache')
depends=('dbus-glib' 'faac' 'faad2' 'ffmpeg' 'gst-plugins-base-libs' 'libcups'
'libgssglue' 'libxkbcommon' 'libxinerama' 'libxkbfile' 'libxrandr' 'mbedtls'
'pcsclite')
makedepends=('cmake' 'docbook-xsl' 'krb5' 'xmlto' 'xorgproto')
provides=('libwinpr-tools2.so' 'libfreerdp-client2.so' 'libfreerdp2.so'
'libwinpr2.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver/_/-}.tar.gz)
sha256sums=('3406f3bfab63f81c1533029a5bf73949ff60f22f6e155c5a08005b8b8afe6d49')

prepare() {
  mv -v "${_name}-${pkgver/_/-}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  mkdir -pv build
}

build() {
  cd "${pkgname}-${pkgver}/build"

  [[ $CARCH == "arm" ]] && ABI="soft"
  [[ $CARCH == "armv6h" || $CARCH == "armv7h" ]] && ABI="hard"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_NEON=OFF \
	-DARM_FP_ABI=${ABI} \
        -DWITH_MBEDTLS=ON \
        -DWITH_PULSE=ON \
        -DWITH_CUPS=ON \
        -DWITH_PCSC=ON \
        -DWITH_JPEG=ON \
        -DWITH_GSM=ON \
        -DWITH_LAME=ON \
        -DWITH_FAAD2=ON \
        -DWITH_FAAC=ON \
        -DWITH_SOXR=ON \
        -DWITH_SERVER=ON \
        -DWITH_CHANNELS=ON \
        -DWITH_CLIENT_CHANNELS=ON \
        -DWITH_SERVER_CHANNELS=ON \
        -DCHANNEL_URBDRC_CLIENT=ON \
        ..
  make VERBOSE=1
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
