# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define WITH_NEON and ARM_FP_ABI

pkgname=freerdp
epoch=1
pkgver=1.2.0_20170201
_commit=6001cb710dc67eb8811362b7bf383754257a902b
_pkgver=${pkgver/_/-}
_pkgver=${_pkgver/+/-}
pkgrel=3
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse' 'libxkbfile'
	 'libxrender' 'libxfixes' 'gst-plugins-base-libs')
makedepends=('krb5' 'cmake' 'damageproto' 'fixesproto' 'renderproto'
	     'xmlto' 'docbook-xsl' 'git')
provides=('libwinpr-tools2.so' 'libfreerdp-client2.so' 'libfreerdp2.so'
          'libwinpr2.so')
#source=($pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/FreeRDP/archive/${pkgver/_/-}.tar.gz)
source=("FreeRDP-${_pkgver}::git://github.com/FreeRDP/FreeRDP.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
  cd "$srcdir"/FreeRDP-${_pkgver}

  [[ $CARCH == "arm" ]] && ABI="soft"
  [[ $CARCH == "armv6h" || $CARCH == "armv7h" ]] && ABI="hard"

  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSE=ON \
	-DWITH_CUPS=ON \
	-DWITH_CHANNELS=ON \
	-DWITH_CLIENT_CHANNELS=ON \
	-DWITH_SERVER_CHANNELS=ON \
	-DWITH_WAYLAND=ON \
	-DWITH_NEON=OFF \
	-DARM_FP_ABI=${ABI} \
	.
  make
}

package() {
  cd "$srcdir"/FreeRDP-${_pkgver}
  make DESTDIR="${pkgdir}" install
}
