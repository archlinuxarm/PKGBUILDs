# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define WITH_NEON and ARM_FP_ABI

pkgname=freerdp
epoch=1
pkgver=2.0.0_rc4
#_commit=6001cb710dc67eb8811362b7bf383754257a902b
_pkgver=${pkgver/_/-}
_pkgver=${_pkgver/+/-}
pkgrel=1
pkgdesc="Free RDP client"
arch=('x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse' 'libxkbfile'
	 'libxrender' 'libxfixes' 'gst-plugins-base-libs')
makedepends=('krb5' 'cmake' 'damageproto' 'fixesproto' 'renderproto'
	     'xmlto' 'docbook-xsl' 'git')
provides=('libwinpr-tools2.so' 'libfreerdp-client2.so' 'libfreerdp2.so'
          'libwinpr2.so')
#source=("FreeRDP-${_pkgver}::git://github.com/FreeRDP/FreeRDP.git#commit=${_commit}")
source=($pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/FreeRDP/archive/${pkgver/_/-}.tar.gz
	fix_nla-8cd156a-backport.patch)
sha256sums=('3406f3bfab63f81c1533029a5bf73949ff60f22f6e155c5a08005b8b8afe6d49'
            '2d449e0c1adee1ef131501e1613fc4c59fd122ca866d09f52eeff61236de81e4')

prepare() {
 cd "$srcdir"/FreeRDP-${_pkgver}
# patch -Np1 -i "${srcdir}/fix_nla-8cd156a-backport.patch"
}

build() {
  cd "$srcdir"/FreeRDP-${_pkgver}

  [[ $CARCH == "arm" ]] && ABI="soft"
  [[ $CARCH == "armv6h" || $CARCH == "armv7h" ]] && ABI="hard"

  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSE=ON \
	-DWITH_CUPS=ON \
	-DWITH_CHANNELS=ON \
	-DWITH_CLIENT_CHANNELS=ON \
	-DWITH_SERVER_CHANNELS=ON \
	-DWITH_WAYLAND=ON \
	-DWITH_NEON=OFF \
	-DARM_FP_ABI=${ABI} \
	.
  make
}

package() {
  cd "$srcdir"/FreeRDP-${_pkgver}
  make DESTDIR="${pkgdir}" install
}
