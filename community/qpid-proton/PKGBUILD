# Maintainer: Nicola Squartini <tensor5@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO

pkgname=qpid-proton
pkgver=0.37.0
pkgrel=3
pkgdesc='High-performance, lightweight messaging library'
arch=('x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
depends=('libjsoncpp.so' 'python')
makedepends=('cmake' 'doxygen' 'go' 'python-setuptools' 'python-sphinx' 'python-wheel' 'swig')
options=(!emptydirs)
source=("https://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz"
        qpid-proton-0.37.0-openssl3.patch::https://github.com/apache/qpid-proton/commit/5aaa655cb1023e9fb48e59f64e3d0c1307ea54f2.patch)
sha512sums=('84d67aab0e81f371fbcee3db89a8a585794966e17ffcd2e2e9418cb587efc0bc4e635c6ce251ebefd716817632ea5b049b759678cebbad64a0d0c07285bd5962'
            '6d891975d88a76583b5f101ce6edc15736f2f44121f15c822b4ba4816d27fb3e6e0f54e80c7ddf466b9cc3e720fb3b0ee9e74c9de3b44b564bf7753462ef8d53')

build() {
    cd ${pkgname}-${pkgver}
    patch -Np1 -i ../qpid-proton-0.37.0-openssl3.patch

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        -DENABLE_LINKTIME_OPTIMIZATION=OFF \
        -DENABLE_TOX_TEST=OFF \
        -DBUILD_TLS=ON \
        ..
    make
}

check() {
    cd ${pkgname}-${pkgver}/build

    make test || true
}

package() {
    cd ${pkgname}-${pkgver}/build

    make DESTDIR="${pkgdir}" install
}
