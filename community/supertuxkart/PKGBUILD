# $Id: PKGBUILD 1768 2009-08-31 06:46:44Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>

pkgname=supertuxkart
pkgver=0.6.2
pkgrel=1
pkgdesc="A new and improved version of TuxKart, a kart racing game featuring Tux and his friends"
arch=('i686' 'x86_64')
url="http://supertuxkart.sourceforge.net/"
license=('GPL2')
depends=('sdl>=1.2' 'libvorbis' 'libmikmod' 'freealut' 'libgl' 'freeglut')
makedepends=('plib>=1.8.4')
source=(http://downloads.sourceforge.net/project/$pkgname/SuperTuxKart/$pkgver/$pkgname-$pkgver-src.tar.bz2)
md5sums=('bfee9844acc2d4e168d903f6aae6d591')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --datadir=/usr/share
  
  # those configure flags really mean nothing...
  sed -i "s#/usr/local#/usr#" src/file_manager.cpp
  sed -i "s#/games##" $(grep -Rl "/games" *)
  
  make || return 1
  make DESTDIR=${pkgdir} install

  # fix executable location...
  install -dm755 ${pkgdir}/usr/bin
  mv ${pkgdir}/usr/supertuxkart ${pkgdir}/usr/bin/supertuxkart
  sed -i "s#usr/supertuxkart#usr/bin/supertuxkart#" \
    ${pkgdir}/usr/share/applications/supertuxkart.desktop
}
