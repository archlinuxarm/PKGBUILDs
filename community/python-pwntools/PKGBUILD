# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ding Xiao <tinocodfcdsa10@mails.tsinghua.edu.cn>
# Contributor: Firmy <firmianay@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepend on pandoc

pkgname=python-pwntools
_pyname=pwntools
pkgver=4.7.1
pkgrel=1
pkgdesc='CTF framework and exploit development library'
url='https://github.com/Gallopsled/pwntools'
arch=('any')
license=('MIT' 'GPL2' 'BSD')
depends=('python'
         'python-mako'
         'python-pyelftools'
         'python-capstone'
         'ropgadget'
         'python-pyserial'
         'python-requests'
         'python-pip'
         'python-pygments'
         'python-pysocks'
         'python-dateutil'
         'python-packaging'
         'python-psutil'
         'python-intervaltree'
         'python-sortedcontainers'
         'python-unicorn'
         'python-paramiko'
         'python-colored-traceback'
         'python-rpyc'
         'ropgadget')
makedepends=('python-setuptools')
source=(https://github.com/Gallopsled/pwntools/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        pwn.conf)
sha256sums=('f16f73068b62fae0ffc0e06dcd1b2bcb037827204e6d6a50b5f811052a1df007'
            '50916e4e45d026422cd6bfe8de50e7246c61b0b1e0007571e7c04a994282fe89')
b2sums=('5f77acc8188fe4042d96a438308ad1e7c22e2a8636b66320d18394833add7d7baef4fc8e6d925c302eac91d331da6aa7f89d91b17864f13db141ad378c0d36df'
        'cac0a12eea8abd3d2ee21632e7f2281e7e35c2d7839cfbcba83ae5d0e8f427e9cc2462a6d17d187252b99d9dbbb14393b9d5a63996c42043f553e9b66bfe9e07')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd "${_pyname}-${pkgver}"
  PYTHONPATH=. PWNLIB_NOTERM=true python -c 'import pwn'
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build --only-use-pwn-command
  install -Dm 644 "${srcdir}/pwn.conf" -t "${pkgdir}/etc"
  install -Dm 644 LICENSE-pwntools.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/*.{txt,md}

  install -Dm 644 extra/bash_completion.d/pwn -t  "${pkgdir}/usr/share/bash-completion/completions/"
  install -Dm 644 extra/zsh_completion/_pwn -t "${pkgdir}/usr/share/zsh/site-functions/"
}

# vim:set ts=2 sw=2 et:
