# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Peter Simons <simons@cryp.to>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v7/v8 with -fPIC

pkgname=shhmsg
pkgver=1.4.2
pkgrel=2.1
pkgdesc="Library for displaying messages"
arch=('i686' 'x86_64')
url="http://shh.thathost.com/pub-unix"
license=('PerlArtistic')
options=('staticlibs')
source=($url/files/$pkgname-$pkgver.tar.gz)
md5sums=('30cab93040d2892539847437b7246b8b')

build() {
  cd $pkgname-$pkgver

  [[ $CARCH == "armv7h" || $CARCH == "aarch64" ]] && CFLAGS+=" -fPIC"
  make OPTIM="$CFLAGS" dep
  make OPTIM="$CFLAGS"
}

package() {
  cd $pkgname-$pkgver

  make OPTIM="$CFLAGS" INSTBASEDIR="$pkgdir"/usr/ install

  for i in ChangeLog CREDITS INSTALL README; do
    install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
  done
}
