# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch and libgcc hack to fix ARM FTBFS

pkgname=tuxcmd
pkgver=0.6.70
pkgrel=5
pkgdesc='A GTK2 file manager with two panels'
arch=('i686' 'x86_64')
url='http://tuxcmd.sourceforge.net/'
license=('GPL')
depends=('gtk2' 'hicolor-icon-theme')
makedepends=('fpc' 'desktop-file-utils')
optdepends=('gnome-vfs: support for gvfs'
            'tuxcmd-modules: modules for tuxcmd')
options=('!docs' '!emptydirs')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-dev-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'tuxcmd-arm-gcc_s.so-linking.patch')
md5sums=('52e3bfa7c4118539b4c4b83e335290d1'
         'abee41cc4c60f19655e81a27f1a60d4e')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../tuxcmd-arm-gcc_s.so-linking.patch
  ln -s /usr/lib/libgcc_s.so.1 libgcc_s.so
}

build() {
  cd ${pkgname}-${pkgver}

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir}/usr install

  # remove license and README file
  rm -rf ${pkgdir}/usr/share/doc/
}
