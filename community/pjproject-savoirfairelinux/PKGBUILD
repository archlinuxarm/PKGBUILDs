# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora to fix ARM build
#  - autoconf for patch

_pkgname=pjproject
pkgname=${_pkgname}-savoirfairelinux
pkgver=2.6
pkgrel=6
pkgdesc="Open source SIP stack and media stack, built with patches from SavoirFaire Linux (mostly GnuTLS support)"
arch=('i686' 'x86_64')
url="http://www.pjsip.org/"
license=('GPL')
depends=('util-linux' 'gnutls' 'portaudio' 'speex' 'alsa-lib' 'libsamplerate' 'ffmpeg' 'libsrtp' 'opus')
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("http://www.pjsip.org/release/${pkgver}/${_pkgname}-${pkgver}.tar.bz2"
        endianness.patch
        gnutls.patch
        notestsapps.patch
        fix_base64.patch
        ipv6.patch
        ice_config.patch
        multiple_listeners.patch
        pj_ice_sess.patch
        fix_turn_fallback.patch
        fix_ioqueue_ipv6_sendto.patch
        add_dtls_transport.patch
        rfc6062.patch
        pjproject-sse2.patch)
sha256sums=('2f5a1da1c174d845871c758bd80fbb580fca7799d3cfaa0d3c4e082b5161c7b4'
            '294d9fba18a8c903979de2c9b531a3ca32a28f90658bf0613a32ebfa7d5e3a69'
            '8dedb243a390af62741d4e3c0ea6b7f970cd6d67934f03d7ef8b69c745a6523b'
            'f88f3e73d2f62ae60d93e84e08f98da7d5febe93f1f390286cafa106178c4f27'
            '25c808206aa5028f29f66ea5364b93be94d0d5feac7d97165cd4ba3493aae6ec'
            'fdf64f3260aecbbc5433ae784e12dde462f1a15361f14c5cd0f7be0a3d13f802'
            '12fa1dd99fda01e49df862e02f3ae64f1d31290400236bd50fa930fd20f3b799'
            '7aabc43556456085ca1bb9e17ef11ae5f4701dd392028335a65c06fd2bc1f6f8'
            '62ae6bb0bca51196e82984d6b0851a87421307ab0631d92e32966d2a656e7349'
            '5289671ee86618d665556e5591ef5409354611a6cc0512bfe4100bc6b94fea67'
            '15524714c2bb48612b6504864b279fc56af1d756a197a8afc8141556889cfb70'
            '5b901de12d49d79aba8777f3c7b59d703603be5ed2e1be6696f5d01be584e739'
            '21b1cf20d343e283498ace80ede97ce00062697a26c71ece09cea38a0278ad24'
            '8413201293d22b11d4294c2741c53f2db875a86434cbc0a8bacf2ad71f1adc7a')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  for patch in endianness.patch gnutls.patch notestsapps.patch fix_base64.patch \
               ipv6.patch ice_config.patch multiple_listeners.patch \
               pj_ice_sess.patch fix_turn_fallback.patch \
               fix_ioqueue_ipv6_sendto.patch add_dtls_transport.patch \
               rfc6062.patch pjproject-sse2.patch
  do
    msg2 "Applying patch $patch"
    patch -p1 < ../"$patch"
  done
  autoconf aconfigure.ac > aconfigure
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -DNDEBUG -fPIC -DPJSIP_MAX_PKT_LEN=8000"
  export CFLAGS="${CXXFLAGS}"
  ./configure \
      --prefix=/usr \
      --enable-shared \
      --enable-ssl=gnutls \
      --with-external-speex \
      --with-external-srtp \
      --with-external-pa \
      --with-external-gsm \
      --enable-ext-sound \
      --disable-oss \
      --disable-opencore-amr \
      --disable-v4l2 \
      --disable-video \
      --disable-sound
  make dep
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
