# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: DonVla <donvla@users.sourceforge.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with neon (-DNoNeonPlease=1)

pkgname=zynaddsubfx
pkgver=3.0.3
pkgrel=1
pkgdesc="Opensource software synthesizer capable of making a countless number of instruments."
arch=('x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL2')
depends=('fltk' 'portaudio' 'fftw' 'lash' 'mxml' 'libxpm' 'liblo')
makedepends=('asciidoc' 'cmake' 'doxygen' 'gnuplot' 'dblatex' 'imagemagick' 'ghostscript' 'texlive-pstricks')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('e60e22656388d444114959859c12014ec79d7ba555b0d217ad98ad06e273916eea0eee521bb9dbfdef5be34205280d355b44a8f15760eeff06b16410d560b5d7')

build() {
  cd "${pkgname}-${pkgver}"

  [ -d build ] || mkdir build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDefaultOutput=jack \
    -DDefaultInput=jack \
    -DGuiModule=fltk \
    -DNoNeonPlease=1 \
    ..
  make

  # build external programs
  cd ../ExternalPrograms/Spliter && make
  cd ../Controller && make

  # build documentation
  cd ../../doc
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}/" install

  # install external programs
  install -Dm755 ../ExternalPrograms/Spliter/spliter "${pkgdir}/usr/bin/spliter"
  install -Dm755 ../ExternalPrograms/Controller/controller "${pkgdir}/usr/bin/controller"
  install -Dm644 ../ExternalPrograms/Spliter/readme.txt "${pkgdir}/usr/share/doc/${pkgname}/SPLITER.txt"

  # banks and examples
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -a ../instruments/banks "${pkgdir}/usr/share/${pkgname}"
  cp -a ../instruments/examples "${pkgdir}/usr/share/${pkgname}"

  # icons
  install -Dm644 "../${pkgname}.ico" "${pkgdir}/usr/share/pixmaps/${pkgname}.ico"
  install -Dm644 "../${pkgname}.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"

  # man page
  install -Dm644 "../doc/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"

  # documentation
  install -Dm644 "../doc/${pkgname}.html" "${pkgdir}/usr/share/doc/${pkgname}/${pkgname}.html"
  install -Dm644 "../doc/${pkgname}.pdf" "${pkgdir}/usr/share/doc/${pkgname}/${pkgname}.pdf"
  cp -a ../doc/images "${pkgdir}/usr/share/doc/${pkgname}/"
}
