# $Id: $
# Maintainer: Biru Ionut <ionut@archlinux.ro>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=zynaddsubfx
pkgver=2.4.0
pkgrel=2
pkgdesc="An opensource software synthesizer capable of making a countless number of instruments"
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL')
depends=('fltk' 'fftw' 'jack-audio-connection-kit')
makedepends=('mxml')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/ZynAddSubFX-${pkgver}.tar.bz2 \
        zynaddsubfx.desktop Makefile.inc.patch)
md5sums=('b60b3c8a61c91e1aca54866d23c69f90'
         '892bf54cfc691ed07d66e4e061b5a2d0'
         '654fc59abdfeac7fd39673fa87093fe5')

build() {
  cd ${srcdir}/ZynAddSubFX-${pkgver}/src

  patch -p0 < ${srcdir}/Makefile.inc.patch || return 1

  make -j1 || return 1
  install -v -D -m 0755 zynaddsubfx $pkgdir/usr/bin/zynaddsubfx || return 1

  cd ${srcdir}/ZynAddSubFX-${pkgver}/ExternalPrograms/Spliter
  make -j1 || return 1
  install -v -m 0755 spliter ${pkgdir}/usr/bin/spliter || return 1
  install -v -D -m 0644 readme.txt ${pkgdir}/usr/share/${pkgname}/spliter-readme.txt || return 1

  cd ${srcdir}/ZynAddSubFX-${pkgver}/ExternalPrograms/Controller
  make -j1 || return 1
  install -v -m 0755 controller ${pkgdir}/usr/bin/controller || return 1

  cd ${srcdir}/ZynAddSubFX-${pkgver}
  cp -r banks ${pkgdir}/usr/share/${pkgname}/
  cp -r examples ${pkgdir}/usr/share/${pkgname}/
  install -v -m 0644 *.txt ${pkgdir}/usr/share/${pkgname}/ || return 1

  install -v -D -m 0644 src/zynaddsubfx_icon.svg ${pkgdir}/usr/share/pixmaps/zynaddsubfx.svg || return 1
  install -v -D -m 0644 ${srcdir}/zynaddsubfx.desktop ${pkgdir}/usr/share/applications/zynaddsubfx.desktop || return 1
}
