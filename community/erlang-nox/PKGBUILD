# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>
# Contributor: Ricardo Catalinas Jiménez <jimenezrick@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-pgo to fix ARM FTBFS

pkgname=erlang-nox
pkgver=21.1.3
_docver=21.1
pkgrel=1
pkgdesc='General-purpose concurrent functional programming language (headless version)'
arch=(x86_64)
url='https://www.erlang.org/'
license=(Apache)
depends=(ncurses openssl)
makedepends=(fop git java-environment libxslt lksctp-tools unixodbc)
conflicts=(erlang)
optdepends=('erlang-unixodbc: database support'
            'java-environment: for Java support'
            'lksctp-tools: for SCTP support')
options=(staticlibs)
source=("git+https://github.com/erlang/otp.git#tag=OTP-$pkgver"
        "http://www.erlang.org/download/otp_doc_man_$_docver.tar.gz"
        epmd.conf
        epmd.service
        epmd.socket)
sha256sums=('SKIP'
            '021e47b5036eaa4671b6d87a910403b775c967bfcb79b56a87f2183ddc5a5df5'
            '78ce5e67b21758c767d727e56b20502f75dc4385ff9b6c6db312d8e8506f2df2'
            'b121ec9053fb37abca5f910a81c526f93ec30fe13b574a12209223b346886a9e'
            '998a759e4cea4527f9d9b241bf9f32527d7378d63ea40afa38443c6c3ceaea34')

prepare() {
  cd otp && ./otp_build autoconf
}

build() {
  cd otp
  ./configure \
    --prefix=/usr \
    --enable-smp-support \
    --with-odbc \
    --enable-builtin-zlib \
    --disable-pgo
  make
}

package() {
  make -C otp DESTDIR="$pkgdir" install

  # Documentation
  install -d "$pkgdir/usr/share/doc/erlang"
  install -m0644 "$srcdir/otp/README.md" \
    "$srcdir"/COPYRIGHT \
    "$pkgdir/usr/share/doc/erlang"

  # Compressed man pages
  for page in "$srcdir/man/man?/*"; do gzip $page; done
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang/"

  # License
  install -Dm0644 "$srcdir/otp/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  # Remove files that are packaged as erlang-unixodbc
  rm -rf "$pkgdir/usr/lib/erlang/"{lib/odbc*,man/man3/odbc.3.gz}

  # epmd service, socket and conf
  cd "$srcdir"
  install -Dm644 epmd.service "$pkgdir/usr/lib/systemd/system/epmd.service"
  install -Dm644 epmd.socket "$pkgdir/usr/lib/systemd/system/epmd.socket"
  install -Dm644 epmd.conf "$pkgdir/etc/conf.d/epmd"
}

# getver: raw.githubusercontent.com/erlang/otp/maint/OTP_VERSION
# vim: ts=2 sw=2 et
