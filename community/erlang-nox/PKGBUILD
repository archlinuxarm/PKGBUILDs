# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>
# Contributor: Ricardo Catalinas Jiménez <jimenezrick@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-pgo to fix ARM FTBFS

pkgname=erlang-nox
pkgver=24.1.0
pkgrel=1
_docver=24.1
pkgdesc='General-purpose concurrent functional programming language (headless version)'
arch=(x86_64)
url='https://erlang.org/'
license=(Apache)
depends=(ncurses openssl)
makedepends=(fop git java-environment libxslt lksctp-tools unixodbc)
conflicts=(erlang)
optdepends=('erlang-unixodbc: database support'
            'java-environment: for Java support'
            'lksctp-tools: for SCTP support')
options=(staticlibs)
source=(epmd.conf
        epmd.service
        epmd.socket
        "https://www.erlang.org/download/otp_doc_man_$_docver.tar.gz"
        "git+https://github.com/erlang/otp#commit=1b0a9a039a1d078c8e4f25f3eaa765259b256ace") # tag: OTP-24.1
b2sums=('1675ac9bf948ab19e8b63077d870ccf356fcdbce14de2777f00b3488aa1ce34a5e0a5cdc0428707f744dee5940b12653a44e0ded0554de95ebb31bce4676ff87'
        '1f765318bcd322ce697a48c064ccdb1bd107b4a84e164a87f62678391cb82a9ab17a397d679cf266e037a5a2d91772b85f7a97f9cc5478c0f18118743b30045f'
        'c37706f5f6e2d49100104fb442ebf15edd6ee0a1e8038d0364794cf34fd3f02a61b9ad2706228b3327b85b0cadcd11aac6087e7c98408eae2d307a09191b6553'
        '092c7870914e108e3fe06b68be6c54cff508cd938b5b510ca9a7c946b786e5725ffa1f2c87282a36480a9c1c7d7c899e8118546102a98136f813309a7f0bd334'
        'SKIP')

build() {
  cd otp
  ./otp_build autoconf
  ./configure \
    --enable-builtin-zlib \
    --enable-smp-support \
    --prefix=/usr \
    --with-odbc \
    --disable-pgo
  make
}

package() {
  make -C otp DESTDIR="$pkgdir" install

  # services and configuration
  install -Dm644 epmd.service "$pkgdir/usr/lib/systemd/system/epmd.service"
  install -Dm644 epmd.socket "$pkgdir/usr/lib/systemd/system/epmd.socket"
  install -Dm644 epmd.conf "$pkgdir/etc/conf.d/epmd"

  # readme and licenses
  install -Dm644 otp/README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 otp/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 otp/CONTRIBUTING.md "$pkgdir/usr/share/doc/$pkgname/CONTRIBUTING.md"
  install -Dm644 otp/AUTHORS "$pkgdir/usr/share/doc/$pkgname/AUTHORS"

  # man pages
  cp -r man "$pkgdir/usr/lib/erlang/"

  # remove files that are included in the erlang-unixodbc package
  rm -rf "$pkgdir/usr/lib/erlang/"{lib/odbc*,man/man3/odbc.3}
}

# getver: raw.githubusercontent.com/erlang/otp/maint/OTP_VERSION
