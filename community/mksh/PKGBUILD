# $Id: PKGBUILD 85891 2013-03-08 15:20:48Z ttoepper $
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Daniel Hommel <dhommel@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - commented test.sh - breaks in chroot

pkgname=mksh
pkgver=R46
pkgrel=3
pkgdesc='The MirBSD Korn Shell - an enhanced version of the public domain ksh'
url='https://www.mirbsd.org/mksh.htm'
license=('custom')
arch=('i686' 'x86_64')
depends=('gcc-libs')
install=mksh.install
source=("https://www.mirbsd.org/MirOS/dist/mir/mksh/$pkgname-$pkgver.tgz"
        'https://www.mirbsd.org/TaC-mksh.txt')
md5sums=('77c108d8143a6e7670954d77517d216d'
         'a231b325d5f2155a6c667a9323986718')

build() {
  cd "$srcdir/$pkgname"

  sh Build.sh -r -c lto
  #./test.sh
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m 755 mksh "$pkgdir/usr/bin/mksh"
  install -D -m 644 mksh.1 "$pkgdir/usr/share/man/man1/mksh.1"
  install -D -m 644 dot.mkshrc "$pkgdir/etc/skel/.mkshrc"
  install -D -m 644 "$srcdir/TaC-mksh.txt" "$pkgdir/usr/share/licenses/mksh/TaC-mksh.txt"
}

# vim:set ts=2 sw=2 et:
