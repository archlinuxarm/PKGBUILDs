# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch, should be in next version

pkgname=musescore
pkgver=2.0.2
pkgrel=2
pkgdesc='Create, play and print beautiful sheet music'
arch=('i686' 'x86_64')
url='http://musescore.org/'
license=('GPL')
depends=('desktop-file-utils' 'gtk-update-icon-cache' 'libpulse' 'portaudio'
         'qt5-quick1' 'qt5-svg' 'qt5-tools' 'shared-mime-info')
makedepends=('cmake' 'doxygen' 'lame' 'qt5-script' 'texlive-core')
optdepends=('lame: MP3 export')
install='musescore.install'
source=("musescore-${pkgver}.tar.gz::https://github.com/musescore/MuseScore/archive/v${pkgver}.tar.gz"
        'https://github.com/musescore/MuseScore/commit/78605d6aefe45f44edd38bf38d027f0f0e53feeb.patch')
sha256sums=('92f35403d1cb87bdb080a18bc37b2023a998ed9a26e7f50a5b6d30dbc3c1db45'
            '5ca08b7cc3dc5f0269150faea7651745d96fa450fa83a406b9bc795baac10cd6')

prepare() {
  cd MuseScore-${pkgver}

  patch -p1 -i ../78605d6aefe45f44edd38bf38d027f0f0e53feeb.patch
}

build() {
  cd MuseScore-${pkgver}

  make PREFIX='/usr' release
}

package() {
  cd MuseScore-${pkgver}/build.release

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
