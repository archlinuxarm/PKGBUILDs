# Maintainer: Robin Broda <robin@broda.me>
# Contributor: X0rg
# Contributor: marlock <marlock87@hotmail.com>
# Contributor: Diogo Leal <estranho@diogoleal.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depend on libxnvctrl

pkgname=psensor
pkgver=1.2.0
pkgrel=6
pkgdesc='A graphical hardware temperature monitor for Linux'
arch=('x86_64')
url='https://wpitchoune.net/psensor'
license=('GPL2')
depends=('lm_sensors' 'dconf' 'gtk3' 'udisks2' 'curl' 'libgtop' 'libnotify'
         'json-c' 'libmicrohttpd')
makedepends=('help2man')
source=("https://wpitchoune.net/${pkgname}/files/${pkgname}-${pkgver}.tar.gz"
        'is_error.patch')
sha512sums=('274928190a53f3603f7db4ea81057497bfa01b9d0f8b4eb2f5f5094fd944ee384aff8f9c21b9c85ea001cbe877e7dc020963c0570f000ef42b302cb54b038e4d'
            'fa1f0a8cdbd75fba2ad980a5beb1807e533ef12154db1f0cc97c9130080584cb0251e29a56068b0400f152e540f326a7b16749bf11b1e844d1d519d7bb0e71ed')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/is_error.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
