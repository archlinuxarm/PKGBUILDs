# Maintainer: Robin Broda <robin@broda.me>
# Contributor: X0rg
# Contributor: marlock <marlock87@hotmail.com>
# Contributor: Diogo Leal <estranho@diogoleal.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depend on libxnvctrl

pkgname=psensor
pkgver=1.2.1
pkgrel=1
pkgdesc='A graphical hardware temperature monitor for Linux'
arch=('x86_64')
url='https://wpitchoune.net/psensor'
license=('GPL2')
depends=('lm_sensors' 'dconf' 'gtk3' 'udisks2' 'curl' 'libgtop' 'libnotify'
         'json-c' 'libmicrohttpd')
makedepends=('help2man')
source=("https://wpitchoune.net/${pkgname}/files/${pkgname}-${pkgver}.tar.gz")
sha512sums=('931b8ab654cd708d128e0a659e94049fa69dd33dc354b17ef08e05b8da65588f5c5c1c2a4a42f7341a7990f999152c9aebf3c58484eea38e476034d60ac2ff41')

prepare() {
  find $pkgname-$pkgver -name Makefile* | xargs sed -e 's|-Werror||g' -i # Kill -Werror
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
