# Maintainer: Robin Broda <robin@broda.me>
# Contributor: X0rg
# Contributor: marlock <marlock87@hotmail.com>
# Contributor: Diogo Leal <estranho@diogoleal.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depend on libxnvctrl

pkgname=psensor
pkgver=1.2.0
pkgrel=4
pkgdesc='A graphical hardware temperature monitor for Linux'
arch=('x86_64')
url='https://wpitchoune.net/psensor'
license=('GPL2')
depends=('lm_sensors' 'dconf' 'gtk3' 'udisks2' 'curl' 'libgtop' 'libnotify'
         'json-c' 'libmicrohttpd')
makedepends=('help2man')
source=("https://wpitchoune.net/${pkgname}/files/${pkgname}-${pkgver}.tar.gz"
        'is_error.patch')
md5sums=('0d8ac0a1312e96f2101ecc7c684e2863'
         '04c45b3527e6cc4012fe60491751a0ac')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/is_error.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
