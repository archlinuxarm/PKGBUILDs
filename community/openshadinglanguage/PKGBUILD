# $Id: PKGBUILD 88615 2013-04-20 16:45:08Z heftig $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed intel-tbb from deps, set -DUSE_TBB=0

pkgname=openshadinglanguage
pkgver=1.3.0
pkgrel=4
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="http://code.google.com/p/openshadinglanguage/"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'llvm' 'freetype2' 'libpng' 'libtiff')
makedepends=('boost' 'cmake' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
md5sums=('1e8c47475cb092cec0aada843b2893ed')

build() {
  cd OpenShadingLanguage-Release-$pkgver/src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  # TODO: Remove LLVM_STATIC with llvm 3.3 when we'll have shared libs
  # Check blender for crash with llvm pipe (run LIBGL_ALWAYS_SOFTWARE=1 blender)
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVM_STATIC=ON \
    -DUSE_TBB=0 \
    ..
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir"/usr/share/OSL/
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/LICENSE "$pkgdir"/usr/share/licenses/$pkgname # TODO: Tell upstream about this shit
  mv "$pkgdir"/usr/{CHANGES,README.md,INSTALL} "$pkgdir"/usr/share/OSL/
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/OSL/doc
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders
}

# vim:set ts=2 sw=2 et:
