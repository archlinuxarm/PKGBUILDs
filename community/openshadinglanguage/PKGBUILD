# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed intel-tbb from deps
#  - set -DUSE_LLVM_BITCODE=OFF for v5 and v6

pkgname=openshadinglanguage
pkgver=1.9.8
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'llvm' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
sha512sums=('99a8f4af955058ad94326795caba12599f7ba81e5304389fc48cc23cb12357954609bd27785c0b36d81626da24ad91c1ebeb39303fe5fec08fd7b48c3fa0bd42')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver

  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  #patch -Np1 < $srcdir/llvm-static-system-libs.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver/src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  [ "${CARCH}" == "arm" -o "${CARCH}" == "armv6h" ] && BITCODE="-DUSE_LLVM_BITCODE=OFF"

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLERTTI=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLLVM_STATIC=0 \
    -DOSL_BUILD_CPP11=1 \
    -DSTOP_ON_WARNING=OFF \
    $BITCODE
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$pkgdir"/usr/share/doc/OSL/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
