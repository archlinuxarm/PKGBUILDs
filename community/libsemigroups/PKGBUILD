# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -mavx from configure and Makefile

pkgname=libsemigroups
pkgver=1.0.0
pkgrel=1
pkgdesc="C++ library for semigroups and monoids"
url="https://libsemigroups.github.io/libsemigroups/"
arch=(x86_64)
license=(GPL3)
depends=(gcc-libs)
makedepends=(fmt)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        system-fmt.patch)
sha256sums=('db58bddf29ef58b4caeba42fa7cf5e1b52406c6b5c5c6c23c7e9d571b3f3de48'
            '86c0ae8613df2959924d86c16874ee8d72288b10f87225c3909132b1d2a83fdf')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../system-fmt.patch
  rm -r extern/fmt-*
  sed -i 's/-mavx//g' configure.ac Makefile.am
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
