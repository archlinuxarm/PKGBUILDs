# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=haproxy
pkgver=2.1.4
pkgrel=1

pkgdesc='Reliable, high performance TCP/HTTP load balancer'
url='https://www.haproxy.org/'
arch=('x86_64')
license=('GPL')

depends=('openssl' 'pcre' 'zlib' 'lua' 'systemd-libs')

backup=('etc/haproxy/haproxy.cfg')

install=haproxy.install

source=(https://www.haproxy.org/download/${pkgver%.*}/src/haproxy-$pkgver.tar.gz
        haproxy.cfg
        haproxy.sysusers
        0001-Use-CFLAGS-and-LDFLAGS-when-building-contrib.patch)

sha256sums=('51030ff696d7067162b4d24d354044293aecfbb36d7acc2f840c8d928bfe91cd'
            'e88edeb2fe23508565e46ff485c93cfb7c2e9c64f231538452ec7ecd7ec37a42'
            'b02b4088205039f0979aef0ef3e22957f493dd54eabefaee7011f2d795303b3e'
            'ba489cec86ea7dae643128825d69fd560a53dd9dc85d0bf4e81f2614aade164e')

prepare() {
  cd haproxy-$pkgver

  patch -p1 <"$srcdir"/0001-Use-CFLAGS-and-LDFLAGS-when-building-contrib.patch
}

build() {
  cd haproxy-$pkgver

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && ADDLIB="-latomic"
  make CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    ADDLIB=$ADDLIB \
    CPU=generic \
    TARGET=linux-glibc \
    USE_GETADDRINFO=1 \
    USE_OPENSSL=1 \
    USE_PCRE=1 USE_PCRE_JIT=1 \
    USE_ZLIB=1 \
    USE_LUA=1 \
    USE_SYSTEMD=1 \
    EXTRA_OBJS="contrib/prometheus-exporter/service-prometheus.o"

  for contrib in halog iprange ip6range systemd; do
    make -C contrib/$contrib SBINDIR=/usr/bin OPTIMIZE= CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS"
  done
}

package() {
  cd haproxy-$pkgver
  make \
    PREFIX="$pkgdir"/usr \
    SBINDIR="$pkgdir"/usr/bin \
    DOCDIR="$pkgdir"/usr/share/$pkgname \
    install

  for contrib in halog iprange ip6range; do
    install -Dm755 contrib/$contrib/$contrib "$pkgdir"/usr/bin/$contrib
  done

  install -Dm644 ../haproxy.sysusers "$pkgdir"/usr/lib/sysusers.d/haproxy.conf
  install -Dm644 ../haproxy.cfg "$pkgdir"/etc/haproxy/haproxy.cfg
  install -Dm644 contrib/systemd/haproxy.service \
    "$pkgdir"/usr/lib/systemd/system/haproxy.service
  cp -r examples "$pkgdir"/usr/share/haproxy/examples
}
