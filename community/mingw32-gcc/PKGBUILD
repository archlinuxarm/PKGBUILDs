# $Id: PKGBUILD 1538 2009-08-25 14:03:47Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Ondrej Jirman <megous@megous.com>

pkgname=mingw32-gcc
pkgver=4.4.0
pkgrel=1
arch=(i686 x86_64)
pkgdesc="A C and C++ cross-compilers for building Windows executables on Linux"
depends=(mingw32-runtime mingw32-binutils mingw32-w32api)
replaces=(mingw32-gcc-base)
provides=(mingw32-gcc-base)
options=(!strip force)
url="http://mingw.sf.net"
license=(GPL LGPL)
source=(http://downloads.sourceforge.net/project/mingw/GCC%20Version%204/Current%20Release_%20gcc-$pkgver/gcc-$pkgver-src.tar.bz2)
md5sums=('cf5d787bee57f38168b74d65a7c0e6fd')

build()
{
  mkdir -p $srcdir/build
  cd $srcdir/build

  unset CFLAGS CXXFLAGS

  chmod ugo+x $srcdir/gcc-$pkgver/configure
  chmod ugo+x $srcdir/gcc-$pkgver/move-if-change

  $srcdir/gcc-$pkgver/configure \
     --target=i486-mingw32 \
     --host=$CHOST \
     --build=$CHOST \
     --prefix=/usr \
     --enable-languages=c,c++ \
     --enable-sjlj-exceptions \
     --enable-hash-synchronization \
     --disable-nls \
     --disable-libssp \
     --disable-shared \
    || return 1

  make || return 1
  make install DESTDIR=$pkgdir || return 1

  cd $pkgdir
  rm -rf usr/bin/i486-mingw32-{gcov,gccbug,gcc-*} \
    usr/{info,man,lib/libiberty.a}

  strip usr/bin/*
  strip usr/libexec/gcc/i486-mingw32/$pkgver/{cc1*,collect2}
  i486-mingw32-strip -g usr/lib/gcc/i486-mingw32/$pkgver/*.a
}
