# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DOPENDHT_LTO=OFF
#  - explicitly link v5/v6 with libatomic

pkgname=opendht
epoch=1
pkgver=2.1.9.5
pkgrel=1
pkgdesc="C++14 Distributed Hash Table (DHT) implementation"
arch=(x86_64)
url="https://github.com/savoirfairelinux/opendht"
license=(GPL3)
depends=(openssl gnutls nettle argon2 readline fmt http-parser jsoncpp libjsoncpp.so)
makedepends=(msgpack-c cmake cython cppunit restinio)
optdepends=('python: to use the Python bindings')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-fix-systemd-makedep.patch::${url}/pull/535.patch)
sha256sums=('74eb58f0589577a9df378772f1dd2edc1c3f1b16a045057c3bcb120d2c16403f'
            '7774b345aa48f948bb0dcca3cd4341da53b8a0df8f8ba96440e9a0b0776e40e9')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../${pkgname}-fix-systemd-makedep.patch
}

build() {
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(opendht PRIVATE atomic)" >> ${pkgname}-${pkgver}/CMakeLists.txt
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOPENDHT_TESTS=ON \
    -DOPENDHT_DOCUMENTATION=OFF \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_SYSTEMD_UNIT_FILE_LOCATION=/usr/lib/systemd/system/ \
    -DOPENDHT_LTO=OFF \
    -DOPENDHT_TOOLS=ON \
    -DOPENDHT_INDEX=ON \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_HTTP=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON
  make -C build
}

#check() {
#  make -C build test
#}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
