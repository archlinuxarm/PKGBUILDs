# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DOPENDHT_LTO=OFF
#  - explicitly link v5/v6 with libatomic

pkgname=opendht
pkgver=1.7.2
pkgrel=1
epoch=1
pkgdesc="A C++11 implementation of the Kademlia DHT (Distributed Hash Table)"
arch=('x86_64')
depends=('gnutls' 'nettle' 'readline' 'argon2' 'jsoncpp' 'restbed')
makedepends=('msgpack-c' 'cmake' 'cython' 'systemd' 'cppunit')
optdepends=('python: to use the Python bindings')
url="https://github.com/savoirfairelinux/opendht"
license=('GPL3')
source=("https://github.com/savoirfairelinux/opendht/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5030f011eba421f67847ee94d906aabbdf409268cdc1119e15bb8f2863ed0bbc')
sha512sums=('dcb70c8f38f067f1035cbfdc7897a8245bacaef547afa48652e56e39e4641f7bb1ea6bc2bf3e46f8454cdbe3743eab318eca9fb6a02921d5f5fa792748a14cd1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(opendht PRIVATE atomic)" >> CMakeLists.txt

  msg2 'Building...'
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=None \
    -DOPENDHT_TESTS=ON \
    -DOPENDHT_DOCUMENTATION=OFF \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_LTO=OFF \
    -DOPENDHT_TOOLS=ON \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cd build
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg2 'Installing...'
  cd build
  make DESTDIR="$pkgdir" install
  cd ..

  msg2 'Installing documentation...'
  install -D -m644 README.md "${pkgdir}/usr/share/doc/opendht/README.md"
}

# vim:set ts=2 sw=2 et:
