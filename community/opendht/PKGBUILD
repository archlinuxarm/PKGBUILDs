# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DOPENDHT_LTO=OFF
#  - explicitly link v5/v6 with libatomic

pkgname=opendht
pkgver=1.7.0rc5
pkgrel=1
epoch=1
pkgdesc="A C++11 implementation of the Kademlia DHT (Distributed Hash Table)"
arch=('x86_64')
depends=('gnutls' 'nettle' 'readline' 'argon2' 'jsoncpp' 'restbed')
makedepends=('msgpack-c' 'cmake' 'cython' 'systemd')
optdepends=('python: to use the Python bindings')
url="https://github.com/savoirfairelinux/opendht"
license=('GPL3')
source=("https://github.com/savoirfairelinux/opendht/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('504621eb88dd1a9ab9d179e9cc86959e3484dd1f0d0e486131c833aaa260545d')
sha512sums=('259b3bd0d9b2fabaf93a875e8f4082d0cc9ce9e35b6726b554e6485688dc825d014fad2bf4464bc55f80941f2f0ae365fbea7f6ee0ecda5e08c9d680bf335bc3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(opendht PRIVATE atomic)" >> CMakeLists.txt

  msg2 'Building...'
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_LTO=OFF \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg2 'Installing...'
  cd build
  make DESTDIR="$pkgdir" install
  cd ..

  msg2 'Installing documentation...'
  install -D -m644 README.md "${pkgdir}/usr/share/doc/opendht/README.md"
}

# vim:set ts=2 sw=2 et:
