# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DOPENDHT_LTO=OFF
#  - explicitly link v5/v6 with libatomic

pkgname=opendht
epoch=1
pkgver=2.1.6
pkgrel=1
pkgdesc="C++14 Distributed Hash Table (DHT) implementation"
arch=(x86_64)
url="https://github.com/savoirfairelinux/opendht"
license=(GPL3)
depends=(openssl gnutls nettle argon2 readline fmt http-parser jsoncpp libjsoncpp.so)
makedepends=(msgpack-c cmake cython cppunit restinio)
optdepends=('python: to use the Python bindings')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('53f362f75a5dda2e88eb145671d01b2998cc5f55d9e1e37fa28b18a324f1159c')

prepare() {
  mkdir -p build
}

build() {
  cd build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(opendht PRIVATE atomic)" >> ../${pkgname}-${pkgver}/CMakeLists.txt

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOPENDHT_TESTS=ON \
    -DOPENDHT_DOCUMENTATION=OFF \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_SYSTEMD_UNIT_FILE_LOCATION=/usr/lib/systemd/system/ \
    -DOPENDHT_LTO=OFF \
    -DOPENDHT_TOOLS=ON \
    -DOPENDHT_INDEX=ON \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_HTTP=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON
  make
}

#check() {
#  cd build
#  make test
#}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
