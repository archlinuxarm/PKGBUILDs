# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DOPENDHT_LTO=OFF
#  - explicitly link v5/v6 with libatomic

pkgname=opendht
epoch=1
pkgver=1.10.1
pkgrel=7
pkgdesc="A C++11 implementation of the Kademlia DHT (Distributed Hash Table)"
arch=(x86_64)
url="https://github.com/savoirfairelinux/opendht"
license=(GPL3)
depends=(gnutls nettle readline argon2 restbed jsoncpp libjsoncpp.so)
makedepends=(msgpack-c cmake cython systemd cppunit)
optdepends=('python: to use the Python bindings')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
         opendht-gcc10.patch::"https://github.com/savoirfairelinux/opendht/commit/e58fc4ef.patch")
sha512sums=('d0ab4aa376ecd4ac57af78a1491cd0f466021790ebea0e00032a503d61127f323cb37f04d94f2a35797dc52051407708f49ed99be558003acc102ff017dbdf29'
            'ed43ee79f350f8b86119e4336de5a28eab4c4d6eedb578d714dd51a1c8109c0a31756ea0cf807d3c6e636b8ccfb52c8c43f81220b4fd5105bd200ae76e175977')

prepare() {
  mkdir -p build

  patch -d $pkgname-$pkgver -p1 -i ../opendht-gcc10.patch # Fix build with GCC 10
}

build() {
  cd build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(opendht PRIVATE atomic)" >> ../${pkgname}-${pkgver}/CMakeLists.txt

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOPENDHT_TESTS=ON \
    -DOPENDHT_DOCUMENTATION=OFF \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_LTO=OFF \
    -DOPENDHT_TOOLS=ON \
    -DOPENDHT_INDEX=ON \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
