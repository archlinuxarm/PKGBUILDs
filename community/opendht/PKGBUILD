# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DOPENDHT_LTO=OFF
#  - explicitly link v5/v6 with libatomic

pkgname=opendht
epoch=1
pkgver=1.9.5
pkgrel=2
pkgdesc="A C++11 implementation of the Kademlia DHT (Distributed Hash Table)"
arch=(x86_64)
url="https://github.com/savoirfairelinux/opendht"
license=(GPL3)
depends=(gnutls nettle readline argon2 jsoncpp restbed)
makedepends=(msgpack-c cmake cython systemd cppunit)
optdepends=('python: to use the Python bindings')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7712f158754234be39d2839d3677525eaf1e753505fc965e24b49d9088ff9415')

prepare() {
  mkdir -p build
}

build() {
  cd build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(opendht PRIVATE atomic)" >> ../${pkgname}-${pkgver}/CMakeLists.txt

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOPENDHT_TESTS=ON \
    -DOPENDHT_DOCUMENTATION=OFF \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_LTO=OFF \
    -DOPENDHT_TOOLS=ON \
    -DOPENDHT_INDEX=ON \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
