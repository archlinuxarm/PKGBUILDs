# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Co-Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to disable LTO

pkgname=libqtxdg
pkgver=3.3.1
pkgrel=5
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt."
arch=("x86_64")
url="https://lxqt.org"
license=("LGPL" "custom")
depends=("libsm" "libxkbcommon-x11" "qt5-base" "qt5-svg")
makedepends=("cmake" "lxqt-build-tools")
checkdepends=("xorg-server-xvfb")
source=(
  "https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
  "https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
  '0001-disable-LTO.patch'
)
sha256sums=('1061fa6a4783df5e33ea2876f03f88b9ffef8157d37da97ea6b217b736c2d6d6'
            'SKIP'
            '8d6d7370b40bd54bd7582138926fc258a055e30afeb8de1c3385de333542b54a')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-disable-LTO.patch
}

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTS=ON
  make
}

check() {
  cd build

  xvfb-run make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname-$pkgver"/{AUTHORS,COPYING,Digia-Qt-LGPL-Exception-1.1} \
    -t "$pkgdir"/usr/share/licenses/$pkgname
}
