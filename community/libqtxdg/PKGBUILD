# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to disable LTO

pkgname=libqtxdg
pkgver=3.1.0
pkgrel=4
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt."
arch=("x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("libsm" "libxkbcommon-x11" "qt5-base" "qt5-svg")
makedepends=("cmake")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	'0001-disable-LTO.patch'
)
sha256sums=(
	"0ee1a4684b782e34950c55dc06df1b4ef2dc3d9b3562a512de004191c18a2e08"
	"SKIP"
	"8d6d7370b40bd54bd7582138926fc258a055e30afeb8de1c3385de333542b54a"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)

prepare() {
	cd $pkgname-$pkgver
	patch -p1 -i ../0001-disable-LTO.patch
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
