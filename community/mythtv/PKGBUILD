# $Id: PKGBUILD 73064 2012-06-30 06:07:26Z jconder $
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: <kleptophobiac@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - correctly define ARM cpus for configure
#  - disabled vdpau, mmx

pkgname=mythtv
pkgver=0.26.1
pkgrel=1
epoch=1
pkgdesc="A Homebrew PVR project"
arch=('i686' 'x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('avahi' 'fftw' 'lame' 'libass' 'libavc1394' 'libcdio' 'libiec61883'
         'libpulse' 'libva' 'libvdpau' 'libxinerama' 'lirc-utils'
	 'mariadb-clients' 'mysql-python' 'perl-dbd-mysql' 'perl-io-socket-inet6'
	 'perl-libwww' 'perl-net-upnp' 'python2-lxml' 'qtwebkit' 'urlgrabber'
	 'x264')
makedepends=('glew' 'libcec' 'libxml2' 'mesa' 'mesa-libgl' 'openssl' 'yasm')
optdepends=('glew: for GPU commercial flagging'
            'libcec: for consumer electronics control capabilities'
	    'libxml2: to read blu-ray metadata'
	    'openssl: for AirTunes (RAOP) support'
            'xmltv: to download tv listings')
conflicts=('myththemes' 'mythplugins-mythvideo')
replaces=('myththemes' 'mythplugins-mythvideo')
backup=()
install='mythtv.install'
source=("ftp://ftp.osuosl.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
        'alsa.patch'
	'mythbackend.service'
	'libva-compat.patch::https://github.com/garybuhrmaster/mythtv/commit/2add868d2f18b8b47d9747e043f052e88869bfc3.patch'
	'http://code.mythtv.org/trac/raw-attachment/ticket/11338/libcec-2-support.patch')
md5sums=('ef25a9b86f4a23d6f07d83791a1e5c9e'
         'f64b8219e3d27a2edf96733b851e576b'
	 'e4d572dcc307d6d8ae26bee5aebf9f3a'
	 'ae88a16e9f42a913f395bf71af18e931'
         '6e156c9fc0b369346d93ccafad46cba3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  patch -Np1 -i "$srcdir/alsa.patch"
  patch -Np2 -i "$srcdir/libva-compat.patch"
  patch -Np2 -i "$srcdir/libcec-2-support.patch"

  sed -re 's@Key, &@Key, @' -i 'bindings/php/MythBase.php'

  [[ $CARCH == "arm" ]] && ARCH="armv5te"
  [[ $CARCH == "armv6h" ]] && ARCH="armv6l"
  [[ $CARCH == "armv7h" ]] && ARCH="armv7-a"
  ./configure --prefix=/usr \
              --cpu="$ARCH" \
              --disable-mmx \
              --disable-vdpau \
	      --disable-altivec \
	      --disable-audio-jack \
	      --disable-ccache \
	      --disable-distcc \
	      --enable-libfftw3 \
	      --enable-libmp3lame \
	      --enable-libx264 \
	      --enable-vaapi \
	      --python=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install

  install -D -m644 "$srcdir/mythbackend.service" "$pkgdir/usr/lib/systemd/system/mythbackend.service"
  install -D -m644 'database/mc.sql' "$pkgdir/usr/share/mythtv/mc.sql"

  mkdir -p "$pkgdir/usr/share/mythtv"
  cp -R 'contrib' "$pkgdir/usr/share/mythtv"
  mkdir -p "$pkgdir/var/log/mythtv"
}
