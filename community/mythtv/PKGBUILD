# $Id: PKGBUILD 48169 2011-05-29 12:28:03Z jconder $
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: <kleptophobiac@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - correctly define ARM cpus for configure
#  - disabled vdpau

plugrel=1

pkgname=mythtv
pkgver=0.24.1
pkgrel=3
epoch=1
pkgdesc="A Homebrew PVR project"
arch=('i686' 'x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('lame' 'libavc1394' 'libiec61883' 'libpulse' 'libvdpau' 'libxinerama'
         'libxvmc' 'lirc-utils' 'mesa' 'mysql-clients' 'mysql-python'
	 'perl-dbd-mysql' 'perl-libwww' 'perl-net-upnp' 'python-lxml' 'qt'
	 'qtwebkit' 'wget')
makedepends=('yasm')
optdepends=('xmltv: to download tv listings')
backup=('etc/conf.d/mythbackend')
install='mythtv.install'
source=("ftp://ftp.osuosl.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
        'mythbackend.rc'
        'mythbackend.conf'
        'mythtv-v4l2-fix.patch'
        'build.patch')
md5sums=('6870c679619ec58456e76839745411d8'
         'feadcc9ad064d93d6dceab1efc0bd9ed'
         'bb8e4033d82428d827570fae9ba15e6a'
         'ba5c91df80f0d3f7563a873e71a3725c'
         '939329598673cf63cfe9b14e4ebcd06e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  patch -Np2 -i ${srcdir}/mythtv-v4l2-fix.patch
  patch -Np1 -i ${srcdir}/build.patch

  [[ $CARCH == "arm" ]] && ARCH="armv5te"
  [[ $CARCH == "armv7" ]] && ARCH="armv7-a"
  ./configure --prefix=/usr --cpu="$ARCH" \
              --enable-mmx \
              --enable-audio-oss \
              --enable-audio-alsa \
              --disable-audio-jack \
              --enable-audio-pulse \
              --disable-altivec \
              --disable-distcc \
              --disable-ccache \
              --disable-vdpau \
              --enable-dvb \
              --dvb-path=/usr/include \
              --enable-lirc \
              --enable-joystick-menu \
              --enable-v4l \
              --enable-ivtv \
              --enable-xvmc \
              --enable-xvmcw \
              --enable-xvmc-vld \
              --enable-firewire \
              --enable-opengl-vsync \
              --enable-xrandr \
              --enable-xv \
              --enable-x11 \
              --with-bindings=perl,python \
              --python=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install

  install -D -m755 "$srcdir/mythbackend.rc" "$pkgdir/etc/rc.d/mythbackend"
  install -D -m644 "$srcdir/mythbackend.conf" "$pkgdir/etc/conf.d/mythbackend"
  install -D -m644 'database/mc.sql' "$pkgdir/usr/share/mythtv/mc.sql"

  mkdir -p "$pkgdir/usr/share/mythtv"
  cp -R 'contrib' "$pkgdir/usr/share/mythtv"
}
