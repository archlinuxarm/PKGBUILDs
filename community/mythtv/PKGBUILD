# $Id: PKGBUILD 73064 2012-06-30 06:07:26Z jconder $
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: <kleptophobiac@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - correctly define ARM cpus for configure
#  - disabled vdpau, mmx

pkgname=mythtv
pkgver=0.25.1
pkgrel=2
epoch=1
pkgdesc="A Homebrew PVR project"
arch=('i686' 'x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('avahi' 'fftw' 'lame' 'libass' 'libavc1394' 'libcdio' 'libiec61883'
         'libpulse' 'libva' 'libvdpau' 'libxinerama' 'lirc-utils' 'mesa'
	 'mysql-clients' 'mysql-python' 'perl-dbd-mysql' 'perl-io-socket-inet6'
	 'perl-libwww' 'perl-net-upnp' 'python2-lxml' 'qtwebkit' 'urlgrabber'
	 'x264')
makedepends=('glew' 'libcec' 'libxml2' 'openssl' 'yasm')
optdepends=('glew: for GPU commercial flagging'
            'libcec: for consumer electronics control capabilities'
	    'libxml2: to read blu-ray metadata'
	    'openssl: for AirTunes (RAOP) support'
            'xmltv: to download tv listings')
conflicts=('myththemes' 'mythplugins-mythvideo')
replaces=('myththemes' 'mythplugins-mythvideo')
backup=('etc/conf.d/mythbackend')
install='mythtv.install'
source=("ftp://ftp.osuosl.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
        'mythbackend.rc'
        'mythbackend.conf'
	'mythbackend.service')
md5sums=('4d6375cc3a820abdb2ac5300734687ac'
         'c8f935d42fb8617e9279bd539811ca5f'
         'ab962d83614cbd0ac11ce3fcc929829d'
	 '813008d14a44e9c6212f364723aeaa74')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  sed -re 's@Key, &@Key, @' -i 'bindings/php/MythBase.php'

  [[ $CARCH == "arm" ]] && ARCH="armv5te"   
  [[ $CARCH == "armv7" ]] && ARCH="armv7-a"
  ./configure --prefix=/usr \
              --cpu="$ARCH" \
              --disable-mmx \
              --disable-vdpau \
	      --disable-altivec \
	      --disable-audio-jack \
	      --disable-ccache \
	      --disable-distcc \
	      --enable-libfftw3 \
	      --enable-libmp3lame \
	      --enable-libx264 \
	      --enable-vaapi \
	      --python=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install

  install -D -m755 "$srcdir/mythbackend.rc" "$pkgdir/etc/rc.d/mythbackend"
  install -D -m644 "$srcdir/mythbackend.conf" "$pkgdir/etc/conf.d/mythbackend"
  install -D -m644 "$srcdir/mythbackend.service" "$pkgdir/usr/lib/systemd/system/mythbackend.service"
  install -D -m644 'database/mc.sql' "$pkgdir/usr/share/mythtv/mc.sql"

  mkdir -p "$pkgdir/usr/share/mythtv"
  cp -R 'contrib' "$pkgdir/usr/share/mythtv"
  mkdir -p "$pkgdir/var/log/mythtv"
}
