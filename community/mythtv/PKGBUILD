# $Id: PKGBUILD 45087 2011-04-16 08:29:05Z jconder $
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: <kleptophobiac@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - correctly define ARM cpus for configure
#  - disabled vdpau

plugrel=1

pkgname=mythtv
pkgver=0.24
pkgrel=3.${plugrel}
epoch=1
pkgdesc="A Homebrew PVR project"
arch=('i686' 'x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('lame' 'libavc1394' 'libiec61883' 'libvdpau' 'libxinerama' 'libxvmc'
         'lirc-utils' 'mesa' 'mysql-clients' 'mysql-python' 'perl-dbd-mysql'
	 'perl-libwww' 'perl-net-upnp' 'python-lxml' 'qt' 'wget')
makedepends=('yasm')
optdepends=('xmltv: to download tv listings')
backup=('etc/conf.d/mythbackend')
install='mythtv.install'
source=("ftp://ftp.osuosl.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
        'mythbackend.rc'
        'mythbackend.conf')
md5sums=('1abe1d3cc72fdbde6daa4247dedbf29a'
         'feadcc9ad064d93d6dceab1efc0bd9ed'
         'bb8e4033d82428d827570fae9ba15e6a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
#  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  [[ $CARCH == "arm" ]] && CPU="armv5te"
  [[ $CARCH == "armv7" ]] && CPU="armv7-a"
  ./configure --prefix=/usr --cpu="armv5te" \
              --enable-audio-oss \
              --enable-audio-alsa \
              --disable-audio-jack \
              --disable-audio-pulse \
              --disable-altivec \
              --disable-distcc \
              --disable-vdpau \
              --disable-ccache \
              --enable-dvb \
              --dvb-path=/usr/include \
              --enable-lirc \
              --enable-joystick-menu \
              --enable-v4l \
              --enable-ivtv \
              --enable-xvmc \
              --enable-xvmcw \
              --enable-xvmc-vld \
              --enable-firewire \
              --enable-opengl-vsync \
              --enable-xrandr \
              --enable-xv \
              --enable-x11 \
              --with-bindings=perl,python --python=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install

  install -D -m755 "$srcdir/mythbackend.rc" "$pkgdir/etc/rc.d/mythbackend"
  install -D -m644 "$srcdir/mythbackend.conf" "$pkgdir/etc/conf.d/mythbackend"
  install -D -m644 'database/mc.sql' "$pkgdir/usr/share/mythtv/mc.sql"

  mkdir -p "$pkgdir/usr/share/mythtv"
  cp -R 'contrib' "$pkgdir/usr/share/mythtv"
}
