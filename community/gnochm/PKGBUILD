# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: iom <pizorn@gmail.com>

pkgname=gnochm
pkgver=0.9.11
pkgrel=3
pkgdesc="A CHM file viewer for GNOME"
arch=('i686' 'x86_64')
url="http://gnochm.sourceforge.net"
license=('GPL')
depends=('python-pychm' 'gnome-python-extras' 'libgtkhtml' 'gconf>=2.18.0.1-4')
makedepends=('perlxml' 'gnome-doc-utils>=0.11.2')
install=gnochm.install
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}-${pkgver}.tar.gz)
md5sums=('d77cc819eaa64b3ab333c4a73e88cfc0')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-mime-update --disable-desktop-update
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  install -Dm644 data/gnochm.desktop ${pkgdir}/usr/share/applications/gnochm.desktop
}
