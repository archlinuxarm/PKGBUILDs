# Maintainer: Kyle Keen <keenerd@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v7 without neon to fix FTBFS

pkgname=libvolk
epoch=2
pkgver=2.5.2
_cpuver=0.6.0
pkgrel=1
pkgdesc="The Vector-Optimized Library of Kernels from Gnuradio"
arch=('x86_64')
url="http://libvolk.org/"
license=('GPL3')
depends=('gcc-libs' 'boost-libs' 'orc' 'python' 'python-six')
makedepends=('boost' 'cmake' 'python-mako')
#source=("http://libvolk.org/releases/volk-$pkgver.tar.gz"{,.asc})
#source=("http://libvolk.org/releases/volk-$pkgver.tar.gz")
#source=("https://github.com/gnuradio/volk/releases/download/v$pkgver/volk-v$pkgver.tar.gz"{,.asc})
source=("volk-$pkgver.tgz::https://github.com/gnuradio/volk/archive/v$pkgver.tar.gz"
        "cpufeatures-$_cpuver.tgz::https://github.com/google/cpu_features/archive/v$_cpuver.tar.gz")
# new upstream maintainer, no sig for 2.1.0 or real release?
md5sums=('c2ad8ba1d7eaf24f372a259acf04b3a2'
         '72a153f809bfab2253978959da0915ef')
validpgpkeys=('09E749D885FA881A7E84E823385323EE6402091D'
              'D74F9F146E7F755783583158B343B2BA293E5174')

# doxygen for docs

prepare() {
  cd "$srcdir/volk-$pkgver"
  rmdir cpu_features
  ln -sf "$srcdir/cpu_features-$_cpuver" cpu_features
}

build() {
  export PYTHON=python3
  cd "$srcdir/volk-$pkgver"

  [[ $CARCH == "armv7h" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/neon/vfpv3/'` && CXXFLAGS="$CFLAGS"

  mkdir -p build
  cd build
  cmake \
    -DPYTHON_EXECUTABLE=$(which python3) \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev ../
  make
}

check() {
  cd "$srcdir/volk-$pkgver/build"
  export PYTHON=python3
  return
  if [[ "$CARCH" == "x86_64" ]]; then
    make test
  fi
}

package() {
  cd "$srcdir/volk-$pkgver/build"
  make DESTDIR="$pkgdir" install
}
