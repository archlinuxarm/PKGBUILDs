# $Id: PKGBUILD 70305 2012-05-04 08:21:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix build on v5 (https://github.com/surfly/gevent/issues/361)

pkgname=python2-gevent
pkgver=1.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
depends=('python2-greenlet')
makedepends=('git' 'cython2')
provides=('python2-gevent-beta')
conflicts=('python2-gevent-beta')
replaces=('python2-gevent-beta')
source=("git://github.com/surfly/gevent.git#tag=$pkgver"
        'arm.patch')

build() {
	cd gevent
	patch -p1 -i ../arm.patch

    LIBEV_EMBED=1 \
    CARES_EMBED=1 \
    CYTHON=cython2 \
    PYTHON=python2 \
	python2 setup.py build
}

package() {
	cd gevent
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

md5sums=('SKIP'
         '236cc90a02935adfeb2c70cd90d3e63f')
