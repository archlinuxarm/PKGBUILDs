# $Id: PKGBUILD 59085 2009-11-20 06:29:43Z giovanni $
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - added PYTHON env variable, we need python2 specifically

plugrel=1

pkgname=distcc
pkgver=3.1
pkgrel=3
pkgdesc="A distributed C, C++, Obj C compiler"
arch=('i686' 'x86_64')
url="http://code.google.com/p/distcc/"
license=('GPL')
depends=('gcc' 'popt')
makedepends=('gtk2' 'pkgconfig')
optdepends=('gtk2: for distccmon-gnome')
backup=(etc/conf.d/distccd)
source=(http://distcc.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2 
        distccd 
        distccd.conf.d)
md5sums=('a1a9d3853df7133669fffec2a9aab9f3' 
         '1c918474372c09304772c20c4de484fa'
         '89aaf6e9072092e283465a14e83f0f94')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON="/usr/bin/python2" ./configure --prefix=/usr \
              --with-gtk \
              --sysconfdir=/etc \
              --mandir=/usr/share/man || return 1
  make || return 1
  make DESTDIR="${pkgdir}/" install || return 1
  install -D -m644 ${srcdir}/distccd.conf.d ${pkgdir}/etc/conf.d/distccd
  install -D -m755 ${srcdir}/distccd ${pkgdir}/etc/rc.d/distccd

  install -d ${pkgdir}/usr/lib/${pkgname}/bin
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/gcc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/g++
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cpp
}
