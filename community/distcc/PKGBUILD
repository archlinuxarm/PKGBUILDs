# $Id: PKGBUILD 57046 2011-10-19 10:30:08Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --without-avahi to configure
#  - removed gtk2 from deps and --with-gtk from configure
#  - added patch to provide user-configurable IO timeouts (credit: Sascha Demetrio)

pkgname=distcc
pkgver=3.1
pkgrel=12.1
pkgdesc="A distributed C, C++, Obj C compiler"
arch=('i686' 'x86_64')
url="http://code.google.com/p/distcc/"
license=('GPL')
depends=('gcc' 'popt')
makedepends=('gtk2' 'pkgconfig' 'python2')
optdepends=('python2')
backup=('etc/conf.d/distccd'
	'etc/distcc/hosts')
source=(http://distcc.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        distccd.conf.d
        distccd.service
        alarm.patch)
md5sums=('a1a9d3853df7133669fffec2a9aab9f3'
         '239aae53250e3e35288cba566bc0bbf1'
         '09f0688da9c1840e518d2593bd5c3830'
         '5cdf2b5be232a9da1d473e36dda978a0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/alarm.patch
  [ -f Makefile ] || PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--mandir=/usr/share/man \
	--without-avahi

  make WERROR_CFLAGS= INCLUDESERVER_PYTHON=/usr/bin/python2
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
#  make TEST_PYTHON=/usr/bin/python2 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" INCLUDESERVER_PYTHON=/usr/bin/python2 install
  install -D -m644 ${srcdir}/distccd.conf.d ${pkgdir}/etc/conf.d/distccd

  install -d ${pkgdir}/usr/lib/${pkgname}/bin
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/gcc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/g++
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/c++
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cpp

  install -Dm0644 $srcdir/distccd.service $pkgdir/usr/lib/systemd/system/distccd.service
}
