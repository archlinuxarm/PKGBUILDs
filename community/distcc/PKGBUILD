# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --without-avahi to configure
#  - removed gtk2 from deps and --with-gtk from configure
#  - patch to allow zero timeout (INT_MAX time to complete)

pkgname=distcc
pkgver=3.2rc1
pkgrel=3
pkgdesc='Distributed C, C++ and Objective-C compiler'
arch=('x86_64')
url='https://github.com/distcc/distcc'
license=('GPL')
depends=('popt')
makedepends=('gtk2' 'python2')
optdepends=('python2')
backup=('etc/conf.d/distccd'
        'etc/distcc/hosts')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        distccd.conf.d
        distccd.service
        allow-zero-timeout.patch)
sha256sums=('33e85981ff6afd94efc38b23b2d8b9036b3dff2dc6eac6982b9ff0ae1de64caa'
            '43e02b461841ca2976816c244a0eca8b24820ca143f73cc0924403d75a8c012f'
            '0f18ffb58647eeba643f28e9029a5d307100df3470eb228097f2b3f4196d8098'
            '73ac95393ee94bcb28f49499e8eec61bfd29a7de38889eaebd237fbbcba6e2c6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
    patch -p1 -i ../allow-zero-timeout.patch
  ./autogen.sh
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -f Makefile ] || PYTHON=/usr/bin/python2 \
    ./configure \
    --prefix=/usr \
	--sysconfdir=/etc \
	--mandir=/usr/share/man \
	--enable-rfc2553 \
	--without-avahi

  make WERROR_CFLAGS= INCLUDESERVER_PYTHON=/usr/bin/python2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" INCLUDESERVER_PYTHON=/usr/bin/python2 install

  install -d "$pkgdir/usr/lib/$pkgname/bin"
  for bin in cc gcc g++ c++ cpp; do
    ln -sf "/usr/bin/$pkgname" "$pkgdir/usr/lib/$pkgname/bin/$bin"
  done

  install -Dm644 "$srcdir/distccd.conf.d" "$pkgdir/etc/conf.d/distccd"
  install -Dm644 "$srcdir/distccd.service" "$pkgdir/usr/lib/systemd/system/distccd.service"
}

# vim:set ts=2 sw=2 et:
