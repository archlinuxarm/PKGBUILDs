# $Id: PKGBUILD 57046 2011-10-19 10:30:08Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --without-avahi to configure
#  - removed gtk2 from deps and --with-gtk from configure
#  - patch to allow zero timeout (INT_MAX time to complete)

pkgname=distcc
pkgver=3.2rc1
pkgrel=1.1
pkgdesc="A distributed C, C++, Obj C compiler"
arch=('i686' 'x86_64')
url="http://code.google.com/p/distcc/"
license=('GPL')
depends=('gcc' 'popt')
makedepends=('gtk2' 'pkgconfig' 'python2')
optdepends=('python2')
options=('!distcc')
backup=('etc/conf.d/distccd'
	'etc/distcc/hosts')
source=(http://distcc.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        distccd.conf.d
        distccd.service
        allow-zero-timeout.patch)
md5sums=('18cd4f33f5cfc3e75675cafc568fb9cf'
         '239aae53250e3e35288cba566bc0bbf1'
         '09f0688da9c1840e518d2593bd5c3830'
         '6d6abde46f321355eb0eaafc03c2ad1d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../allow-zero-timeout.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [ -f Makefile ] || PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--mandir=/usr/share/man \
	--without-avahi

  make WERROR_CFLAGS= INCLUDESERVER_PYTHON=/usr/bin/python2
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
#  make TEST_PYTHON=/usr/bin/python2 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" INCLUDESERVER_PYTHON=/usr/bin/python2 install
  install -D -m644 ${srcdir}/distccd.conf.d ${pkgdir}/etc/conf.d/distccd

  install -d ${pkgdir}/usr/lib/${pkgname}/bin
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/gcc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/g++
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/c++
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cpp

  install -Dm0644 $srcdir/distccd.service $pkgdir/usr/lib/systemd/system/distccd.service
}
