# Contributor: Martin Wimpress <code@flexion.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

_ver=1.16
_pkgbase=mate-sensors-applet
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libatasmart' 'libnotify' 'lm_sensors' 'mate-panel-gtk3')
makedepends=('docbook-xsl' 'intltool' 'itstool')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('71f7f75946ae0a0f3b266f79dcabc58a34a0ecba')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=3.0

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
