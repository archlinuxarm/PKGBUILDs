# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=electricsheep
pkgver=2.7b12
pkgrel=1
pkgdesc="A screensaver that realize the collective dream of sleeping computers from all over the internet"
arch=('i686' 'x86_64')
url="http://community.sheepserver.net/"
license=('GPL')
depends=('curl' 'expat' 'flam3' 'ffmpeg' 'mplayer' 'libglade')
makedepends=('xscreensaver')
optdepends=('xscreensaver: to use electricsheep with xscreensaver')
source=(ftp://ftp.archlinux.org/other/electricsheep/${pkgname}-${pkgver}.tar.bz2)
md5sums=('9c66691538941593ca9442e087b183d1')
sha1sums=('fd1c307912905ba456189efbb546ef0fe336fa32')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man || return 1
  make || return 1
  install -d "${pkgdir}/usr/share/applications/screensavers" || return 1
  install -d "${pkgdir}/usr/lib/gnome-screensaver/gnome-screensaver" || return 1
  make DESTDIR="${pkgdir}" GNOME_SCREENSAVER_DATADIR="${pkgdir}/usr/share/applications/screensavers" \
    GNOME_SCREENSAVER_PRIVEXEDIR="${pkgdir}/usr/lib/gnome-screensaver/gnome-screensaver" install || return 1
  install -D -m644 electricsheep.desktop.kde "${pkgdir}/usr/share/kde4/services/ScreenSavers/electricsheep.desktop" || return 1
  install -D -m755 electricsheep-saver ${pkgdir}/usr/bin/electricsheep-saver
}
