# $Id: PKGBUILD 60062 2011-12-04 15:25:13Z bluewind $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip lib32, etc. from ARM deps as well as i686

plugrel=1

pkgname=nspluginwrapper
pkgver=1.4.4
pkgrel=2
pkgdesc="Cross-platform NPAPI compatible plugin viewer"
arch=('i686' 'x86_64')
url="http://nspluginwrapper.davidben.net/"
license=('GPL')
depends=(
  'curl'
  'libxt' 'lib32-libxt'
  'gcc-libs' 'lib32-gcc-libs'
  'gtk2' 'lib32-gtk2'
)
makedepends=('gcc-multilib')
install="install"
source=(http://nspluginwrapper.davidben.net/download/$pkgname-$pkgver.tar.gz)
md5sums=('36f3e290fc4ce56f65ee695078961188')

if [[ $CARCH == i686 || $CARCH == arm || $CARCH == armv7h ]]; then
  # Strip lib32 etc. on i686 and ARM
  depends=(${depends[@]/*32-*/})
  makedepends=(${makedepends[@]/*32-*/})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
fi

build() {
  cd "$srcdir/$pkgname-$pkgver"

  configure_args=""
  if [[ $CARCH == x86_64 ]]; then
    configure_args="$configure_args --with-lib32=lib32 --with-lib64=lib"
  fi

  ./configure $configure_args
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
