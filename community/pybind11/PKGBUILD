# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Matthew Ellison <matt+aur@arroyonetworks.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto via -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=OFF

pkgname=pybind11
pkgver=2.7.0
pkgrel=2
pkgdesc='A lightweight header-only library that exposes C++ types in Python and vice versa'
arch=('any')
url='https://pybind11.readthedocs.org/'
license=('BSD')
optdepends=('python: for python bindings')
makedepends=('cmake' 'boost' 'eigen' 'python' 'python-setuptools' 'python-pytest')
             #'python-sphinx' 'python-sphinx_rtd_theme' 'python-breathe')
checkdepends=('python-numpy' 'python-scipy')
source=("https://github.com/pybind/pybind11/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-pybind11-fix-hardcoded-path.patch'::'https://github.com/pybind/pybind11/commit/7cc0ebb4753770ace452d118bb327cdd9e76dcbb.patch')
sha256sums=('6cd73b3d0bf3daf415b5f9b87ca8817cc2e2b64c275d65f9500250f9fee1677e'
            'd582a0f8e6ba2961e398d49d7693c647eb4aab501743d431db4d85fe652f005f')

prepare() {
    # FS#71638
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-pybind11-fix-hardcoded-path.patch"
}

build () {
    cd "${pkgname}-${pkgver}"
    python setup.py build
    
    # tests
    cmake \
        -B "${srcdir}/build-cmake" \
        -S "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=OFF \
        -Wno-dev
    make -C "${srcdir}/build-cmake" all mock_install
    
    # manpage (needs python-sphinxcontrib-{moderncmakedomain,svg2pdfconverter})
    # make -C "${srcdir}/${pkgname}-${pkgver}/docs" man
}

check() {
    make -C build-cmake check
}

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="$pkgdir" --skip-build --optimize='1'
    
    # symlinks
    local _pyver
    _pyver="$(python -c 'import sys; print("%s.%s" %sys.version_info[0:2])')"
    install -d -m755 "${pkgdir}/usr"/{include,lib/cmake}
    ln -s "../lib/python${_pyver}/site-packages/pybind11/include/pybind11" "${pkgdir}/usr/include/pybind11"
    ln -s "../../lib/python${_pyver}/site-packages/pybind11/share/cmake/pybind11" "${pkgdir}/usr/lib/cmake/pybind11"
    
    # man page
    # install -D -m644 "docs/.build/man/${pkgname}.1" "${pkgdir}/usr/share/man/man7/${pkgname}.7"
    # sed -i '/^\.TH/s/"1"/"7"/' "${pkgdir}/usr/share/man/man7/${pkgname}.7"
    
    # license
    install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
