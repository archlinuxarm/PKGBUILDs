# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Marco Fister <marco@rda26.net>

pkgname=acerhk
pkgver=0.5.35
pkgrel=14
pkgdesc="Acer hotkey driver"
url="http://www.cakey.de/acerhk/"
arch=('i686') # Unavailable for x86_64
license=('GPL')
depends=('kernel26>=2.6.30' 'kernel26<2.6.31')
source=(http://www.cakey.de/acerhk/archives/${pkgname}-${pkgver}.tgz acerhk.rc
        2.6.30.patch)
install=acerhk.install
md5sums=('05255919f312cb76af473a760c284433' '935584b3e4bfbb3af8460558e5b98bb5'
         '2a0bc99e5b12e25096441df4d53d7df7')
_kernver=2.6.30-ARCH

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's/CFLAGS/EXTRA_CFLAGS/' Makefile
  patch -Np1 -i ${srcdir}/2.6.30.patch || return 1
  make acerhk.ko || return 1
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/acerhk.ko \
    ${pkgdir}/lib/modules/${_kernver}/kernel/drivers/block/acerhk.ko
  install -Dm755 ${srcdir}/acerhk.rc ${pkgdir}/etc/rc.d/acerhk
}

# vim:set ts=2 sw=2 et:
