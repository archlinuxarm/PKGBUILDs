# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Peter Reschenhofer <peter.reschenhofer@gmail.com>

# ALARM: Andrea Scarpino <andrea@archlinux.org>
#  - disable documentation build

_pkgauthor=rfjakob
pkgname=gocryptfs
pkgver=1.7.1
pkgrel=2
pkgdesc='Encrypted overlay filesystem written in Go.'
arch=('x86_64')
url="https://github.com/${_pkgauthor}/${pkgname}"
license=('MIT')
depends=('gcc-libs' 'openssl' 'fuse')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz.asc")
sha256sums=('d3fc2c87b869025cd51e4abea030e58e7383197a7458f26bf99a71b224402bda'
            'SKIP')
validpgpkeys=('FFF3E01444FED7C316A3545A895F5BC123A02740')

prepare() {
    mkdir -p "${srcdir}/gopath/src/github.com/${_pkgauthor}"
    ln -rTsf "${srcdir}/${pkgname}_v${pkgver}_src-deps" "${srcdir}/gopath/src/github.com/${_pkgauthor}/${pkgname}"
}

build() {
    export GOPATH="${srcdir}/gopath"
    cd "${srcdir}/gopath/src/github.com/${_pkgauthor}/${pkgname}"
    ./build.bash
}

package() {
    cd "${srcdir}/gopath/src/github.com/${_pkgauthor}/${pkgname}"
    install -Dm755 -t "${pkgdir}/usr/bin/" gocryptfs
    install -Dm755 -t "${pkgdir}/usr/bin/" gocryptfs-xray/gocryptfs-xray
    install -Dm644 -t "${pkgdir}/usr/share/licenses/gocryptfs" LICENSE
}
