# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Joo Paulo F Farias <jpaulofarias@yahoo.com.br>

pkgname=xv
pkgver=3.10a
pkgrel=11
pkgdesc="xv is a nice image viewer"
arch=('i686' 'x86_64')
url="http://www.trilon.com/xv/"
license=('custom')
depends=('libpng' 'libx11' 'libtiff' 'libjpeg>=7')
makedepends=('libxt' 'jasper')
source=(ftp://ftp.trilon.com/pub/xv/${pkgname}-${pkgver}.tar.gz license.txt \
        http://downloads.sourceforge.net/sourceforge/png-mng/xv-3.10a-jumbo-patches-20070520.tar.gz)
md5sums=('2d4fbeec1561304362781cc8e2f7f72d' 'a26707358621d05501528171978280d8'\
         '9fb2d0ec320498105cafeba38a588472')
sha1sums=('9e6372f154be9e9e355972cbeb91d98d9c342474'
          '4561344e8f2c30eee9c55b14a14f062d89d6e7b7'
          'd00308c1687d9d803d26ef40c73d19a0f593c626')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../xv-3.10a-jumbo-fix-enh-patch-20070520.txt || return 1
  make JP2KDIR=/usr/lib || return 1
  install -d ${pkgdir}/usr/{bin,lib,share/man/man1}
  make DESTDIR=${pkgdir} PREFIX=/usr install || return 1

# installing source tarball content (to comply with license)	
  rm -r ${pkgdir}/usr/lib
  install -d ${pkgdir}/usr/share 
  tar -xzf ${srcdir}/${pkgname}-${pkgver}.tar.gz -C ${pkgdir}/usr/share
  mv ${pkgdir}/usr/share/${pkgname}-${pkgver} ${pkgdir}/usr/share/${pkgname}
  install -D -m644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

# fixing permissions/ownerswhip
  chown -R root:root ${pkgdir}/usr/share/${pkgname}
  chmod 644 ${pkgdir}/usr/share/xv/{tiff,vms}/*
  chmod 755 ${pkgdir}/usr/share/xv/tiff/RANLIB.csh
  rm -r ${pkgdir}/usr/share/xv/.xvpics
}
