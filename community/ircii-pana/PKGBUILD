# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@seznam.cz>
# inspired by Gentoo: http://kambing.ui.edu/gentoo-portage/net-irc/bitchx/files/

pkgname=ircii-pana
pkgver=1.1
pkgrel=3
pkgdesc="A console-based IRC client - AKA BitchX."
arch=('i686' 'x86_64')
url="http://www.bitchx.org"
license=('GPL2')
depends=('ncurses')
makedepends=('patch')
source=(http://downloads.sourceforge.net/bitchx/${pkgname}-${pkgver}-final.tar.gz \
        bitchx-${pkgver}-gcc43.diff \
        bitchx-${pkgver}-inline.diff \
        bitchx-${pkgver}-freenode.diff \
        bitchx-${pkgver}-latin1.diff \
        bitchx-${pkgver}-64bit.diff)

md5sums=('611d2dda222f00c10140236f4c331572'
         'e1efc5e52658e6651918e07a116bce18'
         '4824ffd55d51099dd9ee035f4b78d6bf'
         '741e91dd6267d3436c43756429342217'
         '9316105f23a67a9bb7ecfc7485dcd098'
         'c86f7758db7f438d726fd447dd4a83b0')

build() {
  cd "${srcdir}/BitchX"

  patch -Np1 -i "${srcdir}/bitchx-${pkgver}-gcc43.diff" || return 1
  patch -Np1 -i "${srcdir}/bitchx-${pkgver}-inline.diff" || return 1
  patch -Np1 -i "${srcdir}/bitchx-${pkgver}-freenode.diff" || return 1
  patch -Np1 -i "${srcdir}/bitchx-${pkgver}-latin1.diff" || return 1

  if [ "$CARCH" == "x86_64" ] ; then
	  patch -Np1 -i "${srcdir}/bitchx-${pkgver}-64bit.diff" || return 1
  fi
  ./configure --prefix=/usr
  make || return 1
  make prefix="${pkgdir}/usr" install

#we don't want to link to ${pkgdir}, correcting it then
  cd "${pkgdir}/usr/bin" || return 1
  rm -f "BitchX" && ln -s "BitchX-${pkgver}-final" "BitchX" || return 1

#correcting man page
  rm -rf "${pkgdir}/usr/man" || return 1
  install -D -m644 "${srcdir}/BitchX/doc/BitchX.1" "${pkgdir}/usr/share/man/man1/BitchX.1" || return 1
}
