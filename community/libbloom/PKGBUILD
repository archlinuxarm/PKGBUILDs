# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set BITS=default instead of MM=" "

pkgname=libbloom
pkgver=1.6
pkgrel=2
pkgdesc="A simple and small bloom filter implementation in plain C"
arch=('x86_64')
url="https://github.com/jvirkki/libbloom"
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jvirkki/libbloom/archive/v$pkgver.tar.gz")
sha512sums=('3dd4343d68def05800dd6ffc5a05b05d0702a8c0c9f0a397f105d4389c88a7bffa419734aeb4fa4e1a2c418b12a8192fa27ddcbaeee72906450d64cbfd64db15')

build() {
  cd libbloom-$pkgver
  BITS=default make
}

check() {
  cd libbloom-$pkgver
  BITS=default make test
}

package() {
  cd libbloom-$pkgver
  install -Dm644 bloom.h "$pkgdir"/usr/include/bloom.h
  install -Dm755 build/libbloom.so.$pkgver "$pkgdir"/usr/lib/libbloom.so.$pkgver
  ln -s libbloom.so.$pkgver "$pkgdir"/usr/lib/libbloom.so
  ln -s libbloom.so.$pkgver "$pkgdir"/usr/lib/libbloom.so.1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
