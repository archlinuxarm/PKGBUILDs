# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depends on libsmbios, efivar, fwupd-efi
#  - remove makedepends on valgrind, gnu-efi-libs, tpm2-tss
#  - meson with -Dplugin_dell=false (smbios dependency)
#  - meson with -Dplugin_uefi_capsule=false -Dplugin_uefi_pk=false -Dplugin_redfish=false -Dplugin_nvme=false (efivar, gnu-efi-libs dependency)
#  - meson with -Dplugin_msr=false
#  - meson with -Dplugin_intel_spi=false
#  - replace efi_sbat_distro_url with ALARM URL
#  - remove makedepend on pandoc, meson with -Dman=disabled

pkgname=fwupd
pkgver=1.8.13
pkgrel=1
pkgdesc="Simple daemon to allow session software to update firmware"
arch=(x86_64)
url="https://github.com/fwupd/fwupd"
license=(LGPL)
depends=(libxmlb python libgusb
         polkit shared-mime-info tpm2-tss flashrom
         libjcat gcab hicolor-icon-theme
         bluez gnutls
         libarchive.so libcurl.so libcbor.so
         libjson-glib-1.0.so libgudev-1.0.so libmm-glib.so
         libqmi-glib.so libprotobuf-c.so)
optdepends=(
    'udisks2: UEFI firmware upgrade support'
)
makedepends=(meson gobject-introspection gi-docgen
             python-cairo noto-fonts noto-fonts-cjk python-gobject vala
             bash-completion python-pillow)
checkdepends=(umockdev)
provides=(libfwupd.so)
backup=('etc/fwupd/daemon.conf'
        'etc/fwupd/redfish.conf'
        'etc/fwupd/remotes.d/dell-esrt.conf'
        'etc/fwupd/remotes.d/fwupd-tests.conf'
        'etc/fwupd/remotes.d/lvfs-testing.conf'
        'etc/fwupd/remotes.d/lvfs.conf'
        'etc/fwupd/remotes.d/vendor-directory.conf'
        'etc/fwupd/remotes.d/vendor.conf'
        'etc/fwupd/thunderbolt.conf'
        'etc/fwupd/uefi_capsule.conf'
)
source=("https://github.com/fwupd/fwupd/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('82616a3a239574020edbe6d026fb1652a0b0d1e173cb67f85d05829d4ab498852d5137019665de3c7102f16853aa4b195a9de7df039bc18857b226b04b5aec71'
            'SKIP')
b2sums=('fb4d172e88dd15e13e4a682e87ce65f7b7ff25bd952fc2efed54bf5d8ad32874f91f143c17c90d4d63ec9138f214338f55d073a2c82bc86bf01b2531ef4a88cc'
        'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
    arch-meson ${pkgname}-${pkgver} build \
        -D b_lto=false \
        -D docs=enabled \
        -D plugin_intel_spi=false \
        -D supported_build=enabled \
        -D efi_binary=false \
        -Dplugin_dell=false -Dplugin_uefi_capsule=false -Dplugin_uefi_pk=false -Dplugin_redfish=false -Dplugin_nvme=false -Dplugin_msr=false -Dman=disabled
    meson compile -C build
}

check() {
    meson test -C build
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
    # Fixup mode to match polkit
    install -d -o root -g 102 -m 750 "${pkgdir}"/usr/share/polkit-1/rules.d
    # Remove the tests
    rm -r "${pkgdir}"/usr/share/installed-tests/
    #mv "${pkgdir}"/usr/bin/{,fwupd-}dbxtool
    #mv "${pkgdir}"/usr/share/man/man1/{,fwupd-}dbxtool.1
    # Remove msr module-load config as it is built-in
    rm -f "${pkgdir}"/usr/lib/modules-load.d/fwupd-msr.conf
    rm -rf "${pkgdir}"/usr/lib/modules-load.d
}
