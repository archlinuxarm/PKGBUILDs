# $Id: PKGBUILD 57041 2011-10-19 06:13:23Z schuay $
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Thayer Williams <thayer@archlinux.org>

# Kevin Mihelich <kevin@archlinuxarm.org>
#  - rebuild for poppler, remove when bumped upstream

pkgname=epdfview
pkgver=0.1.8
pkgrel=3.1
pkgdesc="A free lightweight PDF document viewer."
url="http://www.emma-soft.com/projects/epdfview/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('poppler-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'gtk2')
makedepends=('pkgconfig')
install='epdfview.install'
source=("http://www.emma-soft.com/projects/${pkgname}/chrome/site/releases/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop.patch"
        "0001-When-using-Poppler-0.17.0-I-needed-to-swap-the-blue-.patch")
md5sums=('e50285b01612169b2594fea375f53ae4'
         'fbf22bbabdbb7544db615ac5775d57e2'
         '7f9ea101a41f5b4e999fd024f423d41f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/${pkgname}.desktop.patch"
  patch -Np1 -i "${srcdir}/0001-When-using-Poppler-0.17.0-I-needed-to-swap-the-blue-.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for size in 24 32 48; do
      install -Dm644 data/icon_${pkgname}-${size}.png \
          "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png"
  done
}
