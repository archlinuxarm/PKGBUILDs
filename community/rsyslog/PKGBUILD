# $Id: PKGBUILD 2589 2009-09-19 17:18:10Z ibiru $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rsyslog
pkgver=3.22.1
pkgrel=2
pkgdesc="An enhanced multi-threaded syslogd with a focus on security and reliability"
url="http://www.rsyslog.com/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('zlib')
makedepends=('postgresql-libs>=8.4.1' 'libmysqlclient')
optdepends=('postgresql-libs: PostgreSQL Database Support'
            'libmysqlclient: MySQL Database Support')
backup=('etc/rsyslog.conf' \
	'etc/logrotate.d/rsyslog')
options=('strip' 'zipman')
source=("http://download.rsyslog.com/${pkgname}/${pkgname}-${pkgver}.tar.gz" \
        'rsyslog' \
	'rsyslog.logrotate')
md5sums=('64bed3d5535c17d631f7432a5badf959'
         '136d3ec0c7b46979f8750ae4c45b3ed5'
         '8065db4bef3061a4f000ba58779f6829')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --enable-mysql \
              --enable-pgsql \
              --enable-mail \
              --enable-imfile \
              --enable-imtemplate \
              --enable-inet || return 1
  make || return 1
  make install DESTDIR=${pkgdir} || return 1

  # Install Daemons and Configuration Files
  install -D -m755 ${srcdir}/${pkgname} ${pkgdir}/etc/rc.d/${pkgname}d && \
  install -D -m644 ${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf && \
  install -D -m644 $srcdir/${pkgname}.logrotate ${pkgdir}/etc/logrotate.d/${pkgname}
}
