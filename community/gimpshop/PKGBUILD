# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Woody Gilk <woody.gilk@gmail.com>

pkgname=gimpshop
pkgver=2.2.11
pkgrel=5
pkgdesc="A modified Gimp that features menu layouts and naming conventions similar to those in Photoshop."
arch=('i686' 'x86_64')
url="http://plasticbugs.com/?page_id=294"
license=("GPL")
options=('!libtool')
depends=('libxmu' 'libxpm' 'gtk2' 'librsvg' 'libmng' 'lcms' 'libexif' 'libwmf' 'libart-lgpl' 'alsa-lib' 'libgtkhtml')
makedepends=('pkgconfig' 'intltool')
conflicts=('gimp' 'gimp-devel')
provides=('gimp' 'gimp-devel')
install=gimpshop.install
source=(http://www.plasticbugs.com/blogimg/$pkgname-$pkgver.tar.bz2
	png_patch.diff
	linux.gpl)
md5sums=('78e25b31b1dab1469cc24ba2a8e30954'
	 '8d44110eaa10420b467c40bbf23ade4b'
         'bb27bc214261d36484093e857f015f38')

build() {
  cd $startdir/src/gimp-$pkgver

  patch -p1 < ../png_patch.diff || return 1

  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-mp --enable-gimp-console \
    --enable-python --with-gif-compression=lzw --without-aa --disable-print --disable-mmx
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

  install -D -m644 $startdir/src/linux.gpl \
    $startdir/pkg/usr/share/gimp/2.0/palettes/Linux.gpl && \
  ln -s /usr/bin/gimp-2.2 $startdir/pkg/usr/bin/gimpshop || return 1

  cd $startdir/pkg
  mkdir -p usr/share/{applications,mime-info}
  mv usr/share/gimp/2.0/misc/gimp.desktop usr/share/applications/ && \
  mv usr/share/gimp/2.0/misc/gimp.keys usr/share/mime-info/ && \
  rm -r usr/share/gimp/2.0/misc
}
