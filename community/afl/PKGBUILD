# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with AFL_NOX86=1

pkgname=afl
pkgver=1.90b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
arch=('i686' 'x86_64')
url="http://lcamtuf.coredump.cx/afl/"
license=('Apache')
depends=('glibc' 'bash')
optdepends=(
  'gcc: gcc instrumentation support'
  'clang: clang instrumentation support'
)
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz)
sha512sums=('eb78f1ab61af887179b1c49a7a2e67ab6a67c61eefe574492db488aaab5ee0dd0468761d08f7c37bccab3f09fd62cbc74283ab429e1fe3d11bcb60663a093b93')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NOX86=1
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NOX86=1 DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
