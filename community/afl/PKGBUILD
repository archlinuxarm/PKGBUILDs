# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with AFL_NO_X86=1

pkgname=afl
pkgver=2.50b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
url='http://lcamtuf.coredump.cx/afl/'
arch=('i686' 'x86_64')
license=('Apache')
depends=('glibc' 'bash')
optdepends=('gcc: gcc instrumentation support'
            'clang: clang instrumentation support'
            'llvm: experimental clang-fast instrumentation support'
            'gnuplot: graph plotting support')
makedepends=('clang' 'llvm' 'gcc' 'wget' 'python')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz)
sha256sums=('0bdb0efd77b394b8b1a816e957a6dc27ffe01007d73c6247b9e424f0e36176c5')
sha512sums=('a06cc3c8f0613e105805b4dfba59fffcc2b6d9b519df56dc4af7085f1edffa1a63b9a5cccd1b65354b87584c7b060f71b50e7ad29f55584799d20479b898a4f9')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NO_X86=1
  make -C llvm_mode PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NO_X86=1 DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
