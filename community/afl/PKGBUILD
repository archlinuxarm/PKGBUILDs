# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with AFL_NOX86=1

pkgname=afl
pkgver=1.99b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
url="http://lcamtuf.coredump.cx/afl/"
arch=('i686' 'x86_64')
license=('Apache')
depends=('glibc' 'bash')
optdepends=(
  'gcc: gcc instrumentation support'
  'clang: clang instrumentation support'
  'llvm: experimental clang-fast instrumentation support'
  'gnuplot: graph plotting support'
)
makedepends=('clang' 'llvm' 'gcc')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz)
sha512sums=('10c01016ac356f79f72d012ed342efd88f0e8fe8c2f00bac82cd1b084ec536be0f3fb77485d8baacf0aeb51835c47fffdd7baf3498d5d779499524ada6a5ef88')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NOX86=1
  make -C llvm_mode PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NOX86=1 DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm "${pkgdir}/usr/share/doc/${pkgname}/README.llvm"
}

# vim: ts=2 sw=2 et:
