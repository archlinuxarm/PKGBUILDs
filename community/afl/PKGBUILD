# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with AFL_NOX86=1

pkgname=afl
pkgver=1.95b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
arch=('i686' 'x86_64')
url="http://lcamtuf.coredump.cx/afl/"
license=('Apache')
depends=('glibc' 'bash')
optdepends=(
  'gcc: gcc instrumentation support'
  'clang: clang instrumentation support'
  'llvm: experimental clang-fast instrumentation support'
  'gnuplot: graph plotting support'
)
makedepends=('clang' 'llvm' 'gcc')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz)
sha512sums=('833f8a5d6f1c80afcfe07dd07f845f502106be5aa647f50a8ac430cfc929731f339797e74cbf835629de441af3dd5171fa99a8d9844f31a34fa65ee645850420')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NOX86=1
  make -C llvm_mode PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr AFL_NOX86=1 DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm "${pkgdir}/usr/share/doc/${pkgname}/README.llvm"
}

# vim: ts=2 sw=2 et:
