# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dmitriy Morozov <foxcub>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - qt5-svg for aarch64 only due to build issue with Qt GLES

pkgname=cgal
pkgver=4.9.1
pkgrel=1
pkgdesc="Computational Geometry Algorithms Library"
arch=('i686' 'x86_64')
url="http://www.cgal.org"
license=('GPL' 'LGPL')
source=("https://github.com/CGAL/cgal/releases/download/releases%2FCGAL-$pkgver/CGAL-$pkgver.tar.xz")
depends=('mpfr' 'gmp' 'mesa' 'glu' 'boost-libs')
optdepends=('eigen: for some packages, see the CGAL manual')
optdepends_aarch64=('qt5-svg: for CGAL_Qt5')
makedepends=('cmake' 'eigen' 'boost')
makedepends_aarch64=('qt5-svg')
md5sums=('820ef17ffa7ed87af6cc9918a961d966')

# consider building with swig for python stuff
# https://gforge.inria.fr/frs/?group_id=52  for _pkgid

# 4.8 supposedly does not depend on boost?  good luck...

build() {
  cd "$srcdir/CGAL-$pkgver"
  mkdir -p build
  cd build
  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "$srcdir/CGAL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  # The tarball still has all these licenses included
  for _license in LICENSE{,.FREE_USE,.GPL,.LGPL}; do
    install -D -m644 "$srcdir/CGAL-$pkgver/$_license" "$pkgdir/usr/share/licenses/$pkgname/$_license"
  done
}

