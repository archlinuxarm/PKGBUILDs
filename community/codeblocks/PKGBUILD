# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Roman Mikhayloff <rimf@inbox.ru>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - comment out x86 cflags

pkgname=codeblocks
pkgver=16.01
pkgrel=7
pkgdesc='Cross-platform C/C++ IDE'
arch=('x86_64' 'i686')
url='http://www.codeblocks.org/'
license=('GPL3')
depends=('boost-libs' 'desktop-file-utils' 'gamin' 'gtk-update-icon-cache'
         'hicolor-icon-theme' 'hunspell' 'shared-mime-info' 'wxgtk2.8' 'libsm' 'webkitgtk2')
makedepends=('boost' 'zip' 'webkitgtk2')
source=("http://sourceforge.net/projects/codeblocks/files/Sources/$pkgver/codeblocks_$pkgver.tar.gz")
sha256sums=('30fb9d206df4ab3c2e2fec2451a0a7c9723bfed9a673cc9be71e881c529d5a03')

build() {
  cd "$pkgname-$pkgver.release"

  # Fix build with boost 1.59
  export CPPFLAGS+=" -DBOOST_ERROR_CODE_HEADER_ONLY"
  export WX_CONFIG_PATH=/usr/bin/wx-config-2.8

  # FS#49799
  #export CFLAGS="-mtune=generic -O0 -pipe -fstack-protector-strong --param=ssp-buffer-size=4"
  #export CXXFLAGS="-mtune=generic -O0 -pipe -fstack-protector-strong --param=ssp-buffer-size=4"
  #export LDFLAGS="-Wl,-O1,--sort-common,--as-needed,-z,relro"

  ./bootstrap
  ./configure --prefix=/usr --with-contrib-plugins=all
  make
}

package() {
  make -C "$pkgname-$pkgver.release" DESTDIR="$pkgdir" install
}

# getver: -u 2 codeblocks.org/downloads/source
# vim:set ts=2 sw=2 et:
