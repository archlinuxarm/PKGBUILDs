# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Anders Bergh <anders1@gmail.com>
# Contributor: Ustalov "eveel" Dmitry <eveeel@gmail.com>
pkgname=dsss
pkgver=0.78
pkgrel=1
pkgdesc="The D Shared Software System"
arch=(i686)
url="http://dsource.org/projects/dsss"
license=('MIT')
makedepends=('dmd' 'libphobos')
source=(http://svn.dsource.org/projects/dsss/downloads/$pkgver/dsss-$pkgver.tar.bz2
        fix-dmd-tango.patch)
md5sums=('e018d5174a8d1a0c873056634389a060'
         'f6fb11f5d816351c4d35f9f11d8cd360')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -p1 < $srcdir/fix-dmd-tango.patch

  # rebuild makefile is broken
  export MAKEFLAGS="-j1"

  make -f Makefile.dmd.posix

  ./dsss install \
    --prefix=$pkgdir/usr \
    --libdir=$pkgdir/usr/lib \
    --includedir=$pkgdir/usr/include/d \
    --docdir=$pkgdir/usr/share/doc \
    --sysconfdir=$pkgdir/etc \
    --scratchdir=$pkgdir/tmp

  install -Dm644 docs/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

