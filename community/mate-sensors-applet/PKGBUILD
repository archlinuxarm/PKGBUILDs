# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

pkgname=mate-sensors-applet
pkgver=1.20.1
pkgrel=1
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libatasmart' 'libnotify' 'lm_sensors' 'mate-panel')
makedepends=('docbook-xsl' 'intltool' 'itstool')
groups=('mate-extra')
conflicts=('mate-sensors-applet-gtk3')
replaces=('mate-sensors-applet-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bd00e792ec3948bf895c8ff590184da30fa5614bc6969d450cfc8381a51cd952')

build() {
	cd ${pkgname}-${pkgver}
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname}

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  	make
}

package() {
    	cd ${pkgname}-${pkgver}
    	make DESTDIR="${pkgdir}" install
}
