# Maintainer:
# Contributor: Martin Wimpress <code@flexion.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

pkgname=mate-sensors-applet
pkgver=1.18.3
pkgrel=1
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings"
url="https://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libatasmart' 'libnotify' 'lm_sensors' 'mate-panel')
makedepends=('docbook-xsl' 'intltool' 'itstool')
groups=('mate-extra')
conflicts=('mate-sensors-applet-gtk3')
replaces=('mate-sensors-applet-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('9dd5f097b4332a8d84b5b6e842ad529f0ebcd72ffa69ce8fde6f8e736146a6da')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname}

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
