# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove dependency on numactl

pkgname=xtrabackup
pkgver=8.0.4
_boost_ver=1.67.0
pkgrel=2
pkgdesc='Non-blocking backup tool for MySQL'
arch=('x86_64')
url='https://www.percona.com/software/mysql-database/percona-xtrabackup'
license=('GPL')
depends=('libaio' 'libev' 'libgcrypt' 'curl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib' 'vim' 'libedit' 'libevent' 'protobuf' 're2')
optdepends=('qpress: for compressed backups')
source=(https://www.percona.com/downloads/XtraBackup/Percona-XtraBackup-$pkgver/source/tarball/percona-xtrabackup-$pkgver.tar.gz
        http://sourceforge.net/projects/boost/files/boost/${_boost_ver}/boost_${_boost_ver//./_}.tar.gz)
sha256sums=('0a530146ef7abbfef65231848da0c077e77b0a2512ce4aeddc5e38fb645b37bd'
            '8aa4e330c870ef50a896634c931adf468b21f8a69b77007e45c444151229f665')
build() {
  cd percona-xtrabackup-$pkgver
  cmake -DBUILD_CONFIG=xtrabackup_release \
        -DCMAKE_BUILD_TYPE=Release \
        -DMYSQL_DATADIR=/var/lib/mysql \
        -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
        -DWITH_LIBEVENT=system \
        -DWITH_SSL=system \
        -DWITH_EDITLINE=system \
        -DWITH_LZ4=system \
        -DWITH_LZMA=bundled \
        -DWITH_PROTOBUF=system \
        -DWITH_RE2=system \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DINSTALL_PLUGINDIR="/usr/lib/xtrabackup/plugin" \
        -DINSTALL_MANDIR="/usr/share/man" \
        -DINSTALL_STATIC_LIBRARIES=OFF \
        -DWITH_BOOST="../boost_${_boost_ver//./_}"
  make
}

package() {
  cd percona-xtrabackup-$pkgver
  make DESTDIR="$pkgdir" install

  cd  "$pkgdir"
  rm -rf usr/xtrabackup-test usr/lib/xtrabackup/plugin/debug
}
