# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove dependency on numactl

pkgname=xtrabackup
pkgver=8.0.10
_boostver=1.70.0
pkgrel=1
pkgdesc='Non-blocking backup tool for MySQL'
arch=('x86_64')
url='https://www.percona.com/software/mysql-database/percona-xtrabackup'
license=('GPL')
depends=('libaio' 'libev' 'libgcrypt' 'curl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib' 'vim' 'libedit' 'libevent' 'protobuf' 're2')
optdepends=('qpress: for compressed backups')
source=("https://www.percona.com/downloads/Percona-XtraBackup-LATEST/Percona-XtraBackup-${pkgver%.*}-${pkgver##*.}/source/tarball/percona-xtrabackup-$pkgver.tar.gz"
        "https://dl.bintray.com/boostorg/release/${_boostver}/source/boost_${_boostver//./_}.tar.bz2")

sha256sums=('6d1b6ed78ace1b9e8ca6fe3ec73e07db8d662e4270fff4f77cd41e4021ee9364'
            '430ae8354789de4fd19ee52f3b1f739e1fba576f0aded0897c3c2bc00fb38778')
build() {
  mkdir build
  cd build
  cmake -DBUILD_CONFIG=xtrabackup_release \
        -DCMAKE_BUILD_TYPE=Release \
        -DMYSQL_DATADIR=/var/lib/mysql \
        -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
        -DWITH_LIBEVENT=system \
        -DWITH_SSL=system \
        -DWITH_EDITLINE=system \
        -DWITH_LZ4=system \
        -DWITH_LZMA=bundled \
        -DWITH_PROTOBUF=system \
        -DWITH_RE2=system \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DINSTALL_PLUGINDIR="/usr/lib/xtrabackup/plugin" \
        -DINSTALL_MANDIR="/usr/share/man" \
        -DINSTALL_STATIC_LIBRARIES=OFF \
        -DWITH_BOOST="../boost_${_boostver//./_}" \
        ../percona-xtrabackup-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  cd  "$pkgdir"
  rm -rf usr/xtrabackup-test \
         usr/lib/xtrabackup/plugin/debug \
         usr/docs \
         usr/share/man/man8 \
         usr/share/man/man1/{c,i,m,l,p,z}*
}
