# Maintainer : Daenyth <Daenyth+Arch _AT_ gmail _DOT_ com>
# Contributor : wizzomafizzo <wizzomafizzo@gmail.com>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: sh__

pkgname=uqm
pkgver=0.6.2
pkgrel=4
pkgdesc="The Ur-Quan Masters is a port of Star Control 2"
arch=('i686' 'x86_64')
url="http://sc2.sf.net/"
license=('GPL')
depends=('libmikmod' 'libvorbis' 'sdl_image' 'libgl')
makedepends=('sed')
source=(http://downloads.sourceforge.net/sourceforge/sc2/uqm-${pkgver}-source.tgz
        http://downloads.sourceforge.net/sourceforge/sc2/uqm-0.6.0-content.uqm
        config.state uqm)
md5sums=('ba4ee595b62349873d510b9827e7648d' '7e8f0ed8490e24231431420ea2ba6a03'
         '07f0a0e74a0b1c706940cc43d5a4160c' '07e64632fce6323a14ab558d0214b885')

build() {
  cd $srcdir/$pkgname-$pkgver/sc2
  
  cp $srcdir/config.state .
  sed -e "/INPUT_install_prefix/ s|replaceme|$pkgdir/usr|" \
      -i config.state

  echo | ./build.sh uqm config

  ./build.sh uqm
  ./build.sh uqm install

  install -Dm644 $srcdir/$pkgname-0.6.0-content.uqm \
     $pkgdir/usr/share/uqm/content/packages/$pkgname-0.6.0-content.uqm

  rm $pkgdir/usr/bin/uqm
  install -Dm755 $srcdir/uqm $pkgdir/usr/bin/uqm
}

# vim:set ts=2 sw=2 et:
