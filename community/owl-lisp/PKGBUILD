# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepends on pandoc
#  - don't make manual.md, don't install it

pkgname=owl-lisp
pkgver=0.1.17
pkgrel=1
pkgdesc='Simple purely functional lisp'
url='https://gitlab.com/owl-lisp/owl.git'
arch=('x86_64')
license=('MIT')
depends=('glibc')
source=(${pkgname}-${pkgver}.tar.gz::https://gitlab.com/owl-lisp/owl/-/archive/v${pkgver}/owl-v${pkgver}.tar.gz)
sha256sums=('02128f0ab7e5a05fec958f687cb0edac18a077fb0c339807d9660d35e18bf59a')
sha512sums=('78e845027d388b963bf320b603b204632a1771235a6a054f605e63f6dc7ac222c7bd42115211a32b23439220957ef1390f83aec86f836e1d6c32d62193fa819c')

prepare() {
  cd owl-v${pkgver}
  # support distro CFLAGS and CPPFLAGS
  sed -e'/export CFLAGS/d' -e '/CFLAGS =/d' \
    -e 's|$(CFLAGS)|$(CFLAGS) $(CPPFLAGS)|g' -i Makefile
  # make build reproducible, we compress ourselves
  sed -e 's|gzip -9|@echo|g' -e 's|\.gz||g' -i Makefile
}

build() {
  cd owl-v${pkgver}
  make all
}

package() {
  cd owl-v${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 fasl/* -t "${pkgdir}/var/lib/${pkgname}/fasl"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
