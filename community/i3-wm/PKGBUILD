# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cd into ${CHOST} directory for build/package

pkgname=i3-wm
_pkgsourcename=i3
pkgver=4.13
pkgrel=1
pkgdesc='An improved dynamic tiling window manager'
arch=('i686' 'x86_64')
url='http://i3wm.org/'
license=('BSD')
replaces=('i3' 'i3bar')
groups=('i3')
depends=('xcb-util-cursor' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm'
         'libev' 'yajl' 'startup-notification' 'pango' 'libxkbcommon-x11')
makedepends=('bison' 'flex')
optdepends=('dmenu: As menu.'
            'i3lock: For locking your screen.'
            'i3status: To display systeminformation with a bar.'
            'perl: i3-save-tree and i3-dmenu-desktop'
            'perl-anyevent-i3: Features like saving the layout.'
            'perl-json-xs: Features like saving the layout.')
options=('docs' '!strip')
source=("http://i3wm.org/downloads/${_pkgsourcename}-${pkgver}.tar.bz2"
        "http://i3wm.org/downloads/${_pkgsourcename}-${pkgver}.tar.bz2.asc")
md5sums=('08d17dcf1fde665a15f7d411486546ae'
         'SKIP')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

build() {
  cd "$srcdir/$_pkgsourcename-$pkgver"
  
  ./configure --prefix=/usr --sysconfdir=/etc
  cd ${CHOST}
  # In order to avoid problems with bison use only a single process
  MAKEFLAGS="-j1"
  make CPPFLAGS+="-U_FORTIFY_SOURCE"
}

package() {
  cd "$srcdir/$_pkgsourcename-$pkgver/${CHOST}"
  make DESTDIR="$pkgdir/" install
  
  mkdir -p "${pkgdir}/usr/share/man/man1/"
  install -m644 ../man/*.1 "${pkgdir}/usr/share/man/man1/"

  install -Dm644 ../LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
