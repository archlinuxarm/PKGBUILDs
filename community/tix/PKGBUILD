# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brice Méalier <mealier_brice@yahoo.fr>

pkgname=tix
pkgver=8.4.2
pkgrel=4
pkgdesc="Tk Interface eXtension, a powerful set of user interface components"
arch=(i686 'x86_64')
url="http://tix.sourceforge.net/"
license=("BSD")
depends=('tk' 'python' 'libx11')
source=(http://downloads.sourceforge.net/tix/Tix$pkgver-src.tar.gz)
md5sums=('422a4e1c5f64da203c753a596b0d74de')

build() {
  cd $startdir/src/Tix$pkgver

  export CFLAGS="$CFLAGS -DERR_IN_PROGRESS=2"

  ./configure --prefix=/usr --with-tclconfig=/usr/lib/ --with-tclinclude=/usr/include --with-tkconfig=/usr/lib --with-tkinclude=/usr/include
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1
  # move things around

  install -m755 tools/tixindex $startdir/pkg/usr/bin/tixindex && \
  rm -rf $startdir/pkg/usr/lib/Tix8.4/html && \
  rm -f $startdir/pkg/usr/lib/Tix8.4/{README.txt,license.terms} && \
  ln -s Tix$pkgver/libTix$pkgver.so $startdir/pkg/usr/lib/libTix$pkgver.so || return 1

  mkdir -p $startdir/pkg/usr/share/man/man1 && \
  mkdir -p $startdir/pkg/usr/share/man/man3 && \
  cp -p man/tixwish.1 $startdir/pkg/usr/share/man/man1 || return 1
  cd $startdir/src/Tix$pkgver/man
  for i in *.n; do \
	  cp -p $i $startdir/pkg/usr/share/man/man3/${i%n}3  || return 1; \
  done

  mkdir -p $startdir/pkg/usr/share/licenses/tix && \
  cp $startdir/src/Tix$pkgver/license.terms  $startdir/pkg/usr/share/licenses/tix/
}
