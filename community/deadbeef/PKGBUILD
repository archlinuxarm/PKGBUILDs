# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Alexey Yakovenko <waker@users.sourceforge.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patches to fix ARM FTBFS

pkgname=deadbeef
pkgver=0.7.2
pkgrel=4
pkgdesc='A GTK+ audio player for GNU/Linux.'
arch=('x86_64')
url='http://deadbeef.sourceforge.net'
license=('GPL2')
depends=('alsa-lib' 'hicolor-icon-theme' 'desktop-file-utils' 'jansson')
makedepends=('libvorbis' 'libmad' 'flac' 'curl' 'imlib2' 'wavpack' 'libsndfile' 'libcdio' 'libcddb'
             'libx11' 'faad2' 'zlib' 'intltool' 'pkgconfig' 'libpulse' 'libzip' 'libsamplerate'
             'yasm' 'ffmpeg' 'gtk2' 'gtk3')
optdepends=('gtk2: for the GTK2 interface'
            'gtk3: for the GTK3 interface'
            'libsamplerate: for Resampler plugin'
            'libvorbis: for Ogg Vorbis playback'
            'libmad: for MP1/MP2/MP3 playback'
            'mpg123: for MP1/MP2/MP3 playback'
            'flac: for FLAC playback'
            'curl: for Last.fm scrobbler, SHOUTcast, Icecast, Podcast support'
            'imlib2: for artwork plugin'
            'wavpack: for WavPack playback'
            'libsndfile: for Wave playback'
            'libcdio: audio cd plugin'
            'libcddb: audio cd plugin'
            'faad2: for AAC/MP4 support'
            'dbus: for OSD notifications support'
            'pulseaudio: for PulseAudio output plugin'
            'libx11: for global hotkeys plugin'
            'zlib: for Audio Overload plugin'
            'libzip: for vfs_zip plugin'
            'ffmpeg: for ffmpeg plugin')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'https://github.com/DeaDBeeF-Player/deadbeef/commit/394536bd135dd5f9fac211c391ce2e1a04bda6da.patch'
        'https://github.com/DeaDBeeF-Player/deadbeef/commit/7163099d55c64bf11c3dac4dadfd83a0495ea207.patch'
        'https://github.com/DeaDBeeF-Player/deadbeef/commit/8fdeb8102d7a8be6773dc4accc5fe87901488724.patch')
md5sums=('f8f5353e7c201fce316f8b082ab408bb'
         '27a0ace231268e3aa9f46e2fe7d83c46'
         '064940a72fcd5c74890893ccfa0c5d14'
         '864a557631a61306270a8bdcc879c79f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../394536bd135dd5f9fac211c391ce2e1a04bda6da.patch
  patch -p1 -i ../7163099d55c64bf11c3dac4dadfd83a0495ea207.patch
  patch -p1 -i ../8fdeb8102d7a8be6773dc4accc5fe87901488724.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
}
