# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Frank Ickstadt (frank dot ickstadt at gmail dot com)

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't run makepackagebin.sh, not ARM friendly, not needed

pkgname=argyllcms
_pkgname=Argyll
pkgver=1.7.0
_pkgver="V${pkgver}"
pkgrel=1
pkgdesc="An ICC compatible color management system with support for different colorimeter hardware"
arch=(i686 x86_64)
depends=('libxss' 'libxinerama' 'libxxf86vm' 'libxrandr' 'zlib')
makedepends=('ftjam' 'zip' 'unzip')
# You need to fake an UserAgent or it doesn't download
source=(http://www.argyllcms.com/${_pkgname}_${_pkgver}_src.zip)
url="http://www.argyllcms.com/"
license=("GPL" "AGPL")
install=${pkgname}.install
sha256sums=('dac51cf5d8f6d04bb02f2f5b119fa0e8b773a010e6377116768b082ef018f663')

prepare() {
  sed -i 's:^TEST=="/lib/udev/usb-db", IMPORT{program}="usb-db %p":IMPORT{builtin}="hwdb --subsystem=usb":' ${_pkgname}_${_pkgver}/usb/55-Argyll.rules
}

build() {
  cd ${_pkgname}_${_pkgver}
  echo "***running makeall.sh***"
  sh ./makeall.sh
  echo "***running makeinstall.sh***"
  sh ./makeinstall.sh
  #echo "***running makepackagebin.sh***"
  #sh ./makepackagebin.sh
  rm {bin,ref}/License.txt
  rm {doc,ref}/afiles
}

package() {
  mkdir -p "${pkgdir}"/usr/bin
  install -m755 ${_pkgname}_${_pkgver}/bin/* "${pkgdir}"/usr/bin
  mkdir -p "${pkgdir}"/usr/share/${pkgname}/ref
  install -m644 ${_pkgname}_${_pkgver}/ref/*.* "${pkgdir}"/usr/share/${pkgname}/ref
  install -m644 ${_pkgname}_${_pkgver}/profile/*.sp "${pkgdir}"/usr/share/${pkgname}/ref
  install -m644 ${_pkgname}_${_pkgver}/scanin/*.c?? "${pkgdir}"/usr/share/${pkgname}/ref
  # Because there is just html documentation we have to move it
  mkdir -p "${pkgdir}"/usr/share/${pkgname}/doc/ccmxs
  install -m644 ${_pkgname}_${_pkgver}/doc/ccmxs/*.ccmx "${pkgdir}"/usr/share/${pkgname}/doc/ccmxs
  install -m644 ${_pkgname}_${_pkgver}/doc/*.* "${pkgdir}"/usr/share/${pkgname}/doc
  mkdir -p "${pkgdir}"/usr/lib/udev/rules.d
  install -m644 ${_pkgname}_${_pkgver}/usb/55-Argyll.rules "${pkgdir}"/usr/lib/udev/rules.d/55-Argyll.rules
}

# vim:set ts=2 sw=2 et:
