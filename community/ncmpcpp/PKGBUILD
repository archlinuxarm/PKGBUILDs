# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -march=native from extras Makefile

pkgname=ncmpcpp
pkgver=0.8
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
url='http://ncmpcpp.rybczak.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
makedepends=('boost')
source=(${pkgname}-${pkgver}.tar.bz2::https://rybczak.net/ncmpcpp/stable/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('b51217df85f95efa345f0484f16024bd74c1c0e22f2c2b25238335fc2f00caf81877088170f241c1d97fd328d104b19f022dc4de9895ff420f1fcbea341e7cdb')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/-march=native//' extras/Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  BOOST_LIB_SUFFIX='' ./configure \
    --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
