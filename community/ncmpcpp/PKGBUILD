# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -march=native from extras Makefile

pkgname=ncmpcpp
pkgver=0.8.2
pkgrel=14
pkgdesc='Almost exact clone of ncmpc with some new features'
url='https://ncmpcpp.rybczak.net/'
arch=('x86_64')
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
makedepends=('boost')
source=(${pkgname}-${pkgver}.tar.bz2::https://rybczak.net/ncmpcpp/stable/${pkgname}-${pkgver}.tar.bz2
        gcc10.patch)
sha512sums=('9384edd162f40af23e4f26f437356520f117e4e2ab513ed3de132d2f11e597082d1cb7f0dd3696660d0c9bb85ad03e31fb921c97a11c7b28b32c5907fdbb8e8f'
            '99298d813391c03fc70ab16ffe8dd57c6151b87f4cd84f25a1747691d8f9f41e5f647b59c0d2ca25808acb995c7df6c99f386a4cc1f3f133b722731ef99a8a3b')

prepare() {
  cd ${pkgname}-${pkgver}

  # https://github.com/ncmpcpp/ncmpcpp/pull/385
  patch -Np1 -i ../gcc10.patch

  sed -i 's/-march=native//' extras/Makefile
}

build() {
  cd ${pkgname}-${pkgver}

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CPPFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  BOOST_LIB_SUFFIX='' ./configure \
    --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
