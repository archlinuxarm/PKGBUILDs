# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=schroot
pkgver=1.2.3_1
_basever=1.2.3
pkgrel=2
pkgdesc="Allows users to execute shell commands under different root filesystems. \
	 (Successor to dchroot)."
url="http://packages.qa.debian.org/s/schroot.html"
license=('GPL3')
depends=('pam' 'lockdev' 'boost' 'e2fsprogs')
arch=('i686' 'x86_64')
conflicts=('dchroot')
replaces=('dchroot')
provides=('schroot' 'sbuild' 'dchroot')
backup=('etc/schroot/schroot.conf' 'etc/schroot/script-arch32'
	'etc/schroot/mount-arch32' 'etc/schroot/copyfiles-arch32')
source=("http://ftp.debian.org/debian/pool/main/s/$pkgname/${pkgname}_${_basever}.orig.tar.gz"
	"http://ftp.debian.org/debian/pool/main/s/$pkgname/${pkgname}_${pkgver/_/-}.diff.gz"
	'script-arch32'
	'copyfiles-arch32'
	'mount-arch32')
md5sums=('cc54e994c7eb714fe140a8cc6f812a9f'
         'e3284851e3e8aac0b318f344a2e8780a'
         '8e86445e188129232e1782d978dfc967'
         'd4558d5691fbdaa46ce669e8f327133b'
         'f0d5d5b5e34a860f6f90b5628c680f46')

build() {
  cd $startdir/src/${pkgname}-${_basever}
  if [ -f ../${pkgname}_${pkgver/_/-}.diff ]; then
    patch < ../${pkgname}_${pkgver/_/-}.diff || return 1
  fi
  # Fix pam file - we don't have 'common-auth' etc., but instead include the 'other' pam configuration
  sed -i -r 's/^@include +common-(auth|account|session|password)/\1 include other/' bin/schroot/pam/schroot

  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc --localstatedir=/var \
    --enable-dchroot --enable-csbuild
  make || return 1
  make DESTDIR=$startdir/pkg  install
  cd $startdir/src
  install -m 644 script-arch32 $startdir/pkg/etc/schroot
  install -m 644 mount-arch32 $startdir/pkg/etc/schroot
  install -m 644 copyfiles-arch32 $startdir/pkg/etc/schroot
  cat >> $startdir/pkg/etc/schroot/schroot.conf << EOF

[Arch32]
description=Arch32
type=directory
location=/opt/arch32
priority=1
#users=username
groups=users
root-groups=root
run-setup-scripts=true
run-exec-scripts=false
script-config=script-arch32
personality=linux32
aliases=32,default

EOF
}
