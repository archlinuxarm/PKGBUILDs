# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: John Sowiak <john@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend, optdepend on cuda
#  - remove depend on openimagedenoise
#  - set -DWITH_CYCLES=OFF, -DWITH_RAYOPTIMIZATION=OFF
# ALARM: Rostislav MedvÄ›d <rostik.medved@gmail.com>
#  - Removed the unnecessary Optix source
#  - Enabled Cycles and ray optimisation
#  - Disabled CUDA and Optix Cycles device support
#  - Tested building on ARMv6 (Pi 0 W) and ARMv7 (Odroid XU-4)
#  - Enabled building for ARMv6 and ARMv7
#  - Lowered number of building threads to 1 to stay below 500MB RAM usage
#  - Applied patch for building on ARMv6 and ARMv7
#  - This SHOULD build on ARMv6, but I couldn't test, because I keeep running out of RAM

buildarch=28
highmem=0

# Sometimes blender.org takes some time to release patch releases and because Arch users
# are impatient, we sometimes need to build from git directly.
# Update because I get so many queries on this:
# Due to our other rolling deps, it's sometimes not possible to build Blender stable releases.
# More often than not, a new openshadinglanguage breaks it and I could either backport fixes
# or simply roll with a new version. I usually choose the latter when the former seems
# unreasonable.

# For legal reasons, we can't separately package the Optix headers so we'll
# just build the package against them. I checked with NVIDIA and this way is
# fine with them.

_gittag=v2.81a
# _gitcommit=054dbb833e15275e0e991e2c15e754a3e7583716

pkgname=blender
pkgver=2.81.a
[[ -n $_gitcommit ]] && pkgver=${pkgver}.git1.${_gitcommit:0:8}
pkgrel=4.1
epoch=17
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL')
url="http://www.blender.org"
depends=('libpng' 'libtiff' 'openexr' 'python' 'desktop-file-utils' 'python-requests'
         'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils' 'glew' 'openjpeg' 'python-numpy'
         'freetype2' 'openal' 'ffmpeg' 'fftw' 'boost-libs' 'opencollada' 'alembic'
         'openimageio' 'libsndfile' 'jack' 'opencolorio' 'openshadinglanguage'
         'jemalloc' 'libspnav' 'ptex' 'opensubdiv' 'openvdb' 'log4cplus' 'sdl2')
makedepends=('cmake' 'boost' 'mesa' 'git' 'llvm' 'ninja')
options=(!strip)
source=("git://git.blender.org/blender-addons.git"
        "git://git.blender.org/blender-addons-contrib.git"
        "git://git.blender.org/blender-translations.git"
        "git://git.blender.org/blender-dev-tools.git"
        "dna_gpencil_types.patch"
        "dna_scene_types.patch")
if [[ -n $_gittag ]]; then
    source+=("${pkgname}-${pkgver}::git://git.blender.org/blender.git#tag=${_gittag}")
elif [[ -n $_gitcommit ]]; then
    source+=("${pkgname}-${pkgver}::git://git.blender.org/blender.git#commit=${_gitcommit}")
fi
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '3f72497c41bda9acd0abd63c113f29d8d0c45978f5665d82fee7664727f8623eb1e7ebc4f5442cfe8e4a574ea6afe8eb30c8f7dfcf5666ba46fb2443d81c4d9a'
            'c38cc98624be3d3e98005a8ef970f1566e71cc56f89ce7adc376a6737fb0c85ec14465c0e0c855b1e8a4e42c662feffb6d5c24f2f649b1be1a6ce0900a240d76')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  git submodule init
  git config submodule."release/scripts/addons".url "${srcdir}/blender-addons"
  git config submodule."release/scripts/addons_contrib".url "${srcdir}/blender-addons-contrib"
  git config submodule."release/datafiles/locale".url "${srcdir}/blender-translations"
  git config submodule."source/tools".url "${srcdir}/blender-dev-tools"
  git submodule update

  if [[ $(uname -m) == armv7l ]] || [[ $(uname -m) == armv6l ]]
  then
    patch source/blender/makesdna/DNA_gpencil_types.h -i $srcdir/../dna_gpencil_types.patch
    patch source/blender/makesdna/DNA_scene_types.h -i $srcdir/../dna_scene_types.patch
  else
    :
  fi

  mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver"/build

  cmake \
    -GNinja \
    -C../build_files/cmake/config/blender_release.cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_INSTALL_PORTABLE=OFF \
    -DWITH_PYTHON_INSTALL=OFF \
    -DWITH_CYCLES=ON \
    -DWITH_RAYOPTIMIZATION=ON \
    -DPYTHON_VERSION=3.8 \
    -DPYTHON_LIBPATH=/usr/lib \
    -DPYTHON_LIBRARY=python3.8 \
    -DPYTHON_INCLUDE_DIRS=/usr/include/python3.8 \
    -DWITH_CYCLES_DEVICE_CUDA=OFF \
    -DWITH_CYCLES_DEVICE_OPTIX=OFF

  ninja -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  DESTDIR="${pkgdir}" ninja install
  install -Dm755 ../release/bin/blender-softwaregl "${pkgdir}/usr/bin/blender-softwaregl"
  python -m compileall "${pkgdir}/usr/share/blender"
  python -O -m compileall "${pkgdir}/usr/share/blender"
}

