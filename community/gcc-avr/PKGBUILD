# $Id: PKGBUILD 2131 2009-09-09 00:52:56Z cprimier $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=gcc-avr
pkgver=4.4.1
pkgrel=1
pkgdesc="The GNU avr Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'custom')
url="http://gcc.gnu.org/"
depends=('binutils-avr>=2.19.1' 'cloog-ppl' 'gcc-libs' 'mpfr')
makedepends=('flex')
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
source=(http://ftp.gnu.org/gnu/gcc/${pkgname/-avr}-${pkgver}/gcc-{core,g++}-${pkgver}.tar.bz2)
md5sums=('d19693308aa6b2052e14c071111df59f' 'd449047b5761348ceec23739f5553e0b')

build() {
  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  cd ${srcdir}/${pkgname/-avr}-${pkgver}

  mkdir build
  cd build
  ../configure --disable-libssp \
               --disable-nls \
               --enable-languages=c,c++ \
               --infodir=/usr/share/info \
               --libdir=/usr/lib \
               --libexecdir=/usr/lib \
               --mandir=/usr/share/man \
               --prefix=/usr \
               --target=avr \
               --with-gnu-as \
               --with-gnu-ld \
               --with-as=/usr/bin/avr-as \
               --with-ld=/usr/bin/avr-ld

  make || return 1
  make -j1 DESTDIR=${pkgdir} install || return 1

  rm -f ${pkgdir}/usr/lib/libiberty.a || return 1
  rm -rf ${pkgdir}/usr/share/man/man7 || return 1
  rm -rf ${pkgdir}/usr/share/info || return 1
}

# vim:set ts=2 sw=2 et:
