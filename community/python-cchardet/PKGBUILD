# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=python-cchardet
pkgname=(python-cchardet python2-cchardet)
pkgver=1.0.0
pkgrel=3
pkgdesc="Universal encoding detector faster than chardet."
url="http://pypi.python.org/pypi/cchardet/"
makedepends=('cython' 'cython2' 'python-setuptools' 'python2-setuptools')
license=('MIT')
arch=('i686' 'x86_64')
source=(http://pypi.python.org/packages/source/c/cchardet/cchardet-$pkgver.tar.gz
        0001-Add-AArch64-support.patch)
sha256sums=('98e6dc7ca225abfa7e559a6450404aeb2f5bea0713afd6dd492c1a51cec57e63'
            'bb1d1ecbbfb3e26d529209d6e1a6e741fb1a6fe11869f94e8bd567d080b6dfb8')

prepare() {
    cd "$srcdir/cchardet-$pkgver"
    patch -p1 -i ../0001-Add-AArch64-support.patch
}

build() {
    cd "$srcdir/cchardet-$pkgver"

    rm -rf ../buildpy3; mkdir ../buildpy3
    python setup.py build -b ../buildpy3

    rm -rf ../buildpy2; mkdir ../buildpy2
    python2 setup.py build -b ../buildpy2
    find ../buildpy2 -name \*.py -exec sed -r 's|^#!(.*)python$|#!\1python2|' -i {} +
}

package_python-cchardet() {
    depends=(python)

    cd "$srcdir/cchardet-$pkgver"
    rm -rf build; ln -s ../buildpy3 build
    python setup.py install --skip-build -O1 --root="$pkgdir"
}

package_python2-cchardet() {
    depends=(python2)

    cd "$srcdir/cchardet-$pkgver"
    rm -rf build; ln -s ../buildpy2 build
    python2 setup.py install --skip-build -O1 --root="$pkgdir"
}

