# $Id: PKGBUILD 3519 2009-10-04 17:09:16Z ibiru $
# Maintainer: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Giacomo Rizzo <alt@free-os.it>

pkgname=gpsd
pkgver=2.39
pkgrel=2
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('i686' 'x86_64')
url="http://gpsd.berlios.de"
license=('GPL')
depends=('lesstif' 'libxaw' 'python>=2.6')
options=('!libtool')
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz
	gpsd.confd gpsd 99-gpsd-usb.rules)
install=gpsd.install
build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  sed -i -e "s/gpsd.hotplug/gpsd/g" gpsd.hotplug gpsd.usermap
  install -D gpsd.usermap "$pkgdir/etc/hotplug/usb/gpsd.usermap" || return 1
  install -m755 gpsd.hotplug "$pkgdir/etc/hotplug/usb/gpsd" || return 1

  install -D -m644 "$srcdir/gpsd.confd" "$pkgdir/etc/conf.d/gpsd" || return 1
  install -D -m755 "$srcdir/gpsd" "$pkgdir/etc/rc.d/gpsd" || return 1
  install -D -m644 "$srcdir/99-gpsd-usb.rules"  "$pkgdir/etc/udev/rules.d/99-gpsd-usb.rules" || return 1

  mkdir -p "$pkgdir/var/run/gpsd"
}
md5sums=('3db437196a6840c252fca99b6c19d4d0'
         'f83f1cbb96c7f3b102683940b5f39faa'
         'fd02537eb14455ddb33dd1f29b514168'
         '09df327b369c1224730f5114c59d83ca')
