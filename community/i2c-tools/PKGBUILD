# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !makeflags because concurrency fails

pkgname=i2c-tools
pkgver=4.1
pkgrel=4
pkgdesc="Heterogeneous set of I2C tools for Linux that used to be part of lm-sensors"
arch=('x86_64')
url="http://www.lm-sensors.org/wiki/I2CTools"
license=('GPL')
depends=('perl')
makedepends=('python' 'git')
optdepends=('read-edid: for decode-edid script'
            'python: for smbus module')
options=('!makeflags')
_commit=42b58a1f25d45859295110e9ae2f094776bddfed
source=("git+https://git.kernel.org/pub/scm/utils/i2c-tools/i2c-tools.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"
  make
  make -C eepromer CFLAGS="$CFLAGS -I../include"
  cd py-smbus
  CPPFLAGS="$CPPFLAGS -I../include" python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}"
  make PREFIX="${pkgdir}/usr" sbindir="$pkgdir/usr/bin" install
  install -Dm755 eepromer/eeprom eepromer/eepromer "${pkgdir}/usr/bin"
  cd py-smbus
  python setup.py install --prefix "${pkgdir}/usr"
  rm -rf "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
