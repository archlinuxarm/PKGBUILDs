# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

# ALARM::Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove bad mcpu/mtune from configure for ARM

pkgname=hercules
pkgver=3.12
pkgrel=2
pkgdesc='Software implementation of System/370 and ESA/390'
arch=('x86_64' 'i686')
url='http://www.hercules-390.eu/'
license=('custom')
depends=('bzip2' 'libgcrypt' 'zlib' 'perl')
options=('!makeflags')
source=("http://downloads.hercules-390.eu/$pkgname-$pkgver.tar.gz")
sha256sums=('aefbe9d2f09372a530c17c177b7d2a0f4bcf8cfd28474672935b067e5f9ee373')

prepare() {
  # Change module extension from .la to .so.
  sed '/HDL_MODULE_SUFFIX/ s/\.la/.so/' -i "$pkgname-$pkgver/hdl.h"
  sed 's/-mcpu=$host_cpu -mtune=$host_cpu//' -i "$pkgname-$pkgver/configure"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-optimization=-O2
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/hercules/qpl1"
}

# vim: ts=2 sw=2 et:
