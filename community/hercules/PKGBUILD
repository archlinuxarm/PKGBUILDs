# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

# ALARM::Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove bad mcpu/mtune from configure for ARM

pkgname=hercules
pkgver=3.13.00
pkgrel=3
pkgdesc='Software implementation of System/370 and ESA/390'
arch=('x86_64')
url='http://www.hercules-390.eu/'
license=('custom')
depends=('bzip2' 'libnsl' 'zlib')
options=('!makeflags')
source=("http://downloads.hercules-390.eu/$pkgname-${pkgver%.00}.tar.gz")
sha512sums=('76f75ef3f1eb10c0fac0d6fa1ab9809b8d1dfe3deccbcd69366b05ee58f1ecb8ea0f387f7201ab4722b121478676f00e707ad27b6ecf1980fb09e900de63d718')

prepare() {
  ln -sf "$pkgname-${pkgver%.00}" p
  # Change module extension from .la to .so.
  sed '/HDL_MODULE_SUFFIX/ s/\.la/.so/' -i p/hdl.h
  sed 's/-mcpu=$host_cpu -mtune=$host_cpu//' -i p/configure
}

build() {
  cd p
  ./configure --prefix=/usr --enable-optimization=-O3
  make
}

package() {
  cd p
  DESTDIR="$pkgdir" make install
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/hercules/qpl1"
}

# vim: ts=2 sw=2 et:
# getver: raw.githubusercontent.com/rbowler/spinhawk/master/makemsi/Hercules-W64.VER
