# $Id: PKGBUILD 3736 2009-10-08 15:29:09Z dgriffiths $
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Benjamin Wild <benwild@gmx.de>

pkgname=exaile
pkgver=0.3.0.1
pkgrel=2
pkgdesc="A media player aiming to be similar to KDE's AmaroK, but for GTK+"
arch=('i686' 'x86_64')
url="http://www.exaile.org"
license=('GPL')
depends=('pygtk>=2.10' 'gstreamer0.10-python' \
         'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' \
         'dbus-python' 'mutagen')
makedepends=('pkgconfig' 'ca-certificates')
optdepends=('cddb-py: for CD playback'
            'libgpod: for iPod suport'
            'gstreamer0.10-ugly-plugins: for mp3 support'
            'gstreamer0.10-ffmpeg: for wma support'
            'gstreamer0.10-bad-plugins: for equalizer support'
            'python-notify: for libnotify plugin'
            'xulrunner: for extended information tabs'
            'gnome-python-extras: for extended information tabs')
source=(https://www.launchpad.net/exaile/0.3.0/${pkgver}/+download/exaile-${pkgver}.tar.gz)
md5sums=('c6bd14cea747a808570b304e647544f1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make || return 1
  make PREFIX=/usr DESTDIR=${pkgdir} install

  # fix for clicking files with spaces in names from nautilus
  sed -i "s#%u#%f#" ${pkgdir}/usr/share/applications/exaile.desktop
}
