# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -fsupport_aesni and -fsupport_sse

buildarch=4

_hkgname=cryptonite
pkgname=haskell-cryptonite
pkgver=0.25
pkgrel=19
pkgdesc="Cryptography Primitives sink"
url="https://github.com/vincenthz/cryptonite"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-basement' 'haskell-memory')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e2c246b85595cca60c4d0f4eda737ab3be2a73ee012ba234c2b1a0111a7b77e04c6ba5597c807e3d2d48dc41e9b4fe0419ce95072e9977903c437b36350f0405')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsupport_deepseq -finteger-gmp -f-support_pclmuldq -fsupport_rdrand \
            -f-old_toolchain_inliner -f-check_alignment
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
