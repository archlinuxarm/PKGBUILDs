# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hans Janssen <janserv@gmail.com>
# Contributor: my64 <packages@obordes.com>
# Contributor: Colin Pitrat <colin.pitrat@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch for signed char
#  - comment moving non-existent lib64 directory in package

pkgname=openscenegraph34
pkgver=3.4.1
pkgrel=1
pkgdesc='Open Source, high performance real-time graphics toolkit (version 3.4)'
url='http://www.openscenegraph.org'
arch=('x86_64')
license=('custom:OSGPL')
depends=('giflib' 'jasper' 'librsvg' 'xine-lib' 'curl' 'pth')
makedepends=('cmake' 'libvncserver' 'qt5-base' 'ffmpeg' 'mesa')
optdepends=('libvncserver' 'gdal' 'openexr' 'poppler-glib' 'qt5-base' 'ffmpeg')
conflicts=('openthreads' 'openscenegraph')
provides=('openthreads' 'openscenegraph')
source=(https://github.com/openscenegraph/OpenSceneGraph/archive/OpenSceneGraph-${pkgver}.tar.gz
        https://github.com/openscenegraph/OpenSceneGraph/commit/01c712e85195803993acb1d7712796cf494943c6.patch)
sha256sums=('930eb46f05781a76883ec16c5f49cfb29a059421db131005d75bec4d78401fd5'
            '7019627aab6f5e8bfdf08d6aad824d7d3408ef08daa4122e69b142c9d34fc241')
sha512sums=('4a65625e208d24256b5564bbe9d6f557758b51fae0acf85d1f71968a237fec299088536830fc862d25e02039de9ea13176b298a68aaa644eac958acbe1f4e143'
            'a21c930f7166e0aec2607569e8b3fc167a13a697ed8d5bfee4ee027d054e64de3aa83020ef1e6d2bd52c180f3bd1d8deab22242856dfc3ea8b4fc8d539f6f3d2')

prepare() {
  cd OpenSceneGraph-OpenSceneGraph-$pkgver
  patch -p1 -i ../01c712e85195803993acb1d7712796cf494943c6.patch
}

build() {
  mkdir -p OpenSceneGraph-OpenSceneGraph-${pkgver}/build
  cd OpenSceneGraph-OpenSceneGraph-${pkgver}/build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    ..
  make
}

package() {
  cd OpenSceneGraph-OpenSceneGraph-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  #mv "${pkgdir}/usr/lib64" "${pkgdir}/usr/lib"
}

# vim: ts=2 sw=2 et:
