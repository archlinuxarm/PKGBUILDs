# $Id: PKGBUILD 3743 2009-10-08 15:56:25Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=emacs-cvs
pkgver=20091008
pkgrel=1
pkgdesc="Gnu Emacs from CVS"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=("GPL")
depends=('ncurses' 'libpng' 'libtiff' 'libungif' 'gtk2' 'libxpm' 'gpm' 'librsvg' 'dbus' 'libjpeg')
makedepends=('cvs' 'openssh' 'texinfo')
conflicts=('emacs')
provides=('emacs')
options=(docs)
install=emacs.install
source=()
md5sums=()

_cvsroot=":pserver:anonymous@cvs.savannah.gnu.org:/sources"
_cvsmod="emacs"

build() {
  cd $startdir/src

  unset CVS_RSH

  if [ -d ${_cvsmod}/CVS ]; then
    cd ${_cvsmod}
    cvs -q update -dA
  else
    cvs -q -z3 -d${_cvsroot}/${_cvsmod} co ${_cvsmod}
    cd emacs
  fi

  ./configure --prefix=/usr --localstatedir=/var/lib/emacs --libexecdir=/usr/lib/emacs \
		    --with-xpm  --with-jpeg --with-tiff --with-gif \
		    --with-png --with-x-toolkit=gtk --without-sound \
		    --enable-font-backend \
		    --with-freetype --with-xft --with-libotf


  make bootstrap
  make || return 1
  make \
	    prefix=$startdir/pkg/usr \
	    libexecdir=$startdir/pkg/usr/lib/emacs \
	    localstatedir=$startdir/pkg/var/lib/emacs install || return 1

  mv $startdir/pkg/usr/bin/{ctags,ctags.emacs} || return 1
  mv $startdir/pkg/usr/bin/{etags,etags.emacs} || return 1
  mv $startdir/pkg/usr/share/man/man1/{etags.1,etags.emacs.1} || return 1
  mv $startdir/pkg/usr/share/man/man1/{ctags.1,ctags.emacs.1} || return 1

  rm $startdir/pkg/usr/share/info/dir && \
  gzip -9nf $startdir/pkg/usr/share/info/* || return 1

  find $startdir/pkg/usr/share/emacs -type d -exec chmod 755 {} \; && \
  chown -R root:root $startdir/pkg
}
