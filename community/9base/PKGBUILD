# $Id: PKGBUILD 2913 2009-09-23 11:43:45Z cbrannon $
# Maintainer: Jeff Mickey <j@codemac.net>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Aaron, phrakture, Griffin <aaron@archlinux.org>
# Contributor: Jeffrey 'jf' Lim <jfs.world@gmail.com>

pkgname=9base
pkgver=4
pkgrel=1
pkgdesc="Port of various original Plan9 tools to unix"
url="http://suckless.org"
source=(http://dl.suckless.org/tools/$pkgname-$pkgver.tar.gz \
	9 config.mk plan9.sh)
md5sums=('af10410542ea6b6875a71e06fdc12c66'
         'ae7108b9f26bed388e9055f35eef2986'
         '92a4b1039d6dc377d30950ddd146ebb1'
         '62a9e52043d9c32967fcae9018fffb56')
conflicts=('plan9port' '9rc-devel')
provides=('plan9')
arch=('i686' 'x86_64')
license=('custom')

build()
{
  cd "$srcdir/$pkgname-$pkgver"
  cp -f ../config.mk .

  if [ $CARCH = "i686" ]; then
    sed -i "s#x86_64#386#" config.mk
  fi
  sed -i "s#%CFLAGS%#$CFLAGS#g" config.mk
  sed -i "s#%LDFLAGS%#$LDFLAGS -L$srcdir/$pkgname-$pkgver/lib9#g" config.mk
  make || return 1
  make DESTDIR="$pkgdir" install

  install -m755 ../9 "$pkgdir/opt/plan9/bin/"

  install -d -m755 "$pkgdir/etc/profile.d/"
  install -m755 ../plan9.sh "$pkgdir/etc/profile.d/"

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/9base/LICENSE"
}
