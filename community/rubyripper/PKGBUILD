# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=rubyripper
pkgver=0.5.4
pkgrel=1
pkgdesc="Secure audiodisc ripper"
arch=('i686' 'x86_64')
url="http://code.google.com/p/rubyripper/"
license=('GPL3')
depends=('ruby' 'cdparanoia' 'gtk2' 'hicolor-icon-theme')
makedepends=('ruby-gettext')
optdepends=('ruby-gettext: Internationalization support'
            'ruby-gtk2: GTK+ GUI'
            'cd-discid: Freedb support'
            'eject: Eject support'
            'lame: MP3 encoding support'
            'vorbis-tools: Ogg Vorbis encoding support'
            'flac: FLAC encoding support'
            'wavegain: WAV ReplayGain support'
            'mp3gain: MP3 ReplayGain support'
            'vorbisgain: Ogg Vorbis ReplayGain support'
            'normalize: Normalization support')
install=rubyripper.install
source=(http://rubyripper.googlecode.com/files/rubyripper-$pkgver.tar.bz2)
md5sums=('7c728ddd517c36c1c6b853c7e0bff8a3')

build() {
  cd "$srcdir/rubyripper-$pkgver"

  ./configure --prefix=/usr --enable-gtk2 --enable-cli --enable-lang-all || return 1
  make DESTDIR="$pkgdir" install || return 1

  # Fix oggenc parameters
  sed -i 's|oggenc -o "#{filename}" -p 4|oggenc -o "#{filename}"|' \
    "$pkgdir/usr/lib/ruby/site_ruby/1.8/rr_lib.rb" || return 1
}
