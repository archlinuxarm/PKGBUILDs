# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Zephyr

pkgname=mupen64plus
pkgver=1.5
pkgrel=1
pkgdesc="Nintendo64 Emulator"
arch=('i686' 'x86_64')
url="http://code.google.com/p/mupen64plus/"
license=('GPL')
depends=('gtk2' 'sdl_ttf' 'yasm' 'mesa')
makedepends=('qt')
optdepends=('qt: qt4 frontend')
source=(http://mupen64plus.googlecode.com/files/Mupen64Plus-${pkgver//./-}-src.tar.gz)
md5sums=('c224b045d343ff02f6f933d328861b01')

build() {
  cd ${srcdir}/Mupen64Plus-${pkgver//./-}-src
  make PREFIX=/usr all || return 1
  make PREFIX=${pkgdir}/usr install

  make PREFIX=/usr GUI=QT4 all
  install -m755 mupen64plus $pkgdir/usr/bin/mupen64plus-qt
  
  cd $pkgdir/usr/share/applications
  cp mupen64plus.desktop mupen64plus-qt.desktop
  sed -i "s#Name=Mupen64Plus#Name=Mupen64Plus (Qt)#" mupen64plus-qt.desktop
  sed -i "s#Exec=mupen64plus#Exec=mupen64plus-qt#" mupen64plus-qt.desktop
}
