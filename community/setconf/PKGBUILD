# $Id: PKGBUILD 89807 2013-05-02 13:36:01Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - revert the retard x86-restricted if statement around makedepends

pkgname=setconf
pkgver=0.5.3
pkgrel=2
pkgdesc='Utility to easily change settings in configuration files or Makefiles'
arch=('x86_64' 'i686')
url='http://setconf.roboticoverlords.org/'
license=('GPL2')
depends=('pcre' 'gc')
makedepends=('shedskin')
source=("http://setconf.roboticoverlords.org/$pkgname-$pkgver.tar.xz")
sha256sums=('f063b308bcebefe139418587527262ffce776584b1952224ec9a41d0562d55b6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -x /usr/bin/shedskin ] && shedskin "$pkgname.py" && make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  if [ -e "$pkgname" ]; then
    install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  else
    depends=('python')
    install -Dm755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  fi
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
}

# vim:set ts=2 sw=2 et:
