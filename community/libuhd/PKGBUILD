# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon for AArch64

pkgname=libuhd
pkgver=3.9.2
_verstring='003_009_002'
pkgrel=1
pkgdesc="Universal Software Radio Peripheral (USRP) userspace driver"
arch=('x86_64' 'i686')
url="http://www.ettus.com/kb/category/software-documentation/uhd-manual"
license=('GPL')
depends=('boost-libs' 'orc' 'libusbx')
optdepends=('python2: usrp utils')
makedepends=('cmake' 'boost' 'python2-cheetah' 'python2-mako')

source=("libuhd-$pkgver.tar.gz::https://github.com/EttusResearch/uhd/archive/release_${_verstring}.tar.gz")
md5sums=('d402bc374938118cdc66e8b74aab2cd0')

build() {
  cd "$srcdir/uhd-release_$_verstring/host"
  # fix for py2
  find -name "*.py" -or -name '*.py.in' | xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"

  [[ $CARCH == "aarch64" ]] && CONFIG="-DHAVE_ARM_NEON_H=OFF"

  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr/ \
           -DPYTHON_EXECUTABLE=/usr/bin/python2 \
           -DENABLE_EXAMPLES=OFF \
           -DENABLE_UTILS=ON \
           -DENABLE_TESTS=OFF \
           -DENABLE_E100=ON \
           -DENABLE_E300=ON $CONFIG
  make
}

check() {
  cd "$srcdir/uhd-release_$_verstring/host/build"
  make test
}

package() {
  cd "$srcdir/uhd-release_$_verstring/host/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "../utils/uhd-usrp.rules" "$pkgdir/usr/lib/udev/rules.d/10-uhd-usrp.rules"
} 

