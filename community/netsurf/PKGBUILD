# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
pkgname=netsurf
pkgver=2.1
pkgrel=3
pkgdesc="Lightweight, fast web browser"
arch=("i686" "x86_64")
url="http://www.netsurf-browser.org/"
license=("GPL")
depends=('libmng' 'librsvg' 'curl' 'libglade' 'lcms' 'libjpeg>=7')
makedepends=('re2c' 'libnsbmp' 'libnsgif' 'hubbub')
source=("http://www.netsurf-browser.org/downloads/releases/${pkgname}-${pkgver}-src.tar.gz"
        "lemon.c::http://www.sqlite.org/cvstrac/getfile?f=sqlite/tool/lemon.c&v=1.69"
        "lempar.c::http://www.sqlite.org/cvstrac/getfile?f=sqlite/tool/lempar.c&v=1.69")
md5sums=('f0a34fd076b492c1a13b45432e8d7e49'
         '3a8db045f48383021b5311b21b02ba0e'
         '097ade562c5ff523df15e20ea440e879')

build() {
  # Fix the css parser code
  cd "${srcdir}/${pkgname}/css"
  cp "${srcdir}/lem"{on,par}.c .
  gcc ${CFLAGS} -o lemon lemon.c || return 1
  ./lemon parser.y
  # Build netsurf
  cd "${srcdir}/${pkgname}"
  make install DESTDIR="${pkgdir}" PREFIX=/usr NETSURF_USE_HARU_PDF=NO
}
