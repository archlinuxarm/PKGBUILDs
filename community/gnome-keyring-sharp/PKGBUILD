# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

# remove when bumped upstream

pkgname=gnome-keyring-sharp
pkgver=1.0.2
pkgrel=6
pkgdesc="A fully managed implementation of libgnome-keyring"
arch=('i686' 'x86_64')
url="https://github.com/mono/gnome-keyring-sharp"
license=('MIT')
depends=('libgnome-keyring' 'gtk-sharp-2')
source=(http://www.go-mono.com/archive/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('043e3e22132bdfe34e8ca314b6fd34bb0bc55a23eeb102778c64c243bb595315')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/AssemblyKeyFile/d' src/Gnome.Keyring/AssemblyInfo.cs.in
  sed -i '/GACUTIL/d' src/Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 src/Gnome.Keyring.dll "$pkgdir/usr/lib/mono/gnome-keyring-sharp-1.0/Gnome.Keyring.dll"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
