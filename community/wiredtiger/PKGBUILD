# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with armv8-a CRC extensions

buildarch=8

pkgname=wiredtiger
pkgver=2.9.3
pkgrel=1
pkgdesc="High performance, scalable, production quality, NoSQL, Open Source extensible platform for data management"
arch=('x86_64')
url="http://source.wiredtiger.com/"
license=('GPL')
depends=('snappy' 'lz4' 'zlib' 'gperftools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wiredtiger/wiredtiger/archive/$pkgver.tar.gz")
sha512sums=('3ba7e3bd7990cdfc870618bfcd5e5c08bde617b835c563ce063fb7954f9ebf107ab3557ff70ea78fa4cc28b9762e8be2b2acb31c366782fc5c67ec554f5dd9c5')

build() {
  cd wiredtiger-$pkgver

  CFLAGS=`echo $CFLAGS | sed -e 's/armv8-a/armv8-a+crc/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-leveldb \
              --enable-lz4 \
              --enable-tcmalloc \
              --with-builtins=snappy,zlib
  make
}

check() {
  cd wiredtiger-$pkgver
  make test
}

package() {
  cd wiredtiger-$pkgver
  make DESTDIR="$pkgdir" install
}
