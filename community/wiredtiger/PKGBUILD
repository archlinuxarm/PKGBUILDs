# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with armv8-a CRC extensions

buildarch=8

pkgname=wiredtiger
pkgver=2.9.1
_commit=13d9445453ff5e4727040ceed972dc0923bb8ae5
pkgrel=1
pkgdesc="High performance, scalable, production quality, NoSQL, Open Source extensible platform for data management"
arch=('x86_64')
url="http://source.wiredtiger.com/"
license=('GPL')
depends=('snappy' 'lz4' 'zlib' 'gperftools')
makedepends=('git')
source=("git://github.com/wiredtiger/wiredtiger.git#commit=$_commit")
md5sums=('SKIP')

build() {
  cd "$srcdir/wiredtiger"
  
  CFLAGS=`echo $CFLAGS | sed -e 's/armv8-a/armv8-a+crc/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-leveldb \
              --enable-lz4 \
              --enable-tcmalloc \
              --with-builtins=snappy,zlib
  make
}

check() {
  cd "$srcdir/wiredtiger"
  make test
}

package() {
  cd "$srcdir/wiredtiger"
  make DESTDIR="$pkgdir/" install
}
