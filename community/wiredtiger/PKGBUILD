# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with armv8-a CRC extensions

buildarch=8

pkgname=wiredtiger
pkgver=3.0.0.20180406
_commit=1e889a7c966f63c15eadf9b3e7b48247d50ab14c
pkgrel=1
pkgdesc="High performance, scalable, production quality, NoSQL, Open Source extensible platform for data management"
arch=('x86_64')
url="http://source.wiredtiger.com/"
license=('GPL')
depends=('snappy' 'lz4' 'zlib' 'gperftools')
source=("$pkgname-$_commit.tar.gz::https://github.com/wiredtiger/wiredtiger/archive/$_commit.tar.gz")
sha512sums=('28c4ed65b7bcafd59e526f9213fe39380ebdced94943e26c60c1e66ab1605b3b5efcb6f2903f59d1b0f81d789b676a1e07e2fe798cdc55778793eac2e4923410')

prepare() {
  mv wiredtiger-{$_commit,$pkgver}
}

build() {
  cd wiredtiger-$pkgver

  CFLAGS=`echo $CFLAGS | sed -e 's/armv8-a/armv8-a+crc/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-leveldb \
              --enable-lz4 \
              --enable-tcmalloc \
              --enable-verbose \
              --with-builtins=snappy,zlib
  make
}

check() {
  cd wiredtiger-$pkgver
  make test
}

package() {
  cd wiredtiger-$pkgver
  make DESTDIR="$pkgdir" install
}
