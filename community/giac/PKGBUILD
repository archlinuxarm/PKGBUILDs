# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinxuarm.org>
#  - delete shipped x86 binary src/mkjs to force rebuild for ARM

pkgname=giac
_pkgver=1.9.0-23
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='A free computer algebra system'
arch=(x86_64)
url='http://www-fourier.ujf-grenoble.fr/~parisse/giac.html'
license=(GPL3)
depends=(fltk mpfi gsl pari ntl lapack curl glpk libao gmp-ecm libsamplerate)
makedepends=(python libjpeg texlive-core texlive-science hevea nauty)
optdepends=('perl: for pgiac')
replaces=(libgiac xcas)
provides=(libgiac xcas)
source=(http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgname}_$_pkgver.tar.gz
        giac-pari-2.15.patch)
sha256sums=('b98e3c90977aedf45748582b45b5127285f46d83fe8e6f3632f41dcb56527a1e'
            'cb1d6e783f2a1f05c2ec7319874089e6a2ab6316f65661087796452b9b8efb21')

prepare() {
  cd $pkgname-${pkgver%.*}
  patch -p1 < ../giac-pari-2.15.patch # Fix build with pari 2.15
  rm -f src/mkjs # force rebuild for ARM
}

build() {
  cd $pkgname-${pkgver%.*}
  CXXFLAGS+=" -Wp,-U_GLIBCXX_ASSERTIONS" # Fix crashes
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
