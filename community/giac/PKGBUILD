# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinxuarm.org>
#  - delete shipped x86 binary src/mkjs to force rebuild for ARM

pkgname=giac
_pkgver=1.9.0-33
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='A free computer algebra system'
arch=(x86_64)
url='http://www-fourier.ujf-grenoble.fr/~parisse/giac.html'
license=(GPL3)
depends=(fltk mpfi gsl pari ntl lapack curl glpk libao gmp-ecm libsamplerate)
makedepends=(python libjpeg texlive-core texlive-science hevea nauty)
optdepends=('perl: for pgiac')
replaces=(libgiac xcas)
provides=(libgiac xcas)
source=(http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgname}_$_pkgver.tar.gz
        format-security.patch)
sha256sums=('056b5941d0b0e86162f16e727e0722460e07496ea1a02d5cddb713eda7720d02'
            '04288944e408807a15aa502e1215087870d8921229c9f9890d862d3c9de337cd')

prepare() {
  cd $pkgname-${pkgver%.*}
  patch -p1 < ../format-security.patch # Fix -Werror=format-security errors
  rm -f src/mkjs # force rebuild for ARM
}

build() {
  cd $pkgname-${pkgver%.*}
  CXXFLAGS+=" -Wp,-U_GLIBCXX_ASSERTIONS" # Fix crashes
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
