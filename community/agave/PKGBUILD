# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - compat-round.patch from debian to fix aarch64 FTBFS

pkgname=agave
pkgver=0.4.7
pkgrel=10
pkgdesc="Colorscheme designer tool for GNOME"
arch=('x86_64')
url="https://web.archive.org/web/20170327063642/http://home.gna.org/colorscheme/"
license=('GPL')
depends=('libglademm')
makedepends=('gnome-doc-utils' 'intltool' 'boost')
source=(http://ftp.debian.org/debian/pool/main/a/$pkgname/${pkgname}_$pkgver.orig.tar.gz
        agave-0.4.7-mdv-fix-str-fmt.patch
        schemebox.patch
        drop-libgnome.patch
        fix-build-without-gconf.patch
        compat-round.patch)
sha256sums=('4005361ab1b137029c7d50afbbfbae7d83bdde7cec3e155a85eed59f6ec4e6fc'
            '132008f0bbe040c59bbc134d9ca3eb244967a66f95bf113d94e976b8ed776c6c'
            '0471ac9d7057f83d3fc667d7387837a2d85cb0d15a58853dd673a6ea1459623c'
            '8651250047b5882b00d28ac6d7ff587f53f99bd681c201545b6cf25c5acaaac6'
            '225f9a5cfebea4f9c1d4120611394eb148ffb9a6c151e79f749df1749e015c91'
            '1cade4acc25b4da1999d6eb148e2f48ac3d6633fe4e695a6a1fc217d7c477a78')

prepare() {
  cd $pkgname-$pkgver

  # Build fix from Fedora
  patch -Np1 -i ../agave-0.4.7-mdv-fix-str-fmt.patch

  # Another build fix
  patch -Np1 -i ../schemebox.patch

  # Remove deprecated libgnome dependency
  patch -Np1 -i ../drop-libgnome.patch

  # Fix build without gconfmm installed
  patch -Np1 -i ../fix-build-without-gconf.patch

  # Build fix from Debian
  patch -Np1 -i ../compat-round.patch
}

build() {
  cd $pkgname-$pkgver
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper --disable-gnome --disable-gconf
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/agave.schemas"
}
