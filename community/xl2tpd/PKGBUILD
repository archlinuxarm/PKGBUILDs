# $Id: PKGBUILD 422 2009-07-29 14:50:48Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hengfu <rhfcaesar@gmail.com>

pkgname=xl2tpd
pkgver=1.2.4
pkgrel=1
pkgdesc="xl2tpd is an open source implementation of the L2TP tunneling protocol. xl2tpd is forked from l2tpd and is maintained by Xelerance Corporation."
arch=('i686' 'x86_64')
url="http://www.xelerance.com"
license=('GPL')
depends=('ppp')
options=(emptydirs zipman docs)
backup=(etc/ppp/options.l2tpd)
source=(ftp://ftp.xelerance.com/$pkgname/$pkgname-$pkgver.tar.gz \
	xl2tpd.rc \
	xl2tpd.conf.client-example \
	xl2tpd.conf.server-example \
	options.l2tpd)
md5sums=('d94fc8a13596f12a561240dfcea9f977'
         'd5e58e13a4ab17e7253d9c1f1a507b5a'
         '5f0ae07026e61a3dd15a76fd4144a6d5'
         '16ed0886115d4dcc21072cfb847250bb'
         'ed57eb67335cdf246251285dad502cc7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make KERNELSRC=/usr/src/linux-`uname -r` PREFIX=/usr || return 1
  make KERNELSRC=/usr/src/linux-`uname -r` PREFIX=/usr DESTDIR=$pkgdir install || return 1

  install -D -m0755 $srcdir/xl2tpd.rc $pkgdir/etc/rc.d/xl2tpd && \
  install -d -m0755 $pkgdir/var/run/xl2tpd && \
  install -D -m0644 $srcdir/xl2tpd.conf.client-example $pkgdir/etc/xl2tpd/xl2tpd.conf.client-example && \
  install -D -m0644 $srcdir/xl2tpd.conf.server-example $pkgdir/etc/xl2tpd/xl2tpd.conf.server-example && \
  install -D -m0644 $srcdir/options.l2tpd $pkgdir/etc/ppp/options.l2tpd
}
