# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pypandoc
pkgver=1.6
pkgrel=3
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
depends=('python-setuptools' 'pandoc' 'python-wheel' 'texlive-core' 'texlive-latexextra')
makedepends=('python-setuptools' 'pandoc')
checkdepends=('texlive-core' 'texlive-latexextra' 'pandoc-citeproc')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bebraw/pypandoc/archive/$pkgver.tar.gz")
sha512sums=('b9e7d769aa75b96b7f04cce0cc93045f38434df88433c2bd392653fa744a28c96392777dfc0c0c96cfbc6a187be8ec64b9ac3491f8b5d346d949b4dd0000e4a5')

prepare() {
  sed -i "s/install_requires = .*/install_requires = ['setuptools'],/" pypandoc-$pkgver/setup.py
}

build() {
  cd "$srcdir"/pypandoc-$pkgver
  python setup.py build
}

package() {
  cd pypandoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
