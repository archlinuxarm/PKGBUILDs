# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pypandoc
pkgver=1.5
pkgrel=4
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
depends=('python-setuptools' 'pandoc' 'texlive-core' 'texlive-latexextra')
makedepends=('python-setuptools' 'pandoc')
checkdepends=('texlive-core' 'texlive-latexextra' 'pandoc-citeproc')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bebraw/pypandoc/archive/$pkgver.tar.gz")
sha512sums=('bc1a18115827ac0868b887fa3c9be46736670de8f095cb49b444e61f66779497db9a08bdac0de30f821a64cd6bad195f1d3a29d0159e6e3ad050a78ca0ecea82')

prepare() {
  # We don't really need pip and wheel at runtime
  sed -i "s/install_requires = .*/install_requires = ['setuptools'],/" pypandoc-$pkgver/setup.py
}

build() {
  cd "$srcdir"/pypandoc-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pypandoc-$pkgver
  python setup.py test
}

package() {
  cd pypandoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
