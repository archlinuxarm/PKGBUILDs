# $Id$
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Martin Panter <vadmium+aur@gmail.com>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed --enable-runtime-cpudetection - not supported on ARM

pkgname=mplayer2
pkgver=20130309
pkgrel=1
pkgdesc='Advanced general-purpose media player. A fork of the original MPlayer project'
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
install=$pkgname.install
depends=('a52dec' 'aalib' 'cdparanoia' 'desktop-file-utils' 'enca' 'faad2' 'ffmpeg'
         'fontconfig' 'freetype2' 'jack' 'ladspa' 'lame' 'libass' 'libbluray'
         'libcaca' 'libcdio-paranoia' 'libdca' 'libdvdcss' 'libdvdnav' 'libdvdread'
         'libgl' 'libjpeg' 'libmad' 'libpulse' 'libquvi' 'libtheora' 'libvdpau'
         'libxinerama' 'libxss' 'libxv' 'libxxf86dga' 'libxxf86vm' 'lirc-utils'
         'mpg123' 'ncurses' 'sdl' 'ttf-dejavu')
makedepends=('mesa' 'mesa-libgl' 'unzip' 'yasm' 'python' 'python-docutils')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer')
conflicts=('mplayer')
options=(!emptydirs)
source=(ftp://mirrors.kernel.org/archlinux/other/community/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('03aeccbd82b3598e0656b54ae6ab60c79afdb78eabe1b2915e7912db7efbd343')

build() {
  cd $pkgname-$pkgver

  sed 's/gmplayer/mplayer/g' -i etc/mplayer.desktop
  find -type f -exec sed -e 's/python3/python/' -i {} \;

  ./configure --prefix=/usr --confdir=/etc/mplayer \
              --enable-translation --language=all \
              --enable-joystick \
              --disable-speex \
              --disable-openal \
              --disable-libdv \
              --disable-musepack
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  install -Dm644 etc/{codecs.conf,input.conf,example.conf} $pkgdir/etc/mplayer/
  install -dm755 $pkgdir/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf $pkgdir/usr/share/mplayer/subfont.ttf

  install -dm755 $pkgdir/usr/share/applications/
  install -m 644 etc/mplayer.desktop $pkgdir/usr/share/applications/
}
