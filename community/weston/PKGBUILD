# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export flags for rpi

pkgname=weston
pkgver=1.3.1
pkgrel=1.1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('i686' 'x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('libxkbcommon' 'libunwind' 'poppler-glib' 'mtdev' 'libxcursor' 'glu' 'pango' 'colord')
source=("http://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz")
sha1sums=('56acdfde4d1a3871d6e54f0cdbcf2736e73e979a')

build() {
	cd $pkgname-$pkgver
	if [ "$CARCH" == 'armv6h' ]; then
		export RPI_BCM_HOST_LIBS="-L/opt/vc/lib"
		export RPI_BCM_HOST_CFLAGS="-I/opt/vc/include -I/opt/vc/include/interface/vcos/pthreads -I/opt/vc/include/interface/vmcs_host/linux"
		export WESTON_NATIVE_BACKEND="rpi-backend.so"
	fi
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--enable-demo-clients
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
