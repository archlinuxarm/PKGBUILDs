# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export flags for rpi
#  - patch to not call vc_dispmanx_set_wl_buffer_in_use

pkgname=weston
pkgver=1.9.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('i686' 'x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('libxkbcommon' 'libinput' 'libunwind' 'poppler-glib' 'mtdev' 'libxcursor' 'glu' 'pango' 'colord')
[ "$CARCH" == 'armv6h' ] && makedepends=('raspberrypi-firmware')
source=("http://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz")
sha1sums=('94b70baca9a6ba007c696b740a711586f9dc274f')

build() {
	cd $pkgname-$pkgver
	if [ "$CARCH" == 'armv6h' ]; then
		export RPI_BCM_HOST_LIBS="-L/opt/vc/lib"
		export RPI_BCM_HOST_CFLAGS="-I/opt/vc/include -I/opt/vc/include/interface/vcos/pthreads -I/opt/vc/include/interface/vmcs_host/linux"
		export WESTON_NATIVE_BACKEND="rpi-backend.so"
		export RPI_COMPOSITOR_LIBS="-ludev -lmtdev -lbcm_host"
		CONFIG='--disable-egl'
	fi
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--enable-libinput-backend \
		--enable-demo-clients-install $CONFIG
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
