# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Bug <bug2000@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc
#  - setup.py with --without-docs

pkgname=xpra
pkgver=4.4.3
pkgrel=2
pkgdesc="multi-platform screen and application forwarding system screen for X11"
arch=('x86_64')
url='https://www.xpra.org'
license=('GPL2')
depends=('gtk3' 'python' 'libxtst' 'python-cairo' 'python-gobject' 'python-pillow' 'python-lz4'
         'ffmpeg' 'libvpx' 'xf86-video-dummy' 'libxkbfile' 'libxres'
         'python-netifaces' 'python-rencode' 'python-opengl'
         'python-lz4')
optdepends=('x264: Codec' 'python-dbus: dbus features'
            'python-pycups: Printing support'
            'python-cryptography: Cryptography'
            'python-numpy: GPU accelerated capture and compression for NVIDIA cards'
            'gst-python: Sound Forwarding'
            'opencv: Webcam Forwarding')
makedepends=('pkgconf' 'pygobject-devel' 'python-setuptools' 'cython' 'uglify-js')
backup=('etc/xpra/xpra.conf' 'etc/xpra/xorg.conf'
        'etc/xpra/conf.d/05_features.conf'
        'etc/xpra/conf.d/10_network.conf'
        'etc/xpra/conf.d/12_ssl.conf'
        'etc/xpra/conf.d/15_file_transfers.conf'
        'etc/xpra/conf.d/16_printing.conf'
        'etc/xpra/conf.d/20_sound.conf'
        'etc/xpra/conf.d/30_picture.conf'
        'etc/xpra/conf.d/35_webcam.conf'
        'etc/xpra/conf.d/40_client.conf'
        'etc/xpra/conf.d/42_client_keyboard.conf'
        'etc/xpra/conf.d/50_server_network.conf'
        'etc/xpra/conf.d/55_server_x11.conf'
        'etc/xpra/conf.d/60_server.conf'
        'etc/xpra/conf.d/65_proxy.conf'
        'etc/pam.d/xpra')
source=($pkgname-$pkgver.tar.xz::$url/src/$pkgname-$pkgver.tar.xz
        $pkgname-$pkgver.tar.xz.asc::$url/src/$pkgname-$pkgver.tar.xz.gpg
        ffmpeg6.patch)

md5sums=('856b0a39b61360e740b9d85cc374169d'
         'SKIP'
         '80fdd373202346789d79819ecdcd63b4')
sha1sums=('c9c078fa4296eee752aa3471578e56add7a07933'
          'SKIP'
          '6b792bbb73f902dc79ef27873916387870b06b5e')
sha256sums=('8fbb474f8f3aca5c96026477e01056c3df876c33e760cbc753cf0757e0acd70f'
            'SKIP'
            '93611b74af5ed243874e5e8a67910cc8cc1febd33ef4f2ebf9f33deba765c175')
validpgpkeys=('C11C0A4DF702EDF6C04F458C18ADB31CF18AD6BB') # Antoine Martin <antoine@nagafix.co.uk>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../ffmpeg6.patch # Fix build with FFmpeg 6
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  python setup.py build --without-docs
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build --without-docs
  sed -i 's|/build/xpra/pkg/xpra/etc/xpra/xorg.conf|/etc/xpra/xorg.conf|' "${pkgdir}"/etc/xpra/conf.d/55_server_x11.conf
  mv "${pkgdir}"/lib/* "${pkgdir}"/usr/lib/
  rmdir "${pkgdir}/lib"
  mkdir -p "${pkgdir}"/usr/share/dbus-1
  #Move D-BUS Policy
  mv "${pkgdir}"/{etc,usr/share}/dbus-1/system.d
  #Fix for FS#69804
  chmod +x "${pkgdir}"/usr/bin/*
}
