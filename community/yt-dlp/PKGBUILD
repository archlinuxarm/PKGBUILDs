# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: katt <magunasu.b97@gmail.com>

# ALARM: Rijnder Wever <rien334@gmail.com>
#  - change make target from 'pypi-files' to 'yt-dlp' to avoid pandoc dependency
#  - add zip to makedepends since the 'yt-dlp' target requires it

pkgname=yt-dlp
pkgver=2022.02.04
pkgrel=1
pkgdesc='A youtube-dl fork with additional features and fixes'
arch=(any)
url='https://github.com/yt-dlp/yt-dlp'
license=(Unlicense)
depends=(python-mutagen python-pycryptodomex python-websockets)
makedepends=(python-setuptools zip)
optdepends=('ffmpeg: for video post-processing'
            'rtmpdump: for rtmp streams support'
            'atomicparsley: for embedding thumbnails into m4a files'
            'aria2: for using aria2 as external downloader')
source=($pkgname-$pkgver.tar.gz::https://github.com/yt-dlp/yt-dlp/releases/download/$pkgver/yt-dlp.tar.gz)
sha256sums=('c8dd119c28a5d256322ddb3a2fff72bc02ace43fc5880c0065d97f7a48d7a8e3')

build() {
  cd $pkgname
  make yt-dlp
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
