# $Id: PKGBUILD 3127 2009-09-28 08:49:58Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=librcc
pkgver=0.2.6
pkgrel=2
pkgdesc="Charset Conversion Library"
arch=(i686 x86_64)
url="http://rusxmms.sourceforge.net/"
license=('GPL')
depends=(aspell enca libxml2 db librcd)
makedepends=(patch gtk gtk2)
install=librcc.install
source=(http://downloads.sourceforge.net/rusxmms/${pkgname}-${pkgver}.tar.bz2
	librcc-strnlen.patch)
md5sums=('9bbf248c7312c73c0b6ca19b9c5a2af1'
	 '040313d1d8f166ccf2b128cea4c05f21')

build() {
  cd ${startdir}/src/$pkgname-${pkgver}
  ./configure --prefix=/usr

  patch -p1 <$srcdir/librcc-strnlen.patch

  make || return 1

  mkdir -p $startdir/pkg/etc/rcc
  mkdir -p $startdir/pkg/usr/lib/rcc/engines
  mkdir -p $startdir/pkg/usr/bin

  make DESTDIR=$startdir/pkg install

  rm -f $startdir/pkg/usr/lib/*.la
  rm -f $startdir/pkg/usr/lib/rcc/engines/*.a
  rm -f $startdir/pkg/usr/lib/rcc/engines/*.la

  make -C examples
  make -C examples install DESTDIR=$startdir/pkg
  rm -f $startdir/pkg/usr/bin/example*

  install -m 644 examples/rcc.xml $startdir/pkg/etc

  if [ -f $startdir/pkg/usr/bin/rcc-gtk2-config ]; then
      ln -s rcc-gtk2-config $startdir/pkg/usr/bin/rcc-config
  elif [ -f $startdir/pkg/usr/bin/rcc-gtk-config ]; then
      ln -s rcc-gtk-config $startdir/pkg/usr/bin/rcc-config
  else
      echo "#!/bin/bash" > $startdir/pkg/usr/bin/rcc-config
      echo "echo \"Configuration UI is not available!\"" >> $startdir/pkg/usr/bin/rcc-config
  fi
}
