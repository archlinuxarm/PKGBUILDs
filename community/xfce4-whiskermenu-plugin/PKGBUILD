# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DENABLE_LINK_TIME_OPTIMIZATION=off

pkgname=xfce4-whiskermenu-plugin
pkgver=2.7.3
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=('x86_64')
url='https://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-goodies')
license=('GPL2')
depends=('garcon' 'xfce4-panel' 'gtk3>=3.22')
makedepends=('cmake' 'ninja')
source=("https://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('da2699ec9665388cf7fa1090088290f78245667fa6846ffca14116264a9fe6e9')

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DENABLE_LINK_TIME_OPTIMIZATION=off \
		-GNinja
  	ninja
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
