# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DENABLE_LINK_TIME_OPTIMIZATION=off

pkgname=xfce4-whiskermenu-plugin
pkgver=2.4.1
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=('x86_64')
url='https://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-goodies')
license=('GPL2')
depends=('garcon' 'xfce4-panel' 'gtk3>=3.22')
makedepends=('cmake' 'ninja')
source=("https://archive.be.xfce.org/src/panel-plugins/xfce4-whiskermenu-plugin/${pkgver%.*}/xfce4-whiskermenu-plugin-$pkgver.tar.bz2") # no https available
sha256sums=('53fc81a936792aedfca00f3d66fec9e3eef6fbb77e16c366e425bf3f5fdfb67c')

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DENABLE_LINK_TIME_OPTIMIZATION=off \
		-GNinja
  	ninja
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
