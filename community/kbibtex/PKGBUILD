# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to remove qreal usage on v5/6/7

pkgname=kbibtex
pkgver=0.6
_branch=0.6
pkgrel=1
pkgdesc="A BibTeX editor for KDE"
arch=('i686' 'x86_64')
url='http://home.gna.org/kbibtex/'
license=('GPL')
depends=('kdebase-runtime' 'poppler-qt4')
optdepends=('kdegraphics-okular: Document preview')
makedepends=('cmake' 'automoc4')
install=$pkgname.install
source=("http://download.gna.org/$pkgname/$_branch/$pkgname-$pkgver.tar.xz"
        '0001-remove-qreal-cast-for-arm.patch')
md5sums=('f1a9c1867d94c36fe88cfa0470b85dac'
         'fae1a894b4dbc9339b0ad70a3a3b185c')

prepare() {
  cd "$pkgname-$pkgver"

  [[ $CARCH != "aarch64" ]] && patch -p1 -i ../0001-remove-qreal-cast-for-arm.patch
}

build() {
  cd "$pkgname-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
