# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fabien Dubosson <fabien.dubosson@gmail.com>
# Contributor: Konstantin Gizdov <arch@kge.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set DISPATCH=0, x86 only

pkgname=xxhash
pkgver=0.8.1
pkgrel=4
pkgdesc='Extremely fast non-cryptographic hash algorithm'
arch=(x86_64)
url=https://cyan4973.github.io/xxHash/
license=(
  GPL2
  BSD
)
depends=(glibc)
makedepends=(git)
provides=(libxxhash.so)
_tag=35b0373c697b5f160d3db26b1cbb45a0d5ba788c
source=(
  git+https://github.com/Cyan4973/xxHash.git#tag=${_tag}
  xxhash-man-symlinks.patch
)
b2sums=(
  SKIP
  83236ec7363f98842a5c8deb52e0a200321a722748a69b472b42b65eb85fa06ac5490926ea7612c0c455bf4998e109567706cf0f03e2c52c1e497abf258e5219
)

prepare() {
  cd xxHash
  patch -Np1 -i ../xxhash-man-symlinks.patch
}

pkgver() {
  cd xxHash
  git describe --tags | sed 's/^v//'
}

build() {
  make PREFIX=/usr DISPATCH=0 -C xxHash
}

package() {
  make PREFIX=/usr DISPATCH=0 DESTDIR="${pkgdir}" -C xxHash install
  install -Dm 644 xxHash/LICENSE -t "${pkgdir}"/usr/share/licenses/xxhash
}

# vim: ts=2 sw=2 et:
