# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export --max-old-space-size option to fix 32-bit FTBFS

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=2.0.1
pkgrel=2
pkgdesc="JupyterLab computational environment"
arch=(any)
url="https://github.com/jupyterlab/jupyterlab"
license=(custom)
makedepends=(python-setuptools)
depends=(jupyterlab_server nodejs)
optdepends=('npm: to install extensions')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('5d1a833a9e49e6d90d68191917cb16a97ff4ec668c26c85d33ebce629df14141')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|~=|>=|' -i setup.py # Fix jupyterlab_server version
}

build() {
  cd $pkgname-$pkgver
  export NODE_OPTIONS=--max-old-space-size=1024
  python setup.py build 
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
