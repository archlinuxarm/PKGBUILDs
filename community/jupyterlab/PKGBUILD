# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export --max-old-space-size option to fix 32-bit FTBFS

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=3.3.2
pkgrel=2
pkgdesc='JupyterLab computational environment'
arch=(any)
url='https://github.com/jupyterlab/jupyterlab'
license=(custom)
makedepends=(python-build python-installer)
depends=(python-jupyterlab_server python-jupyter_packaging jupyter-nbclassic nodejs)
optdepends=('npm: to install extensions' 'jupyterlab-widgets: ipywidgets support')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('3c716bf5592cb28c5c55c615c6e5bd3efc71898f6957d13719b56478bbbb587a')

prepare() {
  sed -e 's|nbclassic~=0.2|nbclassic|' -i $pkgname-$pkgver/setup.cfg # Remove dependency version constraint
}

build() {
  cd $pkgname-$pkgver
  export NODE_OPTIONS=--max-old-space-size=1024
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
