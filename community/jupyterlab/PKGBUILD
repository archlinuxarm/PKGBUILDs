# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export --max-old-space-size option to fix 32-bit FTBFS

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=3.3.2
pkgrel=3
pkgdesc='JupyterLab computational environment'
arch=(any)
url='https://github.com/jupyterlab/jupyterlab'
license=(custom)
makedepends=(python-build python-installer)
depends=(python-jupyterlab_server python-jupyter_packaging jupyter-notebook-shim nodejs)
optdepends=('npm: to install extensions' 'jupyterlab-widgets: ipywidgets support')
checkdepends=('python-pytest-tornasync' 'npm')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz
        no-nbclassic.patch
        jupyterlab-fix-tests.patch::https://github.com/jupyterlab/jupyterlab/commit/56b437fc.patch)
sha256sums=('3c716bf5592cb28c5c55c615c6e5bd3efc71898f6957d13719b56478bbbb587a'
            '923954cca5ac139091f3a6c7c922c3bf3b4173375bb28a1cc016f74f82869d7e'
            '5db8f09ecb71ca93c09d050940c9767218bf4ffc5368225eb7ebd72a7f02ed90')

prepare() {
  patch -d $pkgname-$pkgver -p1 < no-nbclassic.patch # Drop nbclassic dependency
  patch -d $pkgname-$pkgver -p1 < jupyterlab-fix-tests.patch # Fix tests
}

build() {
  cd $pkgname-$pkgver
  export NODE_OPTIONS=--max-old-space-size=1024
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -v -k 'not test_build'
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
