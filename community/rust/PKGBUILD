# $Id$
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: userwithuid <userwithuid@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add a link to g++ to compensate for broken cross-compiler decisions

buildarch=28
highmem=1

pkgname=('rust' 'rust-docs')
epoch=1
pkgver=1.20.0
pkgrel=1

pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
arch=('x86_64' 'i686')
license=('MIT' 'Apache')

makedepends=('rust' 'libffi' 'perl' 'python2' 'curl' 'llvm' 'cmake')

options=('!emptydirs')

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        0001-librustc_llvm-build-Force-link-against-libffi.patch
        bootstrap-config.toml)

sha256sums=('2aa4875ff4472c6e35262bbb9052cb2623da3dae6084a858cc59d36f33f18214'
            'SKIP'
            '563529da8e02f575ddde7247d5fc5350d74031daa54743b86d075f3c45ae2713'
            '4252d4873a840216115ee306e4d689819bc791ee05bedd1faa0b00c49ff91c62')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"

  patch -p1 <"$srcdir"/0001-librustc_llvm-build-Force-link-against-libffi.patch
  cp "$srcdir"/bootstrap-config.toml config.toml

  if [[ $CARCH == armv7h ]]; then
    mkdir path
    ln -s /usr/bin/g++ path/arm-linux-gnueabihf-g++
    export PATH="$srcdir/path:$PATH"
  fi
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python2 ./x.py build
}

package_rust() {
  depends=('gcc-libs' 'llvm-libs')
  provides=('cargo')
  conflicts=('cargo')
  replaces=('cargo')


  cd "rustc-$pkgver-src"

  DESTDIR="$pkgdir" python2 ./x.py install

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

  cd "$pkgdir/usr/lib"

  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

  # move docs out of the way
  mv "$pkgdir/usr/share/doc" "$srcdir/"
}

package_rust-docs() {
  msg2 "Packaging documentation"
  install -d "$pkgdir/usr/share/doc/"
  mv "$srcdir"/doc/* "$pkgdir"/usr/share/doc/rust/

  msg2 "Packaging license files for the documentation"
  for license in APACHE MIT; do install -Dm644 "rustc-$pkgver-src/LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

# vim:set ts=2 sw=2 et:
