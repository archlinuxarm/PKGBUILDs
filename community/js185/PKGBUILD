# $Id: PKGBUILD 183425 2013-04-21 22:11:08Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix mozilla stupidity
#  - patch to fix mozilla stupidity

pkgname=js185
pkgver=1.0.0
pkgrel=2.1
pkgdesc="JavaScript interpreter and libraries (legacy)"
arch=(i686 x86_64)
url="https://developer.mozilla.org/En/SpiderMonkey/1.8.5"
license=(MPL)
depends=(nspr gcc-libs)
makedepends=(python2 zip)
options=(!staticlibs)
source=(http://ftp.mozilla.org/pub/mozilla.org/js/$pkgname-$pkgver.tar.gz
        ihatemozilla.patch no-neon.patch)
md5sums=('a4574365938222adca0a6bd33329cb32'
         '9add35f52965e4643b9235307d0d6cc5'
         '5d4817fadff71765c9b5d1f7b90f904f')

build() {
  cd js-1.8.5/js/src

  # _FORTIFY_SOURCE causes configure error
  unset CPPFLAGS

  patch -p3 -i "$srcdir/ihatemozilla.patch"
  patch -p3 -i "$srcdir/no-neon.patch"

  ./configure --prefix=/usr --with-system-nspr \
     --enable-threadsafe
  make
}

package() {
  cd js-1.8.5/js/src
  make DESTDIR="$pkgdir" install
  install -Dm755 shell/js "$pkgdir/usr/bin/js"
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -x {} +

  # Bad symlinks (absolute, including DESTDIR!)
  cd "$pkgdir/usr/lib"
  ln -sf libmozjs185.so.1.0.0 libmozjs185.so.1.0
  ln -sf libmozjs185.so.1.0 libmozjs185.so
}

# vim:set ts=2 sw=2 et:
