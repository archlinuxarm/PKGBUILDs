# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: SanskritFritz (gmail)

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO

pkgname=netdata
pkgver=1.7.0
pkgrel=1
pkgdesc="Real-time performance monitoring, in the greatest possible detail, over the web"
url="https://github.com/firehol/netdata/wiki"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmnl' 'libnetfilter_acct' 'zlib')
optdepends=('nodejs: for monitoring named and SNMP devices'
            'lm_sensors: for monitoring hardware sensors'
            'iproute2: for monitoring Linux QoS'
            'python: for most of the external plugins'
            'python-psycopg2: for monitoring PostgreSQL databases'
            'python-mysqldb: for monitoring mysql databases'
            'python-requests: for monitoring elasticsearch'
            'hddtemp: for monitoring hhd temperature'
            'network-ups-tools: for monitoring UPS'
            'apcupsd: for monitoring APC UPS'
            'iw: for monitoring Linux as access point')
backup=('etc/netdata/'{apps_groups,charts.d,health_alarm_notify,health_email_recipients,netdata,python.d}.conf
        'etc/netdata/health.d/'{apache,cpu,disks,entropy,memcached,mysql,named,net,nginx,qos,ram,redis,retroshare,squid,swap}.conf
        'etc/netdata/python.d/'{apache_cache,apache,cpufreq,dovecot,example,exim,hddtemp,ipfs,memcached,mysql,nginx,nginx_log,phpfpm,postfix,redis,retroshare,sensors,squid,tomcat}.conf)
install="$pkgname.install"
source=("https://github.com/firehol/netdata/releases/download/v${pkgver}/netdata-${pkgver}.tar.xz")
sha512sums=('68b2d2f5380120a3babe13be948515387d6fcfb35cdb50d5ae79dff2a09bc7f56623a2c352645b6a0ded585ea6dcc45f4d28a0250de5b9cba9239e6a22558462')

build() {
	cd "$pkgname-$pkgver"

	./autogen.sh
	./configure \
		--prefix="/usr" \
		--sbindir="/usr/bin" \
		--sysconfdir="/etc" \
		--libexecdir="/usr/lib" \
		--localstatedir="/var" \
		--with-zlib --with-math --with-user=netdata --disable-lto
	make
}

package() {
	cd "$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install

	touch "$pkgdir/etc/netdata/netdata.conf"

	install -Dm0644 "system/netdata.service" "$pkgdir/usr/lib/systemd/system/netdata.service"
	install -Dm0644 "system/netdata.logrotate" "$pkgdir/etc/logrotate.d/netdata"
}

