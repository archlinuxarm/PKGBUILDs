# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: Bjørn Lindeijer <bjorn lindeijer nl>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable sse

pkgbase=dumb
pkgname=(dumb dumb-a4)
pkgver=2.0.3
pkgrel=1
pkgdesc='IT, XM, S3M and MOD player library'
arch=('x86_64')
license=('custom:dumb' 'GPL')
url='http://dumb.sf.net/'
makedepends=('allegro' 'allegro4' 'cmake' 'ninja')
source=($pkgname-$pkgver.tar.gz::"https://github.com/kode54/dumb/archive/$pkgver.tar.gz")
sha256sums=('99bfac926aeb8d476562303312d9f47fd05b43803050cd889b44da34a9b2a4f9')

build() {
  cp -r "$pkgname-$pkgver" "$pkgname-$pkgver-a4"
  mkdir -p build-a4 build-a5

  # Configure for Allegro 4
  cd build-a4
  cmake "$srcdir/$pkgname-$pkgver-a4" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_ALLEGRO4=ON \
    -DBUILD_EXAMPLES=OFF \
    -DUSE_SSE=OFF \
    -GNinja

  # Configure for Allegro 5
  cd "$srcdir/build-a5"
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_ALLEGRO4=OFF \
    -DBUILD_EXAMPLES=OFF \
    -DUSE_SSE=OFF \
    -GNinja

  # Build both
  cd "$srcdir"
  ninja -C build-a4
  ninja -C build-a5
}

package_dumb() {
  conflicts=('dumb-a4')
  DESTDIR="$pkgdir" ninja -C build-a5 install
  install -Dm644 "$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_dumb-a4() {
  conflicts=('dumb')
  provides=('dumb')
  depends=('allegro4')
  pkgdesc='IT, XM, S3M and MOD player library (for Allegro 4)'

  DESTDIR="$pkgdir" ninja -C build-a4 install
  install -Dm644 "$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
