# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: Bjørn Lindeijer <bjorn lindeijer nl>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable sse

pkgname=dumb
pkgver=2.0.0
pkgrel=1
pkgdesc='IT, XM, S3M and MOD player library'
arch=('x86_64' 'i686')
license=('custom:dumb' 'GPL')
url='http://dumb.sourceforge.net/'
makedepends=('cmake')
depends=('glibc')
source=($pkgname-$pkgver.tar.gz::"https://github.com/kode54/dumb/archive/$pkgver.tar.gz")
sha256sums=('251d0ef34d9b7491ebb119c63b9d8a5c12fd635d6899d45bc7949e195388cc60')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_ALLEGRO4=OFF \
    -DBUILD_EXAMPLES=OFF \
    -DUSE_SSE=OFF
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
