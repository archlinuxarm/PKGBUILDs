# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix signed char usage

pkgname=opencollada
pkgver=1.6.59
pkgrel=1
epoch=1
pkgdesc="Stream based reader and writer library for COLLADA files"
arch=(i686 x86_64)
url="https://github.com/KhronosGroup/OpenCOLLADA"
license=('GPL')
depends=('libxml2' 'pcre')
makedepends=('git' 'cmake')
source=('opencollada.conf'
        "git://github.com/KhronosGroup/OpenCOLLADA.git#tag=v${pkgver}"
        'openCOLLADA-narrowing_gcc6.patch')
md5sums=('5f7e9d79ab86756648b648ee5ed6ce1d'
         'SKIP'
         '1b1ee8b442ffdf2ff30f0877f43e07bc')

prepare() {
  cd "$srcdir"/OpenCOLLADA

  patch -p1 -i ../openCOLLADA-narrowing_gcc6.patch
}

build() {
  cd "$srcdir"/OpenCOLLADA

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SHARED=ON \
    -DUSE_STATIC=OFF
  make
}

package() {
  cd "$srcdir"/OpenCOLLADA/build

  make DESTDIR=$pkgdir install

  install -Dm644 $srcdir/opencollada.conf $pkgdir/etc/ld.so.conf.d/opencollada.conf
}

# vim:set ts=2 sw=2 et:
