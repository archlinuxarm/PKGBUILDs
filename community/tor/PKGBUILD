# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable seccomp on aarch64

pkgname=tor
pkgver=0.3.1.9
pkgrel=2
pkgdesc='Anonymizing overlay network.'
arch=('x86_64')
url='https://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash' 'libseccomp')
optdepends=('torsocks: for torify')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist')
source=("https://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc'
        'tor.service'
        'tor.sysusers'
        'tor.tmpfiles')
md5sums=('585e62d086ae7df7cd873f735d726118'
         'SKIP'
         'c1dd4004c63edaeaa829e01be5413cfc'
         '503cedd9679f9817b2c27916ba989a74'
         '5de6b0aff2a152a03d8eb7a473edc5ff'
         '30e307d882c818cb44bcb2d6b81789a3')
validpgpkeys=('2133BC600AB133E1D826D173FE43009C4607B1FB') # Nick Mathewson

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ $CARCH == 'aarch64' ]] && CONFIG="--disable-seccomp"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var $CONFIG
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
  install -Dm0644 "${srcdir}/tor.sysusers" "${pkgdir}/usr/lib/sysusers.d/tor.conf"
  install -Dm0644 "${srcdir}/tor.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/tor.conf"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
