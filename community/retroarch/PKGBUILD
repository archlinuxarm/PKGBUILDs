# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Themaister <maister@archlinux.us>
# Contributor: lifning <definelightning@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable GLES

pkgname=retroarch
pkgver=1.4.1
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=('i686' 'x86_64')
url='http://www.libretro.com/'
license=('GPL')
groups=('libretro')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libdrm' 'libgl' 'libpulse' 'libusb'
         'libx11' 'libxcb' 'libxext' 'libxinerama' 'libxkbcommon' 'libxv'
         'libxxf86vm' 'mesa' 'openal' 'sdl2' 'wayland' 'zlib'
         'libass.so' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
         'libfreetype.so' 'libswresample.so' 'libswscale.so' 'libudev.so')
makedepends=('git' 'vulkan-icd-loader')
optdepends=('libretro-desmume: Nintendo DS core'
            'libretro-gambatte: Nintendo Game Boy/Game Boy Color core'
            'libretro-genesis-plus: Sega Master System/Genesis/Game Gear/CD/32X core'
            'libretro-mgba: Nintendo Game Boy Advance core'
            'libretro-mupen64plus: Nintendo 64 core'
            'libretro-nestopia: Nintendo Entertainment System core'
            'libretro-pcsx-rearmed: Sony PlayStation core'
            'libretro-reicast: Sega Dreamcast core'
            'libretro-snes9x: Super Nintendo Entertainment System core'
            'libretro-yabause: Sega Saturn core'
            'libretro-overlays: Collection of overlays'
            'libretro-shaders: Collection of shaders'
            'retroarch-assets-xmb: XMB menu assets'
            'retroarch-autoconfig-udev: udev joypad autoconfig')
backup=('etc/retroarch.cfg')
_commit=('d8855caae9fe3e249a2aab358480e2fe16c4ef0e')
source=("git+https://github.com/libretro/RetroArch.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
  cd RetroArch

  ./configure \
    --prefix='/usr' \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --enable-gles
  make
  make -C gfx/video_filters
}

package() {
  cd RetroArch

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/retroarch/filters/video
  install -m 644 gfx/video_filters/*.{filt,so} "${pkgdir}"/usr/lib/retroarch/filters/video/
}

# vim: ts=2 sw=2 et:
