# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added alarm.patch

pkgname=percona-server
pkgver=5.5.28_rel29.2
pkgrel=1
pkgdesc="A backwards-compatible drop-in replacement for MySQL that provides improved performance, diagnostics and instrumentation"
arch=('i686' 'x86_64')

depends=('mysql-clients' 'libaio')
conflicts=('mysql')
provides=('mysql')
optdepends=('perl-dbi' 'perl-dbd-mysql')
makedepends=('cmake' 'openssl' 'zlib')

license=('GPL')
url="http://www.percona.com/software/percona-server/"
options=('!libtool' 'emptydirs')
backup=('etc/mysql/my.cnf')
install=percona.install
source=("http://www.percona.com/downloads/Percona-Server-${pkgver%.*_*}/Percona-Server-${pkgver/_rel/-}/source/Percona-Server-${pkgver/_/-}.tar.gz"
        'mysqld.rc'
        'my.cnf'
        'mysqld-post.sh'
        'mysqld.service'
        'mysqld-tmpfile.conf'
        'alarm.patch')

build() {
	cd "${srcdir}/Percona-Server-${pkgver/_/-}"
	sed -i 's|ADD_SUBDIRECTORY(libmysqld/examples)|# ADD_SUBDIRECTORY(libmysqld/examples)|' CMakeLists.txt
    patch -p1 -i "${srcdir}/alarm.patch"

	cd "${srcdir}"
	rm -rf build
	mkdir build
	cd build

	cmake "${srcdir}/Percona-Server-${pkgver/_/-}" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONFDIR=/etc/mysql \
		-DMYSQL_DATADIR=/var/lib/mysql \
		-DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
		-DDEFAULT_CHARSET=utf8 \
		-DDEFAULT_COLLATION=utf8_general_ci \
		-DENABLED_LOCAL_INFILE=ON \
		-DINSTALL_INFODIR=share/mysql/docs \
		-DINSTALL_MANDIR=share/man \
		-DINSTALL_PLUGINDIR=lib/mysql/plugin \
		-DINSTALL_SCRIPTDIR=bin \
		-DINSTALL_INCLUDEDIR=include/mysql \
		-DINSTALL_DOCREADMEDIR=share/mysql \
		-DINSTALL_SUPPORTFILESDIR=share/mysql \
		-DINSTALL_MYSQLSHAREDIR=share/mysql \
		-DINSTALL_DOCDIR=share/mysql/docs \
		-DINSTALL_SHAREDIR=share/mysql \
		-DWITH_READLINE=ON \
		-DWITH_ZLIB=system \
		-DWITH_SSL=system \
		-DWITH_LIBWRAP=OFF \
		-DWITH_LIBEDIT=OFF \
		-DWITH_UNIT_TESTS=OFF \
		-DWITH_MYSQLD_LDFLAGS="${LDFLAGS}" \
		-DWITH_EXTRA_CHARSETS=complex \
		-DWITH_EMBEDDED_SERVER=ON \
		-DWITH_INNOBASE_STORAGE_ENGINE=ON \
		-DWITH_PARTITION_STORAGE_ENGINE=ON \
		-DWITH_PERFSCHEMA_STORAGE_ENGINE=ON \
		-DWITH_ARCHIVE_STORAGE_ENGINE=ON \
		-DWITH_BLACKHOLE_STORAGE_ENGINE=ON \
		-DWITH_FEDERATED_STORAGE_ENGINE=OFF \
		-DWITH_EXAMPLE_STORAGE_ENGINE=OFF \
		-DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
		-DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti -fpermissive"

	make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR=${pkgdir} install

	install -Dm644 "${srcdir}"/my.cnf "${pkgdir}"/etc/mysql/my.cnf
	install -Dm755 "${srcdir}"/mysqld.rc "${pkgdir}"/etc/rc.d/mysqld
	install -Dm755 "${srcdir}"/mysqld-post.sh "${pkgdir}"/usr/bin/mysqld-post
	install -Dm644 "${srcdir}"/mysqld-tmpfile.conf "${pkgdir}"/usr/lib/tmpfiles.d/mysqld.conf
	install -Dm644 "${srcdir}"/mysqld.service "${pkgdir}"/usr/lib/systemd/system/mysqld.service

	# provided by libmysqlclient
	rm ${pkgdir}/usr/bin/mysql_config
	rm ${pkgdir}/usr/lib/libmysql*
	rm -r ${pkgdir}/usr/include/
	rm ${pkgdir}/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1

	# provided by mysql-clients
	rm ${pkgdir}/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
	rm ${pkgdir}/usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

	# not needed
	rm -r ${pkgdir}/usr/{data,mysql-test,sql-bench}
	rm "${pkgdir}"/usr/share/man/man1/mysql-test-run.pl.1

	install -dm700 "${pkgdir}"/var/lib/mysql
}

sha256sums=('4f2ca2b4bf8ebdde0d5c643cc382fd7242492c084715b5c7d83619a9ff7a40a4'
            'e875fac56f69634b4f1720aec3b952fc63903414e9345af630e5fd74b5373eb4'
            'dd5dc155de468d4ab26971109861ae19b53574f28785bac5ff7b717fb65c0760'
            '4894d39605950d86841ad982e619daa6c8d5d60ca30928d7dbb582e8cb3ade32'
            'ca10dec3cc162baede651980e675fd8094e55cb2403edffaa3d0865a6184553f'
            'badf6a701d9dc6ea3b4ddca26cb0f42b6236432ccdbc14c64962147802594a60')
