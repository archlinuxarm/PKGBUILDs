# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !strip option, stripped binary fails under ARM
#  - build with go instead of gcc-go

pkgname=gendesk
pkgver=0.7
pkgrel=2
pkgdesc='Utility for generating desktop files'
arch=('x86_64')
url='http://gendesk.roboticoverlords.org/'
license=('MIT')
makedepends=('go' 'git' 'mercurial')
options=('!strip')
source=("http://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.xz"{,.asc}
        'http://roboticoverlords.org/images/default.png')
validpgpkeys=('962855F072C7A01846405864FCF3C8CB5CF9C8D4')
sha256sums=('3100f40a32e2588188b8af0822110f9ba4689f2a0dae7347a3b917d0d12e24a1'
            'SKIP'
            '4d96eded48e536d02e35727c36dc20844c2e44654e81baf78e10aee4eb48e837')

prepare() {
  cd "$pkgname-$pkgver"

  GOPATH=`pwd` go get -d -v
}

build() {
  cd "$pkgname-$pkgver"

  GOPATH=`pwd` go build
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "$pkgname-$pkgver" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
