# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !strip option, stripped binary fails under ARM

pkgname=gendesk
pkgver=1.0.4
pkgrel=1
pkgdesc='Utility for generating desktop files'
arch=(x86_64)
url='https://gendesk.roboticoverlords.org/'
license=(MIT)
makedepends=(gcc-go)
depends=(gcc-libs)
options=('!strip')
source=("https://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.xz"{,.asc}
        'https://roboticoverlords.org/images/default.png')
validpgpkeys=('962855F072C7A01846405864FCF3C8CB5CF9C8D4')
sha256sums=('d3f2c1c3508a013bece9ef5cada26326a7dc1c0ff935861a6fe4a006ea7dbccf'
            'SKIP'
            '4d96eded48e536d02e35727c36dc20844c2e44654e81baf78e10aee4eb48e837')

build() {
  cd $pkgname-$pkgver
  go build -mod=vendor -buildmode=pie -gccgoflags="-s -w $LDFLAGS"
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
