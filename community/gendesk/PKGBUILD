# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !strip option, stripped binary fails under ARM

pkgname=gendesk
pkgver=1.0.5
pkgrel=2
pkgdesc='Utility for generating desktop files'
arch=(x86_64)
url='https://gendesk.roboticoverlords.org/'
license=(MIT)
makedepends=(go)
depends=(glibc)
options=('!strip')
source=("https://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.xz"{,.asc}
        'https://roboticoverlords.org/images/default.png')
validpgpkeys=('962855F072C7A01846405864FCF3C8CB5CF9C8D4')
sha256sums=('5e85b056e4c372c7d3ffe389734bfa1aacd7406f71573fff597f04f0c24f238b'
            'SKIP'
            '4d96eded48e536d02e35727c36dc20844c2e44654e81baf78e10aee4eb48e837')

build() {
  cd $pkgname-$pkgver
  go build -v -mod=vendor -trimpath -buildmode=pie -ldflags="-s -w -extldflags $LDFLAGS"
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
