# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !strip option, stripped binary fails under ARM

pkgname=gendesk
pkgver=0.5.5
pkgrel=1.1
pkgdesc='Utility to generate .desktop files and download icons'
arch=('x86_64' 'i686')
url='http://roboticoverlords.org/gendesk/'
license=('MIT')
makedepends=('go')
depends=('glibc')
options=('!strip')
source=("http://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.xz"
        'http://roboticoverlords.org/images/default.png')
sha256sums=('52a13d3952f0c00a65f05d01a9ca0fc1f5337f3379ff6017aafb686e5caf0942'
            '4d96eded48e536d02e35727c36dc20844c2e44654e81baf78e10aee4eb48e837')

build() {
  cd "$pkgname-$pkgver"

  go build
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "$pkgname-$pkgver" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
