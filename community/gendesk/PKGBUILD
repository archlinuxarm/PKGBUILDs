# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !strip option, stripped binary fails under ARM

pkgname=gendesk
pkgver=0.6.2
pkgrel=1
pkgdesc='Utility to generate .desktop files and download icons'
arch=('x86_64' 'i686')
url='http://roboticoverlords.org/gendesk/'
license=('MIT')
makedepends=('go' 'git' 'mercurial')
depends=('glibc')
options=('!strip')
source=("http://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.xz"
        'http://roboticoverlords.org/images/default.png')
sha256sums=('22c77afc907dd6de2d58bb045d6a8de8873e18eee5bbe020a3c7b882dc4211ce'
            '4d96eded48e536d02e35727c36dc20844c2e44654e81baf78e10aee4eb48e837')

prepare() {
  export GOPATH="$srcdir"
  go get code.google.com/p/goconf/conf
  go get github.com/xyproto/textgui
}

build() {
  cd "$pkgname-$pkgver"

  GOPATH="$srcdir" go build
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "$pkgname-$pkgver" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
