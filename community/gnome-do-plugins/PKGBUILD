# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do-plugins
pkgver=0.8.2
pkgrel=1
pkgdesc="Plugins for gnome-do"
arch=('i686' 'x86_64')
url="http://do.davebsd.com/"
license=('GPL3')
depends=('gnome-do>=0.8.1')
makedepends=('intltool' 'pkgconfig' 'banshee' 'flickrnet' 'evolution-sharp' 'monodevelop')
optdepends=('evolution-sharp: Evolution plugin'
            'flickrnet: Flickr plugin')
source=(http://code.edge.launchpad.net/do-plugins/0.8/0.8.2/+download/${pkgname}-${pkgver}.tar.gz
        killsomeplugins.patch)
md5sums=('6e79a666619aae1b1e2916f0fe364d94'
         '0322d0374f80f607535120f545ab6ed7')
build() {
  export MONO_SHARED_DIR=$srcdir/.wabi
  mkdir -p "${MONO_SHARED_DIR}"

  cd $srcdir/$pkgname-$pkgver

  # GNOME dictionary plugin cause system lock-up (infinite memory leak)
  patch -Np1 -i $srcdir/killsomeplugins.patch || return 1

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  rm -rf ${pkgdir}/usr/share/gnome-do/plugins/*.mdb
}
