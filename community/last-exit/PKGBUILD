# $Id: PKGBUILD 94 2009-07-17 23:29:34Z cprimier $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=last-exit
pkgver=6
pkgrel=3
pkgdesc="A last.fm player"
arch=('i686' 'x86_64')
url="http://lastexit-player.org"
license=('GPL')
depends=('dbus' 'dbus-sharp' 'gconf>=2.18.0.1-4' 'gnome-sharp'
         'gstreamer0.10-ugly-plugins' 'gtk-sharp-2' 'hicolor-icon-theme' 'libsexy'
         'rarian')
makedepends=('gnome-doc-utils>=0.11.2' 'intltool')
options=('!libtool')
install=last-exit.install
source=(http://lastexit-player.org/releases/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ef0bffe352073b7663441aa9b80237b1')

build() {
  export MONO_SHARED_DIR=${srcdir}/.wabi
  mkdir -p ${MONO_SHARED_DIR}

  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-schemas-install \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/data/icons/last-exit.svg \
    ${pkgdir}/usr/share/pixmaps/last-exit.svg
}

# vim:set ts=2 sw=2 et:
