# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor : Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

# Remove when bumped upstream

pkgbase=docutils
pkgname=('python-docutils' 'python2-docutils')
pkgver=0.12
pkgrel=1
pkgdesc='Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX'
arch=('any')
url='http://docutils.sourceforge.net'
license=('custom')
makedepends=('python' 'python2')
source=("http://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz"
        '01-rst2odt_prepstyles-elementtree.patch')
md5sums=('4622263b62c5c771c03502afa3157768'
         '34952e8a50692b628a8aa2dde3072f07')

build() {
  cd $pkgbase-$pkgver
  for _p in "$srcdir"/*.patch; do
    msg2 "${_p##*/}"
    patch -p1 -i "$_p"
  done
  msg2 python3
  python3 setup.py build --build-lib=build/python
  find build/python -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python3,' {} \;
  msg2 python2
  python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

check() {
  cd $pkgbase-$pkgver
  # we need utf locale to valid utf8 tests
  export LANG=en_US.UTF-8
  # Disable python3 check
  #msg2 'python checks'
  #PYTHONPATH="$PWD/build/python/" python3 test3/alltests.py
  msg2 'python2 checks'
  PYTHONPATH="$PWD/build/python2/" python2 test/alltests.py
}

package_python-docutils() {
  depends=('python')

  cd $pkgbase-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
  # symlink without .py
  for f in "$pkgdir"/usr/bin/*.py; do
      ln -s "$(basename $f)" "$pkgdir/usr/bin/$(basename $f .py)"
  done
  # setup license
  install -D -m644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING.txt"
  install -D -m644 licenses/python* "$pkgdir/usr/share/licenses/$pkgname/"
}

package_python2-docutils() {
  depends=('python2')
  provides=("docutils=$pkgver")
  replaces=('docutils')
  install=python2-docutils.install

  cd $pkgbase-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
  # fix python-docutils conflict
  for _f in "$pkgdir"/usr/bin/*.py; do
      mv -v "$_f" "${_f%.py}2.py"
  done
  # symlink without .py
  for _f in "$pkgdir"/usr/bin/*.py; do
      ln -s "$(basename $_f)" "$pkgdir/usr/bin/$(basename $_f .py)"
  done
  # setup license
  install -D -m644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING.txt"
  install -D -m644 licenses/python* "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
