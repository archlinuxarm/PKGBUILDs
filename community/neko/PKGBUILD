# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Caleb McCombs <erdrick016+aur@gmail.com>
# Contributor: Christian Hesse <arch@eworm.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove unsupported gcc flag on !aarch64

pkgname=neko
pkgver=2.2.0
pkgrel=4
pkgdesc='High-level and dynamically typed programming language'
url='http://nekovm.org/' # no https
license=('LGPL')
arch=('x86_64')
depends=('gc' 'gtk2' 'libmariadbclient' 'mbedtls' 'sqlite')
makedepends=('apache' 'apr' 'cmake' 'git' 'mbedtls' 'ninja')
optdepends=('apache: for extending Apache with mod_neko')
options=('!strip')
source=("git+https://github.com/HaxeFoundation/neko#tag=v${pkgver//./-}")
sha256sums=('SKIP')

prepare() {
  sed -i '/xlocale.h/d' "$pkgname/libs/std/sys.c"
  if [[ $CARCH != "aarch64" ]]; then sed -i '/mincoming-stack-boundary/d' $pkgname/CMakeLists.txt; fi
}

build() {
  mkdir -p build
  cd build

  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: nekovm.org/download
# vim: ts=2 sw=2 et:
