# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Biru Ionut <biru.ionut at gmail.com>
pkgname=empathy
pkgver=2.26.2
pkgrel=3
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('enchant' 'gnome-panel' 'telepathy-mission-control>=4.61' 'telepathy-farsight'
         'libcanberra' 'libnotify')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils>=0.12.0')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/2.26/$pkgname-$pkgver.tar.gz empathy-idle.c.patch)

build() {
    # Build and install
    cd $srcdir/$pkgname-$pkgver
    #http://bugzilla.gnome.org/show_bug.cgi?id=583208
    patch -Np1 -i $srcdir/empathy-idle.c.patch || return 1

    ./configure --prefix=/usr --sysconfdir=/etc  \
                --libexecdir=/usr/lib/empathy 
    make || return 1
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install || return 1 

    # Merge schemas file in a single file and remove trash
    mkdir -p $pkgdir/usr/share/gconf/schemas
    gconf-merge-schema $pkgdir/usr/share/gconf/schemas/$pkgname.schemas \
        $pkgdir/etc/gconf/schemas/*.schemas
    rm -rf $pkgdir/etc/gconf
}
md5sums=('0f89ff4e210357f32af5fdfcccc9bb75'
         'ed35acb8aa37f6a452b5c793d40e62fc')
