# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

# ALARM: dequis <dx@dxzone.com.ar>
#  - remove haskell-xcffib dependency by using the pypi release instead

pkgname=python-xcffib
pkgver=0.11.1
pkgrel=5
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-six' 'python-cffi' 'libxcb')
makedepends=('python-setuptools' 'python-cffi' 'python-six' 'libxcb')
checkdepends=('python-nose' 'xorg-server-xvfb' 'xorg-xeyes')
_name=${pkgname#python-}
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('09f4e19b88258446af26628c3a8e694c1f938fe3b5456e797b06174602fa0e9ef6e4eab76daccfd169d98fb7e869c910e6216d1f71fa2853e62b82c62c667669')

prepare() {
  touch xcffib-$pkgver/test/__init__.py
}

build() {
  cd xcffib-$pkgver
  python setup.py build
}

check() {
  cd xcffib-$pkgver
  xvfb-run nosetests3
}

package() {
  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
