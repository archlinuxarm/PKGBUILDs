# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

# ALARM: dequis <dx@dxzone.com.ar>
#  - remove haskell-xcffib dependency by using the pypi release instead

pkgname=python-xcffib
pkgver=0.10.1
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-six' 'python-cffi' 'libxcb')
makedepends=('python-setuptools' 'python-cffi' 'python-six' 'libxcb')
checkdepends=('python-nose' 'xorg-server-xvfb' 'xorg-xeyes')
_name=${pkgname#python-}
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('98412fe36962108902dc0599f05ac9be5d7412d7f12fc6566e020c2ce84ae4a536a20ae2dfddd2dc9a536f37cd82539bf19e1993ee65f0b09c44372008cd7edf')

prepare() {
  touch xcffib-$pkgver/test/__init__.py
}

build() {
  cd "$srcdir"/xcffib-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/xcffib-$pkgver
  xvfb-run nosetests3
}

package() {
  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
