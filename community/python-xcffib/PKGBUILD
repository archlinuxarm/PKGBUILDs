# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

# ALARM: dequis <dx@dxzone.com.ar>
#  - remove haskell-xcffib dependency by using the pypi release instead

pkgname=python-xcffib
pkgver=1.0.0
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-cffi' 'libxcb')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-xeyes')
_name=${pkgname#python-}
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('ab6ff69b9e2f4f8adc87ddb090dc6c374db781098800c07ce00bfc843fb0a617c6c8ebcc6cf5bd2efc0e9a581d79110f65656814a7bc116fc4b12f8594b26fa8')

prepare() {
  touch xcffib-$pkgver/test/__init__.py
}

build() {
  cd xcffib-$pkgver
  python setup.py build
}

check() {
  cd xcffib-$pkgver
  xvfb-run pytest
}

package() {
  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
