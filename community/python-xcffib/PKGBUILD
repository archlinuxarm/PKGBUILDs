# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

# ALARM: dequis <dx@dxzone.com.ar>
#  - remove haskell-xcffib dependency by using the pypi release instead

pkgname=python-xcffib
pkgver=1.1.0
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-cffi' 'libxcb')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-xeyes')
_name=${pkgname#python-}
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('7aed8fb876d23228070bf2253245aeae5754404e8a47513cb3545ff7b7738bac39a740d656669e09175d1a684b0c69c88a91a08650a21733de926a3f07da6d62')

prepare() {
  touch xcffib-$pkgver/test/__init__.py
}

build() {
  cd xcffib-$pkgver
  python setup.py build
}

check() {
  cd xcffib-$pkgver
  xvfb-run pytest
}

package() {
  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
