# Maintainer: Christian Hesse <mail@eworm.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix signed char declaration for ARM

pkgname=gimp-nufraw
_realver=0.43-3
pkgver=0.43.3
pkgrel=4
pkgdesc='Converter for raw files; utility and GIMP plugin'
url='https://sourceforge.net/projects/nufraw/'
arch=('x86_64')
license=('GPL')
depends=('cfitsio' 'exiv2' 'gtkimageview' 'lcms2' 'lensfun' 'libjpeg-turbo')
makedepends=('gimp')
conflicts=('gimp-ufraw')
replaces=('gimp-ufraw')
optdepends=('gimp: to use the gimp import plugin for raw images')
source=("https://downloads.sourceforge.net/nufraw/nufraw-${_realver}.tar.gz"
         nufraw-glib-2.70.patch
         signed-char.patch)
sha256sums=('2c7dcc1590993ec267b3112562b2433010d6f94f6acc6506c76aa29cf8c6c32c'
            '0e422f77b277f3c38b454426ee155d1004e47ced8d5d05942448584557c9ed07'
            '93b20b1b6634de7b56a6b50fe35f320dbf68e9dc7b530c5540a88637a14d3fa0')

prepare() {
  cd "nufraw-${_realver}"

  patch -p1 < ../nufraw-glib-2.70.patch # Fix build with glib 2.70
  patch -p1 < ../signed-char.patch

  autoreconf -fi

  mv nufraw.desktop{.desktop,}
  chmod +x generate_schemas.sh
}

build() {
  cd "nufraw-${_realver}"

  ./configure \
    --prefix=/usr \
    --enable-extras \
    --enable-mime \
    --enable-openmp \
    --with-gimp \
    --with-cinepaint \
    --enable-contrast
  make
}

package() {
  make -C "nufraw-${_realver}" DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

