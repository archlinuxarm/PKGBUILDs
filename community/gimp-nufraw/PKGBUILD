# Maintainer: Christian Hesse <mail@eworm.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to CXXFLAGS

pkgname=gimp-nufraw
pkgver=0.42
pkgrel=3
pkgdesc='Converter for raw files; utility and GIMP plugin'
url='https://sourceforge.net/projects/nufraw/'
arch=('x86_64')
license=('GPL')
depends=('cfitsio' 'exiv2' 'gtkimageview' 'lcms2' 'lensfun' 'libjpeg-turbo')
makedepends=('gimp')
conflicts=('gimp-ufraw')
replaces=('gimp-ufraw')
optdepends=('gimp: to use the gimp import plugin for raw images')
source=("https://downloads.sourceforge.net/nufraw/nufraw-$pkgver.tar.gz")
sha256sums=('ddfb592a183618c802e990211f6efce75f4801a7708ecc364666f53af25c9f90')

prepare() {
  cd "nufraw-$pkgver"

  autoreconf -fi

  mv nufraw.desktop{.desktop,}
  chmod +x generate_schemas.sh
}

build() {
  cd "nufraw-$pkgver"

  CXXFLAGS+=" -fsigned-char"

  ./configure \
    --prefix=/usr \
    --enable-extras \
    --enable-mime \
    --enable-openmp \
    --with-gimp \
    --with-cinepaint \
    --enable-contrast
  make
}

package() {
  make -C "nufraw-$pkgver" DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

