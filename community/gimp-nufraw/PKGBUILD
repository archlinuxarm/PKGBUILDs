# Maintainer: Christian Hesse <mail@eworm.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to CXXFLAGS

pkgname=gimp-nufraw
_realver=0.43-3
pkgver=0.43.3
pkgrel=2
pkgdesc='Converter for raw files; utility and GIMP plugin'
url='https://sourceforge.net/projects/nufraw/'
arch=('x86_64')
license=('GPL')
depends=('cfitsio' 'exiv2' 'gtkimageview' 'lcms2' 'lensfun' 'libjpeg-turbo')
makedepends=('gimp')
conflicts=('gimp-ufraw')
replaces=('gimp-ufraw')
optdepends=('gimp: to use the gimp import plugin for raw images')
source=("https://downloads.sourceforge.net/nufraw/nufraw-${_realver}.tar.gz")
sha256sums=('2c7dcc1590993ec267b3112562b2433010d6f94f6acc6506c76aa29cf8c6c32c')

prepare() {
  cd "nufraw-${_realver}"

  autoreconf -fi

  mv nufraw.desktop{.desktop,}
  chmod +x generate_schemas.sh
}

build() {
  cd "nufraw-${_realver}"

  CXXFLAGS+=" -fsigned-char"

  ./configure \
    --prefix=/usr \
    --enable-extras \
    --enable-mime \
    --enable-openmp \
    --with-gimp \
    --with-cinepaint \
    --enable-contrast
  make
}

package() {
  make -C "nufraw-${_realver}" DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

