# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on python-pypandoc

pkgname=hid-tools
pkgver=0.2
pkgrel=4
arch=('any')
url='https://gitlab.freedesktop.org/libevdev/hid-tools'
license=('GPL2')
pkgdesc='Python scripts to manipulate HID data'
depends=('python-parse' 'python-pyudev')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('580635bc679b8b6dd396c72b9a5d6560a56a3c3fa4b1d83ebbe82508977e23ce3abf0892406605937e62cde6335d205eba283b6c67dfe65630a2a27d74747ffb')

build() {
  cd $pkgname-$pkgver

  python setup.py build

  make doc
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root "$pkgdir" --skip-build -O1

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$pkgname
}

