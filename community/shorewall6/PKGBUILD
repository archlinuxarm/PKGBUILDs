# $Id: PKGBUILD 1861 2009-09-05 19:36:36Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=shorewall6
pkgver=4.4.1
pkgrel=2
pkgdesc="IPv6 support for Shorewall"
arch=('i686' 'x86_64')
url="http://www.shorewall.net"
backup=(etc/shorewall6/{shorewall.conf,zones,functions,policy,interfaces,ipsec,hosts,rules,nat,netmap,params,proxyarp,routestopped,maclist,masq,modules,tcclasses,tcdevices,tcfilters,tcrules,whitelist})
license=('GPL')
depends=('shorewall')
source=(http://downloads.sourceforge.net/shorewall/shorewall4-ipv6/$pkgver/$pkgname-$pkgver.tar.bz2)
md5sums=('075835cd382634fa25372bcf183bcce5')

build() {
  cd ${srcdir}/$pkgname-$pkgver
 
  sed -e 's!LOGFILE=/var/log/messages!LOGFILE=/var/log/messages.log!' -i shorewall6.conf
  sed -e 's!LOGFILE=/var/log/messages!LOGFILE=/var/log/messages.log!' -i shorewall6

  mv init.archlinux.sh init.sh
  DEST=/etc/rc.d INIT=shorewall6 PREFIX=${pkgdir} ./install.sh
  cp -rv ${srcdir}/$pkgname-$pkgver/Samples6 ${pkgdir}/usr/share/shorewall6

  sed -e 's!/subsys!!' -i ${pkgdir}/etc/shorewall6/shorewall6.conf

  for file in ${pkgdir}/etc/shorewall6/*; do
    chmod 644 ${pkgdir}/etc/shorewall6/$(basename $file)
  done
  chmod 644 ${pkgdir}/usr/share/shorewall6/modules
}
