# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=freemat
pkgver=3.6
pkgrel=3
pkgdesc="A free environment for rapid engineering, scientific prototyping and data processing"
arch=('i686' 'x86_64')
url="http://freemat.sourceforge.net"
license=('GPL')
depends=('qt' 'umfpack' 'arpack' 'libmatio' 'ffcall' 'fftw' 'blas' 'lapack')
install=freemat.install
source=(http://downloads.sourceforge.net/sourceforge/freemat/FreeMat-$pkgver.tar.gz \
	freemat-gcc-4.3.0.patch)
md5sums=('85df162d519014c92197f334e9c3eeeb'
         '3dc3848949183de2664c2cf67663c892')

build() {
  cd $startdir/src/FreeMat-$pkgver

  patch -p1 <../freemat-gcc-4.3.0.patch

  ./configure --prefix=/usr

  if [ $CARCH == "x86_64" ]; then
    patch -p0 libs/libFreeMat/Compiler.cpp <<EOF
735c735
<     sprintf(buffer,"%x",(unsigned)topval.p);
---
>     sprintf(buffer,"%Lx",(unsigned long long)topval.p);
EOF
  fi

  make || return 1
  make DESTDIR=$startdir/pkg install || return 1
  sed -i "s|/FreeMat-.*/|/FreeMat-$pkgver/|g" $startdir/freemat.install
}
