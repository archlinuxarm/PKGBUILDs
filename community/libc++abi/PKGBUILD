# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Benno Fünfstück <pyhaxor@googlemail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix for ARM builds

pkgname=libc++abi
pkgver=3.7.1
pkgrel=1
pkgdesc='A new implementation of low level support for a standard C++ library'
arch=('i686' 'x86_64')
url="http://libcxxabi.llvm.org/"
license=('MIT' 'custom:University of Illinois/NCSA Open Source License')
makedepends=(libunwind clang libc++)
source=("http://www.llvm.org/releases/$pkgver/libcxxabi-${pkgver}.src.tar.xz"
        "http://www.llvm.org/releases/$pkgver/libcxxabi-${pkgver}.src.tar.xz.sig")
md5sums=('52d925afac9f97e9dcac90745255c169'
         'SKIP')
validpgpkeys=(
              # Bill Wendling <void@llvm.org>
              54E3BDE33185D9F69664D22455F5CD70BB5A0569
              # Tom Stellard <tom@stellard.net>
              11E521D646982372EB577A1F8F0871F202119294
              # Hans Wennborg <hans@chromium.org>
              B6C8F98282B944E3B0D5C2530FC3042E345AD05D
)

prepare() {
  cd libcxxabi-${pkgver}.src/include
  sed -i 's/#define LIBCXXABI_ARM_EHABI 1/#define LIBCXXABI_ARM_EHABI 0/' __cxxabi_config.h
}

build() {
  cd libcxxabi-${pkgver}.src/lib
  ./buildit
}

package() {
  cd libcxxabi-${pkgver}.src/
  mkdir -p "$pkgdir/usr/include"
  install -Dm644 lib/libc++abi.so.1.0 "$pkgdir/usr/lib/libc++abi.so.1.0"
  ln -s /usr/lib/libc++abi.so.1.0 "$pkgdir/usr/lib/libc++abi.so.1"
  ln -s /usr/lib/libc++abi.so.1.0 "$pkgdir/usr/lib/libc++abi.so"
  cp -a include/ "$pkgdir/usr/include/cxxabi"
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # fix incorrect permissions
  chmod -R o=g "$pkgdir/usr/include"
}
