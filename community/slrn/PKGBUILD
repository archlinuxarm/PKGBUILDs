# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=slrn
pkgver=0.9.9p1
pkgrel=5
pkgdesc="An open source text-based news client"
arch=('i686' 'x86_64')
url="http://www.slrn.org/"
license=('GPL')
depends=('openssl' 'slang')
makedepends=('uudeview')
optdepends=('metamail')
options=('!makeflags' 'docs' 'zipman')
backup=(etc/slrnrc)
source=(http://downloads.sourceforge.net/slrn/$pkgname-$pkgver.tar.gz)
md5sums=('6cc8ac6baaff7cc2a8b78f7fbbe3187f')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
  	--enable-setgid-code \
  	--with-slrnpull --with-ssl --with-uu \
	--with-uulib=/usr/lib/uudeview \
	--with-uuinc=/usr/include/uudeview \
	--enable-spoilers

  make || return 1
  make DESTDIR=$pkgdir install

  install -D -m644 doc/slrn.rc $pkgdir/etc/slrnrc
}
