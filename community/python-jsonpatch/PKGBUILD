# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop python-pypandoc makedepends

pkgname=python-jsonpatch
pkgver=1.26
pkgrel=1
pkgdesc="An implementation of the JSON Patch format"
arch=("any")
url="https://github.com/stefankoegl/python-json-patch"
license=("BSD")
depends=("python-jsonpointer")
makedepends=("python-setuptools")
checkdepends=('python-coverage')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stefankoegl/python-json-patch/archive/v$pkgver.tar.gz")
sha512sums=('1ab25c8e091cd0b772062a8c448fee4772081d8bd715489b2f7a76e4c0bdff8b26dd38b26c3037cf9bdf8780b97e62a20b6a5bdc43f43af16fdae51d1291bf52')

build() {
  cd "$srcdir"/python-json-patch-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-json-patch-$pkgver
  coverage run --source=jsonpatch tests.py
}

package_python-jsonpatch() {
  cd "$srcdir"/python-json-patch-$pkgver

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  python setup.py install --prefix=/usr --root="$pkgdir"
}
