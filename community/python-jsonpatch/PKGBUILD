# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop python{,2}-pypandoc makedepends

pkgbase=python-jsonpatch
_pkgname=jsonpatch
pkgname=(python-jsonpatch python2-jsonpatch)
pkgver=1.13
pkgrel=2
pkgdesc="An implementation of the JSON Patch format"
arch=("any")
url="https://github.com/stefankoegl/python-json-patch"
license=("BSD")
makedepends=("python-setuptools" "python2-setuptools" "python-jsonpointer" "python2-jsonpointer"
             "git")
checkdepends=('python-coverage' 'python2-coverage')
source=("git+https://github.com/stefankoegl/python-json-patch.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare(){
  cp -a python-json-patch{,-py2}
  find python-json-patch-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir"/python-json-patch
  python setup.py build

  cd "$srcdir"/python-json-patch-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-json-patch
  coverage run --source=jsonpatch tests.py

  cd "$srcdir"/python-json-patch-py2
  coverage2 run --source=jsonpatch tests.py
}

package_python-jsonpatch() {
  depends=("python-jsonpointer")

  cd "$srcdir"/python-json-patch

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  python setup.py install --prefix=/usr --root="$pkgdir"
}
package_python2-jsonpatch() {
  depends=("python2-jsonpointer")

  cd "$srcdir"/python-json-patch-py2

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  for _bin in "$pkgdir"/usr/bin/*; do
    mv "$_bin"{,2}
  done
}
