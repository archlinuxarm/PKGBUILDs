# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop python{,2}-pypandoc makedepends

pkgbase=python-jsonpatch
pkgname=(python-jsonpatch python2-jsonpatch)
pkgver=1.20
pkgrel=1
pkgdesc="An implementation of the JSON Patch format"
arch=("any")
url="https://github.com/stefankoegl/python-json-patch"
license=("BSD")
makedepends=("python-setuptools" "python2-setuptools" "python-jsonpointer" "python2-jsonpointer")
checkdepends=('python-coverage' 'python2-coverage')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/stefankoegl/python-json-patch/archive/v$pkgver.tar.gz")
sha512sums=('7b4222115bcffbd4928a8b10f00432ec2861e4b09e3f39bff8169ca036f2894d6f1fb94d96b049b5271f0aa5474f49ee67168caf0fe62260cf939290e6b87437')

prepare(){
  cp -a python-json-patch-$pkgver{,-py2}
  find python-json-patch-$pkgver-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir"/python-json-patch-$pkgver
  python setup.py build

  cd "$srcdir"/python-json-patch-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-json-patch-$pkgver
  coverage run --source=jsonpatch tests.py

  cd "$srcdir"/python-json-patch-$pkgver-py2
  coverage2 run --source=jsonpatch tests.py
}

package_python-jsonpatch() {
  depends=("python-jsonpointer")

  cd "$srcdir"/python-json-patch-$pkgver

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  python setup.py install --prefix=/usr --root="$pkgdir"
}
package_python2-jsonpatch() {
  depends=("python2-jsonpointer")

  cd "$srcdir"/python-json-patch-$pkgver-py2

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  for _bin in "$pkgdir"/usr/bin/*; do
    mv "$_bin"{,2}
  done
}
