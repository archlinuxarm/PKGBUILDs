# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop python-pypandoc makedepends

pkgname=python-jsonpatch
pkgver=1.29
pkgrel=1
pkgdesc="An implementation of the JSON Patch format"
arch=("any")
url="https://github.com/stefankoegl/python-json-patch"
license=("BSD")
depends=("python-jsonpointer")
makedepends=("python-setuptools")
checkdepends=('python-coverage')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stefankoegl/python-json-patch/archive/v$pkgver.tar.gz")
sha512sums=('eea5f38797c74d420c2eaf0bd1145d3ecf1ef595b1fe0f9816a031023fd98cd34013e12ed8ef3636715b2761a692303abee5ec814b44d4d9b9c0363a5b736979')

build() {
  cd python-json-patch-$pkgver
  python setup.py build
}

check() {
  cd python-json-patch-$pkgver
  coverage run --source=jsonpatch tests.py
}

package_python-jsonpatch() {
  cd python-json-patch-$pkgver

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  python setup.py install --prefix=/usr --root="$pkgdir"
}
