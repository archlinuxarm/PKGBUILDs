# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop python-pypandoc makedepends

pkgname=python-jsonpatch
pkgver=1.27
pkgrel=1
pkgdesc="An implementation of the JSON Patch format"
arch=("any")
url="https://github.com/stefankoegl/python-json-patch"
license=("BSD")
depends=("python-jsonpointer")
makedepends=("python-setuptools")
checkdepends=('python-coverage')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stefankoegl/python-json-patch/archive/v$pkgver.tar.gz")
sha512sums=('297ca02d114e6b199bd516f69dca147fbe309f0a1b031046859271720645322607236f97b489c242575f7e1ad3cab0c68b1b7f11894c5af82be24013e376c0a4')

build() {
  cd python-json-patch-$pkgver
  python setup.py build
}

check() {
  cd python-json-patch-$pkgver
  coverage run --source=jsonpatch tests.py
}

package_python-jsonpatch() {
  cd python-json-patch-$pkgver

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  python setup.py install --prefix=/usr --root="$pkgdir"
}
