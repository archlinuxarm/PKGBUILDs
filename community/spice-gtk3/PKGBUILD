# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop libcacard dep

pkgname=spice-gtk3
pkgver=0.27
pkgrel=1
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'gtk3' 'libpulse' 'usbredir' 'libsoup')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils'
	     'vala' 'sdl')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2")
sha256sums=('5075e07010db434146649110ade31f5a6cdcdb4efbb4bedd40f1a977f090430c')

prepare() {
  cd "$srcdir/spice-gtk-$pkgver"
  sed -i 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py
  sed -i 's|#define SPICE_GTK_MICRO_VERSION.*|#define SPICE_GTK_MICRO_VERSION (0)|g' gtk/spice-version.h*
}

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr \
    --disable-static --enable-vala --with-gtk=3.0 \
    --with-audio=pulse \
    --with-coroutine=gthread --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
}
