# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Hilton Medeiros <medeiros.hilton AT gmail DOT com>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Paolo Fagni <paolo.fagni AT mail DOT com>
# Contributor: Lukas Kropatschek <lukas.krop AT gmail DOT com>

# remove when bumped upstream

pkgbase=irrlicht
pkgname=('irrlicht' 'irrlicht-docs')
pkgver=1.8.5
pkgrel=1.1
pkgdesc="An open source high performance realtime 3D graphics engine."
arch=('x86_64')
url="http://irrlicht.sourceforge.net/"
license=('ZLIB')
depends=('libgl' 'libjpeg' 'bzip2' 'libpng')
makedepends=('mesa' 'unzip' 'libxcursor')
optdepends=('libxcursor: run examples')
source=("https://downloads.sourceforge.net/irrlicht/$pkgname-$pkgver.zip")
noextract=($pkgname-$pkgver.zip)
sha512sums=('d11c7a056bfb8c9737ed583c5bc5794223bc59fb4620411b63bc4d1eedc41db2ed1cab5cb7a37fee42a7f38c0e0645f5fc53fd329fff0f2aa78e0df6804c47c9')

prepare() {
  unzip $pkgbase-$pkgver.zip
  cd $pkgbase-$pkgver

  sed -r '/^#define _IRR_USE_NON_SYSTEM_(JPEG_LIB|LIB_PNG|ZLIB|BZLIB)_/d' -i include/IrrCompileConfig.h
  sed -r '/^(ZLIB|JPEGLIB|LIBPNG|BZIP2)OBJ/d' -i source/Irrlicht/Makefile
  sed -r '/^sharedlib: LDFLAGS/s,\r?$, -lz -ljpeg -lpng -lz -lbz2,' -i source/Irrlicht/Makefile
  rm -r source/Irrlicht/{bzip2,jpeglib,libpng,zlib}
}

build() {
  cd $pkgbase-$pkgver/source/Irrlicht

  make NDEBUG=1 sharedlib
}

package_irrlicht() {
  cd $pkgbase-$pkgver/source/Irrlicht

  sed -i "/^INSTALL_DIR/s:=.*:=$pkgdir/usr/lib:" Makefile

  make install

  cd "$srcdir"/$pkgbase-$pkgver/
  install -Dm644 readme.txt $pkgdir/usr/share/licenses/$pkgbase/LICENSE

  # fix headers permissions
  chmod 644 "$pkgdir"/usr/include/$pkgbase/*

  ln -s libIrrlicht.so.$pkgver "$pkgdir"/usr/lib/libIrrlicht.so.1
}

package_irrlicht-docs() {
  cd $pkgbase-$pkgver/

  install -d "$pkgdir"/usr/share/$pkgbase "$pkgdir"/usr/share/doc/$pkgbase

  # install media files for examples
  cp -r media "$pkgdir"/usr/share/$pkgbase

  # install documentation
  cp -r doc/* "$pkgdir"/usr/share/doc/$pkgbase
  rm -f "$pkgdir"/usr/share/doc/$pkgbase/*.txt
}
