# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - replace make/optdepends on python{,2}-cairocffi with python{,2}-cairo (cffi has haskell dep)
#  - disable lto via setup.cfg

pkgname=python-matplotlib
pkgver=3.3.2
pkgrel=1
pkgdesc="A python plotting library, making publication quality plots"
arch=(x86_64)
url="https://matplotlib.org"
license=(custom)
depends=(freetype2 python-cycler python-dateutil python-kiwisolver python-numpy python-pillow python-pyparsing qhull)
optdepends=('tk: Tk{Agg,Cairo} backends'
            'python-pyqt5: Qt5{Agg,Cairo} backends'
            'python-gobject: for GTK3{Agg,Cairo} backend'
            'python-wxpython: WX{,Agg,Cairo} backend'
            'python-cairo: {GTK3,Qt5,Tk,WX}Cairo backends'
            'python-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies'
            'texlive-latexextra: usetex usage with pdflatex')
makedepends=(git rsync python-setuptools
             tk python-pyqt5 python-gobject
             python-wxpython python-cairo python-tornado
             ghostscript texlive-bin)
             # agg missing some non-upstreamed patches?
checkdepends=(python-pytest-xdist python-pytest-runner xorg-server-xvfb
              texlive-core texlive-latexextra inkscape mencoder
              ffmpeg imagemagick ttf-freefont python-pandas)
source=("https://github.com/matplotlib/matplotlib/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('553469fe1f4a8608b832f71805b97582dc5df75ed7a4f2e3ff12a9a862069d62909cacd50f01891bb0f6b161e630ea887a7674e904ea1a963c62877dec2ec5cb')

prepare() {
  cd matplotlib-$pkgver
# Use system freetype and qhull, disable LTO
  sed -e 's|#system_freetype = False|system_freetype = True|' -e 's|#system_qhull = False|system_qhull = True|' -e 's|#enable_lto = True|enable_lto = False|' setup.cfg.template > setup.cfg
}

build() {
  cd matplotlib-$pkgver
  python setup.py build
}

check() {
  cd matplotlib-$pkgver
  xvfb-run -a -s "+extension GLX +extension RANDR +render -screen 0 1280x1024x24" \
    python setup.py pytest --addopts="-n auto" || warning "Tests failed" # Different font rendering details
}

package_python-matplotlib() {
  cd matplotlib-$pkgver
  python setup.py install --root "$pkgdir" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 doc/users/license.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
  # Needed since https://github.com/matplotlib/matplotlib/pull/14170
  rm -r "$pkgdir"/usr/lib/python3.8/site-packages/{matplotlib,mpl_toolkits}/tests/
}
