# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - replace make/optdepends on python{,2}-cairocffi with python{,2}-cairo (cffi has haskell dep)

pkgname=python-matplotlib
pkgver=3.0.1
pkgrel=1
pkgdesc="A python plotting library, making publication quality plots"
arch=('x86_64')
url="https://matplotlib.org"
license=('custom')
depends=('freetype2' 'libpng' 'python-numpy' 'python-cycler' 'python-dateutil' 'python-kiwisolver' 'python-pyparsing')
optdepends=('tk: Tk{Agg,Cairo} backends'
            'python-pyqt5: Qt5{Agg,Cairo} backends'
            'python-gobject: for GTK3{Agg,Cairo} backend'
            'python-wxpython: WX{,Agg,Cairo} backend'
            'python-cairo: {GTK3,Qt5,Tk,WX}Cairo backends'
            'python-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'python-pillow: for reading/saving jpeg/bmp/tiff files'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies')
makedepends=('git' 'rsync' 'python-setuptools'
             'tk' 'python-pyqt5' 'python-gobject'
             'python-wxpython' 'python-cairo' 'python-tornado'
             'python-pillow' 'ghostscript' 'texlive-bin')
             # 'qhull' missing pkg-config file
             # 'agg' missing some non-upstreamed patches?
checkdepends=('python-pytest-xdist' 'python-pytest-runner' 'xorg-server-xvfb'
              'texlive-core' 'texlive-latexextra' 'inkscape' 'mencoder'
              'ffmpeg' 'imagemagick' 'ttf-freefont' 'python-pandas')
source=($pkgname-$pkgver.tar.gz::"https://github.com/matplotlib/matplotlib/archive/v$pkgver.tar.gz"
        setup.cfg)
sha512sums=('1773846f4bec863d323652c9a89b5c087049f9170c8a18a2c7e1d818f8cb46f34e7fc9fc7a2a8ab70c80dbc2bcaf8428b6a9fb941e85ec7b054d2204d0a794bb'
            'f08c0b2e94599fdf2b736b8a655d862209934441d90f20ed872cfc128b8d2a6525763ab7cf64f2b3630e22a90798f4c52aa93b18087e9b48708d9bae34aada0b')

prepare() {
  cp -a matplotlib-$pkgver{,-test}

  # Configure tests (FS#48175)
  cp setup.cfg matplotlib-$pkgver-test/
}

build() {
  cd matplotlib-$pkgver
  python setup.py build
}

check() {
  cd matplotlib-$pkgver-test
  xvfb-run -a -s "+extension GLX +extension RANDR +render -screen 0 1280x1024x24" \
    python setup.py pytest --addopts="-n auto" || warning "Tests failed" # Different font rendering details
}

package_python-matplotlib() {
  cd matplotlib-$pkgver
  python setup.py install --root "$pkgdir" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 doc/users/license.rst -t "$pkgdir"/usr/share/licenses/${pkgname}/
}
