# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=eclipse-emf
pkgver=2.5.0
_reldate=200906151043
pkgrel=1
pkgdesc="EMF and XSD frameworks for the Eclipse platform"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/modeling/emf/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip')
source=(http://download.eclipse.org/modeling/emf/emf/downloads/drops/${pkgver}/R${_reldate}/emf-xsd-SDK-${pkgver}.zip)
md5sums=('321e9f9ccb2eb3c5a602490e14adb8bc')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/eclipse

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

# vim:set ts=2 sw=2 et:
