# $Id: PKGBUILD 3722 2009-10-08 07:29:44Z foutrelis $
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>

pkgname=frostwire
pkgver=4.18.3
pkgrel=1
pkgdesc="P2P Gnutella/Bittorrent client"
arch=('any')
url="http://www.frostwire.com"
license=('GPL')
depends=('java-runtime' 'desktop-file-utils' 'hicolor-icon-theme')
conflicts=('java-gcj-compat')
install=$pkgname.install
source=(http://main2.frostwire.com/$pkgname/$pkgver/$pkgname-$pkgver.noarch.tar.gz
        $pkgname
        $pkgname.desktop
        icons.tar.gz
        do-not-run-unpack200-py.patch
        java-version-detection.patch)
md5sums=('bd7e49ea43fa7834e94da0631dddd6c6'
         '88647e1dbbd3a86472739546b7107949'
         '89684c639a868fd0352a20fc1e81996c'
         'dbea03ff4e35cc6c190976282682c2cf'
         '1bfdaa7c31704645aebf9161bca6947f'
         'b1e6c951e218886c7f6f7511bf722871')

build() {
  cd "$srcdir/$pkgname-$pkgver.noarch"

  # FS#15544 (Java version detection fails when _JAVA_OPTIONS is set)
  patch -p1 -i "$srcdir/java-version-detection.patch"
  
  # Do not attempt to run unpack200.py on startup
  patch -p1 -i "$srcdir/do-not-run-unpack200-py.patch"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -r * "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/icons" "$pkgdir/usr/share"

  install -D "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/icons/hicolor/16x16/apps/$pkgname.png" \
                 "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Remove uneeded files
  rm -rf "$pkgdir"/usr/share/frostwire/{runFrostwireOSX.sh,unpack200.py,root/{.svn,magnet10/.svn}}
}
