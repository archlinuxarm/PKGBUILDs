# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Balló György <ballogyor+arch at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - mozconfig changes
#  - disable gold linker on AArch64
#  - patch for Mozilla bug 1151829 (https://bugzilla.mozilla.org/show_bug.cgi?id=1151829)
#  - patch for Mozilla bug 1141642 (https://bugzilla.mozilla.org/show_bug.cgi?id=1141642)

highmem=1

pkgname=bluegriffon
pkgver=1.8
_mozilla_ver=fec90cbfbaad
_github_ver=5961e4cbc9ea546f9d7e4b9a7f2bb6884d66a378
pkgrel=1
pkgdesc="The next-generation Web Editor based on the rendering engine of Firefox"
arch=('i686' 'x86_64')
url="http://bluegriffon.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'desktop-file-utils' 'dbus-glib' 'gtk2' 'hunspell' 'mozilla-common'
         'nss' 'libevent' 'libvpx' 'libxt' 'python2' 'startup-notification')
makedepends=('autoconf2.13' 'libpulse' 'unzip' 'zip')
options=('!emptydirs')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/therealglazou/${pkgname}/archive/${_github_ver}.tar.gz"
        "mozilla-central-${_mozilla_ver}.tar.bz2"::"http://hg.mozilla.org/mozilla-central/archive/${_mozilla_ver}.tar.bz2"
        "bug_1194520_add_ftfntfmt.patch"::"https://hg.mozilla.org/mozilla-central/raw-rev/af40750720b5"
        "bug_1153109_CXX11_ABI.patch"::"https://hg.mozilla.org/mozilla-central/raw-rev/070d6e282fe4"
        "bug_1151829_profiler.patch"::"https://hg.mozilla.org/mozilla-central/raw-rev/ac8f93de22cb"
        "bug_1141642_skia.patch"::"https://hg.mozilla.org/releases/mozilla-beta/raw-rev/538fd67bb637"
        "mozconfig"
        "${pkgname}.desktop")
sha256sums=('0c629f882a7b7fcdf4416871db8d522f43653b62c0e5a25168b1914af8448a11'
            '95a9868b809664bd79727119eace53b9f6e97c54a137c28b7e6dda688aade6fd'
            '3aea9a83bf304da5525f34a911712cf42f8ded1c8b6becf0a2cf8a4b4f7facd6'
            '2df11bae529ce96d0133ba3ce076e0016350d66a7800a2253b19db13785a87fd'
            'e8b554858413218589cd9147fc0ba1944fcb4a6286a723b70045edd838454d0b'
            '97ec774dee2ed6c954abe6e169d01a0502e649799cedce7ba5d480513052b12f'
            '2625f693cd06b2120f07cc9701ec7b2bf5566b9bcf64d2888796e6e81eea3a86'
            '882133b77840356985a81e496b4ddc443f8f33c068cfb7ef0858497820adcfff')

prepare() {
  cd "${srcdir}/mozilla-central-${_mozilla_ver}"

  # Move files to their correct location
  cp -r "${srcdir}/${pkgname}-${_github_ver}" bluegriffon
  cp "${srcdir}/mozconfig" .mozconfig

  # Disable gold on AArch64
  [[ $CARCH == "aarch64" ]] && echo "ac_add_options --disable-gold" >> .mozconfig

  # Apply patches
  patch -p1 -i "${srcdir}/bug_1194520_add_ftfntfmt.patch"
  patch -p1 -i "${srcdir}/bug_1153109_CXX11_ABI.patch"
  patch -p1 -i "${srcdir}/bug_1151829_profiler.patch"
  patch -p1 -i "${srcdir}/bug_1141642_skia.patch"

  # Apply BlueGriffon's upstream customizations
  patch -Np1 -i bluegriffon/config/content.patch

  # python2 fix
  find . -name \*.py -exec sed -i 's@^#.*python$@#!/usr/bin/python2@' '{}' \;
}

build() {
  cd "${srcdir}/mozilla-central-${_mozilla_ver}"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  export CPPFLAGS+=' -O2'
  export PYTHON=python2

  # Build the package
  ./mach build
}

package() {
  cd "${srcdir}/mozilla-central-${_mozilla_ver}"

  # Install files
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}"
  cp -LR bg/dist/bin/* "${pkgdir}/usr/lib/${pkgname}"

  # Install executable file
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -sf /usr/lib/${pkgname}/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  # Install icons
  cp bg/dist/branding/mozicon128.png bg/dist/branding/default128.png
  for _icon in 16 32 48 128; do
      install -Dm644 bg/dist/branding/default${_icon}.png \
        "${pkgdir}/usr/share/icons/hicolor/${_icon}x${_icon}/apps/${pkgname}.png"
  done

  # Install desktop file
  install -Dm644 ../${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Use system-provided dictionaries
  rm -rf "${pkgdir}/usr/lib/${pkgname}/dictionaries"
  ln -sf /usr/share/hunspell "${pkgdir}/usr/lib/${pkgname}/dictionaries"
  rm -rf "${pkgdir}/usr/lib/${pkgname}hyphenation"
  ln -sf /usr/share/hyphen "${pkgdir}/usr/lib/${pkgname}/hyphenation"
}
