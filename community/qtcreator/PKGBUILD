# $Id: PKGBUILD 86795 2013-03-22 10:46:14Z svenstaro $
# Maintainer: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to upgrade included botan version so it builds on ARM

pkgname=qtcreator
pkgver=2.8.1
_pkgver=2.8.1
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://qt-project.org'
license=('LGPL')
depends=('qt5-quick1' 'qt5-tools')
makedepends=('git')
options=('docs')
optdepends=('qt5-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project support'
            'openssh-askpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support'
            'valgrind: for analyze support')
install=qtcreator.install
source=("http://download.qt-project.org/official_releases/qtcreator/2.8/${pkgver}/qt-creator-${pkgver}-src.tar.gz"
        'qtcreator.desktop'
        'botan-upgrade.patch')
md5sums=('79ef6c6ece0c00035ef744c9d6e3bd3b'
         '82888d4be900e7833d768050a135cd37'
         '7d71e58bdb7a918adb779b0e527fbba5')

build() {
  cd qt-creator-${pkgver}-src
  patch -p1 -i "${srcdir}"/botan-upgrade.patch
  cd ../

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  qmake ../qt-creator-${pkgver}-src/qtcreator.pro
  make
  make docs -j1
}

package() {
  cd build

  make INSTALL_ROOT="${pkgdir}/usr/" install
  make INSTALL_ROOT="${pkgdir}/usr/" install_docs

  install -Dm644 ${srcdir}/qtcreator.desktop ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${pkgver}-src/LGPL_EXCEPTION.TXT ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}
