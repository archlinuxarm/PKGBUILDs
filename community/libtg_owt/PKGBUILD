# Maintainer: Jiachen YANG <farseerfc at archlinux dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon (-DTG_OWT_ARCH_ARMV7_USE_NEON=OFF)

pkgname=libtg_owt
_commit=75ac669
pkgver=0.git1.${_commit}
pkgrel=1
pkgdesc='WebRTC library - static linked'
arch=('x86_64')
url='https://github.com/desktop-app/tg_owt'
license=('custom:BSD')
depends=('protobuf')
makedepends=('git' 'ninja' 'unzip' 'cmake' 'libxrandr' 'libxcomposite' 'openssl' 'glibc' 'ffmpeg' 'libva' 'opus' 'yasm' 'libjpeg-turbo')
options=('staticlibs')
source=("tg_owt::git+${url}.git#commit=${_commit}"
        "libvpx::git+https://chromium.googlesource.com/webm/libvpx.git"
        "libyuv::git+https://chromium.googlesource.com/libyuv/libyuv.git")
b2sums=('SKIP'
        'SKIP'
        'SKIP')
replaces=('libwebrtc')

prepare() {
  cd tg_owt
  git submodule init
  git config submodule.src/third_party/libvpx/source/libvpx.url "$srcdir"/libvpx
  git config submodule.src/third_party/libyuv.url "$srcdir"/libyuv
  git submodule update
}

build() {
  cd tg_owt
  mkdir build
  cd build
  # path to openssl include is intentionally wrong, so that it will not mess up srtp include headers
  cmake -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DTG_OWT_SPECIAL_TARGET=linux \
    -DTG_OWT_LIBJPEG_INCLUDE_PATH=/usr/include \
    -DTG_OWT_OPENSSL_INCLUDE_PATH=/usr/include \
    -DTG_OWT_OPUS_INCLUDE_PATH=/usr/include/opus \
    -DTG_OWT_FFMPEG_INCLUDE_PATH=/usr/include \
    -DTG_OWT_ARCH_ARMV7_USE_NEON=OFF \
    ..
  ninja
}

package() {
  cd tg_owt/build
  DESTDIR="${pkgdir}/" ninja install
}
