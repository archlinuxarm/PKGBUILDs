# Maintainer: Jiachen YANG <farseerfc at archlinux dot org>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon (-DTG_OWT_ARCH_ARMV7_USE_NEON=OFF)

pkgname=libtg_owt
_commit=575fb17
pkgver=0.git7.${_commit}
pkgrel=1
pkgdesc='WebRTC library'
arch=('x86_64')
url='https://github.com/desktop-app/tg_owt'
license=('BSD')
depends=('protobuf' 'libxrandr' 'libxcomposite' 'openssl' 'glibc' 'ffmpeg' 'libva' 'opus' 'yasm' 'libjpeg-turbo' 'pipewire' 'libxtst')
makedepends=('git' 'ninja' 'unzip' 'cmake')
source=("tg_owt::git+${url}.git#commit=${_commit}"
        "libvpx::git+https://chromium.googlesource.com/webm/libvpx.git"
        "libyuv::git+https://chromium.googlesource.com/libyuv/libyuv.git"
        "pipewire::git+https://github.com/PipeWire/pipewire.git")
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP')
replaces=('libwebrtc')

prepare() {
  cd tg_owt
  git submodule init
  git config submodule.src/third_party/libvpx/source/libvpx.url "$srcdir"/libvpx
  git config submodule.src/third_party/libyuv.url "$srcdir"/libyuv
  git config submodule.src/third_party/pipewire.url "$srcdir"/pipewire
  git submodule update
}

build() {
  cd tg_owt
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DTG_OWT_ARCH_ARMV7_USE_NEON=OFF
  ninja -C build
}

package() {
  cd tg_owt
  DESTDIR="${pkgdir}/" ninja -C build install
  install -Dm644 LICENSE "$pkgdir"/usr/share/license/$pkgname/LICENSE
}
