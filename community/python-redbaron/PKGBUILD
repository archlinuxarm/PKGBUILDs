# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
# - remove makedepend on python-pypandoc

pkgname=python-redbaron
pkgver=0.9.2
pkgrel=1
pkgdesc="Abstraction on top of baron, a FST for python to make writing refactoring code a realistic task"
url="https://github.com/PyCQA/redbaron"
license=('LGPL')
arch=('any')
depends=('python-baron')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/redbaron/archive/$pkgver.tar.gz"
        https://github.com/PyCQA/redbaron/commit/812081b7bd8774ceb61016ac37da43107c26eea2.patch)
sha512sums=('acebde8fcf5fea419960c2647e8251deb6ea9426e886d642d58ee29924f668a8359867cec934aa10a0eeedf511b454a5f9e603458355869989e3b168165d6f6a'
            'ba829d260ae1994ef12faaa3dd10531046ca754627de44a79f0b5ae9a27eed7a8b304ee5cac709586969b8ad2129b6a68b59631f88a4c616a65ca91937d6b45f')

prepare() {
  patch -d redbaron-$pkgver -p1 -i ../812081b7bd8774ceb61016ac37da43107c26eea2.patch
}

build() {
  cd redbaron-$pkgver
  python setup.py build
}

check() {
  cd redbaron-$pkgver
  pytest tests
}

package() {
  cd redbaron-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
