# $Id$
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed intel-tbb from deps, set -DUSE_TBB=0

pkgname=openimageio
pkgver=1.6.8
pkgrel=3
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'openssl')
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa' 'git')
optdepends=('qt4: iv image viewer'
            'python2: bindings support')
source=(git+git://github.com/OpenImageIO/oiio.git#tag=Release-${pkgver})
md5sums=('SKIP')

build() {
  cd oiio

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  # Python is currently broken :(
  cmake .. \
      -DUSE_PYTHON=OFF \
      -DUSE_PYTHON3=OFF \
      -DUSE_OPENSSL=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DOIIO_BUILD_TESTS=ON \
      -DOIIO_BUILD_TOOLS=ON \
      -DUSE_TBB=0
  make
}

package() {
  cd oiio/build

  make DESTDIR="$pkgdir" install

  # license
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
