# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

# ALARM: Alexander Eisele <alexander@eiselecloud.de>
#  - add _pkgarch variable for build and output filename instead of static arm64

buildarch=4

pkgname=gitlab-runner
pkgver=12.10.1
pkgrel=1
pkgdesc="The official GitLab CI runner written in Go"
arch=('x86_64')
url='https://gitlab.com/gitlab-org/gitlab-runner'
license=('GPL3')
depends=('ca-certificates' 'curl' 'git' 'glibc' 'tar')
makedepends=('git' 'go-pie' 'git' 'mercurial' 'gox')
install=gitlab-runner.install
replaces=('gitlab-ci-multi-runner')
backup=('etc/gitlab-runner/config.toml')
noextract=("prebuilt-${pkgver}-x86_64.tar.xz"
           "prebuilt-${pkgver}-arm.tar.xz")

source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitlab-runner/repository/archive?sha=v${pkgver}"
        "prebuilt-${pkgver}-x86_64.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-x86_64.tar.xz"
        "prebuilt-${pkgver}-arm.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-arm.tar.xz"
        "gitlab-runner.service"
        "gitlab-runner.sysusers"
        "gitlab-runner.tmpfiles"
        "config.toml")
sha512sums=('a3ebeffab6e80d590d1a8e9e3470bed717250b029c5f56a308084d5d0b57cb3a2d4f3639d2096e2c6b4b54e33659729baf5487a99001b16426c0675386c91539'
            'd6bc75834f8a51cbebd40ca294bba783f67069cd6a7e60a9bc54cb214530d71e5d0bb089598e87a56da23bb490d450a9071649255d4669cdce9068d80a5f3924'
            'e69d8fa584b4a6ecfbaa07d090b036b16c2d9843bdcdccbd030048ac37598811ceb08e21dbc9f071d14523b563902140551692c75d381514367ec09743b67afb'
            '8a5a8b7654d3864722e784b2814c6278c17876f1c0c4fc0676fbcf6817ad2ba4be55501e67ce88c62b5b63ca886b01afc6feac98ba49842acd244abdd1a8296f'
            '8aa7f08702e99053c696fcc2aaba83beb9e9cd6f31973d82862db9350ac46df3a095377625d31fe909677525290d2de922d7a97930ed235774cb8f0da8944d40'
            '6751d9fa0b27172d1b419c4138f5ac15cbc7c9147653a7258cf1470216142c637210bb60608c7ed0974e0e4057e5ddeae32225df1bb36e7dd1f20fec71e33cc3'
            '9718b94bd0ddb09095ffb8c1e60ca1e9649dabb1747e7fc95e58e404b2f9effdeb4cfd759f5b904443dc53a4e18c02003c38f85584713deb49f6a6d1007503de')

_srcdir="gitlab-runner-v${pkgver}-"
case $CARCH in
    x86_64) _pkgarch="amd64";;
    armv7h) _pkgarch="arm";;
esac

prepare() {
    local revision=$(ls -d ${_srcdir}* | rev | cut -c 33-40 | rev)

    mkdir -p "${srcdir}/src/gitlab.com/gitlab-org/"
    ln -sf "${srcdir}/${_srcdir}"* "${srcdir}/src/gitlab.com/gitlab-org/gitlab-runner"
    cd "${srcdir}/src/gitlab.com/gitlab-org/gitlab-runner"

    local version=$(cat VERSION)

    sed -i "s/export VERSION.*/export VERSION = $version/" Makefile
    sed -i "s/REVISION := .*/REVISION := $revision/" Makefile

    make version

    ln -sf "${srcdir}/prebuilt-${pkgver}-x86_64.tar.xz" prebuilt-x86_64.tar.xz
    ln -sf "${srcdir}/prebuilt-${pkgver}-arm.tar.xz" prebuilt-arm.tar.xz
}

build() {
    cd "${srcdir}/src/gitlab.com/gitlab-org/gitlab-runner"
    make BUILD_PLATFORMS="-osarch linux/${_pkgarch}" build_all
}

package() {
    cd "${srcdir}/src/gitlab.com/gitlab-org/gitlab-runner"

    install -Dm644 "${srcdir}/config.toml" "${pkgdir}/etc/gitlab-runner/config.toml"
    install -Dm644 "${srcdir}/gitlab-runner.service" "${pkgdir}/usr/lib/systemd/system/gitlab-runner.service"
    install -Dm644 "${srcdir}/gitlab-runner.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-runner.conf"
    install -Dm644 "${srcdir}/gitlab-runner.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/gitlab-runner.conf"
    install -Dm755 "out/binaries/gitlab-runner-linux-${_pkgarch}" "${pkgdir}/usr/bin/gitlab-runner"
}
