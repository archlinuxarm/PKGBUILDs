# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBUILD_TESTS=OFF

pkgname=curaengine
pkgver=4.9.1
pkgrel=1
pkgdesc="Engine for processing 3D models into 3D printing instruction for Ultimaker and other GCode based 3D printers."
url="https://github.com/Ultimaker/CuraEngine"
arch=('x86_64')
license=('AGPL')
depends=('arcus' 'polyclipping')
checkdepends=('cppunit')
makedepends=('cmake' 'git' 'gmock' 'gtest' 'rapidjson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('cfa918c6301c9948713306256d74a2a84a1fefbc0f85c7ac0eb182d8363f1eae97aecdcb91c207a2e7e1ab252f63cf4b513734b50d4229da7aa35e095ef88bee')


build() {
  cd CuraEngine-${pkgver}
  mkdir -p build
  cd build
    
   cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DBUILD_TESTS=OFF \
     -DUSE_SYSTEM_LIBS=ON \
     -DCMAKE_BUILD_TYPE=Release

  make
}

check() {
  cd CuraEngine-${pkgver}/build
  make test
}

package() {
  cd CuraEngine-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
