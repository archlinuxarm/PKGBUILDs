# Maintainer: Gaetan Bisson <bisson@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - comment adding -flto to CFLAGS
#  - generalize cd Olinux-* for install-bin-sta

pkgname=pari
pkgver=2.11.4
pkgrel=2
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='https://pari.math.u-bordeaux.fr/'
license=('GPL')
arch=('x86_64')
depends=('gmp' 'readline' 'libx11')
makedepends=('perl' 'texlive-core')
optdepends=('perl: gphelp, tex2mail')
validpgpkeys=('42028EA404A2E9D80AC453148F0E7C2B4522E387')
source=("https://pari.math.u-bordeaux.fr/pub/pari/unix/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('bfc88fc4f7352f4840e6e352c72f0369cbea8a45403b1834a6269f3709970b1c'
            'SKIP')

conflicts=('pari-sage')
replaces=("pari-sage<=1:${pkgver}")

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Upstream Bill Allombert recommends linking gp statically against
	# libpari.so with -flto to recover speed losses incurred when enabling
	# pthread support. That's also what he now does for the Debian package.
	#export CFLAGS+=' -flto'

	./Configure \
		--prefix=/usr \
		--with-readline \
		--mt=pthread \
		--with-gmp \

	make all
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make bench
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -sf gp.1.gz "${pkgdir}"/usr/share/man/man1/pari.1

	cd Olinux-*
	make DESTDIR="${pkgdir}" install-bin-sta 
}
