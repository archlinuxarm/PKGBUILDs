# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ainola
# Contributor: speps
# Contributor: Bernardo Barros

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable NEON
#  - skip aarch64 - incompatible with beta luajit

buildarch=22

pkgbase=csound
pkgname=('csound' 'csound-doc')
pkgver=6.10.0
pkgrel=9
pkgdesc="A programming language for sound rendering and signal processing."
arch=('x86_64')
url="http://csound.com"
license=('LGPL')
# wiiuse needs release before add: https://github.com/rpavlik/wiiuse/issues/92
# current gmm doesn't work https://github.com/csound/csound/issues/920
makedepends=('boost' 'cmake' 'dssi' 'eigen' 'faust' 'fltk' 'fluidsynth'
'java-environment' 'ladspa' 'llvm' 'luajit' 'pd' 'portaudio' 'portmidi'
'python2' 'swig')
source=("${pkgname[0]}-${pkgver}.tar.gz::https://github.com/csound/csound/archive/${pkgver}.tar.gz"
        "${pkgname[0]}.sh"
        "https://github.com/${pkgbase}/${pkgbase}/releases/download/${pkgver}/Csound${pkgver}_manual_html.zip"
        "https://github.com/${pkgbase}/${pkgbase}/releases/download/${pkgver}/Csound${pkgver}_manual_pdf.zip")
sha512sums=('1ccf0a2d5273da016ec585e788e4af0b91f55c3be081a51c53dc39b0fbcfba0cf1308828784a1e5dfc39250dedbd3f4b48afbb5c4140c11fd469ca6753702ed4'
            '26e92e7168961686b3d89e9d552f4b086922469f7fc3e8b4b967d32bb7e41fddf014fd52a0aef30f229db57f7eac2980efe7c54b3fc3a7c321ae7e20adbb3995'
            'f61f46705607eb39897fd3b27353fb233acd9da5e00a2b64d63116f74b889f2daafa72d48ed7c5ec1890e32d81331f91455861f2cb3fad3d13b7d27ec203357a'
            'cf6ec335782032fb0703a3042c0222316cd215eba2be242e65d4f1ae23d886177620c96cc63c1aace43a72c1d6258a88612c1ffd5ef0e43675729bae39969fa1')

prepare() {
  cd "${pkgname[0]}-${pkgver}"
  # requires out-of-tree build
  mkdir bld
  # disable NEON
  sed 's/-W -Wall -mtune=core2/-DPFFFT_SIMD_DISABLE/' Custom.cmake.ex > Custom.cmake
}

build() {
  cd "${pkgname[0]}-${pkgver}/bld"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DJAVA_MODULE_INSTALL_DIR=/usr/lib/csound/java \
           -DBUILD_FAUST_OPCODES=1 \
           -DFAUST_LIBRARY=/usr/lib/libfaust.a \
           -DLUA_MODULE_INSTALL_DIR=/usr/lib/lua/5.1/ \
           -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
           -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
           -DPYTHON_MODULE_INSTALL_DIR=/usr/lib/python2.7/site-packages
  make
}

package_csound() {
  depends=('curl' 'fltk' 'fluidsynth' 'hdf5' 'liblo' 'luajit' 'portaudio' 'portmidi' 'python2' 'stk' 'tk')
  optdepends=('csound-doc: The canonical Csound Reference Manual'
              'csoundqt: Qt frontend'
              'faust: embed faust'
              'java-environment: Java Wrapper'
              'vim-csound: Use csound with vim')
  cd "${pkgname}-${pkgver}/bld"
  make DESTDIR="${pkgdir}/" install

  # export vars in profile.d
  install -vDm755 "${srcdir}/${pkgname[0]}.sh" \
    "${pkgdir}/etc/profile.d/${pkgname[0]}.sh"
}

package_csound-doc() {
  license=('LGPL' 'GPL')
  install -vDm644 Csound${pkgver}_manual.pdf \
    "${pkgdir}/usr/share/doc/${pkgname[1]}/${pkgbase}-manual.pdf"

  install -dv "${pkgdir}/usr/share/doc/${pkgname[1]}/html"
  cp -av "${srcdir}"/html/* "${pkgdir}/usr/share/doc/${pkgname[1]}/html"
  # fix file permissions
  find "${pkgdir}" -type f -exec chmod -c 644 {} \;
}

# vim:set ts=2 sw=2 et:
