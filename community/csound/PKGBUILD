# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ainola
# Contributor: speps
# Contributor: Bernardo Barros

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable NEON
#  - disable java interface
#  - skip aarch64 - incompatible with beta luajit

buildarch=22

pkgbase=csound
pkgname=('csound' 'csound-doc')
pkgver=6.11.0
pkgrel=7
pkgdesc="A programming language for sound rendering and signal processing."
arch=('x86_64')
url="http://csound.com"
license=('LGPL')
# wiiuse needs release before add: https://github.com/rpavlik/wiiuse/issues/92
# current gmm doesn't work https://github.com/csound/csound/issues/920
makedepends=('boost' 'cmake' 'dssi' 'eigen' 'faust' 'fltk' 'fluidsynth' 'git'
'hdf5' 'java-environment' 'ladspa' 'libwebsockets' 'llvm' 'luajit' 'pd'
'portmidi' 'portsmf' 'python2' 'stk' 'swig')
source=("${pkgname[0]}-${pkgver}.tar.gz::https://github.com/csound/csound/archive/${pkgver}.tar.gz"
        "${pkgname[0]}.sh"
        "fix-compiler-flag-overrides.patch"
        "https://github.com/${pkgbase}/${pkgbase}/releases/download/${pkgver}/Csound${pkgver}_manual_html.zip"
        "https://github.com/${pkgbase}/${pkgbase}/releases/download/${pkgver}/Csound${pkgver}_manual_pdf.zip")
sha512sums=('0261a9ed83d1545c4a29ab1e64dfe6ee72b3c4529b8c2107b81d6753a3fd76614aca12d59fc6d59b5cc093be4f631e41fdb3d57121bc4eeb67d6ca44c3ad01ad'
            '26e92e7168961686b3d89e9d552f4b086922469f7fc3e8b4b967d32bb7e41fddf014fd52a0aef30f229db57f7eac2980efe7c54b3fc3a7c321ae7e20adbb3995'
            '67a67dd7718da8e88f7c92f18ce1136372044d40c3a9ef86bdaed5c8172e225aa03448ec656da22d60fe9edfc0f6b10f337b02b96505f2e8c0815ef1b8b6a084'
            'c40fe88be3c101d84c473225183ccf02978113e24ad49d67945a1113012c39f590a7d51aeba939c4c43c691763b155a8f203ea993004f9d67d887f7ddce977d5'
            'c2ed8c9e44c50df7a82c3f10d117007795a38d6d7d5b364240b3def75a5469111d2e1c0ab059825233c765fa7ceb1c876f4439045b6d88bd0060b56343fb7c81')

prepare() {
  cd "${pkgname[0]}-${pkgver}"
  # problems compiler flags overrides: https://github.com/csound/csound/issues/960
  patch -Np1 -i "${srcdir}/fix-compiler-flag-overrides.patch"
  # requires out-of-tree build
  mkdir bld
}

build() {
  cd "${pkgname[0]}-${pkgver}/bld"
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_CXX_FLAGS="-Wno-error -Wno-missing-field-initializers -DPFFFT_SIMD_DISABLE" \
           -DCMAKE_C_FLAGS="-Wno-error -Wno-missing-field-initializers -DPFFFT_SIMD_DISABLE" \
           -DBUILD_JAVA_INTERFACE=0 \
           -DBUILD_FAUST_OPCODES=1 \
           -DFAUST_LIBRARY=/usr/lib/libfaust.a \
           -DLUAJIT_LIBRARY=/usr/lib/libluajit-5.1.so \
           -DLUA_H_PATH=/usr/include/luajit-2.0/ \
           -DLUA_MODULE_INSTALL_DIR=/usr/lib/lua/5.1/ \
           -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
           -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
           -DPYTHON_MODULE_INSTALL_DIR=/usr/lib/python2.7/site-packages
  make
}

package_csound() {
  groups=('pro-audio')
  depends=('fluidsynth' 'git' 'hdf5' 'liblo' 'libwebsockets' 'luajit'
  'portmidi' 'portsmf' 'stk' 'tk')
  optdepends=('csound-doc: The canonical Csound Reference Manual'
              'csoundqt: Qt frontend'
              'fltk: fltk GUIs'
              'faust: Embed faust'
              'java-environment: Use csound with Java'
              'python2: python opcodes'
              'vim-csound: Use csound with vim')
  cd "${pkgname}-${pkgver}/bld"
  make DESTDIR="${pkgdir}/" install

  # export vars in profile.d
  install -vDm 755 "${srcdir}/${pkgname}.sh" -t "${pkgdir}/etc/profile.d/"
  # rename extract executable due to name conflict with libextractor
  # https://github.com/csound/csound/issues/985
  mv -v "${pkgdir}/usr/bin/extract" "${pkgdir}/usr/bin/${pkgname}-extract"
}

package_csound-doc() {
  license=('LGPL' 'GPL')
  install -vDm 644 "Csound${pkgver}_manual.pdf" \
    "${pkgdir}/usr/share/doc/${pkgname}/${pkgbase}-manual.pdf"

  install -dv "${pkgdir}/usr/share/doc/${pkgname}/html"
  cp -av "${srcdir}"/html/* "${pkgdir}/usr/share/doc/${pkgname}/html"
  # fix file permissions
  find "${pkgdir}" -type f -exec chmod -c 644 {} \;
}

# vim:set ts=2 sw=2 et:
