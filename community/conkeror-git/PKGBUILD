# Contributor: Heeru Kiyura <M8R-p9i5nh@mailinator.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=conkeror-git
pkgver=20090920
pkgrel=1
pkgdesc="A highly programmable web browser based on Mozilla XULRunner."
arch=('i686' 'x86_64')
url="http://conkeror.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('gcc-libs' 'xulrunner>=1.9.1')
makedepends=('git')
provides=(conkeror)
install=conkeror.install
source=(http://ftp.eenet.ee/gentoo-portage/www-client/conkeror/files/conkeror.desktop)
md5sums=('480131d88397ede9d92bf27e96d2d84a')

_gitroot="git://repo.or.cz/conkeror.git"
_gitname="conkeror"

build() {
  cd $srcdir

 ## Git checkout
  if [ -d $srcdir/${_gitname} ] ; then
    msg "Git checkout:  Updating existing tree"
    cd ${_gitname} && git pull origin  || return 1
    msg "Git checkout:  Tree has been updated"
  else
    msg "Git checkout:  Retrieving sources"
    git clone ${_gitroot}  || return 1
  fi
  msg "Checkout completed"

 ## Build
  rm -rf $srcdir/${_gitname}-build
  cp -r $srcdir/${_gitname} $srcdir/${_gitname}-build  || return 1
  cd $srcdir/${_gitname}-build

  make  || return 1

 ## Install
  install -d $pkgdir/usr/share/conkeror  || return 1
  install -d $pkgdir/usr/share/man/man1  || return 1
  cp -a  $srcdir/${_gitname}-build/{branding,chrome,components,content,contrib,defaults,locale,modules,search-engines,COPYING,CREDITS,application.ini,conkeror-spawn-helper,help,style,tests} \
    $pkgdir/usr/share/conkeror || return 1
  install -Dm755 /usr/lib/xulrunner-1.9.1/xulrunner-stub \
    $pkgdir/usr/share/conkeror/conkeror || return 1
  install -Dm644 $pkgdir/usr/share/conkeror/contrib/man/conkeror.1 \
    $pkgdir/usr/share/man/man1/conkeror.1 || return 1
  rm -r $pkgdir/usr/share/conkeror/contrib/man
  install -Dm644 $srcdir/conkeror.desktop \
    $pkgdir/usr/share/applications/conkeror.desktop || return 1
} 
