# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop makedepend on pypandoc

pkgbase=python-pytest-plugins
pkgname=('python-pytest-fixture-config' 'python2-pytest-fixture-config'
         'python-pytest-shutil'         'python2-pytest-shutil'
         'python-pytest-virtualenv'     'python2-pytest-virtualenv')
pkgver=1.2.12
pkgrel=1
arch=('any')
license=('BSD')
url='https://github.com/manahl/pytest-plugins'
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools-git' 'python2-setuptools-git'
             'python-execnet' 'python2-execnet'
             'python-path.py' 'python2-path.py' 'python-mock' 'python2-mock' 'python2-contextlib2'
             'python-virtualenv')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/manahl/pytest-plugins/archive/v$pkgver.tar.gz")
sha512sums=('93372f933c88a0775c29426d181c5084a0bfd251342de4443751e770c48ee0ec002c4cb89bbfc053c6415b61981c91d64b7e283592511fedfa26ffc17bf1b259')

prepare() {
  # New pypandoc?
  sed -i "s/'rst')/'rst', format='markdown')/" pytest-plugins-$pkgver/common_setup.py

  # Our /bin is a symlink
  sed -i "s|'/bin'|'/usr/bin'|" pytest-plugins-$pkgver/pytest-shutil/tests/integration/test_cmdline_integration.py

  (cd pytest-plugins-$pkgver; make copyfiles)

  cp -a pytest-plugins-$pkgver{,-py2}
  sed -i '/contextlib2/d' pytest-plugins-$pkgver/pytest-shutil/setup.py
}

build() {
  for _pkg in pytest-fixture-config pytest-shutil pytest-virtualenv; do
    cd "$srcdir"/pytest-plugins-$pkgver/$_pkg
    python setup.py build

    cd "$srcdir"/pytest-plugins-$pkgver-py2/$_pkg
    python2 setup.py build
  done
}

check() {
  # Hack entry points by installing it

  for _pkg in pytest-fixture-config pytest-shutil pytest-virtualenv; do
    cd "$srcdir"/pytest-plugins-$pkgver/$_pkg
    python setup.py install --root="$srcdir"/tmp_install --optimize=1
    PYTHONPATH="$srcdir"/tmp_install/usr/lib/python3.6/site-packages py.test

    cd "$srcdir"/pytest-plugins-$pkgver-py2/$_pkg
    python2 setup.py install --root="$srcdir"/tmp_install --optimize=1
    PYTHONPATH="$srcdir"/tmp_install/usr/lib/python2.7/site-packages py.test2
  done
}

package_python-pytest-fixture-config() {
  pkgdesc='Fixture configuration utils for py.test'
  depends=('python-pytest')

  cd pytest-plugins-$pkgver/pytest-fixture-config
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-fixture-config() {
  pkgdesc='Fixture configuration utils for py.test'
  depends=('python2-pytest')

  cd pytest-plugins-$pkgver-py2/pytest-fixture-config
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-pytest-shutil() {
  pkgdesc='A goodie-bag of unix shell and environment tools for py.test'
  depends=('python-pytest' 'python-execnet' 'python-path.py' 'python-mock')

  cd pytest-plugins-$pkgver/pytest-shutil
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-shutil() {
  pkgdesc='A goodie-bag of unix shell and environment tools for py.test'
  depends=('python2-pytest' 'python2-execnet' 'python2-path.py' 'python2-mock'
           'python2-contextlib2')

  cd pytest-plugins-$pkgver-py2/pytest-shutil
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-pytest-virtualenv() {
  pkgdesc='Virtualenv fixture for py.test'
  depends=('python-pytest-fixture-config' 'python-pytest-shutil' 'python-virtualenv')

  cd pytest-plugins-$pkgver/pytest-virtualenv
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-virtualenv() {
  pkgdesc='Virtualenv fixture for py.test'
  depends=('python2-pytest-fixture-config' 'python2-pytest-shutil' 'python-virtualenv')

  cd pytest-plugins-$pkgver-py2/pytest-virtualenv
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
