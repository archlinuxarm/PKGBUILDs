# $Id: PKGBUILD 3318 2009-10-02 11:38:59Z rvanharen $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=eet-svn
pkgver=42851
pkgrel=1
pkgdesc="A data storage and compression library"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('gnutls' 'libjpeg>=7' 'openssl' 'eina-svn')
makedepends=('subversion')
conflicts=('eet')
provides=('eet')
replaces=('eet-cvs')
options=(!libtool)
source=()
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/eet"
_svnmod="eet"

build() {
   cd $srcdir

msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./autogen.sh --prefix=/usr
  make || return 1
  make DESTDIR=$pkgdir install || return 1
  
  # install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING
  	
  install -Dm644 $srcdir/$_svnmod-build/COPYING-PLAIN \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN
  
  rm -r $srcdir/$_svnmod-build

}
