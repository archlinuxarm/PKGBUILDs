# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

_name=Carla
pkgname=carla
pkgver=2.3.2
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libx11' 'python-pyqt5' 'python-pyliblo'
'python-rdflib' 'qt5-base' 'qt5-svg')
makedepends=('alsa-lib' 'file' 'fluidsynth' 'git' 'glib2' 'gtk2' 'gtk3'
'libsndfile' 'liblo' 'libpulse' 'qt5-tools')
optdepends=(
  'lv2-host: for the LV2 plugin'
  'vst-host: for the VST plugin'
)
provides=('dssi-host' 'ladspa-host' 'lv2-host' 'vst-host' 'vst3-host')
source=("git+https://github.com/falkTX/${pkgname}#tag=v${pkgver}?signed")
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0')  # falkTX <falktx@falktx.com>

build() {
  cd "${pkgname}"
  make NOOPT=true features
  make NOOPT=true
}

package() {
  # libpulse is linked against in carla/libcarla_standalone2.so
  # liblo is linked against in carla/libcarla_standalone2.so,
  # carla/libcarla_native-plugin.so and the lv2 and vst plugins
  depends+=('libasound.so' 'libfluidsynth.so' 'libgdk-3.so'
  'libgdk-x11-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so' 'libgtk-3.so'
  'libgtk-x11-2.0.so' 'liblo.so' 'libmagic.so' 'libpulse.so'
  'libpulse-simple.so' 'libsndfile.so')

  cd "${pkgname}"
  make NOOPT=true DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
