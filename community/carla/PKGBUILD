# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

_name=Carla
pkgname=carla
pkgver=1.9.14
pkgrel=2
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('ffmpeg' 'file' 'fluidsynth' 'gtk2' 'gtk3' 'python-pyqt5'
'python-pyliblo' 'python-rdflib' 'qt5-svg')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('6db46fc7755c1131c69e23a851e7b85b42cbf3e67f3b0a45c8f962f072ef8d7410a8ae968681898a4ec3f38fc72f28711132fb526d55e9b1dd2ced756817836b')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make NOOPT=true features
  make NOOPT=true DEFAULT_QT=5
}

package() {
  cd "${pkgname}-${pkgver}"
  make NOOPT=true DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
