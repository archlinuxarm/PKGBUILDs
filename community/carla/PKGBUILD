# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

_name=Carla
pkgname=carla
pkgver=2.5.0
pkgrel=1
_commit=6bc9a90ebb1767649d2538108ed4a604ce96dacb  # refs/tags/v2.5.0^{}
_commit=3c05e747eb32e5ec8c08bfbc05071bf42ca769ce  # refs/tags/v2.5.0
pkgdesc="Audio Plugin Host"
arch=(x86_64)
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=(GPL2)
groups=(pro-audio)
depends=(alsa-lib gcc-libs glibc hicolor-icon-theme libglvnd libx11
python-pyqt5 python-pyliblo python-rdflib qt5-base qt5-svg)
makedepends=(file fluidsynth freetype2 git libsndfile liblo libpulse qt5-tools)
optdepends=(
  'jack: for using carla with JACK'
  'lv2-host: for the LV2 plugin'
  'vst-host: for the VST plugin'
)
provides=(dssi-host ladspa-host lv2-host vst-host vst3-host)
options=(debug)
source=(
  git+https://github.com/falkTX/$pkgname#tag=$_commit?signed
)
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0')  # falkTX <falktx@falktx.com>

build() {
  make NOOPT=true features -C $pkgname
  make NOOPT=true -C $pkgname
}

package() {
  depends+=(libasound.so libfluidsynth.so libfreetype.so liblo.so libmagic.so
  libpulse.so libsndfile.so)

  make NOOPT=true DEFAULT_QT=5 DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
