# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

_name=Carla
pkgname=carla
pkgver=2.0.0
pkgrel=2
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'python-pyqt5' 'python-pyliblo' 'python-rdflib'
'qt5-base' 'qt5-svg')
makedepends=('ffmpeg' 'file' 'fluidsynth' 'glib2' 'gtk2' 'gtk3' 'libsndfile'
'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}-2.0.0-qt5.13.patch::https://github.com/falkTX/Carla/commit/db6dadd6de2041adf34905d77839cce05273924c.patch")
sha512sums=('7cb1a8a6376e2fbb58a75fc2f60693984b5a8d03e47fe695f59f8a602ccd776ec3a578d93858faa704623fd3bf0521cb2f5e87600f6c55ff2df67f5451c35d5a'
            'f710773c5bc19ac4213df89a33c1f55c991e259f24a7de8a2b961c941aeb3198b46c688e29da8f4b4b02c882bb903c5592534cae5d92501ce776586410012e1c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd $pkgname-$pkgver
  # Fix build with Qt 5.13
  patch -p1 -i "../${pkgname}-2.0.0-qt5.13.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  make NOOPT=true features
  make NOOPT=true DEFAULT_QT=5
}

package() {
  depends+=('libsndfile.so' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
  'libfluidsynth.so' 'libgdk-3.so' 'libgdk-x11-2.0.so' 'libgobject-2.0.so'
  'libglib-2.0.so' 'libmagic.so' 'libgtk-3.so' 'libgtk-x11-2.0.so')
  cd "${pkgname}-${pkgver}"
  make NOOPT=true DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
