# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

_name=Carla
pkgname=carla
pkgver=2.2.0
pkgrel=2
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libx11' 'python-pyqt5' 'python-pyliblo'
'python-rdflib' 'qt5-base' 'qt5-svg')
# libpulse is linked against in carla/libcarla_standalone2.so
# liblo is linked against in carla/libcarla_standalone2.so,
# carla/libcarla_native-plugin.so and the lv2 and vst plugins
makedepends=('alsa-lib' 'file' 'fluidsynth' 'glib2' 'gtk2' 'gtk3' 'libsndfile'
'liblo' 'libpulse' 'qt5-tools')
provides=('dssi-host' 'ladspa-host' 'lv2-host' 'vst-host' 'vst3-host')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('afb2beab979f801817a7fd7663bfdc90800db021679243faa5acb15de60f6dc281032e05c51a8625124c69e7759099fc682678eb98c08507f952763e665ac9c4')
b2sums=('1fd778a1c2904fe982c3aec32e9bb39fff679bea0fe8d1fe41d10bbc5347dd82a6f3dcd33d19aee8bc3b123e387a8b7655c9735fca784d642596f0df9085e38e')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make NOOPT=true features
  make NOOPT=true
}

package() {
  depends+=('libasound.so' 'libfluidsynth.so' 'libgdk-3.so'
  'libgdk-x11-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so' 'libgtk-3.so'
  'libgtk-x11-2.0.so' 'liblo.so' 'libmagic.so' 'libpulse.so'
  'libpulse-simple.so' 'libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make NOOPT=true DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
