# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc

pkgname=python-pysimplesoap
pkgver=1.16.2
_commit=a5c1f885e4af24ed3079bdf7f338523826d3404a
pkgrel=7
pkgdesc="Python simple and lightweight SOAP Library"
url="https://github.com/pysimplesoap/pysimplesoap"
license=('LGPL')
arch=('any')
depends=('python')
checkdepends=('python-nose')
source=("$pkgname-$_commit.tar.gz::https://github.com/pysimplesoap/pysimplesoap/archive/$_commit.tar.gz")
sha512sums=('bd0d1cf9427c4a834051cdb0358d22884d966c0f8ce024e6f5bd61468a2e1fd7d74bc831305a9aafe03df40e5d541447e93beb84939019b129b39f6dbba9aa55')

prepare() {
  mv pysimplesoap-{$_commit,$pkgver}
}

build() {
  cd pysimplesoap-$pkgver
  python setup.py build
}

check() {
  cd pysimplesoap-$pkgver
  nosetests3 -a !internal,!disabled || echo "Tests failed"
}

package() {
  cd pysimplesoap-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
