# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use -fPIC for v7

pkgname=tigervnc
pkgver=1.3.1
pkgrel=2
_xorgver=1.15.2
pkgdesc="suite of VNC servers and clients. VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('fltk' 'pam' 'gnutls' 'libjpeg-turbo' 'libxtst' 'libxfont' 'pixman'
	 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'xorg-xkbcomp'
	 'libgl' 'libgcrypt' 'perl' )
makedepends=('cmake' 'nasm' 'xorg-font-util' 'xorg-util-macros' 'bigreqsproto'
	     'compositeproto' 'damageproto' 'randrproto' 'resourceproto'
	     'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xf86vidmodeproto'
	     'xtrans' 'mesa' 'glproto' 'dri2proto' 'imagemagick')
conflicts=('tightvnc')
source=(http://downloads.sourceforge.net/project/tigervnc/tigervnc/$pkgver/tigervnc-$pkgver.tar.gz
	ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2
	vncserver.service
	vncviewer.desktop
	xserver114.patch
	gethomedir.patch
	getmaster.patch
    xorg115.patch)
md5sums=('e80b16aa74f1d8e89f284a6aef99955d'
         '69229fe5cdf3f20f74665d57b7601d26'
         '0903d5a0dfa38e0b04964505b644585c'
         '71cac0fb9701b0a041430f0fddfe00c8'
         '928faf837ba5b1b34283f9f018df2f5e'
         '22f1523a0eca56ad79cfabd0db6e2cf6'
         'e056a2502dfe0cb3b02e08cab689482f'
         'd7765cb11be43b557ee77dfa486fdb68')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver

  patch -Np1 -i ${srcdir}/gethomedir.patch
  patch -Np1 -i ${srcdir}/getmaster.patch
  patch -Np1 -i ${srcdir}/xorg115.patch 
}

build() {
  if [ "$CARCH" == "armv7h" ]; then
    CFLAGS="$CFLAGS -fPIC"
    CXXFLAGS="$CXXFLAGS -fPIC"
  fi

  cd ${srcdir}/${pkgname}-${pkgver}
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd unix/xserver
  patch -Np1 -i ../xserver114.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
    --disable-wayland --disable-present --disable-dri3 \
	--disable-static --disable-xinerama --without-dtrace \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--disable-unit-tests --disable-devel-docs --disable-selective-werror \
	--disable-dri --enable-dri2 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  sed -i 's/iconic/nowin/' ${pkgdir}/usr/bin/vncserver
  install -Dm0644 $srcdir/vncserver.service $pkgdir/usr/lib/systemd/system/vncserver.service
  install -Dm0644 $srcdir/vncviewer.desktop $pkgdir/usr/share/applications/vncviewer.desktop
}
