# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use -fPIC for v7

pkgname=tigervnc
pkgver=1.4.3
pkgrel=4
_xorgver=1.17.1
pkgdesc="Suite of VNC servers and clients. Based on the VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('fltk' 'pam' 'gnutls' 'libjpeg-turbo' 'libxtst' 'libxfont' 'pixman'
	 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'xorg-xkbcomp'
	 'libgl' 'libgcrypt' 'perl' 'libxdamage')
makedepends=('cmake' 'nasm' 'xorg-font-util' 'xorg-util-macros' 'bigreqsproto'
	     'compositeproto' 'damageproto' 'randrproto' 'resourceproto'
	     'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xf86vidmodeproto'
	     'xtrans' 'glproto' 'dri2proto' 'dri3proto' 'presentproto'
	     'mesa' 'imagemagick')
optdepends=('mesa: for OpenGL functionality in Xvnc')
conflicts=('tightvnc')
source=($pkgname-$pkgver.tar.gz::https://github.com/TigerVNC/tigervnc/archive/v${pkgver}.tar.gz
	ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2
	vncserver.service
	vncviewer.desktop
	gethomedir.patch
	getmaster.patch
	0001-Raise-GnuTLS-requirements-to-3.x.patch
	xorg117.patch)
md5sums=('2177ee42fa1a3902b6feeaa7747f1c2a'
         '5986510d59e394a50126a8e2833e79d3'
         '1aad8f06504b3e29a24ccfbaab617973'
         '71cac0fb9701b0a041430f0fddfe00c8'
         '22f1523a0eca56ad79cfabd0db6e2cf6'
         'e056a2502dfe0cb3b02e08cab689482f'
         'b08aa7418317a8b7a196052d99230f1e'
         'c2d3ba5ef3fc1bc2c11aff178c024eef')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 -i ${srcdir}/gethomedir.patch
  patch -Np1 -i ${srcdir}/getmaster.patch
  patch -Np1 -i ${srcdir}/0001-Raise-GnuTLS-requirements-to-3.x.patch
  patch -Np1 -i ${srcdir}/xorg117.patch
  sed -i 's/iconic/nowin/' unix/vncserver

  cd unix/xserver
  cp -r ${srcdir}/xorg-server-${_xorgver}/* .
}

build() {
  if [ "$CARCH" == "armv7h" ]; then
    CFLAGS="$CFLAGS -fPIC"
    CXXFLAGS="$CXXFLAGS -fPIC"
  fi

  cd ${srcdir}/${pkgname}-${pkgver}

  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd unix/xserver
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-static --disable-xinerama --without-dtrace \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --disable-xwayland \
	--disable-config-hal --disable-config-udev --with-pic \
	--disable-unit-tests --disable-devel-docs --disable-selective-werror \
	--disable-dri --enable-dri2 --enable-dri3 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  install -Dm0644 $srcdir/vncserver.service $pkgdir/usr/lib/systemd/system/vncserver.service
  install -Dm0644 $srcdir/vncviewer.desktop $pkgdir/usr/share/applications/vncviewer.desktop
}
