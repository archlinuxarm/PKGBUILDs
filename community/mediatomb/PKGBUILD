# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Nikhil Bysani <nikron@gmail.com>
# Contributor: Mika Hynn√§ <igheax@gmail.com>
# Maintainer: Desmond Cox <desmondgc@gmail.com>

pkgname=mediatomb
pkgver=0.12.1
pkgrel=1
pkgdesc="Free UPnP MediaServer"
arch=('i686' 'x86_64')
url="http://mediatomb.cc/"
license=('GPL')
depends=('libexif' 'taglib' 'sqlite3' 'expat' 'spidermonkey' 'curl' 'ffmpeg')
optdepends=('ffmpegthumbnailer: video thumbnail generation support')
backup=(etc/conf.d/mediatomb etc/mediatomb/mediatomb.conf)
source=(http://downloads.sourceforge.net/mediatomb/mediatomb-$pkgver.tar.gz
        mediatomb.rc
        mediatomb.conf)
md5sums=('e927dd5dc52d3cfcebd8ca1af6f0d3c2'
         '16be23c425505160c031aaf239a8d9a4'
         '81786546341fc357897d127fce51c8f4')

build() {
    cd $startdir/src/mediatomb-$pkgver || return 1

    ./configure --prefix=/usr --disable-mysql --enable-libmagic --enable-libjs --enable-ffmpeg || return 1
    make || return 1
    make DESTDIR=$startdir/pkg install || return 1

    # daemon script
    mkdir -p $startdir/pkg/etc/rc.d/ || return 1
    install -m0755 ../mediatomb.rc $startdir/pkg/etc/rc.d/mediatomb || return 1

    # daemon config
    mkdir -p $startdir/pkg/etc/conf.d/ || return 1
    install -m0644 ../mediatomb.conf $startdir/pkg/etc/conf.d/mediatomb || return 1

    # daemon database/config dir
    mkdir -p $startdir/pkg/var/lib/mediatomb/ || return 1
}
