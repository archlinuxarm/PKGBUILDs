# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Fabio Castelli <muflone@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building
#  - disable neon

pkgname=libvpx1.3
pkgver=1.3.0
pkgrel=2
pkgdesc='VP8 and VP9 codec'
arch=('x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc')
makedepends=('yasm' 'git')
provides=('libvpx.so')
source=("libvpx-${pkgver}.tar.gz::https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz"
        'libvpx-1.3-gcc-5.2.patch'
        'fix-armhf-link.patch')
sha256sums=('db72881e1c34e4ae92666847844f35dd2dd431bcf6284500b1b396464f882f64'
            'df157b8f0e4469692598f5f12b305561a56a84374880ef740ccb8f0b447851e1'
            'caa188558c36888eb169844b3296039bebf053960257a0581f9dadb41f770eaa')

prepare() {
  cd libvpx-${pkgver}

  patch -Np1 -i ../libvpx-1.3-gcc-5.2.patch
  patch -Np1 -i ../fix-armhf-link.patch
}

build() {
  cd libvpx-${pkgver}

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"

  ./configure \
    --prefix='/usr' \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-pic \
    --disable-install-docs \
    --disable-install-srcs \
    --enable-vp8 \
    --enable-postproc \
    --enable-vp9 $CONFIG

  make
}

package() {
  cd libvpx-${pkgver}

  make DIST_DIR="${pkgdir}"/usr install
  rm -rf "${pkgdir}"/usr/{bin,include,lib/{libvpx.{a,so},pkgconfig},share}

  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx1.3/
}

# vim: ts=2 sw=2 et:
