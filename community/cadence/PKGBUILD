# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: falkTX <falktx at gmail dot com>

# Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove x86isms from Makefile

_name=Cadence
pkgname=cadence
pkgver=0.9.0
pkgrel=4
pkgdesc="JACK toolbox for audio production."
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Cadence"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'jack' 'python-dbus' 'python-pyqt5' 'qt5-svg')
makedepends=('a2jmidid' 'jack_capture' 'libpulse' 'pulseaudio-jack' 'python-rdflib' 'zita-ajbridge')
optdepends=('a2jmidid: ALSA to JACK MIDI bridge'
            'jack_capture: recording via Cadence-Render'
            'pulseaudio-jack: PulseAudio to JACK bridge'
            'python-rdflib: LADSPA-RDF support in Carla'
            'zita-ajbridge: ALSA to JACK bridge')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/Cadence/archive/v0.9.0.tar.gz")
sha512sums=('381c9c57c1d96452a91969e134420d63ef06f5ec144050779d0a06a1098e8ed1693c70165b6640e04acc448d382e717e71ead1b29ba5043f0668675850cad7d1')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  sed -i 's/-mtune=generic -msse -mfpmath=sse//' c++/Makefile.mk
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  # docs
  install -vDm 644 {README.md,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # removing claudia, as its runtime dependency ladish (dead) depends on
  # flowcanvas (dead)
  rm -rfv "${pkgdir}/usr/bin/claudia"* \
          "${pkgdir}/usr/share/applications/claudia"* \
          "${pkgdir}/usr/share/${pkgname}/"{icons,src}/{,ui_}claudia* \
          "${pkgdir}/usr/share/icons/hicolor/"{128x128,16x16,256x256,48x48,scalable}/apps/claudia*
}

