# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -mtune=generic from makefile, replace with our CFLAGS

highmem=1

pkgbase=gmic
pkgname=("gmic" "gimp-plugin-gmic" "zart")
pkgver=1.7.9
_commit=7c19f43888c714d18a13616ff805d246289f4786
pkgrel=3
arch=("i686" "x86_64")
url="http://gmic.eu/"
license=("custom:CeCILL")
makedepends=('gimp' 'qt4' 'fftw' 'openexr' 'opencv' 'mesa' 'gtk2' 'git')
source=("https://gmic.eu/files/source/gmic_$pkgver.tar.gz"
	"git://github.com/dtschump/gmic-community.git#commit=${_commit}")
sha256sums=('152f100eb139a5f6e5b3d1e43aaed34f2b3786f72f52724ebde5e5ccea2c7133'
            'SKIP')

prepare() {
  mv "${srcdir}/gmic-${pkgver}" "${srcdir}/gmic"
  cd "${srcdir}/gmic"
  sed -i -e 's|qmake zart.pro|qmake-qt4 zart.pro|g' \
    -e 's|/etc/bash_completion.d|/usr/share/bash-completion/completions|g' \
    src/Makefile
  ln -sf ../gmic-community/zart zart
  sed -i "s|-mtune=generic|${CFLAGS}|" src/Makefile
}

build() {
  cd "${srcdir}/gmic"
  (cd zart && qmake-qt4 -o Makefile zart.pro)
  make -C src all
  make -C zart all
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=("opencv" "fftw" "libtiff" "libjpeg" "libpng" "openexr" "libx11")
  replaces=("greycstoration")

  cd "${srcdir}/gmic"
  make -C src install DESTDIR="$pkgdir" USR="/usr"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf "${pkgdir}/usr/"{bin/zart,lib/gimp,share/zart}
}

package_zart() {
  pkgdesc="A GUI for G'MIC real-time manipulations on the output of a webcam"
  depends=("opencv" "fftw" "qt4")

  cd "${srcdir}/gmic"
  install -Dm755 zart/zart "${pkgdir}/usr/bin/zart"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=("opencv" "gimp" "fftw")
  replaces=("gimp-plugin-greycstoration" "gimp-plugin-gmic4gimp")

  cd "${srcdir}/gmic"
  install -Dm755 src/gmic_gimp "${pkgdir}/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
