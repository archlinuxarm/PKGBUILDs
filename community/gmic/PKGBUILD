# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -mtune=generic from makefile, replace with our CFLAGS

highmem=1

pkgbase=gmic
pkgname=(gmic zart gimp-plugin-gmic krita-plugin-gmic)
pkgver=2.2.3
_commit=34f7e483a819dd2af4f772d0f748928119f22f6d
pkgrel=1
arch=(x86_64)
url="http://gmic.eu/"
license=(custom:CeCILL)
makedepends=(gimp qt5-base fftw openexr opencv git)
source=("https://gmic.eu/files/source/gmic_$pkgver.tar.gz"
        git://github.com/c-koi/zart.git#commit=$_commit
	gmic-qt-v.$pkgver.tar.gz::"https://github.com/c-koi/gmic-qt/archive/v.$pkgver.tar.gz")
sha256sums=('366e348ebfa85354e99b19eecf2e6b10d445901132eb73c84b18fb23af23f0e8'
            'SKIP'
            '72a6ad1f1acb15f4d9417c9cd16615160d04f764aaa2b0206fa6c80587fb91af')

prepare() {
  mv gmic-$pkgver gmic
  mv gmic-qt-v.$pkgver gmic-qt

  sed -e 's|/etc/bash_completion.d|/usr/share/bash-completion/completions|g' -i gmic/src/Makefile
  sed -e 's|-Ofast|-O2 -fno-fast-math|g' -i gmic-qt/gmic_qt.pro
  sed -e 's/pkg-config opencv --libs ||//' -e 's/-lopencv_highgui/-lopencv_videoio/' -i gmic/src/Makefile # fix overlinking

  sed -i "s|-mtune=generic|${CFLAGS}|" gmic/src/Makefile
}

build() {
  cd gmic
  make -C src all WGET=/bin/true -j1
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=(opencv fftw curl)
  optdepends=('qt5-base: for the Qt UI')

  cd gmic/src
  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -r "$pkgdir"/usr/{bin/zart,lib/gimp,bin/gmic_krita_qt}
}

package_zart() {
  pkgdesc="A GUI for G'MIC real-time manipulations on the output of a webcam"
  depends=(opencv fftw qt5-base)

  install -Dm755 gmic/zart/zart -t "$pkgdir"/usr/bin
  install -Dm644 zart/Licence_CeCILL_V2-en.html -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=(gimp fftw qt5-base curl)

  install -Dm755 gmic/gmic-qt/gmic_gimp_qt "$pkgdir"/usr/lib/gimp/2.0/plug-ins/gmic_gimp
  install -Dm644 gmic-qt/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_krita-plugin-gmic() {
  pkgdesc="Krita plugin for the G'MIC image processing framework"
  depends=(krita)

  install -Dm755 gmic/gmic-qt/gmic_krita_qt -t "$pkgdir"/usr/bin/
}
