# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -mtune=generic from makefile, replace with our CFLAGS

highmem=1

pkgbase=gmic
pkgname=("gmic" "gimp-plugin-gmic" "zart" "krita-plugin-gmic")
pkgver=2.0.4
gmic_qt_ver=.204
_commit=2afae7108fb45d6c88a0a0b0d8e220c6dddb6be1
pkgrel=2
arch=("i686" "x86_64")
url="http://gmic.eu/"
license=("custom:CeCILL")
makedepends=('gimp' 'qt5-base' 'fftw' 'openexr' 'opencv' 'mesa' 'gtk2' 'git' 'hdf5')
source=("https://gmic.eu/files/source/gmic_$pkgver.tar.gz"
	"git://github.com/dtschump/gmic-community.git#commit=${_commit}"
	"gmic-qt-v$gmic_qt_ver.tar.gz::https://github.com/c-koi/gmic-qt/archive/v$gmic_qt_ver.tar.gz")
sha256sums=('d2f9cb362668b1905c62d57a6fb73c15974841961c58c250a568854c0e4c9e55'
            'SKIP'
            'cd4170adbe916802e8ced03b6e63dea5ba9bf26a1e29e01cebf07a4001d1a126')

prepare() {
  mv "${srcdir}/gmic-${pkgver}" "${srcdir}/gmic"
  cd "${srcdir}/gmic"

  ln -sf ../gmic-community/zart zart
  ln -sf ../gmic-qt-v$gmic_qt_ver gmic-qt

  sed -i "s|-mtune=generic|${CFLAGS}|" src/Makefile

  sed -i -e 's|/etc/bash_completion.d|/usr/share/bash-completion/completions|g' \
    src/Makefile
  sed -i -e 's|-Ofast|-O2 -fno-fast-math|g' \
    gmic-qt/gmic_qt.pro
}

build() {
  cd "${srcdir}/gmic"

#  unset CFLAGS
#  unset CXXFLAGS
#  export CPPFLAGS="-Wno-implicit-fallthrough -Wno-format-truncation -fno-fast-math"
#  export QMAKE_CXXFLAGS="-fno-fast-math"
#  export QMAKE_CPPFLAGS="-fno-fast-math"

  (cd zart && qmake -o Makefile zart.pro)

  make -C src all WGET=/bin/true -j1

  make -C zart all WGET=/bin/true
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=("opencv" "fftw" "libtiff" "libjpeg" "libpng" "openexr" "libx11" "hdf5" "curl")

  cd "${srcdir}/gmic"
  make -C src install DESTDIR="$pkgdir" USR="/usr"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf "${pkgdir}/usr/"{bin/zart,lib/gimp,share/zart,bin/gmic_krita_qt}
}

package_zart() {
  pkgdesc="A GUI for G'MIC real-time manipulations on the output of a webcam"
  depends=("opencv" "fftw" "qt5-base")

  cd "${srcdir}/gmic"
  install -Dm755 zart/zart "${pkgdir}/usr/bin/zart"
  install -Dm644 zart/Licence_CeCILL_V2-en.html "${pkgdir}/usr/share/licenses/${pkgname}/Licence_CeCILL_V2-en.html"
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=("opencv" "gimp" "fftw" "qt5-base" "curl")

  cd "${srcdir}/gmic"
  install -Dm755 gmic-qt/gmic_gimp_qt "${pkgdir}/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 gmic-qt/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_krita-plugin-gmic() {
  pkgdesc="Krita plugin for the G'MIC image processing framework"
  depends=('krita')

  cd gmic/gmic-qt
  install -Dm755 gmic_krita_qt -t "${pkgdir}"/usr/bin/
}
