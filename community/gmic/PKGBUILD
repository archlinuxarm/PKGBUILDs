# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -mtune=generic from makefile, replace with our CFLAGS

pkgbase=gmic
pkgname=("gmic" "gimp-plugin-gmic" "zart")
pkgver=1.6.0.2
pkgrel=1
arch=("i686" "x86_64")
url="http://gmic.sourceforge.net"
license=("custom:CeCILL")
makedepends=('gimp' 'qt4' 'fftw' 'openexr' 'opencv' 'mesa' 'gtk2')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_${pkgver}.tar.gz")
md5sums=('dda38aa3ba84c7f1f1ebe5137938a449')

prepare() {
  cd "${srcdir}/gmic-${pkgver}"
  sed -i -e 's|qmake zart.pro|qmake-qt4 zart.pro|g' \
    -e 's|/etc/bash_completion.d|/usr/share/bash-completion/completions|g' \
    src/Makefile
  sed -i "s|-mtune=generic|${CFLAGS}|" src/Makefile
}

build() {
  cd "${srcdir}/gmic-${pkgver}"
  (cd zart && qmake-qt4 -o Makefile zart.pro)
  make -C src all
  make -C zart all
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=("fftw" "libtiff" "libjpeg" "libpng" "openexr" "libx11")
  replaces=("greycstoration")

  cd "${srcdir}/gmic-${pkgver}"
  make -C src install DESTDIR="$pkgdir" USR="/usr"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf "${pkgdir}/usr/"{bin/zart,lib/gimp,share/zart}
}

package_zart() {
  pkgdesc="A GUI for G'MIC real-time manipulations on the output of a webcam"
  depends=("opencv" "fftw" "qt4")

  cd "${srcdir}/gmic-${pkgver}"
  install -Dm755 zart/zart "${pkgdir}/usr/bin/zart"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=("gimp" "fftw")
  replaces=("gimp-plugin-greycstoration" "gimp-plugin-gmic4gimp")

  cd "${srcdir}/gmic-${pkgver}"
  install -Dm755 src/gmic_gimp "${pkgdir}/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
