# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set LTO=off for gmic-qt

highmem=1

pkgbase=gmic
pkgname=(gmic gimp-plugin-gmic)
pkgver=3.1.5
pkgrel=1
arch=(x86_64)
url='https://gmic.eu/'
license=(custom:CeCILL)
makedepends=(cmake gimp qt5-base fftw openexr opencv eigen graphicsmagick qt5-tools)
source=(https://gmic.eu/files/source/gmic_$pkgver.tar.gz
        CMakeLists.txt FindCImg.cmake FindGMicStdlib.cmake)
sha256sums=('fa77e85b3a39638008515ac525f23f0ed7a45b63c463c0ba6292c87f5e88e30d'
            '089c4ad204a1ed43c9425e3f39e5a3a8bab3d6e8c4fb5a6dfc69e0fca15abd15'
            '5ad1b499cb0a9e3d9fff6a851346d6f356dfc592442183d6330726a29ee25384'
            '83e4635a6fd101511381bc865255f4de88834da51911ce3eb45ddac2558cabcc')

prepare() {
  mv CMakeLists.txt gmic-$pkgver
  mkdir -p gmic-$pkgver/cmake
  mv FindCImg.cmake FindGMicStdlib.cmake gmic-$pkgver/cmake
}

build() {
  cmake -B build -S gmic-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DYNAMIC_LINKING=ON \
    -DBUILD_LIB_STATIC=OFF \
    -DENABLE_OPENCV=ON
  cmake --build build

  export LDFLAGS="$LDFLAGS -L../build"
  cmake -B build-qt -S gmic-$pkgver/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_SYSTEM_GMIC=OFF \
    -DENABLE_DYNAMIC_LINKING=ON \
    -DGMIC_PATH="$srcdir"/gmic-$pkgver/src \
    -DGMIC_QT_HOST=none \
    -DENABLE_LTO=OFF
  cmake --build build-qt

  cmake -B build-gimp -S gmic-$pkgver/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_SYSTEM_GMIC=OFF \
    -DENABLE_DYNAMIC_LINKING=ON \
    -DGMIC_PATH="$srcdir"/gmic-$pkgver/src \
    -DGMIC_QT_HOST=gimp \
    -DENABLE_LTO=OFF
  cmake --build build-gimp
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=(graphicsmagick opencv fftw)
  optdepends=('qt5-base: for the Qt UI')
  conflicts=(cimg)

  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  DESTDIR="$pkgdir" cmake --install build-qt

  install -Dm644 gmic-$pkgver/resources/gmic_cluts.gmz -t "$pkgdir"/usr/share/gmic
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=(gimp gmic qt5-base)

  DESTDIR="$pkgdir" cmake --install build-gimp
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
