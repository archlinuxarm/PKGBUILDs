# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Lieven Moors <lievenmoors@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export OPTIMIZATIONS to override Makefiles without x86isms

pkgname=x42-plugins
pkgver=20190413
pkgrel=1
pkgdesc="Collection of LV2 plugins"
arch=('x86_64')
url="https://github.com/x42/x42-plugins"
license=('GPL')
depends=('ftgl' 'glib2' 'jack' 'liblo' 'libltc' 'pango' 'zita-convolver')
makedepends=('gendesk' 'gtk2' 'help2man' 'lv2' 'ttf-dejavu')
groups=('lv2-plugins' 'pro-audio')
source=("http://gareus.org/misc/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('c41eb90ca5d5a746d55f7c1327ecfe58131318be5496c07638b6a891c8060e23fd666d2c3c5227a2a67f9eef8e1713038c65d9a38c5479d92426b930485e979e')

prepare() {
  cd "${pkgname}-${pkgver}"
  declare -A comments=(
    ["dpl1"]="Look-ahead digital peak limiter intended but not limited to the final step of mastering or mixing"
    ["fat1"]="Auto-tuner based on Fons Adriaensen's zita-at1"
    ["fil4"]="4 band parametric equalizer with additional low+high shelf filters, Low and High-pass"
    ["mixtri"]="Matrix mixer and trigger processor"
    ["scope"]="Simple audio oscilloscope with variable time scale, triggering, cursors and numeric readout"
    ["spectr"]="Spectrum Analyzer"
    ["stepseq"]="Simple step sequencer for moddevices.com"
  )
  declare -A generic=(
    ["dpl1"]="Peak Limiter"
    ["fat1"]="Auto Tune"
    ["fil4"]="Parametric Equalizer"
    ["mixtri"]="Mixer'n'Trigger"
    ["scope"]="Oscilloscope"
    ["spectr"]="Spectrum Analyzer"
    ["stepseq"]="Step Sequencer"
  )
  declare -A meter_types=(
    [0]='EBU R128 Meter'
    [1]='K20/RMS Meter (Stereo)'
    [2]='K14/RMS Meter (Stereo)'
    [3]='K12/RMS Meter (Stereo)'
    [4]='BBC Meter (Stereo)'
    [5]='BBC M-6'
    [6]='DIN Meter (Stereo)'
    [7]='EBU Meter (Stereo)'
    [8]='Nordic Meter (Stereo)'
    [9]='VU Meter (Stereo)'
    [10]='True-Peak and RMS Meter (Stereo)'
    [11]='DR-14 - Crest Factor Loudness Range Meter'
    [12]='Stereo Phase-Correlation Meter'
    [13]='Goniometer'
    [14]='Phase/Frequency Wheel'
    [15]='1/3 Octave Spectrum Analyzer'
    [16]='Stereo/Frequency Scope'
    [17]='Signal Distribution Histogram'
    [18]='Bit Meter'
    [19]='Surround Level 8'
  )
  declare -A tuna_types=(
    [0]='Instrument Tuner'
    [1]='Instrument Tuner (Spectrum)'
  )

  # XDG desktop files
  for name in "${!comments[@]}"; do
    gendesk -n \
            --pkgname "x42-${name}" \
            --name "x42-${name}" \
            --pkgdesc "${comments[$name]}" \
            --genericname "${generic[$name]}" \
            --categories "AudioVideo;Audio"
  done
  for type in {0..19}; do
    gendesk -n \
            --pkgname "x42-meter-${type}" \
            --name "x42-meter ${meter_types[$type]}" \
            --pkgdesc "${meter_types[$type]}" \
            --exec "x42-meter $type" \
            --icon "x42-meter" \
            --genericname "Audio Meter" \
            --categories "AudioVideo;Audio"
  done
  for type in {0,1}; do
    gendesk -n \
            --pkgname "x42-tuna-${type}" \
            --name "x42-tuna ${tuna_types[$type]}" \
            --pkgdesc "${tuna_types[$type]}" \
            --exec "x42-tuna $type" \
            --icon "x42-tuna" \
            --genericname "Music Instrument Tuner" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  cd "${pkgname}-${pkgver}"
  # replace ttf-freefont with ttf-liberation
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
  export OPTIMIZATIONS="-ffast-math -fomit-frame-pointer -O3 -fno-finite-math-only"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir/" PREFIX=/usr
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
