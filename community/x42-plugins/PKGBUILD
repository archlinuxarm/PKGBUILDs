# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Lieven Moors <lievenmoors@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export OPTIMIZATIONS to override Makefiles without x86isms

pkgname=x42-plugins
pkgver=20180320
pkgrel=3
pkgdesc="Collection of LV2 plugins"
arch=('x86_64')
url="https://github.com/x42/x42-plugins"
license=('GPL')
depends=('jack' 'libglvnd' 'liblo' 'libltc' 'pango' 'zita-convolver')
makedepends=('gtk2' 'ftgl' 'ttf-freefont' 'lv2')
groups=('lv2-plugins' 'pro-audio')
source=("http://gareus.org/misc/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'zita-convolver4.patch')
sha512sums=('3e2f13179073b38e846e0855031e7f842b535ad13f64b309ca4da8c0738a77ab8b70e117edfa1a97263ff37d279a4d9a26950c1883507d8e710b8cd54061bb32'
            '15dd15c5552492e7cbaad08b7fd42a7778a03fa12838cc930c386cba30e22ce91d9335543b4b229d13514849c3ed221760176a1b95e0ce7d215247c11a05e628')

prepare() {
  cd "${pkgname}-${pkgver}/convoLV2"
  patch -Np1 -i ../../zita-convolver4.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  export OPTIMIZATIONS="-ffast-math -fomit-frame-pointer -O3 -fno-finite-math-only"
  FONTFILE=/usr/share/fonts/TTF/FreeSansBold.ttf make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir/" PREFIX=/usr
}

# vim:set ts=2 sw=2 et:
