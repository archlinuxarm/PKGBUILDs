# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Lieven Moors <lievenmoors@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export OPTIMIZATIONS to override Makefiles without x86isms

pkgname=x42-plugins
pkgver=20190105
pkgrel=2
pkgdesc="Collection of LV2 plugins"
arch=('x86_64')
url="https://github.com/x42/x42-plugins"
license=('GPL')
depends=('ftgl' 'jack' 'liblo' 'libltc' 'pango' 'zita-convolver')
makedepends=('gendesk' 'gtk2' 'help2man' 'lv2' 'ttf-dejavu')
groups=('lv2-plugins' 'pro-audio')
source=("http://gareus.org/misc/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('ad7b42c14427683531aa6db40c8fca7c15d9048e3afd30749d57dba929a43aa57363106f7770fda16ef97dc2e92edeaa8d150f55e9589e64c64b6c388fd44afe')

prepare() {
  cd "${pkgname}-${pkgver}"
  declare -A comments=(
    ["dpl1"]="Look-ahead digital peak limiter intended but not limited to the final step of mastering or mixing"
    ["fat1"]="Auto-tuner based on Fons Adriaensen's zita-at1"
    ["fil4"]="4 band parametric equalizer with additional low+high shelf filters, Low and High-pass"
    ["meter"]="Collection of audio-level meters"
    ["mixtri"]="Matrix mixer and trigger processor"
    ["scope"]="Simple audio oscilloscope with variable time scale, triggering, cursors and numeric readout"
    ["stepseq"]="Simple step sequencer for moddevices.com"
    ["tuna"]="A musical instrument tuner with strobe characteristics"
  )
  declare -A generic=(
    ["dpl1"]="Peak Limiter"
    ["fat1"]="Auto Tune"
    ["fil4"]="Parametric Equalizer"
    ["meter"]="Audio Meter Collection"
    ["mixtri"]="Mixer'n'Trigger"
    ["scope"]="Oscilloscope"
    ["stepseq"]="Step Sequencer"
    ["tuna"]="Music Instrument Tuner"
  )
  # XDG desktop files
  for name in "${!comments[@]}"; do
    gendesk -n \
            --pkgname "x42-${name}" \
            --name "x42-${name}" \
            --pkgdesc "${comments[$name]}" \
            --genericname "${generic[$name]}" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  cd "${pkgname}-${pkgver}"
  # replace ttf-freefont with ttf-liberation
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
  export OPTIMIZATIONS="-ffast-math -fomit-frame-pointer -O3 -fno-finite-math-only"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir/" PREFIX=/usr
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
