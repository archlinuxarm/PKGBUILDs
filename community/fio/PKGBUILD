# $Id$
# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed numactl dependency

pkgname=fio
pkgver=2.1.12
pkgrel=1
pkgdesc='Scriptable I/O tool for storage benchmarks and drive testing'
arch=(i686 x86_64)
url='http://freshmeat.net/projects/fio'
license=(GPL2)
depends=(bash libaio python2)
makedepends=(gtk2)
optdepends=(
  'gtk2: for gfio - fio GUI frontend' 
  'gnuplot: generating plots using fio_generate_plots'
)
source=(http://brick.kernel.dk/snaps/fio-$pkgver.tar.bz2)
sha256sums=('575e64b1269d81c43a4d9787ff8c40e3fe673f6027f7597f7151343d7fe2b94a')

prepare() {
  cd fio-$pkgver

  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i tools/plot/fio2gnuplot 
}

# TODO: enable rdma, need to move the libraries from AUR
build() {
  cd fio-$pkgver
  ./configure --enable-gfio --extra-cflags="$CFLAGS"
}

package() {
  cd fio-$pkgver
  make DESTDIR="$pkgdir" prefix=/usr mandir=/usr/share/man install

  # documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 HOWTO README REPORTING-BUGS SERVER-TODO "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # examples
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
  install -m644 examples/* "$pkgdir/usr/share/doc/$pkgname/examples"
}
