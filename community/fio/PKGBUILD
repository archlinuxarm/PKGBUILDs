# $Id$
# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed numactl dependency

pkgname=fio
pkgver=2.6
pkgrel=1
pkgdesc='Scriptable I/O tool for storage benchmarks and drive testing'
arch=(i686 x86_64)
url='https://github.com/axboe/fio'
license=(GPL2)
# TODO: enable rdma, need to move the libraries from AUR
depends=(bash libaio python2 glusterfs ceph)
makedepends=(gtk2)
optdepends=(
  'gtk2: for gfio - fio GUI frontend' 
  'gnuplot: generating plots using fio_generate_plots'
)
source=(https://github.com/axboe/fio/archive/fio-$pkgver.zip
        types.patch::https://github.com/axboe/fio/commit/d7bb6180f831091c468e5aa749b142efd5eddda4.patch)
sha256sums=('54aafbc715d6e977136795190a5b09d07761153a859cfb17f3f161f1f1ae5ed7'
            '7a28010acafdf8a3fd4b39ffd49ce935cc16fc89bfd4beb448ea9588cdb60031')

prepare() {
  cd fio-fio-$pkgver

  patch -p1 < ../types.patch
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i tools/plot/fio2gnuplot
}

build() {
  cd fio-fio-$pkgver
  ./configure --enable-gfio --extra-cflags="$CFLAGS"
}

package() {
  cd fio-fio-$pkgver
  make DESTDIR="$pkgdir" prefix=/usr mandir=/usr/share/man install

  # documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 HOWTO README REPORTING-BUGS SERVER-TODO "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # examples
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
  install -m644 examples/* "$pkgdir/usr/share/doc/$pkgname/examples"
}
