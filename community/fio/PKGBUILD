# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed numactl dependency

pkgname=fio
pkgver=3.14
pkgrel=1
pkgdesc='Scriptable I/O tool for storage benchmarks and drive testing'
arch=(x86_64)
url='https://github.com/axboe/fio'
license=(GPL2)
# TODO: enable rdma, need to move the libraries from AUR
depends=(bash libaio python2 glusterfs)
makedepends=(gtk2)
optdepends=(
  'gtk2: for gfio - fio GUI frontend' 
  'gnuplot: generating plots using fio_generate_plots'
)
source=(https://github.com/axboe/fio/archive/fio-$pkgver.zip
        fix_glusterfs.patch::https://github.com/axboe/fio/commit/ce4d13ca162df4127ec3b5911553802c53396705.patch)
sha256sums=('d8abdbdab457164243496ca813f6313a7423d384cc70be063811568443475343'
            'c2bcb3421f1fa838759dc5d9055ab9d4c099bb4c94f7ed321b70c401f95aad48')

prepare() {
  cd fio-fio-$pkgver

  # https://github.com/axboe/fio/issues/781
  patch -p1 < ../fix_glusterfs.patch

  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i tools/plot/fio2gnuplot
}

build() {
  cd fio-fio-$pkgver
  ./configure --disable-native --enable-gfio --extra-cflags="$CFLAGS"
  make
}

package() {
  cd fio-fio-$pkgver
  make DESTDIR="$pkgdir" prefix=/usr mandir=/usr/share/man install

  # documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 HOWTO README REPORTING-BUGS SERVER-TODO "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 MORAL-LICENSE "$pkgdir/usr/share/licenses/$pkgname/MORAL-LICENSE"

  # examples
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
  install -m644 examples/* "$pkgdir/usr/share/doc/$pkgname/examples"
}
