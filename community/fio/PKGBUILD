# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed numactl dependency

pkgname=fio
pkgver=3.16
pkgrel=1
pkgdesc='Scriptable I/O tool for storage benchmarks and drive testing'
arch=(x86_64)
url='https://github.com/axboe/fio'
license=(GPL2)
# TODO: enable rdma, need to move the libraries from AUR
depends=(bash libaio python2 glusterfs)
makedepends=(gtk2)
optdepends=(
  'gtk2: for gfio - fio GUI frontend' 
  'gnuplot: generating plots using fio_generate_plots'
)
source=(https://github.com/axboe/fio/archive/fio-$pkgver.zip
        fix_gfio.patch::https://github.com/axboe/fio/commit/5b215853ed4b438b5b2d4ac3e56d5f0d19e145d9.patch)
sha256sums=('89d7ad146da0fbd0bc173f06b024842ffeea121a647bb11a9ac4b724bb4115d6'
            'efb0ba43f62d4a07312d9c57bdfdcb7c598e6bd16b8814fcd164771a7fadc4f6')

prepare() {
  cd fio-fio-$pkgver
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i tools/plot/fio2gnuplot

  patch -p1 < ../fix_gfio.patch
}

build() {
  cd fio-fio-$pkgver
  ./configure --disable-native --enable-gfio --extra-cflags="$CFLAGS"
  make
}

package() {
  cd fio-fio-$pkgver
  make DESTDIR="$pkgdir" prefix=/usr mandir=/usr/share/man install

  # documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 HOWTO README REPORTING-BUGS SERVER-TODO "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 MORAL-LICENSE "$pkgdir/usr/share/licenses/$pkgname/MORAL-LICENSE"

  # examples
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
  install -m644 examples/* "$pkgdir/usr/share/doc/$pkgname/examples"
}
