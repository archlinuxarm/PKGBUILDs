# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# DELETE at next upstream update. Packaging glitch caused need for bump.

pkgname=usb_modeswitch
pkgver=2.1.0
pkgrel=1.1
_pkgdata=20140129
pkgdesc="Activating switchable USB devices on Linux."
arch=('i686' 'x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL')
depends=('libusb' 'tcl')
makedepends=('gcc' 'make')
backup=("etc/$pkgname.conf")
source=("http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-${pkgver}.tar.bz2"
	"http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-data-${_pkgdata}.tar.bz2"
	"usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/usb_modeswitch/device_reference.txt")
md5sums=('9eaf1749996f5f797323d474d1d390c5'
         'e1e7a4c679f8029a8da146477d102c60'
         '1ec5054e071d6af9ab5b862197222455')

prepare() {
  cd $srcdir/usb-modeswitch-$pkgver/
  sed -i 's|/usr/sbin/usb_mode|/usr/bin/usb_mode|g' usb_modeswitch.{conf,tcl,sh}
}

build() {
  cd $srcdir/usb-modeswitch-$pkgver/
  make clean
  make SBINDIR=/usr/bin
}

package() {
  cd $srcdir/usb-modeswitch-$pkgver/

  mkdir -p $pkgdir/usr/lib/udev
  mkdir -p $pkgdir/etc
  mkdir -p $pkgdir/usr/share/man/man1

  make DESTDIR=${pkgdir} UDEVDIR=${pkgdir}/usr/lib/udev SBINDIR=$pkgdir/usr/bin install

  cp $srcdir/usb-modeswitch-${pkgver}-device_reference.txt $pkgdir/etc/usb_modeswitch.setup

  cd $srcdir/usb-modeswitch-data-${_pkgdata}
  make DESTDIR=$pkgdir install RULESDIR=${pkgdir}/usr/lib/udev/rules.d
}
