# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - x86_64 import, no changes
#  - only build for AArch64; 32-bit is unsupported

buildarch=8
highmem=1

pkgname=mongodb
pkgver=3.4.3
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database'
arch=('x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('pcre' 'snappy' 'openssl' 'libsasl' 'boost-libs' 'yaml-cpp' 'lsb-release' 'wiredtiger')
makedepends=('scons' 'readline' 'ncurses' 'libpcap' 'boost')
checkdepends=('python2-pymongo' 'python2-yaml')
optdepends=('libpcap: needed for mongosniff'
            'mongodb-tools: mongoimport, mongodump, mongotop, etc')
backup=('etc/mongodb.conf')
install=mongodb.install
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.conf' 'mongodb.service' 'mongodb-3.2.10-boost-1.62.0.patch'
        'asio-openssl-1.1.0.patch'
        'openssl-1.1.0.patch')
sha512sums=('e2fb5fc2f02e9dbca6c30d2e2aaf1569180ca15198226cdad46f47f44905293afe77763fe2e5c9add0b5bcbd62b7ce7905fb183068c0738cdeeae8d39f848cef'
            '05dead727d3ea5fe8af1a3c3888693f6b3e2b8cb7f197a5d793352e10d2c524e96c9a5c55ad2e88c1114643a9612ec0b26a2574b48a5260a9b51ec8941461f1c'
            '177251404b2e818ae2b546fe8b13cb76e348c99e85c7bef22a04b0f07b600fd515a309ede50214f4198594388a6d2b31f46e945b9dae84aabb4dfa13b1123bb9'
            'd6f014d2778decde268b9e856d812bc61f7c45986aad751e44fdece39aa8a96505b77e0b917ea38880501497e01d4b051a6f3205c82af653425b5247cd813417'
            'aea8fecb17be07e4517822798810751114f61164dc3ab3e335a7b837876acb918d287caa8b9fe32d7b1d9e7cad2e677d9818a1532ae08b8b013840d2a68b25f5'
            '62ec95ac4eef42b6fefa1063336812e5e6a78f2d119e5f9a7206e063c5b5a859d160ce77bc57123150a55a51e8695eefea7e301259154bc009bfe027d40de314')

_scons_args=(
  --use-system-boost
  --use-system-pcre
  --use-system-snappy
  --use-system-yaml
  --use-system-zlib
  --use-system-wiredtiger
  --use-sasl-client
  --ssl
  --disable-warnings-as-errors
  # --use-system-asio     # https://jira.mongodb.org/browse/SERVER-21839
  # --use-system-v8       # Doesn't compile
  # --use-system-tcmalloc # Disabled as upstream suggests in https://jira.mongodb.org/browse/SERVER-17447?focusedCommentId=841890&page=com.atlassian.jira.plugin.system.issuetabpanels:comment-tabpanel#comment-841890
)

prepare() {
  cd mongodb-src-r${pkgver}
  patch -Np1 -i ../mongodb-3.2.10-boost-1.62.0.patch
  patch -Np1 -i ../openssl-1.1.0.patch

  sed -e 's|-std=c++11|-std=gnu++11|g' -i SConstruct  # tests use hex floats, not supported in plain C++

  cd src/third_party/asio-asio-1-11-0
  patch -Np1 -i "${srcdir}/asio-openssl-1.1.0.patch"
}

build() {
  cd mongodb-src-r${pkgver}
  export SCONSFLAGS="$MAKEFLAGS"

  scons core tools "${_scons_args[@]}"
}

check() {
  cd mongodb-src-r${pkgver}
  export SCONSFLAGS="$MAKEFLAGS"

  # Setting LANG to workaround the following test error:
  # std::exception: locale::facet::_S_create_c_locale name not valid
  scons unittests "${_scons_args[@]}"
  sed -i -e '/oplog_buffer_collection_test/d' build/unittests.txt
  LANG=en_US.UTF-8 python2 buildscripts/resmoke.py --suites=unittests

  scons dbtest "${_scons_args[@]}"
  python2 buildscripts/resmoke.py --suites=dbtest

  #scons integration_tests "${_scons_args[@]}"
  #python2 buildscripts/resmoke.py --suites=integration_tests_replset,integration_tests_standalone --dbpathPrefix="$srcdir"
}

package() {
  cd mongodb-src-r${pkgver}

  scons install --prefix="$pkgdir/usr" --nostrip "${_scons_args[@]}"

  install -Dm644 "$srcdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
  install -Dm644 "$srcdir/mongodb.service" "$pkgdir/usr/lib/systemd/system/mongodb.service"
  install -dm700 "$pkgdir/var/lib/mongodb"
  install -dm755 "$pkgdir/var/log/mongodb"
}
