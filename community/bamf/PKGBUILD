# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use --enable-compile-warnings=no

pkgname=bamf
pkgver=0.5.0
pkgrel=3
pkgdesc='Application matching framework'
arch=('i686' 'x86_64')
url='https://launchpad.net/bamf'
license=('GPL')
depends=('libgtop' 'libwnck3')
makedepends=('gobject-introspection' 'python2-lxml' 'vala')
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        'fix-legacy-window-test.patch')
sha256sums=('93b1acced96d32b36270cf18feb2761014ce2226814303c632448f717a8f9113'
            '0d052f6d654ae86d14d7f73c428237b9410d11986ee1112d988a7ffe1df0988e')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np0 -i ../fix-legacy-window-test.patch
}

build() {
  cd ${pkgname}-${pkgver}

  export PYTHON='/usr/bin/python2'
  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' \
              --libexecdir="/usr/lib/${pkgname}" --disable-{static,webapps} --enable-compile-warnings=no
  make
}

package() {
  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
