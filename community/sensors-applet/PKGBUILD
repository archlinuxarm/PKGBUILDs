# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

pkgname=sensors-applet
pkgver=3.0.0
_gitrev=1e38ad607b6a6a0f370dd67f5e94c8a11e03eae5
pkgrel=7
pkgdesc="Applet for GNOME Panel to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings"
arch=('i686' 'x86_64')
url="http://sensors-applet.sourceforge.net/"
license=('GPL')
depends=('gnome-panel' 'libatasmart' 'lm_sensors')
makedepends=('gnome-common' 'intltool' 'yelp-tools')
optdepends=('hddtemp: get HDD temperatures'
            'udisks: get HDD temperatures')
source=(https://sourceforge.net/code-snapshots/git/s/se/$pkgname/code.git/$pkgname-code-$_gitrev.zip)
sha256sums=('7515f7f8a2da15763d2f58add5a91b1c5ac72f622d14ad47d0a9c11183b08edd')

prepare() {
  cd $pkgname-code-$_gitrev
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-code-$_gitrev
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-code-$_gitrev
  make DESTDIR="$pkgdir" install
}
