# Maintainer: Balló György <ballogyor+arch at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

pkgname=sensors-applet
pkgver=3.0.0+13+g0728426
pkgrel=6
pkgdesc='Applet for GNOME Panel to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings'
arch=('x86_64')
url='http://sensors-applet.sourceforge.net/'
license=('GPL')
depends=('gnome-panel' 'libatasmart' 'lm_sensors')
makedepends=('git' 'gnome-common' 'intltool' 'yelp-tools')
optdepends=('hddtemp: get HDD temperatures')
_commit=07284262768e3a7066da09845370d78526be6bc1  # master
source=("$pkgname::git+https://git.code.sf.net/p/sensors-applet/code#commit=$_commit"
        'fix-crash-destroying-active-sensor.patch'
        'port-to-libgnome-panel.patch')
sha256sums=('SKIP'
            '9c4cc2accf326c44cb625f5db0b82a782351cdf8829f1a4067a30f287b6b7156'
            'cb0e35e0d6e6568f14836da7e5b78154bd4d9a4127f0f6dd476e4f7c9d62339d')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../port-to-libgnome-panel.patch
  patch -Np1 -i ../fix-crash-destroying-active-sensor.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
