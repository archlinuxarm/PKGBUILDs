# $Id: PKGBUILD 87427 2013-03-31 19:18:34Z stephane $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - need to build with -O1 to get around gcc ICE bug

pkgname=pdns
pkgver=3.2
pkgrel=5
pkgdesc='Modern, advanced and high performance authoritative-only nameserver'
arch=('x86_64' 'i686')
url='http://www.powerdns.com/'
license=('GPL')
depends=('boost-libs' 'gcc-libs' 'postgresql-libs' 'libmariadbclient' 'sqlite' 'libldap' 'lua51')
makedepends=('boost')
provides=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
replaces=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
conflicts=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
install="$pkgname.install"
options=('!makeflags' '!libtool')
backup=('etc/powerdns/pdns.conf')
source=("http://downloads.powerdns.com/releases/$pkgname-$pkgver.tar.gz"
        'pdns.service'
        'pdns.rc'
        'pdns.conf')
sha256sums=('d1895aba065446dc68e5d7cc792d5303626c71759f61a455531ed65d59c06572'
            '2a60c4407bcfe19026ae77d3e3b7b477d5a0c6323c0bc78892e9d907003b442a'
            '0457ffa2baa40518a03393e4f76964d9452c16aebbcb925582814f88367d62c2'
            '91c8889f41fe9d6bc9478c3ad53a10f6725e36e10f05e8e9257f9d35c9e2bab6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  CFLAGS=`echo $CFLAGS | sed -e 's/-O2/-O1/'` && CXXFLAGS="$CFLAGS"
  touch AUTHORS NEWS
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib/powerdns \
    --mandir=/usr/share/man \
    --with-modules="" \
    --with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo" \
    --disable-recursor \
    --disable-static
  make  
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/etc/rc.d"
  install -m644 "$srcdir/pdns.conf" "$pkgdir/etc/powerdns/"
  install -m755 "$srcdir/pdns.rc" "$pkgdir/etc/rc.d/pdns"
  install -Dm644 "$srcdir/pdns.service" \
    "$pkgdir/usr/lib/systemd/system/pdns.service"
  rm "$pkgdir/etc/powerdns/pdns.conf-dist"
}

# vim:set ts=2 sw=2 et:
