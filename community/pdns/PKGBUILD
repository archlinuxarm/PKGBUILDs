# $Id: PKGBUILD 87427 2013-03-31 19:18:34Z stephane $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - need to build with -O1 to get around gcc ICE bug

pkgname=pdns
pkgver=3.3
pkgrel=3
pkgdesc='Modern, advanced and high performance authoritative-only nameserver'
arch=('x86_64' 'i686')
url='http://www.powerdns.com/'
license=('GPL')
depends=('boost-libs' 'gcc-libs' 'postgresql-libs' 'libmariadbclient' 'sqlite' 'libldap' 'lua51')
makedepends=('boost')
provides=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
replaces=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
conflicts=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
options=('!makeflags')
backup=('etc/powerdns/pdns.conf')
source=("http://downloads.powerdns.com/releases/$pkgname-$pkgver.tar.gz"
        'pdns.service'
        'pdns.conf')
sha256sums=('93417bb418ccd1eaf85fc98aa9f687ed175de388fa2624a1978b7cd40dc2b0b6'
            'b2358a5951ac944feee15ac03d42ffe9c2a72fa6f57746f77ef8997d54a82040'
            'a1afcccb0d2fb6a6f7a5960de95f8d38ce1ac75ac5e3dcecf09e49f0de10166e')

prepare() {
  CFLAGS=`echo $CFLAGS | sed -e 's/-O2/-O1/'` && CXXFLAGS="$CFLAGS"
  touch "$pkgname-$pkgver/AUTHORS" "$pkgname-$pkgver/NEWS"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib/powerdns \
    --mandir=/usr/share/man \
    --with-modules='' \
    --with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo" \
    --disable-static \
    --sbindir=/usr/bin \
    --bindir=/usr/bin
  make  
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
  install -m644 "$srcdir/pdns.conf" "$pkgdir/etc/powerdns/"
  install -Dm644 "$srcdir/pdns.service" \
    "$pkgdir/usr/lib/systemd/system/pdns.service"
  rm -f "$pkgdir/etc/powerdns/pdns.conf-dist"
}

# vim:set ts=2 sw=2 et:
