# $Id: PKGBUILD 79756 2012-11-12 00:04:11Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed linking to boost_graph_parallel and dep on openmpi, there's no need for that

pkgname=pdns
pkgver=3.1
pkgrel=2
pkgdesc="Modern, advanced and high performance authoritative-only nameserver"
arch=('x86_64' 'i686')
url='http://www.powerdns.com/'
license=('GPL')
depends=('gcc-libs' 'zlib' 'postgresql-libs' 'libmysqlclient' 'sqlite' 'libldap' 'lua' 'boost-libs')
makedepends=('boost')
provides=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
replaces=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
conflicts=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
install=pdns.install
options=('!makeflags' '!libtool')
backup=('etc/powerdns/pdns.conf')
source=("http://downloads.powerdns.com/releases/$pkgname-$pkgver.tar.gz"
        'pdns.service'
        'pdns.rc'
        'pdns.conf')
sha256sums=('1400f7bd659207c0b1f4b8296092e559a7b7bf6a2434951970217d9af06922a1'
            '2a60c4407bcfe19026ae77d3e3b7b477d5a0c6323c0bc78892e9d907003b442a'
            '0457ffa2baa40518a03393e4f76964d9452c16aebbcb925582814f88367d62c2'
            '91c8889f41fe9d6bc9478c3ad53a10f6725e36e10f05e8e9257f9d35c9e2bab6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  touch AUTHORS NEWS
  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
  sed -i 's/BOOST/#BOOST/g' configure
  ./configure --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib/powerdns \
    --mandir=/usr/share/man \
    --with-modules="" \
    --with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo" \
    --disable-recursor \
    --disable-static
  make \
    CXXFLAGS="-lboost_serialization -lboost_program_options $CXXFLAGS"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/etc/rc.d"
  rm "$pkgdir/etc/powerdns/pdns.conf-dist"
  install -m644 "$srcdir/pdns.conf" "$pkgdir/etc/powerdns/"
  install -m755 "$srcdir/pdns.rc" "$pkgdir/etc/rc.d/pdns"
  install -Dm644 "$srcdir/pdns.service" \
    "$pkgdir/usr/lib/systemd/system/pdns.service"
}

# vim:set ts=2 sw=2 et:
