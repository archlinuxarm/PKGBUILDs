# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic
#  - remove makedepend on pandoc

pkgname=powerdns-recursor
pkgver=4.1.7
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'boost-libs' 'libsodium' 'libsystemd' 'lua' 'openssl' 'protobuf'
         'libboost_context.so')
makedepends=('boost' 'inetutils' 'ragel' 'systemd')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.bz2{,.asc})
sha512sums=('eb119b0da034624a95aadd4800e376b80f69b7151e773437c0f644bdc5a38a377c5547bcd57606367e148eaddf0b7de8f5f5ff3ab582b50ceb190f2fada5980a'
            'SKIP')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7'  # Pieter Lexis <pieter.lexis@powerdns.com>
              'FBAE0323821C7706A5CA151BDCF513FA7EED19F3'  # Peter van Dijk <peter.van.dijk@powerdns.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF') # Winkels, Erik <erik.winkels@open-xchange.com>

build() {
  cd pdns-recursor-${pkgver}
  [[ $CARCH == arm || $CARCH == armv6h ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/powerdns \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-systemd \
    --enable-protobuf \
    --enable-libsodium
  make
}

package() {
  cd pdns-recursor-${pkgver}
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/powerdns/recursor.conf"{-dist,}
}

# vim: ts=2 sw=2 et:
