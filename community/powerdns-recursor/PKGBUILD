# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic
#  - remove makedepend on pandoc

pkgname=powerdns-recursor
pkgver=4.2.2
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'boost-libs' 'libsodium' 'systemd-libs' 'lua' 'openssl' 'protobuf'
         'libboost_context.so')
makedepends=('boost' 'inetutils' 'ragel' 'systemd')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.bz2{,.asc}
        ${pkgname}-gcc10.patch::https://github.com/PowerDNS/pdns/commit/ffb885e937c27bb1c62dee8f18c58.patch)
sha512sums=('c34708603a46af7dde7ec0ebd352fa001826ac61ecdf1f12c7e297b490886870fe85a94ee846d48ace1de51952da19fa0102ed43e5ec27ee34978cb42414abc0'
            'SKIP'
            '3685c8e08df2073351e3cd722ea1d7d19236c148d3a88368884ae5eb0a4fcd084eb2b62082e7f326e0770cd56c184a0f14fb026c02c0111a8386b6a0a46db3e5')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7'  # Pieter Lexis <pieter.lexis@powerdns.com>
              'FBAE0323821C7706A5CA151BDCF513FA7EED19F3'  # Peter van Dijk <peter.van.dijk@powerdns.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF') # Winkels, Erik <erik.winkels@open-xchange.com>

prepare() {
  cd pdns-recursor-${pkgver}
  patch -Np1 < ../${pkgname}-gcc10.patch
}

build() {
  cd pdns-recursor-${pkgver}
  [[ $CARCH == arm || $CARCH == armv6h ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/powerdns \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-systemd \
    --enable-protobuf \
    --enable-libsodium
  make
}

package() {
  cd pdns-recursor-${pkgver}
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/powerdns/recursor.conf"{-dist,}
}

# vim: ts=2 sw=2 et:
