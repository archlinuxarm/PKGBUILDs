# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=powerdns-recursor
pkgver=4.0.4
pkgrel=5
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gcc-libs' 'boost-libs' 'libsystemd' 'lua' 'openssl' 'protobuf'
         'libboost_context.so')
makedepends=('boost' 'inetutils' 'systemd')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.bz2{,.asc})
sha512sums=('9473dfe9abc509b2bb953139dd7892de2027ee1508902fa0c2cd30dd9a88878fcf44370b8372d573cbab12de32bb8c604005d3b39ea34db2ef86786e689d36ab'
            'SKIP')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7') # Pieter Lexis <pieter.lexis@powerdns.com>

build() {
  cd pdns-recursor-${pkgver}
  [[ $CARCH == arm || $CARCH == armv6h ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/powerdns \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-systemd
  make
}

package() {
  cd pdns-recursor-${pkgver}
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/powerdns/recursor.conf"{-dist,}
}

# vim: ts=2 sw=2 et:
