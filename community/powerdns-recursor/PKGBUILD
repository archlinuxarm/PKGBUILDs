# $Id: PKGBUILD 100035 2013-10-31 11:20:48Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>

# Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix for aeskey.c on ARM (http://wiki.powerdns.com/trac/changeset/3162/)

pkgname=powerdns-recursor
pkgver=3.5.3
pkgrel=2.1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
license=('GPL')
arch=('x86_64' 'i686')
depends=('gcc-libs' 'lua51')
makedepends=('boost')
replaces=('pdns-recursor')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=("http://downloads.powerdns.com/releases/pdns-recursor-$pkgver.tar.bz2"
        'pdns-recursor.service'
        'arm.patch')
sha256sums=('192c0b47a1cfcdccaa88d70fd33766a4c381f4223f966416f15e169df5d58eaa'
            'd4f545e85dffa86bf23c89ff0f352c74ec3524dfc53871f129656299222e4fba'
            'ff51af1648d6049f58887ca4f84fa41955ab5df8156d8a00b4b3b148e2bd789f')

prepare() {
  cd "pdns-recursor-$pkgver"

  patch -p5 -i "$srcdir/arm.patch"
}

build() {
  make -C "pdns-recursor-$pkgver" -j1 LUA=1 \
    LUA_CPPFLAGS_CONFIG="$(pkg-config --cflags lua5.1)" \
    LUA_LIBS_CONFIG="$(pkg-config --libs lua5.1)"
}

package() {
  cd "pdns-recursor-$pkgver"

  make SBINDIR="/usr/bin" DESTDIR="$pkgdir" install
  mv "$pkgdir/etc/powerdns/recursor.conf-dist" \
    "$pkgdir/etc/powerdns/recursor.conf"
  rm -r "$pkgdir/etc/init.d"
  install -Dm644 "$srcdir/pdns-recursor.service" \
    "$pkgdir/usr/lib/systemd/system/pdns-recursor.service"
}

# vim:set ts=2 sw=2 et:
