# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic
#  - remove makedepend on pandoc

pkgname=powerdns-recursor
pkgver=4.3.1
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'boost-libs' 'libsodium' 'systemd-libs' 'luajit' 'openssl' 'protobuf'
         'libboost_context.so' 'fstrm' 'net-snmp' 'libprotobuf.so')
makedepends=('boost' 'inetutils' 'ragel' 'systemd')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.bz2{,.asc}
        sysusers.conf
        ${pkgname}-gcc10.patch::https://github.com/PowerDNS/pdns/commit/ffb885e937c27bb1c62dee8f18c58.patch)
sha512sums=('49ae4957ef202f94ec87e6449dfa9c147689b5576ca1002e0155319099d698300c113e221571f6d41cb1cdc1649619621c2ccf8105620c25452ac30bd83e46b8'
            'SKIP'
            '63b3f0664d38fbbcb6b1c914cee050a27eca0a15550962973743580dd5d3ace2e68272ca2d856d53cc74001fe9246d61207ebbc74524c096736777ed08c88e72'
            '3685c8e08df2073351e3cd722ea1d7d19236c148d3a88368884ae5eb0a4fcd084eb2b62082e7f326e0770cd56c184a0f14fb026c02c0111a8386b6a0a46db3e5')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7'  # Pieter Lexis <pieter.lexis@powerdns.com>
              'FBAE0323821C7706A5CA151BDCF513FA7EED19F3'  # Peter van Dijk <peter.van.dijk@powerdns.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF') # Winkels, Erik <erik.winkels@open-xchange.com>

prepare() {
  cd pdns-recursor-${pkgver}
  patch -Np1 < "${srcdir}/${pkgname}-gcc10.patch"
  autoreconf -i
}

build() {
  cd pdns-recursor-${pkgver}
  [[ $CARCH == arm || $CARCH == armv6h ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/powerdns \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-systemd \
    --with-protobuf \
    --with-libsodium \
    --with-lua=luajit \
    --with-service-user=pdns-recursor \
    --with-service-group=pdns-recursor
  make
}

package() {
  cd pdns-recursor-${pkgver}
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/powerdns/recursor.conf"{-dist,}
  install -Dm 644 "${srcdir}/sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/powerdns-recursor.conf"
}

# vim: ts=2 sw=2 et:
