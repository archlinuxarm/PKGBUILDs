# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=freemind
pkgver=0.8.1
pkgrel=2
pkgdesc="A Java mindmapping tool"
arch=('i686' 'x86_64')
url="http://freemind.sourceforge.net"
license=('GPL')
makedepends=('unzip')
depends=('java-runtime')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-bin-max-${pkgver//./_}.zip
        freemind.png freemind.desktop freemind.run)
md5sums=('e6078a7116e3dca610817c3c60db4cbf' '82a474f26f976974b95da2e25b7480a0'
         '07241361af6cfb965deeab890d17f4a7' '4070fc9f286ad7aaa4eb394203152b33')

build() {
  cd ${srcdir}
  for file in $( find lib plugins -type f ) ; do
    install -Dm644 ${file} ${pkgdir}/usr/share/freemind/${file}
  done

  install -Dm755 ${srcdir}/freemind.run ${pkgdir}/usr/bin/freemind
  install -Dm755 ${srcdir}/freemind.sh ${pkgdir}/usr/share/freemind/freemind.sh
  install -Dm644 ${srcdir}/patterns.xml ${pkgdir}/etc/freemind/patterns.xml
  install -Dm644 ${srcdir}/user.properties ${pkgdir}/usr/share/freemind/user.properties

  install -d ${pkgdir}/usr/share/freemind/accessories
  install -Dm644 ${srcdir}/accessories/* ${pkgdir}/usr/share/freemind/accessories/
  install -Dm644 ${srcdir}/doc/freemind.mm ${pkgdir}/usr/share/freemind/doc/freemind.mm

  install -Dm644 ${srcdir}/freemind.desktop ${pkgdir}/usr/share/applications/freemind.desktop
  install -Dm644 ${srcdir}/freemind.png ${pkgdir}/usr/share/pixmaps/freemind.png
}

