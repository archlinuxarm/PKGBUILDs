# $Id: PKGBUILD 1827 2009-09-05 00:44:14Z cprimier $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

pkgname=eclipse-subclipse
pkgver=1.6.5
pkgrel=1
_urlver=46495
pkgdesc="Subversion integration for the Eclipse platform"
arch=('any')
url="http://subclipse.tigris.org/"
license=('EPL')
depends=('eclipse' 'subversion')
source=(http://subclipse.tigris.org/files/documents/906/${_urlver}/site-${pkgver}.zip)
md5sums=('afedc55073d13ba9ba64bfc718cfc1fd')


build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f -name *.jar | while read feature ; do
    install -dm755 ${_dest}/${feature/.jar}
    cd ${_dest}/${feature/.jar}
    jar xf ${srcdir}/${feature} || return 1
  done

  # Plugins
  find plugins -type f -exec install -Dm644 {} ${_dest}/{} \;
}

# vim:set ts=2 sw=2 et:
