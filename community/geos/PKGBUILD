# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - --disable-inline to fix ARM FTBFS

pkgname=geos
pkgver=3.8.0
pkgrel=1
pkgdesc="C++ port of the Java Topology Suite"
arch=('x86_64')
url="https://trac.osgeo.org/geos/"
license=('LGPL')
depends=('gcc-libs' 'bash')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://download.osgeo.org/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('99114c3dc95df31757f44d2afde73e61b9f742f0b683fd1894cbbee05dda62d5')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --disable-inline
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
