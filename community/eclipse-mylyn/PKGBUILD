# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>

pkgname=eclipse-mylyn
pkgver=3.2.0
pkgrel=1
pkgdesc="A task-focused interface for Eclipse"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/mylyn/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip')
optdepends=('bugzilla: ticketing support')
source=(http://download.eclipse.org/tools/mylyn/update/mylyn-${pkgver}-e3.4.zip)
md5sums=('d6002fd6c9f06ba1c3983cae17324708')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    install -dm755 ${_dest}/${_feature%*.jar}
    cd ${_dest}/${_feature/.jar}
    jar xf ${srcdir}/${_feature} || return 1
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

# vim:set ts=2 sw=2 et:
