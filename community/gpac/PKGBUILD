# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use PIC with armv7 and AArch64

pkgname=gpac
pkgver=0.7.1
pkgrel=2
epoch=1
pkgdesc='A multimedia framework based on the MPEG-4 Systems standard'
arch=('i686' 'x86_64')
url='https://gpac.wp.imt.fr/'
license=('LGPL')
depends=('ffmpeg' 'glu' 'libxv')
makedepends=('git' 'jack' 'a52dec' 'freetype2' 'faad2' 'libmad' 'mesa' 'sdl2')
optdepends=('jack: for jack support'
            'a52dec: for A52 support'
            'faad2: for AAC support'
            'libmad: for mp3 support'
            'sdl2: for sdl support')
provides=('libgpac.so')
options=('staticlibs' '!makeflags')
source=("gpac-${pkgver}.tar.gz::https://github.com/gpac/gpac/archive/v${pkgver}.tar.gz" gpac-openssl-1.1.patch)
sha256sums=('c7a18b9eea1264fee392e7222d16b180e0acdd6bb173ff6b8baadbf50b3b1d7f'
            '3a8b7b51192424df5aee3bf11a0207c9374aabcfba4db90522af2e3bbc37bbbe')

prepare() {
  cd gpac-${pkgver}
  # fix build with openssl 1.1 (rpmfusion)
  patch -p1 -i ../gpac-openssl-1.1.patch
}

build() {
  cd gpac-${pkgver}

  [[ $CARCH == 'armv7h' || $CARCH == 'aarch64' ]] && CONFIG='--enable-pic'

  ./configure \
    --prefix='/usr' \
    --mandir='/usr/share/man' \
    --X11-path='/usr' \
    --use-js='no' $CONFIG
  make
}

package() {
  cd gpac-${pkgver}

  make DESTDIR="${pkgdir}" install install-lib
}

# vim: ts=2 sw=2 et:
