# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use PIC with armv7 and AArch64

pkgname=gpac
pkgver=0.5.2
pkgrel=4
epoch=1
pkgdesc="A multimedia framework based on the MPEG-4 Systems standard"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net"
license=('LGPL')
depends=('ffmpeg' 'glu' 'libxv')
makedepends=('git' 'jack' 'a52dec' 'freetype2' 'faad2' 'libmad' 'mesa' 'sdl2')
optdepends=('jack: for jack support'
	    'a52dec: for A52 support'
            'faad2: for AAC support'
	    'libmad: for mp3 support'
	    'sdl2: for sdl support')
options=('staticlibs' '!makeflags')
source=(git://github.com/gpac/gpac.git#tag=v$pkgver)
sha1sums=('SKIP')

build() {
  cd ${pkgname}
  [[ $CARCH == 'armv7h' || $CARCH == 'aarch64' ]] && CONFIG='--enable-pic'
  ./configure --prefix=/usr --mandir=/usr/share/man --X11-path=/usr --use-js=no $CONFIG
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install install-lib
}
