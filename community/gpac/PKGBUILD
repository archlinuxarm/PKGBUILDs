# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use PIC with armv7

pkgname=gpac
pkgver=5324
pkgrel=1
pkgdesc="A multimedia framework based on the MPEG-4 Systems standard"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net"
license=('LGPL')
depends=('ffmpeg' 'libjpeg' 'libpng' 'glu' 'libxv')
makedepends=('jack' 'a52dec' 'freetype2' 'faad2' 'libmad' 'mesa')
optdepends=('jack: for jack support' 'a52dec: for A52 support'
            'faad2: for AAC support' 'libmad: for mp3 support')
options=('staticlibs' '!makeflags')
source=(ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig} gpac-ffmpeg.patch)
sha1sums=('cd00b88aa6da97708532fc8eb7ab65f6a0d0c1e0'
          'SKIP'
          'e758b8cdb0b4bca37134241a1b8ccc33d4deb54a')
validpgpkeys=('5357F3B111688D88C1D88119FCF2CB179205AC90')

# source PKGBUILD && mksource
mksource() {
  [[ -x /usr/bin/svn ]] || (echo "svn not found. Install subversion." && return 1)
  _svnver=${pkgver}
  _svntrunk="svn://svn.code.sf.net/p/gpac/code/trunk/gpac"
  _svnmod="$pkgname-${pkgver}"
  mkdir ${pkgname}-${pkgver}
  pushd ${pkgname}-${pkgver}
  svn co $_svntrunk --config-dir ./ -r $_svnver $_svnmod
  revision="$(svnversion ${pkgname}-${pkgver})"
  echo "#define GPAC_SVN_REVISION     \"$revision\"" > ${pkgname}-${pkgver}/include/gpac/revision.h
  find . -depth -type d -name .svn -exec rm -rf {} \;
  tar -cJf ../${pkgname}-${pkgver}.tar.xz ${pkgname}-${pkgver}/*
  popd
  rm -r ${pkgname}-${pkgver}
  gpg --detach-sign --use-agent -u ${GPGKEY} ${pkgname}-${pkgver}.tar.xz
}

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p3 -i "${srcdir}/gpac-ffmpeg.patch"
}

build() {
  cd ${pkgname}-${pkgver}

  [[ $CARCH == 'armv7h' ]] && CONFIG='--enable-pic'
  ./configure --prefix=/usr --mandir=/usr/share/man --X11-path=/usr --use-js=no $CONFIG
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-lib
}
