# $Id: PKGBUILD 91302 2013-05-20 23:13:17Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gilles Gagniard <gilles@gagniard.org>
# Contributor: JD Steffen <jd at steffennet dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - get rid of a bad gcc flag in Makefile

pkgname=simutrans
pkgver=112.3
_pkgver=112-3
pkgrel=1
pkgdesc="An open source transportation simulation game"
arch=('i686' 'x86_64')
url="http://simutrans.com/"
license=('PerlArtistic')
depends=('gcc-libs' 'zlib' 'sdl_mixer' 'bzip2' 'simutrans-pak64')
makedepends=('imagemagick')
optdepends=('timidity++: play MIDI music')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-src-$_pkgver.zip
        http://downloads.sourceforge.net/$pkgname/simulinux-$_pkgver.zip
        settings-folder.patch
        path-for-game-data.patch
        config.patch
        simutrans.desktop)
md5sums=('4963f0ad0da5f622f67b7c1fb888a264'
         '8e09f450173110fec097497c7198e50c'
         'c87d9a9910bc371df5d50f7f1ec298bb'
         '4648680290b44775b9c47d3758d3bd6c'
         'd11bc8ee33a34e33341f6ccd90a44dba'
         'f41f7a08ad517ef2b60412859eb49963')

build() {
  cd "$srcdir"

  cp config.template config.default
  patch -Np0 -i "$srcdir/settings-folder.patch"
  patch -Np1 -i "$srcdir/path-for-game-data.patch"
  patch -Np0 -i "$srcdir/config.patch"
  convert simutrans.ico -alpha on simutrans.png
  sed -i 's/-minline-all-stringops/-O3/' Makefile

  make
}

package() {
  cd "$srcdir"

  #binary
  install -Dm755 build/default/sim "$pkgdir/usr/bin/simutrans"

  #data
  mkdir -p "$pkgdir/usr/share/games/$pkgname"
  cp -r "$pkgname"/{config,font,music,script,skin,text} "$pkgdir/usr/share/games/$pkgname"

  #desktop file and icon
  install -Dm644 simutrans.png "$pkgdir/usr/share/pixmaps/simutrans.png"
  install -Dm644 simutrans.desktop "$pkgdir/usr/share/applications/simutrans.desktop"
}
