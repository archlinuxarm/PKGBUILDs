# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - depend on llvm35, make -j1

buildarch=4

# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

# use the following command to print out packages that need to get rebuilt
# grep -r ghc /var/abs/ | awk -F '/' '{ print $5; }' | sort -u

pkgname=ghc
pkgver=7.10.3
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp' 'gcc' 'libffi' 'llvm35')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl')
provides=('haskell-array=0.5.1.0'
          'haskell-base=4.8.2.0'
          'haskell-binary=0.7.5.0'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-bytestring=0.10.6.0'
          'haskell-cabal=1.22.5.0'
          'haskell-containers=0.5.6.2'
          'haskell-deepseq=1.4.1.1'
          'haskell-directory=1.2.2.0'
          'haskell-filepath=1.4.0.0'
          'haskell-ghc-prim=0.4.0.0'
          'haskell-haskeline=0.7.2.1'
          'haskell-hoopl=3.10.0.2'
          'haskell-hpc=0.6.0.2'
          'haskell-integer-gmp=1.0.0.0'
          'haskell-pretty=1.1.2.0'
          'haskell-process=1.2.3.0'
          'haskell-template-haskell=2.10.0.0'
          'haskell-terminfo=0.4.0.1'
          'haskell-time=1.5.0.1'
          'haskell-transformers=0.4.2.0'
          'haskell-unix=2.7.1.0'
          'haskell-xhtml=3000.2.1')
replaces=('haskell-array'
          'haskell-base'
          'haskell-binary'
          'haskell-bin-package-db'
          'haskell-bytestring'
          'haskell-cabal'
          'haskell-containers'
          'haskell-deepseq'
          'haskell-directory'
          'haskell-filepath'
          'haskell-ghc-prim'
          'haskell-haskeline'
          'haskell-hoopl'
          'haskell-hpc'
          'haskell-integer-gmp'
          'haskell-pretty'
          'haskell-process'
          'haskell-template-haskell'
          'haskell-terminfo'
          'haskell-time'
          'haskell-transformers'
          'haskell-unix'
          'haskell-xhtml')
install='ghc.install'
options=('staticlibs')
source=("https://downloads.haskell.org/~ghc/$pkgver/$pkgname-$pkgver-src.tar.xz")
noextract=("$pkgname-$pkgver-src.tar.xz")
md5sums=('00104e629bfda9f956c23752b5f38613')

prepare() {
  # Need to extract this tarball with a UTF-8 locale instead of a chroot's "C"
  # locale; otherwise we get:
  #   bsdtar: Pathname can't be converted from UTF-8 to current locale.
  LANG=en_US.UTF-8 bsdtar xf $pkgname-$pkgver-src.tar.xz

  cd ghc-$pkgver

  # We select the following build profile:
  #   Full build with max optimisation and everything enabled (very slow build)
  cp mk/build.mk{.sample,}
  sed -i '1iBuildFlavour = perf' mk/build.mk
}

build() {
  cd ghc-$pkgver

  ./configure \
    --prefix=/usr \
    --with-system-libffi \
    --with-ffi-includes=$(pkg-config --variable=includedir libffi)
  make -j1
}

package() {
  cd ghc-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 utils/completion/ghc.bash \
    "$pkgdir/usr/share/bash-completion/completions/ghc"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
