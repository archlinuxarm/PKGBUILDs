# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - depend on llvm37, make -j1

buildarch=4

# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

# use the following command to print out packages that need to get rebuilt
# grep -r ghc /var/abs/ | awk -F '/' '{ print $5; }' | sort -u

pkgname=ghc
pkgver=8.0.1
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp' 'gcc' 'libffi' 'llvm37')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl')
provides=('haskell-array=0.5.1.1'
          'haskell-base=4.9.0.0'
          'haskell-binary=0.8.3.0'
          'haskell-bytestring=0.10.8.1'
          'haskell-containers=0.5.7.1'
          'haskell-deepseq=1.4.2.0'
          'haskell-directory=1.2.6.2'
          'haskell-filepath=1.4.1.0'
          'haskell-ghc-boot=8.0.1'
          'haskell-ghc-boot-th=8.0.1'
          'haskell-ghc-prim=0.5.0.0'
          'haskell-haskeline=0.7.2.3'
          'haskell-hoopl=3.10.2.1'
          'haskell-hpc=0.6.0.3'
          'haskell-integer-gmp=1.0.0.1'
          'haskell-pretty=1.1.3.3'
          'haskell-process=1.4.2.0'
          'haskell-template-haskell=2.11.0.0'
          'haskell-terminfo=0.4.0.2'
          'haskell-time=1.6.0.1'
          'haskell-transformers=0.5.2.0'
          'haskell-unix=2.7.2.0'
          'haskell-xhtml=3000.2.1'
          'haskell-cabal=1.24.0.0')
replaces=('haskell-array'
          'haskell-base'
          'haskell-binary'
          'haskell-bytestring'
          'haskell-containers'
          'haskell-deepseq'
          'haskell-directory'
          'haskell-filepath'
          'haskell-ghc-boot'
          'haskell-ghc-boot-th'
          'haskell-ghc-prim'
          'haskell-haskeline'
          'haskell-hoopl'
          'haskell-hpc'
          'haskell-integer-gmp'
          'haskell-pretty'
          'haskell-process'
          'haskell-template-haskell'
          'haskell-terminfo'
          'haskell-time'
          'haskell-transformers'
          'haskell-unix'
          'haskell-xhtml'
          'haskell-cabal')
install='ghc.install'
source=("https://downloads.haskell.org/~ghc/$pkgver/$pkgname-${pkgver}-src.tar.xz"
        ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook)
noextract=("$pkgname-${pkgver}-src.tar.xz")
md5sums=('c185b8a1f3e67e43533ec590b751c2ff'
         'cc9aa11fddde15d8e5e15e99748874a1'
         '20cc84d8058f385fec3c0068e369f1ef'
         'f69583eedff4ad6dedbb7180a9525332')

prepare() {
  # Need to extract this tarball with a UTF-8 locale instead of a chroot's "C"
  # locale; otherwise we get:
  #   bsdtar: Pathname can't be converted from UTF-8 to current locale.
  LANG=en_US.UTF-8 bsdtar xf $pkgname-${pkgver}-src.tar.xz

  cd ghc-$pkgver

  # We select the following build profile:
  #   Full build with max optimisation and everything enabled (very slow build)
  cp mk/build.mk{.sample,}
  sed -i '1iBuildFlavour = perf' mk/build.mk
}

build() {
  cd ghc-$pkgver

  ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/ghc \
    --with-system-libffi \
    --with-ffi-includes=$(pkg-config --variable=includedir libffi)
  make -j1
}

package() {
  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  install -Dm644 utils/completion/ghc.bash \
    "$pkgdir/usr/share/bash-completion/completions/ghc"

  for _hook in ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook; do
    install -D -m644 "$srcdir/$_hook" \
      "$pkgdir/usr/share/libalpm/hooks/$_hook"
  done

  install -d -m755 "${pkgdir}"/usr/share/haskell/{register,unregister}

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
