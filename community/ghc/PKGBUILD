# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - depend on llvm39, make -j1

buildarch=4

# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

shopt -s extglob

pkgbase=ghc
pkgname=(ghc-libs ghc ghc-static)
pkgver=8.2.2
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
makedepends=('ghc-static' 'perl' 'libxslt' 'docbook-xsl' 'python-sphinx' 'haskell-hscolour'
             'texlive-bin' 'texlive-latexextra' 'ttf-dejavu')
source=("https://downloads.haskell.org/~ghc/$pkgver/$pkgbase-${pkgver}-src.tar.xz"
        ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook)
noextract=("$pkgbase-${pkgver}-src.tar.xz")
sha512sums=('6549416f470b599973d409fa45f59c25b07e6a94798cef1a19ad432547dc225338cf4dbc4a4793114b4a417798a3b59b122b92b020251074405c5302b7ffe799'
            'afb119e4f665770c5704b97034d2488504eaa7afcddca2fb6b554079921cd2330599bcb5c36669f0d0e7856dd99ae1deeca1b0e97e2371a783f26e5ef9776ba9'
            'bd65a369b618ec9bee46c028c2b6acff8f883f60f6cad6e5be8561fbcef6118278abec11bb86f9e6f92cb2e05ad74ec54611c1788e8ed95187d0091fcbbf8767'
            'd4bfdd4c8ad9ac612cf187fec150850e9f4068a4a4202503c00dba07ba26f804bc11d7181249f7e3452d7ede60dc5dedea34e73fdb584ac2953068b51c6fd5ad')

prepare() {
  # Need to extract this tarball with a UTF-8 locale instead of a chroot's "C"
  # locale; otherwise we get:
  #   bsdtar: Pathname can't be converted from UTF-8 to current locale.
  LANG=en_US.UTF-8 bsdtar xf $pkgbase-${pkgver}-src.tar.xz

  cd ghc-$pkgver

  cp mk/build.mk{.sample,}
  sed -i '1iBuildFlavour = perf' mk/build.mk
}

build() {
  cd ghc-$pkgver

  ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/ghc \
    --with-system-libffi \
    --with-ffi-includes=$(pkg-config --variable=includedir libffi)
  make -j1
}

package_ghc-static() {
  pkgdesc='The Glasgow Haskell Compiler - Static Libraries and Documentation'
  depends=('ghc')

  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  mv "$pkgdir"/usr/lib/ghc-$pkgver/package.conf.d "$srcdir"/static-package.conf.d
  find "$pkgdir"/usr/lib ! \( -name "*.a" -o -name "*.p_hi" -o -name "*.hi" \) -type f -delete
  find "$pkgdir"/usr/lib -type d -empty -delete
  mv "$srcdir"/static-package.conf.d "$pkgdir"/usr/lib/ghc-$pkgver/
  rm -r "$pkgdir"/usr/bin "$pkgdir"/usr/share/man

  install -Dm644 "$srcdir"/ghc-rebuild-doc-index.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-rebuild-doc-index.hook
}

package_ghc() {
  pkgdesc='The Glasgow Haskell Compiler'
  provides=("haskell-ghc=$pkgver")
  replaces=("haskell-ghc")
  depends=('ghc-libs')

  cd ghc-$pkgver
  make DESTDIR="$pkgdir" -j1 install

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc-pkg is in ghc-libs
  rm "$pkgdir"/usr/lib/ghc-$pkgver/bin/ghc-pkg*
  rm "$pkgdir"/usr/bin/ghc-pkg*
  (cd "$pkgdir"/usr/lib/ghc-$pkgver; rm -r !(bin|ghc-$pkgver))

  # docs moved to ghc-static
  rm -r "$pkgdir"/usr/share/doc

  install -Dm644 utils/completion/ghc.bash \
    "$pkgdir/usr/share/bash-completion/completions/ghc"
}

package_ghc-libs() {
  pkgdesc='The Glasgow Haskell Compiler - Dynamic Libraries'
  install='ghc.install'
  depends=('gcc' 'gmp' 'libffi' 'perl' 'llvm39')
  provides=('haskell-array=0.5.2.0'
            'haskell-base=4.10.1.0'
            'haskell-binary=0.8.5.1'
            'haskell-bytestring=0.10.8.2'
            'haskell-containers=0.5.10.2'
            'haskell-deepseq=1.4.3.0'
            'haskell-directory=1.3.0.2'
            'haskell-filepath=1.4.1.2'
            'haskell-ghc-boot=8.2.2'
            'haskell-ghc-boot-th=8.2.2'
            'haskell-ghc-compact=0.1.0.0'
            'haskell-ghc-prim=0.5.1.1'
            'haskell-haskeline=0.7.4.0'
            'haskell-hoopl=3.10.2.2'
            'haskell-hpc=0.6.0.3'
            'haskell-integer-gmp=1.0.1.0'
            'haskell-pretty=1.1.3.3'
            'haskell-process=1.6.1.0'
            'haskell-template-haskell=2.12.0.0'
            'haskell-terminfo=0.4.1.0'
            'haskell-time=1.8.0.2'
            'haskell-transformers=0.5.2.0'
            'haskell-unix=2.7.2.2'
            'haskell-xhtml=3000.2.2'
            'haskell-cabal=2.0.1.0')
  replaces=('haskell-array'
            'haskell-base'
            'haskell-binary'
            'haskell-bytestring'
            'haskell-containers'
            'haskell-deepseq'
            'haskell-directory'
            'haskell-filepath'
            'haskell-ghc-boot'
            'haskell-ghc-boot-th'
            'haskell-ghc-compact'
            'haskell-ghc-prim'
            'haskell-haskeline'
            'haskell-hoopl'
            'haskell-hpc'
            'haskell-integer-gmp'
            'haskell-pretty'
            'haskell-process'
            'haskell-template-haskell'
            'haskell-terminfo'
            'haskell-time'
            'haskell-transformers'
            'haskell-unix'
            'haskell-xhtml'
            'haskell-cabal')

  provides+=("haskell-ghci=$pkgver")
  conflicts+=('haskell-ghci')

  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc library and other exes are in the ghc package
  rm -r "$pkgdir"/usr/lib/ghc-$pkgver/ghc-$pkgver
  (cd "$pkgdir"/usr/lib/ghc-$pkgver/bin; rm !(ghc-pkg*))
  (cd "$pkgdir"/usr/bin; rm !(ghc-pkg*))

  # docs moved to ghc-static
  rm -r "$pkgdir"/usr/share/{man,doc}

  install -Dm644 "$srcdir"/ghc-register.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-register.hook
  install -Dm644 "$srcdir"/ghc-unregister.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-unregister.hook

  install -dm755 "$pkgdir"/usr/share/haskell/{register,unregister}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
