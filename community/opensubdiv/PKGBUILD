# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on cuda

pkgname=opensubdiv
pkgver=3.4.3
pkgrel=1
pkgdesc="An Open-Source subdivision surface library"
arch=(x86_64)
url="http://graphics.pixar.com/opensubdiv"
license=('apache')
depends=(zlib ptex intel-tbb libxcursor xorg-xrandr libxinerama)
makedepends=(cmake doxygen glfw glew python python-pygments python-docutils opencl-headers)
source=("https://github.com/PixarAnimationStudios/OpenSubdiv/archive/v${pkgver//./_}.tar.gz"
        python3.patch
        cuda9.patch)
sha512sums=('44fd1361d9e7f48c3fa84f3420ed34743571220602aefb082a42f769ca5b8e9ac21907ce8714f6e3a2caadd75adae03c535a1949a8e6450dfea9ae7a2247cfc2'
            '035a6691080e9ec1a6eb5e4a7746f3f0ba6caea4b74c1bfededdc477b701431456b026ee75dbf7ca03fabd6526f62e9181c616a62304f26e614474577b96fcc7'
            '2aa815acdb873274afb5629c7604ad2586c460a32a7b141c04f71313d03c768798461439cd1b66db565170ca21c7b05a3f8456ab7635b5eb73b1762bea312f4b')

prepare() {
  cd "OpenSubdiv-${pkgver//./_}"

  patch -Np1 -i "${srcdir}"/python3.patch
  patch -Np1 -i "${srcdir}"/cuda9.patch
  mkdir build
}

build() {
  cd "OpenSubdiv-${pkgver//./_}"/build

  cmake .. \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "OpenSubdiv-${pkgver//./_}"/build

  DESTDIR="$pkgdir/" make install

  rm -rf ${pkgdir}/usr/bin
}
