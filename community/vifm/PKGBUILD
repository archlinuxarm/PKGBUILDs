# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream commit to fix ARM builds

pkgname=vifm
pkgver=0.8
pkgrel=2
pkgdesc="Ncurses based file manager with vi like keybindings"
arch=('i686' 'x86_64')
url="http://vifm.info/"
license=('GPL')
depends=('ncurses' 'desktop-file-utils')
changelog=$pkgname.changelog
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        0001-Work-around-compilation-on-AArch64-GNU-Linux.patch)
sha256sums=('69eb6b50dcf462f4233ff987f0b6a295df08a27bc42577ebef725bfe58dbdeeb'
            '667842aa73c7e7bdd0a46b850404ef0716f3685905d0fbb7cc9be94b2faa92b7')

prepare() {
   cd "${srcdir}"/$pkgname-$pkgver

   patch -p1 -i ../0001-Work-around-compilation-on-AArch64-GNU-Linux.patch
}

build() {
   cd "${srcdir}"/$pkgname-$pkgver

   ./configure --prefix=/usr
   make 
}

package() {
   cd "${srcdir}"/$pkgname-$pkgver

   make DESTDIR="${pkgdir}" install
}
