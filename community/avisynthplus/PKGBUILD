# Maintainer: Daniel Bermond <dbermond@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=avisynthplus
pkgver=3.7.0
pkgrel=2
pkgdesc='An improved version of the AviSynth frameserver'
arch=('x86_64')
url='https://avs-plus.net/'
license=('GPL')
depends=('gcc-libs' 'hicolor-icon-theme')
optdepends=('devil: for ImageSeq plugin')
makedepends=('cmake' 'devil')
source=("https://github.com/AviSynth/AviSynthPlus/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-avisynthplus-gcc11-fix.patch'::'https://github.com/AviSynth/AviSynthPlus/commit/a1e33be91dfa48b8db5ee358ea4fcea952e80e4f.patch'
        'avisynthplus.xml')
sha256sums=('8906d9e46dc90a194413d69b710e3106c0aafddc0c5c62004885d0c3beb79862'
            '6eb2b108c4689a5ca71e6c3da5af62fd2fb717bdcf4835a3a38eb67159d80072'
            'c4b270a3df7fbe1c153400215169c4ae4cae3b7a8710c843393e3a6ed0fd8a3e')

prepare() {
    patch -d "AviSynthPlus-${pkgver}" -Np1 -i "${srcdir}/010-avisynthplus-gcc11-fix.patch"
}

build() {
    [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(AvsCore atomic)" >> "AviSynthPlus-${pkgver}/avs_core/CMakeLists.txt"
    cmake -B build -S "AviSynthPlus-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -D -m644 avisynthplus.xml -t "${pkgdir}/usr/share/mime/packages"
    
    local _icon
    local _res
    while read -r -d '' _icon
    do
        _res="$(sed 's/[[:alpha:]]*\([0-9]\+\)\.png/\1/' <<< "${_icon##*/}")"
        [ -n "$_res" ] && install -D -m644 "$_icon" \
            "${pkgdir}/usr/share/icons/hicolor/${_res}x${_res}/mimetypes/avisynthplus-script.png"
    done < <(find "AviSynthPlus-${pkgver}/distrib/Icons/Rastered" -type f -name 'AvsPlusIcon*.png' -print0)
}
