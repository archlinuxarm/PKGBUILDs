# Maintainer: Daniel Bermond <dbermond@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=avisynthplus
pkgver=3.7.1
pkgrel=1
pkgdesc='An improved version of the AviSynth frameserver'
arch=('x86_64')
url='https://avs-plus.net/'
license=('GPL')
depends=('gcc-libs' 'hicolor-icon-theme')
optdepends=('devil: for ImageSeq plugin')
makedepends=('cmake' 'devil')
source=("https://github.com/AviSynth/AviSynthPlus/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'avisynthplus.xml')
sha256sums=('96c5043512ecf8dca7a6257b8a54a28dcb8fea454d0dc5a9fd6f61cceda6bbef'
            'c4b270a3df7fbe1c153400215169c4ae4cae3b7a8710c843393e3a6ed0fd8a3e')

build() {
    [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(AvsCore atomic)" >> "AviSynthPlus-${pkgver}/avs_core/CMakeLists.txt"
    cmake -B build -S "AviSynthPlus-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -D -m644 avisynthplus.xml -t "${pkgdir}/usr/share/mime/packages"
    
    local _icon
    local _res
    while read -r -d '' _icon
    do
        _res="$(sed 's/[[:alpha:]]*\([0-9]\+\)\.png/\1/' <<< "${_icon##*/}")"
        [ -n "$_res" ] && install -D -m644 "$_icon" \
            "${pkgdir}/usr/share/icons/hicolor/${_res}x${_res}/mimetypes/avisynthplus-script.png"
    done < <(find "AviSynthPlus-${pkgver}/distrib/Icons/Rastered" -type f -name 'AvsPlusIcon*.png' -print0)
}
