# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: Maxime Gauduin <alucryd at archlinux dot org>
# Contributor: FadeMind <fademind at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Branchini Massimo <max.bra.gtalk at gmail dot com

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove valgrind makedepend, switch HALVE_VALGRIND exports

pkgbase=libdbusmenu
pkgname=("${pkgbase}-glib" "${pkgbase}-gtk"{2,3})
pkgver=16.04.0
pkgrel=3
pkgdesc='Library for passing menus over DBus'
url='https://launchpad.net/libdbusmenu'
arch=('x86_64')
license=('GPL3' 'LGPL2.1' 'LGPL3')
makedepends=('gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gtk2' 'gtk3' 'intltool' 'vala' 'pkg-config')
options=('!emptydirs')
source=(https://launchpad.net/${pkgbase}/${pkgver%.?}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz{,.asc})
sha512sums=('ee9654ac4ed94bdebc94a6db83b126784273a417a645b2881b2ba676a5f67d7fc95dd2bb37bfb0890aa47299ed73cb21ed7de8b75f3fed6b69bfd39065062241'
            'SKIP')
validpgpkeys=('45B1103FB93ACBD90296DBCAE83D089481836EBF')  # Marco Trevisan (at 3v1n0.net) <marco.trevisan@3v1n0.net>

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-gtk2}
}

build() {
  export HAVE_VALGRIND_TRUE=''
  export HAVE_VALGRIND_FALSE='#'

  msg2 "Building gtk3..."
  (cd ${pkgbase}-${pkgver}
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-{dumper,static,tests} \
      --with-gtk=3
    make
  )

  msg2 "Building gtk2..."
  (cd ${pkgbase}-${pkgver}-gtk2
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-{dumper,static,tests} \
      --with-gtk=2
    make
  )
}

package_libdbusmenu-glib() {
  depends=('glib2')

  cd ${pkgbase}-${pkgver}
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
}

package_libdbusmenu-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=("${pkgbase}-glib" 'gtk2')

  cd ${pkgbase}-${pkgver}-gtk2
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-gtk DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" uninstall
}

package_libdbusmenu-gtk3() {
  pkgdesc+=" (GTK+ 3 library)"
  depends=("${pkgbase}-glib" 'gtk3')

  cd ${pkgbase}-${pkgver}
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-gtk DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" uninstall
}

# vim: ts=2 sw=2 et:
