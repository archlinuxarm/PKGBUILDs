# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris
pkgver=8.4.12
pkgrel=1
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
depends=('pygtk' 'gstreamer0.10-base-plugins' 'gnome-python' 'libxxf86vm'
         'python-pysqlite' 'pyxml' 'sdl_mixer' 'desktop-file-utils')
makedepends=('pkgconfig' 'intltool' 'gettext' 'perlxml' 'texi2html' 'gnuchess')
optdepends=('gnucap: for computer simulation within the electricity activity'
            'tuxpaint: for the painting activity'
            'gnuchess: for the chess activity')
options=('!libtool')
install=gcompris.install
source=(http://downloads.sourceforge.net/sourceforge/gcompris/$pkgname-$pkgver.tar.gz)
md5sums=('8a1519e71caa0924d5df73faa311eea3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --disable-gnet
  make || return 1
  make DESTDIR="$pkgdir/" install

  rm -f "$pkgdir/usr/share/info/dir"
}

# vim:set ts=2 sw=2 et:
