# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix AArch64 build

pkgname=mongodb-tools
pkgver=3.4.5
pkgrel=1
pkgdesc="The MongoDB tools provide import, export, and diagnostic capabilities."
arch=('i686' 'x86_64')
url="https://github.com/mongodb/mongo-tools"
license=('Apache')
depends=('libsasl' 'libpcap' 'openssl-1.0')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-tools/archive/r$pkgver.tar.gz"
        "disable-sslv3.patch"
        'https://github.com/mongodb/mongo-tools/commit/3c562dd765f753ed4c8db0453fb35f31b3069471.patch')
sha512sums=('7989c02c6ec6555812a760d3e8ff8ac4c5d1ad46f78ee65cea0207ea9ffacc7a4250509f6ad5c31b88425b2831572f73fa3c4c4084fa8eb9fba158abff4a15e4'
            '674af43ebf9e73f1eae4f81c160cec4c4ea657b7e38f97929a0845d8981f9af904eca79e2144e6150774799a58074cee5b67e4385d082630c471e438981d9ea5'
            '7e5560d630f359ad021e37b60773fd1fe35e54d00e431ddd8b6dc0bf949d810fa9345cc9f4158e1598514a763e2247e667ef81687b5c1594096daf36f318589c')

prepare() {
  cd mongo-tools-r$pkgver
  . ./set_gopath.sh
  mkdir bin

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig

  patch -p1 -F10 -i ../3c562dd765f753ed4c8db0453fb35f31b3069471.patch
}

build() {
  cd mongo-tools-r$pkgver
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog mongoreplay; do
    go build -o bin/$i -tags "ssl sasl" $i/main/$i.go
  done
}

package() {
  cd mongo-tools-r$pkgver
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog mongoreplay; do
    install -Dm755 bin/$i "$pkgdir/usr/bin/$i"
  done
}
