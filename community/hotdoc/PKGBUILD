# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

# remove when bumped upstream

_pkgname=hotdoc
pkgname=hotdoc
pkgver=0.13.4
pkgrel=1
pkgdesc="the tastiest API documentation system"
arch=('x86_64')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('LGPL')
_py_deps=('appdirs' 'cchardet' 'dbus-deviation' 'lxml' 'networkx' 'pkgconfig'
          'schema' 'toposort' 'wheezy-template' 'yaml')
depends=("${_py_deps[@]/#/python-}" 'json-glib')
makedepends=('python-setuptools' 'cmake')
optdepends=('clang: for the C extension'
            'llvm: for the C extension')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('2d917b20f084717ca607d39b4bc498292f9d170ddf3c94f21062cda7a3d5ab91')
b2sums=('e9fa7d39970237a47c77d0c275fd84bf5ce3a446efabf810b23430ef06150d696e6ad223118fdd04632be297a0e564962cf3bbddafcfe9f3795036fbf0dc32a7')

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build/lib.linux-$CARCH-3*

    python -m unittest
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
