class:: HenonL
summary:: Henon map chaotic generator
categories:: UGens>Generators>Chaotic
related:: Classes/HenonC, Classes/HenonN

description::
A linear-interpolating sound generator based on the difference equation:

code::
	x[n+2] = 1 - a * pow(x[n+1], 2) + b * x[n]
::
warning:: revise formulae converted to c like code. ::

This equation was discovered by French astronomer Michel HÃ©non while studying the orbits of stars in globular clusters.

classmethods::
method:: ar
argument:: freq
Iteration frequency in Hertz
argument:: a
Equation variable
argument:: b
Equation variable
argument:: x0
Initial value of x
argument:: x1
Second value of x

examples::
code::
// default initial params
{ HenonL.ar(MouseX.kr(20, SampleRate.ir)) * 0.2 }.play(s);

// mouse-control of params
{ HenonL.ar(SampleRate.ir/4, MouseX.kr(1,1.4), MouseY.kr(0,0.3)) * 0.2 }.play(s);

// randomly modulate params
(
{ HenonL.ar(
	SampleRate.ir/8,
	LFNoise2.kr(1, 0.2, 1.2),
	LFNoise2.kr(1, 0.15, 0.15)
) * 0.2 }.play(s);
)

// as a frequency control
{ SinOsc.ar(HenonL.ar(40, MouseX.kr(1,1.4), MouseY.kr(0,0.3))*800+900)*0.4 }.play(s);
::
