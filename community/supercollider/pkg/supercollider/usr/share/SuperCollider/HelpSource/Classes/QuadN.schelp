class:: QuadN
summary:: General quadratic map chaotic generator
categories:: UGens>Generators>Chaotic
related:: Classes/QuadL, Classes/QuadC

description::
A non-interpolating sound generator based on the difference equation:

code::
	x[n+1] = a * pow(x[n], 2) + b * x[n] + c
::
warning:: revise formulae conversion to c like code ::

classmethods::
method:: ar
argument:: freq
Iteration frequency in Hertz
argument:: a
Equation variable
argument:: b
Equation variable
argument:: c
Equation variable
argument:: xi
Initial value of x
argument:: mul
argument:: add

examples::
code::
// default params
{ QuadN.ar(SampleRate.ir/4) * 0.2 }.play(s);
::

code::
// logistic map
// equation: x1 = -r*x0^2 + r*x0
(
{ var r;
	r = MouseX.kr(3.5441, 4);	// stable range
	QuadN.ar(SampleRate.ir/4, r.neg, r, 0, 0.1) * 0.4;
}.play(s);
)
::

code::
// logistic map as frequency control
(
{ var r;
	r = MouseX.kr(3.5441, 4);	// stable range
	SinOsc.ar(QuadN.ar(40, r.neg, r, 0, 0.1, 800, 900)) * 0.4;
}.play(s);
)
::
