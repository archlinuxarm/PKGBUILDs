# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.12.0
pkgrel=1
pkgdesc="Platform for audio synthesis and algorithmic composition"
arch=('x86_64')
url="https://supercollider.github.io"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc'  'libx11' 'qt5-base' 'qt5-svg' 'qt5-webengine'
'qt5-websockets')
makedepends=('abletonlink' 'alsa-lib' 'avahi' 'boost' 'cmake' 'emacs' 'fftw'
'libsndfile' 'qt5-tools' 'readline' 'systemd-libs' 'yaml-cpp')
checkdepends=('xorg-server-xvfb')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'sc3-plugins: additional extension plugins for scsynth')
provides=('libscsynth.so' "sclang=${pkgver}" "scsynth=${pkgver}")
source=(
    "https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source.tar.bz2"{,.asc}
)
install="${pkgname}.install"
sha512sums=('bd2f627347c5e3a2d1115f7afb9746ffabe14b53a36e4f3eccbd2d0e48f704bfd66a464b44680af274f74fe83ef5dfcf6b03ca6651e723ed99e226239d8ddeb4'
            'SKIP')
b2sums=('2757192699b6168fe8466984a6ba5e7513cafbacdcad7ca454af966c04fa51542a4d5f4976a8a197ecf6c8c31365e115d831d181022b4f10b05d8d79e14cab2c'
        'SKIP')
validpgpkeys=('2E1C4FC2F6BB58FA157B21B8064B501EB7405F04') # Marcin PÄ…czkowski (dyfer)


prepare() {
  mv -v "${_name}-${pkgver}-Source" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DLIBSCSYNTH=ON \
        -DFORTIFY=ON \
        -DSYSTEM_ABLETON_LINK=ON \
        -DSYSTEM_BOOST=ON \
        -DSYSTEM_YAMLCPP=ON \
        -DSC_VIM=OFF \
        -Wno-dev \
        -B build \
        -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
        -DCMAKE_C_FLAGS="${CFLAGS}" \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run make test ARGS="-VV -d -j1" -C build
}

package() {
  depends+=('libasound.so' 'libavahi-common.so' 'libavahi-client.so'
  'libboost_filesystem.so' 'libboost_program_options.so' 'libboost_regex.so'
  'libboost_thread.so' 'libfftw3f.so' 'libjack.so' 'libsndfile.so'
  'libreadline.so' 'libudev.so' 'libyaml-cpp.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {AUTHORS,{CHANGELOG,README,README_LINUX}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
