# $Id: PKGBUILD 90361 2013-05-12 02:59:43Z speps $
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor:  <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.6.5
pkgrel=1
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition"
arch=('i686' 'x86_64' 'armv6h')
url="http://supercollider.sourceforge.net/"
license=('GPL3')
depends=('jack' 'fftw' 'cwiid' 'qtwebkit')
makedepends=('cmake' 'ruby' 'vim' 'emacs')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'vim: vim interface'
            'ruby: vim support')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/$pkgname/Source/${pkgver%.*}/$_name-$pkgver-Source-linux.tar.bz2")
md5sums=('ac176ea512fc2b9a9be645322c5b20b4')
noautobuild=0
buildarch=00010000
highmem=1

build() {
  cd "$srcdir/$_name-Source"
  [ -d bld ] || mkdir bld && cd bld
  _carch='uname -m'
  if [[ "$_carch" == *armv6* ]]; then
    export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
    export CC="gcc"
    export CXX="g++"
    cmake .. -L -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTING=OFF -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON \
      -DNATIVE=OFF -DSC_QT=OFF -DSC_WII=OFF -DSC_ED=OFF -DSC_IDE=OFF -DSC_EL=ON \
      -DCMAKE_C_FLAGS="-march=armv6 -mfpu=vfp -mfloat-abi=hard" \
      -DCMAKE_CXX_FLAGS="-march=armv6 -mfpu=vfp -mfloat-abi=hard"
    make -j4
  else
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
             -DCMAKE_BUILD_TYPE=Release \
             -DLIBSCSYNTH=ON \
             -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
             -DCMAKE_C_FLAGS="${CFLAGS}" -DCMAKE_CXX_FLAGS="${CXXFLAGS}"
    make
  fi
}

package() {
  cd "$srcdir/$_name-Source/bld"
  make DESTDIR="$pkgdir/" install
}
