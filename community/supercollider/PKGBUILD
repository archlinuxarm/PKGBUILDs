# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.8.0
pkgrel=2
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition"
arch=('i686' 'x86_64')
url="http://supercollider.sourceforge.net/"
license=('GPL3')
depends=('jack' 'fftw' 'cwiid' 'qt5-webkit')
makedepends=('cmake' 'ruby' 'vim' 'emacs' 'qt5-tools' 'boost')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'vim: vim interface'
            'ruby: vim support')
source=("https://github.com/supercollider/supercollider/releases/download/Version-$pkgver/SuperCollider-$pkgver-Source-linux.tar.bz2"
        supercollider-cxxflags.patch)
md5sums=('f9c077e92707ae187c5f10cfddccf291'
         '4f62489286fe8008d6013fc59047c20b')

prepare() {
  cd $_name-Source
# Fix CXXFLAGS
  patch -p1 -i ../supercollider-cxxflags.patch
}

build() {
  cd "$srcdir/$_name-Source"
  [ -d b ] || mkdir b && cd b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DLIBSCSYNTH=ON \
           -DCMAKE_CXX_FLAGS="$CXXFLAGS -DBOOST_MATH_DISABLE_FLOAT128" \
           -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
           -DCMAKE_C_FLAGS="${CFLAGS}"
  make
}

package() {
  cd "$srcdir/$_name-Source/b"
  make DESTDIR="$pkgdir/" install
}
