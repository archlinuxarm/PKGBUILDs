# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.10.3
pkgrel=3
pkgdesc="Environment and programming language for real time audio synthesis and algorithmic composition"
arch=('x86_64')
url="https://supercollider.github.io"
license=('GPL3')
groups=('pro-audio')
depends=('alsa-lib' 'avahi' 'boost-libs' 'desktop-file-utils' 'fftw' 'gcc-libs'
'glibc' 'jack' 'libsndfile' 'libx11' 'qt5-base' 'qt5-svg' 'qt5-webengine'
'qt5-websockets' 'readline' 'systemd-libs' 'yaml-cpp')
makedepends=('boost' 'cmake' 'emacs' 'qt5-tools')
checkdepends=('xorg-server-xvfb')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'sc3-plugins: additional extension plugins for scsynth')
provides=('libscsynth.so')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source-linux.tar.bz2"
        "${pkgname}-use_system_boost.patch"
        "${pkgname}-3.10.3-boost-1.70.patch::https://patch-diff.githubusercontent.com/raw/supercollider/supercollider/pull/4491.patch")
install="${pkgname}.install"
sha512sums=('e6427ae367395e50e3f31986bab43aa53894f6d8c35563371c14940e6c3303068d419f95edf198509479d8ac7420400d8f9df7d4168a326db2f262b954ea2c24'
            'd9b2ef108673b11a27c63654ceacf3b7e51df11ce05fc6d9045abd219ea718c2552f5ea026a5182c272639e695a1073192013b14cae257e7e4b00dd3c303ecae'
            'a0d0004d3e0a6e0eb346af4a262131054272ab6f9ddf62baea4bfe1eed332657536d6a80752e8422827bff0241d34e98905f9dc9fc3f02abade60d559a551e15')

prepare() {
  mv -v "${_name}-Source" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # make sure system boost is used
  # https://github.com/supercollider/supercollider/issues/4096
  patch -Np1 -i ../"${pkgname}-use_system_boost.patch"
  patch -Np1 -i ../"${pkgname}-3.10.3-boost-1.70.patch"
  mkdir -v build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBoost_NO_BOOST_CMAKE=ON \
           -DLIBSCSYNTH=ON \
           -DFORTIFY=ON \
           -DSYSTEM_YAMLCPP=ON \
           -DSYSTEM_BOOST=ON \
           -DSC_VIM=OFF \
           -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
           -DCMAKE_C_FLAGS="${CFLAGS}"
  make VERBOSE=1
}

check() {
  cd "${pkgname}-${pkgver}/build"
  xvfb-run make test ARGS="-V" || warning "Known failing tests: https://github.com/supercollider/supercollider/issues/3555"
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 ../{AUTHORS,{CHANGELOG,README,README_LINUX}.md}
}
