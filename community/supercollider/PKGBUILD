# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.9.0
pkgrel=1
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition"
arch=('x86_64')
url="https://supercollider.github.io"
license=('GPL3')
depends=('boost-libs' 'fftw' 'jack' 'qt5-webkit')
makedepends=('boost' 'cmake' 'emacs' 'qt5-tools')
optdepends=('emacs: emacs interface'
'gedit: gedit interface')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source-linux.tar.bz2")
install="${pkgname}.install"
sha512sums=('52274ce0c5e604e44f844e77bb7245d6829c49a002ac0c383c3d150f48c60bd79b8f0481cd629e13544d6a01f2c5fd05d92d24d2c24602df2bf798cff94c0b0f')

prepare() {
  cd "${_name}-Source"
  # requires out-of-tree build
  [ -d bld ] || mkdir bld && cd bld
}

build() {
  cd "${_name}-Source/bld"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DLIBSCSYNTH=ON \
           -DSC_VIM=OFF \
           -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
           -DCMAKE_C_FLAGS="${CFLAGS}"
  make
}

package() {
  cd "${_name}-Source/bld"
  make DESTDIR="${pkgdir}" install
  cd ..
  install -Dm644 AUTHORS ${pkgdir}/usr/share/doc/${pkgname}/AUTHORS
  install -Dm644 CHANGELOG.md ${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG.md
  install -Dm644 README.md ${pkgdir}/usr/share/doc/${pkgname}/README.md
  install -Dm644 README_LINUX.md ${pkgdir}/usr/share/doc/${pkgname}/README_LINUX.md
}
