# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.9.1
pkgrel=2
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition"
arch=('x86_64')
url="https://supercollider.github.io"
license=('GPL3')
depends=('boost-libs' 'fftw' 'jack' 'qt5-webkit')
makedepends=('boost' 'cmake' 'emacs' 'qt5-tools')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source-linux.tar.bz2")
install="${pkgname}.install"
sha512sums=('cd18f250f53c576a6636b98f2c53d0a2d58e885b37b2612d227a7e8043e14b2840e34a32bcfa69a04da56cc9ef96a1e5aa1cd8de15e54db65ace665a1dd0770b')

prepare() {
  mv -v "${_name}-Source" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # requires out-of-tree build
  mkdir -v bld
}

build() {
  cd "${pkgname}-${pkgver}/bld"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DLIBSCSYNTH=ON \
           -DFORTIFY=ON \
           -DSC_VIM=OFF \
           -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
           -DCMAKE_C_FLAGS="${CFLAGS}"
# cmake broken for system libraries:
# https://github.com/supercollider/supercollider/issues/3513
#           -DSYSTEM_BOOST=ON \
#           -DSYSTEM_YAMLCPP=ON \
  make
}

package() {
  cd "${pkgname}-${pkgver}/bld"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 ../{AUTHORS,{CHANGELOG,README,README_LINUX}.md}
}
