# $Id: PKGBUILD 2101 2009-09-08 15:55:51Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Joseph <josephmc@sbcglobal.net>

pkgname=glest
pkgver=3.2.2
pkgrel=4
pkgdesc="Cross-platform 3D realtime strategy game"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/glest"
license=('GPL')
depends=('xerces-c' 'sdl' 'libvorbis' 'openal' 'glest-data' 'mesa' 'lua' 'bash')
makedepends=('jam' 'hd2u' 'unzip' 'wxgtk')
source=(http://downloads.sourceforge.net/glest/glest_source_$pkgver.zip
	glest
	glest.sh
	glest.desktop
	glest.png
	glest-xerces-c-3.patch)
md5sums=('1e961f49c1fb0e59e1e1483d66099a55'
         '4e0c6f5eb8b9be4f957c85a1a71b13ef'
         '2588ceaa24a30c6b797d538b2338c0fb'
         '9c0db07885fe85bf58c6374e2e1abe70'
         '697199c67dfe1e30468006df5f77135e'
         '3c07be3c2b7f13d4886b6eb618376c40')

build() {
  cd $startdir/src/mk/linux

  export CFLAGS="$CFLAGS -I/usr/include/wx-2.8"
  export CXXFLAGS="$CXXFLAGS -I/usr/include/wx-2.8"
  export CPPFLAGS="$CPPFLAGS -I/usr/include/wx-2.8"

  sed -i '1,2i#include <limits.h>' $srcdir/source/glest_game/ai/ai_rule.cpp
  (cd $srcdir/source && patch -p1 <$srcdir/glest-xerces-c-3.patch)

  [ -f ./configure ] || ./autogen.sh
  ./configure --with-wx-prefix=/usr
  jam

  install -d $startdir/pkg/opt/glest/bin $startdir/pkg/etc/profile.d || return 1

  install -m755 glest $startdir/pkg/opt/glest && \
  install -m644 glest.ini $startdir/pkg/opt/glest && \
  install -m755 $startdir/src/glest $startdir/pkg/opt/glest/bin && \
  install -m755 $startdir/src/glest.sh $startdir/pkg/etc/profile.d && \
  strip $startdir/pkg/opt/glest/glest || return 1

  install -D -m0644 $srcdir/glest.desktop $pkgdir/usr/share/applications/glest.desktop && \
  install -D -m0644 $srcdir/glest.png $pkgdir/usr/share/pixmaps/glest.png || return 1
}
