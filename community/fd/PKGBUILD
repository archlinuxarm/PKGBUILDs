# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

# ALARM:
# - compile on aarch64 with JEMALLOC_SYS_WITH_LG_PAGE=16 to support
#   systems with 16k or 64k page size

pkgname=fd
pkgver=8.6.0
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find'
arch=('x86_64')
url=https://github.com/sharkdp/fd
license=('MIT' 'APACHE')
depends=('gcc-libs')
makedepends=('rust')
source=("fd-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('fe8bc0acb1bc6057c8d6bfd41db3fe5600ef81f7c6dab7feba7795ee29de7fb96771adba945c6dac265b72ab2ce1dc2a600570fa3b26045f988b75615744ee97')

build() {
  cd fd-$pkgver
  [[ $CARCH == "aarch64" ]] && export JEMALLOC_SYS_WITH_LG_PAGE=16
  export CFLAGS+=" -ffat-lto-objects"
  cargo build --release --locked
}

check() {
  cd fd-$pkgver
  cargo test --release --locked
}

package() {
  cd fd-$pkgver
  install -Dm755 target/release/fd "$pkgdir"/usr/bin/fd

  make completions
  install -Dm644 autocomplete/fd.bash "$pkgdir"/usr/share/bash-completion/completions/fd
  install -Dm644 autocomplete/fd.fish "$pkgdir"/usr/share/fish/vendor_completions.d/fd.fish
  install -Dm644 autocomplete/_fd "$pkgdir"/usr/share/zsh/site-functions/_fd

  install -Dm644 doc/fd.1 "$pkgdir"/usr/share/man/man1/fd.1
  install -Dm644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/fd/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"/usr/share/licenses/fd/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
