# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

# ALARM:
# - compile on aarch64 with JEMALLOC_SYS_WITH_LG_PAGE=16 to support
#   systems with 16k or 64k page size

pkgname=fd
pkgver=8.5.2
pkgrel=1.1
pkgdesc='Simple, fast and user-friendly alternative to find'
arch=('x86_64')
url=https://github.com/sharkdp/fd
license=('MIT' 'APACHE')
depends=('gcc-libs')
makedepends=('rust')
source=("fd-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('24496f5553eb2e7dfdac295e1844d2ec542e87eef3ffbd888e68aea4f9a235144d4bb5b955dda31510fccbb8f6f1de09f4bfa852ab4990b035e8d8dbbe7a7f70')

build() {
  cd fd-$pkgver
  [[ $CARCH == "aarch64" ]] && export JEMALLOC_SYS_WITH_LG_PAGE=16
  export CFLAGS+=" -ffat-lto-objects"
  cargo build --release --locked
}

check() {
  cd fd-$pkgver
  cargo test --release --locked
}

package() {
  cd fd-$pkgver
  install -Dm755 target/release/fd "$pkgdir"/usr/bin/fd

  make completions
  install -Dm644 autocomplete/fd.bash "$pkgdir"/usr/share/bash-completion/completions/fd
  install -Dm644 autocomplete/fd.fish "$pkgdir"/usr/share/fish/vendor_completions.d/fd.fish
  install -Dm644 autocomplete/_fd "$pkgdir"/usr/share/zsh/site-functions/_fd

  install -Dm644 doc/fd.1 "$pkgdir"/usr/share/man/man1/fd.1
  install -Dm644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/fd/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"/usr/share/licenses/fd/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
