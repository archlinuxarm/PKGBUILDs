# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Jiyunatori <tori@0xc29.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set b_lto=false

pkgname=a2jmidid
pkgver=9
pkgrel=3
pkgdesc="A daemon for exposing legacy ALSA sequencer applications in JACK MIDI system."
arch=('x86_64')
url="https://github.com/linuxaudio/a2jmidid"
license=('GPL2')
groups=('pro-audio')
arch=('x86_64')
depends=('glibc')
makedepends=('alsa-lib' 'dbus' 'jack' 'meson')
optdepends=(
  'bash: for a2j'
  'python-dbus: for a2j and a2j_control')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxaudio/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('5bd13b6904ed68c5bfe40ca516fd49b7eb4d4a946b9908ee04687265848734c8e1a81579f0f1a5bd0752595be8858dc748da10487b7f366394c09a5ffc7d5e5c')
b2sums=('ad079e76a54f56ee8f82150c306f91aead52f86632978ea6f3376a0bf163744379210b34701b39da4ee14fc4d8a1c6be82d1052abeb8ccf79410bc242d174d62')

build() {
  cd "${pkgname}-${pkgver}"
  meson --prefix=/usr \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=false \
        -D b_pie=true \
        build
  ninja -C build
}

package() {
  depends+=('libasound.so' 'libdbus-1.so' 'libjack.so')

  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 {AUTHORS,CHANGELOG,INSTALLATION,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
