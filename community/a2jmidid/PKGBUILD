# Maintainer : David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Jiyunatori <tori@0xc29.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=a2jmidid
pkgver=8
pkgrel=3
pkgdesc="A daemon for exposing legacy ALSA sequencer applications in JACK MIDI system."
arch=('x86_64')
url="http://repo.or.cz/a2jmidid.git"
license=('GPL2')
groups=('pro-audio')
arch=('x86_64')
depends=('jack' 'python2-dbus')
source=("${pkgname}-${pkgver}.tar.gz::http://repo.or.cz/a2jmidid.git/snapshot/7383d268c4bfe85df9f10df6351677659211d1ca.tar.gz"
        'control-unique-port-names-over-dbus.patch'
        'fix-spelling-mistakes-in-man-page.patch'
        'link-to-libpthread.patch'
        'properly-fix-pthread-linking.patch'
        'a2jmidid-aarch64.patch')
sha512sums=('d9675404b9b12a0816ba58e60ee74710a534ac70845ae12e5d64d2f08bc12b415ae8094f7038b8cf7eb8317eccc818ea7dee10276baf8b1420d3e90a2685743d'
            'dfec328ab5cba7f4a1273df24b651a9acaaf4eb54813f9c7351ef44ca95b8d90fb730e95a29a85067c113786c7e864fbf91cf9558591a7c714d6e9f977f7a110'
            '10e2b1b2b7bf5ca97607ad9272a5c548b9435c583e68483d0fb36fa64ab9dc22e324078b4e6535a5087a31e69dab8f7025addc72f9dd9064f0c576488dc3b3d1'
            '22b3794fdf1a20563f02eec1ba94994a55595dd8eaf0144d5088ac8afa43276cfbfcc693320ed46fba794e190f544ba3ddfb586f9dd2104d9d4ac51d3a805214'
            '6accab16e82b4b283db084eea71db0585c957c647ea945ad9ac1736c197119200d0c0dc5275bdd7013634faf7a22df12d727cc78e24dd67de95f2e52d2f0df0f'
            '6f8f018e41b07a783754c6b1010494e6c71c470b02c5c350f6b483cfea2da3f1b68f31086d1c2dac8d5e5ada59032b7eecd87b0d78f03b6546ecebc8f306b7c6')

prepare() {
  cd "${pkgname}-7383d26"

  # "latest" upstream patches
  patch -p1 -i "${srcdir}/link-to-libpthread.patch"
  patch -p1 -i "${srcdir}/properly-fix-pthread-linking.patch"
  patch -p1 -i "${srcdir}/fix-spelling-mistakes-in-man-page.patch"
  patch -p1 -i "${srcdir}/control-unique-port-names-over-dbus.patch"

  # python2 shebang
  sed -i 's/python/&2/' a2j_control

  patch -p1 -i ../a2jmidid-aarch64.patch

  python2 waf configure --prefix=/usr
}

build() {
  cd "${pkgname}-7383d26"
  python2 waf
}

package() {
  cd "${pkgname}-7383d26"
  python2 waf install --destdir="${pkgdir}/"
}
