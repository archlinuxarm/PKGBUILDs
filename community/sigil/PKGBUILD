# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - depend on xerces-c to avoid x86isms in bundled version

pkgname=sigil
pkgver=0.8.7
pkgrel=1
pkgdesc="WYSIWYG ebook editor"
arch=('i686' 'x86_64')
url="https://github.com/user-none/Sigil/"
license=('GPL3')
depends=('qt5-webkit' 'boost-libs' 'python' 'hicolor-icon-theme' 'xerces-c')
makedepends=('qt5-multimedia' 'qt5-tools' 'qt5-svg' 'cmake' 'boost')
install=sigil.install
source=("https://github.com/user-none/Sigil/releases/download/${pkgver}/Sigil-${pkgver}-Code.zip")
md5sums=('1eae1cc28f46f3d0fad185a3b5e34025')

build() {
  cd "${srcdir}/Sigil-${pkgver}"

  mkdir build
  cd build

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
    -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON ..

  make
}

package() {
  cd "${srcdir}"/Sigil-${pkgver}/build
  make install DESTDIR="${pkgdir}"

  for _pic in 16 32 48 128 256; do
    install -D -m 0644 ../src/Sigil/Resource_Files/icon/app_icon_${_pic}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${_pic}x${_pic}/apps/${pkgname}.png
  done
  install -Dm644 ../src/Sigil/Resource_Files/icon/app_icon_128.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
