# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - depend on xerces-c to avoid x86isms in bundled version

pkgname=sigil
pkgver=0.9.8
pkgrel=2
pkgdesc="WYSIWYG ebook editor"
arch=('i686' 'x86_64')
url="http://sigil-ebook.com/"
license=('GPL3')
depends=('qt5-webkit' 'python' 'hicolor-icon-theme' 'python-lxml' 'minizip' 'hunspell' 'xerces-c')
makedepends=('qt5-multimedia' 'qt5-tools' 'qt5-svg' 'cmake')
install=sigil.install
source=("Sigil-${pkgver}.zip::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.zip")
md5sums=('478222c6d79b20ff8e596da78069c1b3')

build() {
  cd "${srcdir}/Sigil-${pkgver}"

  mkdir -p build
  cd build

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
    -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON .. \
    -DUSE_SYSTEM_LIBS=ON

  make
}

package() {
  cd "${srcdir}"/Sigil-${pkgver}/build
  make install DESTDIR="${pkgdir}"

  for _pic in 16 32 48 128 256; do
    install -D -m 0644 ../src/Resource_Files/icon/app_icon_${_pic}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${_pic}x${_pic}/apps/${pkgname}.png
  done
  install -Dm644 ../src/Resource_Files/icon/app_icon_128.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
