# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Gabriel Souza Franco <Z2FicmllbGZyYW5jb3NvdXphQGdtYWlsLmNvbQ==>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Giuseppe Borzi <gborzi@ieee.org>
# Contributor: Brice MÃ©alier <mealier_brice@yahoo.fr>
# Contributor: Michele Mocciola <mickele>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepends on adios2

pkgname=opencascade
pkgver=7.6.0
_pkgver="V${pkgver//./_}"
pkgrel=1
pkgdesc="SDK intended for development of applications dealing with 3D CAD data"
arch=('x86_64')
url="https://www.opencascade.org"
license=('LGPL2.1' 'custom:LGPL-exception')
depends=(
  'ffmpeg'
  'freeimage'
  'gl2ps'
  'intel-tbb'
  'tk'
  'vtk'
)
makedepends=(
  'boost'
  'cmake'
  'eigen'
  'gdal'
  'glew'
  'libharu'
  'liblas'
  'openvr'
  'pdal'
  'proj'
  'pugixml'
  'python'
  'python-mpi4py'
  'qt5-base'
  'rapidjson'
  'unixodbc'
  'utf8cpp'
)
source=(
  "$pkgname-$pkgver.tar.gz::https://git.dev.opencascade.org/gitweb/?p=occt.git;a=snapshot;h=refs/tags/$_pkgver;sf=tgz"
  'opencascade.sh'
  'fix-install-dir-references.patch'
  'cmake-fix-variable.patch'
  'skip-license-installation.patch'
  'opencascade-tbb-2021.patch'
)
sha512sums=('6839eb6641b9e0752474fce11cf1756223067febe0f5bcd5db249e3eccef29517a6952c124aae818220b6360070a792dacd1b0b4ce970b8351ce38598f31b4be'
            'a7516028e55fd303dc1cfb61b75c9cb209d431d854b4d1c58f9c19df8ecee9d79da5c8745676c68a2de0980652de4c4d1c5a927c25db1e5146fb1f1f43c5906b'
            'fc5eefe2a3ee11f77ee7c304d42d3b32dc5c86050879b9ae5f02da6242a87872aa5da7ae40a961a0a799dc241a81394f8a3ca022dd14d35bc2a61599eed3f983'
            'ad6ce2f52462989bd990b52fd5428f3e5cbd4fb15c38d92c0e0954e6afc3368fca961b92efead8e8957213352fb87a469e1bfaccaf14b484351acc0a0bc10485'
            'fd8499793b12e4e59420f63351fef9599c3976d307979beea2681b873a340ae673ba643a7060c74bec1dd4801954c99f8f1f1a8bcc3baa3db9cbfce32153675e'
            '24d81b4db35267464baedffd552d7b846871f64afaff46d92bf309c8113f20d8b575975946de2f1ff561455100541b5a5a6c44f377aa4c3c63f1da9930e34d4f')
b2sums=('c16cd096f20e90267a00b6be21b17b22b491cf5d9aa1a311d64d7707042dc88380c60ececceb6728dbcdd4ff56a67558144292950e11518b21cbc49ace4e88cd'
        'da9db038ed2348d2d7736505eda2f40fe52c836bcedb74d9f369cc53f7d40a330bd87d6aedd773863745cd46e4dbe5876acda2d2d60177f00d5db9cae4f1f102'
        '4fae705cd68a259a831df5cd83c72a8a3e2508a99dcc4cb572164bcbcf0a15f50fa803e520793249043edf78f93eceebfa2311fed5e0391d64476b31ccbec079'
        'bb98b2b53a9a97ae15a353a9dbcc6d92a97ddaa56af0610cefe9de3a4b84c4a6021d9396f7e270c1d379278df069dbf90f9771db64947e52d38ad36bc3c6f913'
        'da815c14ad3a8283c4f3cf41b6b19b40408798f64374b909c776f5ab22ee9ccfc2653c09b33c7b550a138007ff2b36b40d742c9fddc7c1ea499dfe58e59f57ea'
        'f996126927c0f418cf6592300db7f9e8c4c8a899cb4a06f2eb37c7849f40176fc2d81de421ec346aaec5a99243388a466f75a2689ac7e6177ac9092802a31877')

prepare() {
  cd "occt-$_pkgver"

  patch -p1 -i ../fix-install-dir-references.patch
  patch -p1 -i ../cmake-fix-variable.patch
  patch -p1 -i ../skip-license-installation.patch
  patch -p1 -i ../opencascade-tbb-2021.patch # Fix build with TBB 2021
}

build() {
  cmake \
    -B build \
    -S "occt-$_pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_RELEASE_DISABLE_EXCEPTIONS=OFF \
    -DUSE_GL2PS=ON \
    -DUSE_FREEIMAGE=ON \
    -DUSE_FFMPEG=ON \
    -DUSE_VTK=ON \
    -DUSE_RAPIDJSON=ON \
    -DUSE_TBB=ON \
    -D3RDPARTY_VTK_INCLUDE_DIR=/usr/include/vtk

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # environment variables (adapted from adm/templates/env.sh)
  install -vDm755 -t "$pkgdir/etc/profile.d" opencascade.sh

  # remove unnecessary shell scripts
  rm -v "$pkgdir/usr/bin/"*.sh

  cd "occt-$_pkgver"

  # licenses
  install -vDm755 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE_LGPL_21.txt OCCT_LGPL_EXCEPTION.txt
}

# vim:set ts=2 sw=2 et:
