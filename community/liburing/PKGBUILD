# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cherry-pick upstream patch to fix FTBFS

pkgname=liburing
pkgver=0.6
pkgrel=1
pkgdesc='Linux-native io_uring I/O access library'
arch=('x86_64')
url='https://git.kernel.dk/cgit/liburing/'
license=('MIT' 'LGPL2.1')
makedepends=('git')
validpgpkeys=('C0FC392DCADE26D975FA5E4AF7D358FB2971E0A6') # Jens Axboe <axboe@kernel.dk>
source=("git+https://git.kernel.dk/liburing#tag=liburing-$pkgver?signed")
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  git cherry-pick -n 459e895f1167bbfc52649c204abc362a592d2bcb
}

build() {
  cd $pkgname

  ./configure \
    --mandir=/usr/share/man

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

