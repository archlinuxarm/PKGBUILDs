# $Id$
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use -D_FILE_OFFSET_BITS=64 with 32-bit ARM

pkgname=sshfs
pkgver=3.3.0
pkgrel=4
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=(x86_64 i686)
url="http://github.com/libfuse/sshfs"
license=(GPL)
depends=(fuse3 glib2 openssh)
makedepends=('meson' 'python-docutils')
source=(https://github.com/libfuse/sshfs/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz{,.asc}
        001-sshfs-3.3.0-meson.build.patch
        002-sshfs-3.3.0-sshfs.c.patch)
sha256sums=('9ddfcc2564fafe002706b093b6295afbcb2cc0a3bc52bc805cc5361cb69ca51d'
            'SKIP'
            'da928d31291ad86582ab616ea91619c345513dec00f6aafb5bac8f2929159e6b'
            '2655139190ee5d2d295afc89a4ae028e78b881ecad9e03fcf9cd1869b2e4e439')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F') # Nikolaus Rath <Nikolaus@rath.org>

prepare() {
  cd $pkgname-$pkgver

  # patch meson.build
  patch -p1 < ../001-sshfs-3.3.0-meson.build.patch
  # patch sshfs.c
  patch -p1 < ../002-sshfs-3.3.0-sshfs.c.patch
  
  [[ $CARCH == 'i686' || $CARCH == 'arm' || $CARCH == 'armv6h' || $CARCH == 'armv7h' ]] && export CFLAGS+=" -D_FILE_OFFSET_BITS=64"
  rm -rf build
  mkdir build
  cd build
  arch-meson ..
}

build() {
  cd $pkgname-$pkgver/build
  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
