# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use -D_FILE_OFFSET_BITS=64 with 32-bit ARM

pkgname=sshfs
pkgver=3.0.0
pkgrel=1
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=(x86_64 i686)
url="http://github.com/libfuse/sshfs"
license=(GPL)
depends=(fuse3 glib2 openssh)
source=(https://github.com/libfuse/sshfs/releases/download/sshfs-$pkgver/sshfs-$pkgver.tar.gz{,.asc})
sha256sums=('644966c7326c1b788a80318c5806f20f6d42dd72ab686f66d6120bd108b54d2d'
            'SKIP')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F') # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd $pkgname-$pkgver

  [[ $CARCH == 'i686' || $CARCH == 'arm' || $CARCH == 'armv6h' || $CARCH == 'armv7h' ]] && export CFLAGS+=" -D_FILE_OFFSET_BITS=64"
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
