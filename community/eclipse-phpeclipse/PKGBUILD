# $Id: PKGBUILD 2313 2009-09-15 14:46:07Z cprimier $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Frank Oosterhuis <frank@scriptzone.nl>

pkgname=eclipse-phpeclipse
_reldate=200909091335
pkgver=1.2.2
pkgrel=1
pkgdesc="PHP - Support for the Eclipse IDE Framework"
arch=('any')
url="http://www.phpeclipse.de/"
license=('CPL')
depends=('eclipse')
source=("http://downloads.sourceforge.net/phpeclipse/files/a%29%20Eclipse%203.3.x/updateSite-${pkgver}.${_reldate}PRD.zip")
md5sums=('a168e637e310252bbbe187fce70561c8')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

# vim:set ts=2 sw=2 et:
