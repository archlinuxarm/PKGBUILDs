# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=0.6.5.2
pkgrel=1
pkgdesc="A Qt-based Gadu-Gadu client"
arch=('i686' 'x86_64')
url="http://www.kadu.net/"
license=('GPL')
depends=('qt' 'libgadu')
makedepends=('cmake' 'aspell' 'libao' 'libsndfile' 'libxtst' 'curl' 'xmms')
conflicts=('kadu-svn')
install=$pkgname.install
source=(http://www.kadu.net/download/stable/$pkgname-$pkgver.tar.bz2
        mod-weather.diff)

_static="sound_.+ icons_.+ emoticons_.+"
_disable="module_macosx_docking module_growl_notify module_dshow_sound module_win32_sound module_itunes_mediaplayer"
md5sums=('9cc1e59e538ff1aa416d162834ce43bb'
         'e35a10647a48d3cff611ae6b20ebef07')

build() {
  cd $startdir/src/kadu

  # kadu.net tends to get sluggish
  sed -r -i 's/-T [0-9]+/-T 60/g' varia/scripts/autodownload

  sed -r -i 's/(^[0-9a-z_]+=)[yn]$/\1m/' .config
  for m in $_static; do
    sed -r -i "s/^($m)=m/\1=y/" .config || return 1
  done
  for m in $_disable; do
    sed -r -i "s/^($m)=m/\1=n/" .config || return 1
  done

  for p in $startdir/src/mod-*.diff; do 
    m=`basename $p .diff | sed 's/mod-\(.*\)/\1/'`
    mkdir -p modules/patches/$m
    cp $p modules/patches/$m
  done

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

  rm -rf $startdir/pkg/usr/{lib,include}/{libgadu*,pkgconfig}
  rm -rf $startdir/pkg/usr/share/kadu/{HISTORY,README}
}
