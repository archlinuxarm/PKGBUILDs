# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.og>
#  - remove dependency on fwupd, --enable-firmware from configure
#  - restrict to v7/v8 due to valgrind requirement

buildarch=12

pkgname=gnome-software
pkgver=3.24.3+13+g836d2ee8
pkgrel=1
pkgdesc="GNOME Software Tools"
arch=(i686 x86_64)
license=(GPL2)
url="https://wiki.gnome.org/Apps/Software/"
depends=(gtk3 appstream-glib libsoup gsettings-desktop-schemas gnome-desktop flatpak
         packagekit archlinux-appstream-data gtkspell3 libgudev)
makedepends=(intltool python docbook-xsl gnome-common git valgrind)
_commit=836d2ee85991e968c3cb38a006df344aa5493c2c  # gnome-3-24
source=("git+https://git.gnome.org/browse/gnome-software#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_SOFTWARE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --disable-schemas-compile \
    --enable-packagekit \
    --enable-flatpak \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
