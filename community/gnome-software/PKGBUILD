# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.og>
#  - remove dependency on fwupd, specify -Denable-fwupd=false
#  - remove makedepend on valgrind, specify -Denable-valgrind=false

pkgname=gnome-software
pkgver=3.26.1+11+ge1ede3c6
pkgrel=1
pkgdesc="GNOME Software Tools"
arch=(i686 x86_64)
license=(GPL2)
url="https://wiki.gnome.org/Apps/Software/"
depends=(gtk3 appstream-glib libsoup gsettings-desktop-schemas gnome-desktop flatpak
         packagekit archlinux-appstream-data gtkspell3 libgudev liboauth)
makedepends=(python docbook-xsl git meson gobject-introspection gtk-doc)
_commit=e1ede3c607cf2123b67cccc9185b5a1c1ed5a69d  # gnome-3-26
source=("git+https://git.gnome.org/browse/gnome-software#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_SOFTWARE_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname

  # fwupd 1.0
  git cherry-pick -n 798e1ab89 6f17aab14 166a010ba a2eaf2d29 781c81721
}

build() {
  cd build
  arch-meson ../$pkgname -Denable-rpm=false -Denable-fwupd=false -Denable-valgrind=false
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
