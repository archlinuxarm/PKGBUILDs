# $Id: PKGBUILD 150962 2012-02-24 10:13:24Z pierre $
# Maintainer: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to enable ARM building, contributed by vahokif

plugrel=1

pkgname=tightvnc
pkgver=1.3.10
pkgrel=6
pkgdesc="VNC Unix server && viewer"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.tightvnc.com"
depends=('libjpeg' 'zlib' 'libxaw' 'libxp' 'xorg-xauth')
makedepends=('imake')
conflicts=('vnc')
options=('!makeflags')
source=("http://downloads.sf.net/sourceforge/vnc-tight/$pkgname-${pkgver}_unixsrc.tar.bz2"
        'tightvnc-fontpath-xcolors.patch'
        'tightvnc-1.3.10-arm.patch')
md5sums=('397b35faad32d5246b6d44b142f8304f'
         '21aa7f3376ec3c608ff22d199a19c601'
         '0c10d9cd275d90ba5f13ffe3109443a2')

build() {
  cd $srcdir/vnc_unixsrc
  patch -Np1 -i ../tightvnc-fontpath-xcolors.patch
  patch -Np1 -i ../tightvnc-1.3.10-arm.patch
  sed -i 's|/usr/local/|/usr/share/|' vncserver

  xmkmf
  make World
  cd Xvnc
  ./configure
  sed 's|PROTO_DEFINES =|PROTO_DEFINES = -D_XOPEN_SOURCE=500L|' \
    -i programs/Xserver/os/Makefile lib/font/fc/Makefile
  make
}

package() {
  cd $srcdir/vnc_unixsrc
  mkdir -p $pkgdir/usr/bin $pkgdir/usr/share/man/man1
  ./vncinstall $pkgdir/usr/bin $pkgdir/usr/share/man

  # install java classes
  mkdir -p $pkgdir/usr/share/vnc/classes
  install -Dm644 $srcdir/vnc_unixsrc/classes/* \
    $pkgdir/usr/share/vnc/classes
}
