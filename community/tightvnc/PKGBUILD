# $Id: PKGBUILD 67960 2012-03-16 12:47:31Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to enable ARM building, contributed by vahokif

plugrel=1

pkgname=tightvnc
pkgver=1.3.10
pkgrel=8
pkgdesc="VNC Unix server && viewer"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.tightvnc.com"
depends=('libjpeg' 'zlib' 'libxaw' 'libxp' 'xorg-xauth' 'perl')
makedepends=('imake' 'setconf')
options=('!makeflags')
source=("http://downloads.sf.net/sourceforge/vnc-tight/$pkgname-${pkgver}_unixsrc.tar.bz2"
        'http://people.csail.mit.edu/jaffer/Color/rgb.txt'
        'tightvnc-1.3.10-arm.patch')
sha256sums=('f48c70fea08d03744ae18df6b1499976362f16934eda3275cead87baad585c0d'
            'af6c056a95ae725f98534db22b9a4916d17f2356fdca84a4a038211a82fa8a73'
            '8ee519594642ab9309f9f7a91bcd6938f2117a23452d19edf78259b46381bd40')

build() {
  cd $srcdir/vnc_unixsrc
  patch -Np1 -i "${srcdir}"/tightvnc-1.3.10-arm.patch

  setconf vncserver '$vncClasses' \"/usr/share/tightvnc\"\;
  setconf vncserver '$fontPath' \"/usr/share/fonts/misc/,/usr/share/fonts/75dpi/,/usr/share/fonts/100dpi/,/usr/share/fonts/Type1/\"\;
  xmkmf
  make World
  cd Xvnc
  ./configure
  setconf programs/Xserver/dix/Makefile DEFAULTRGBDATABASE '/usr/share/tightvnc/rgb'
  setconf programs/Xserver/os/Makefile PROTO_DEFINES "-D_XOPEN_SOURCE=500L"
  setconf lib/font/fc/Makefile PROTO_DEFINES "-D_XOPEN_SOURCE=500L"
  make
}

package() {
  cd "$srcdir/vnc_unixsrc"

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
  ./vncinstall "$pkgdir/usr/bin" "$pkgdir/usr/share/man"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  install -Dm644 "$srcdir/vnc_unixsrc/classes/"* \
    "$pkgdir/usr/share/$pkgname"
  install -Dm644 "$srcdir/rgb.txt" "$pkgdir/usr/share/$pkgname/rgb.txt"
}

# vim:set ts=2 sw=2 et:
