# $Id: PKGBUILD 869 2009-08-06 21:55:58Z dgriffiths $
# Maintainer: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: Tate "Tatey" Johnson <tatey86@tpg.com.au>

pkgname=rss-glx
pkgver=0.9.0
pkgrel=2
pkgdesc="The Really Slick Screensavers port to GLX"
arch=('i686' 'x86_64')
url="http://rss-glx.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'freealut' 'glew' 'imagemagick')
optdepends=('xscreensaver: xscreensaver integration')
install=rss-glx.install
options=('!libtool')
source=(http://downloads.sourceforge.net/rss-glx/${pkgname}_${pkgver}.tar.bz2 rss-glx.patch)
md5sums=('a8dad80cd33b11443e9d06b33a85fb32' '634245bb4b6e72ed45c7f4699cb0d963')

build() {
  cd ${srcdir}/${pkgname}_${pkgver}
  #patch -Np0 -i ${srcdir}/rss-glx.patch
  [ "$CARCH" = "x86_64" ] && (sed -i -e 's|@LIBS@|@LIBS@ -fopenmp|g' other_src/Makefile.in || return 1)
  ln -sf /usr/share/libtool/config/config.sub ${srcdir}/${pkgname}_${pkgver}/config.sub
  ln -sf /usr/share/libtool/config/config.guess ${srcdir}/${pkgname}_${pkgver}/config.guess
  ln -sf /usr/bin/libtool ${srcdir}/${pkgname}_${pkgver}/libtool
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
	            --program-transform-name='s/plasma/plasma_rss/' \
              --with-configdir=/usr/share/xscreensaver/config \
  	          --with-kdessconfigdir=/usr/share/applnk/System/ScreenSavers \
              --enable-shared=yes --enable-static=no
  make CFLAGS="$CFLAGS -I/usr/include/ImageMagick" || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -d ${pkgdir}/usr/share/applications/screensavers
  install -m644 ${srcdir}/${pkgname}_${pkgver}/src/*.desktop ${pkgdir}/usr/share/applications/screensavers
}

# vim:set ts=2 sw=2 et:
