# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor:JZsolt Varadi (karmaxxl) 

pkgname=gnormalize
pkgver=0.63
pkgrel=1
pkgdesc="An audio normalizer, converter, encoder/decoder, ripper, tagger and player"
arch=('i686' 'x86_64')
url="http://gnormalize.sourceforge.net/"
license=('GPL')
depends=('cddb_get' 'desktop-file-utils' 'gtk2-perl')
makedepends=('lzma')
optdepends=('faac: full MP4 format'
            'flac: FLAC encoding, FLAC playing in conjunction with mplayer'
            'lame: MP3 encoding'
            'mac: full Monkeys Audio Codec support'
            'mpg321: MP3 playing'
            'mplayer: FLAC/WAV playing'
            'musepack-tools: full Musepack support'
            'perl-mp3-info: MP3 ID3 tags'
            'vorbis-tools: full OGG Vorbis support'
            'cdcd: CD playing'
            'cdparanoia: CD ripping'
            'cdrkit: CD ripping - alternative to cdparanoia'
            'wavegain: sound normalization')
install=gnormalize.install
source=(http://downloads.sourceforge.net/sourceforge/gnormalize/${pkgname}-${pkgver}.tar.gz
        gnormalize.desktop)
md5sums=('25ca7520731e61a8b7f4b7fa61928d54' '81c6dffab2139816919842e042705884')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -Dm755 gnormalize ${pkgdir}/usr/bin/gnormalize
  lzma -qd gnormalize.1.lzma
  install -Dm644 gnormalize.1 ${pkgdir}/usr/share/man/man1/gnormalize.1
  install -d ${pkgdir}/usr/share/gnormalize/animations
  install -m644 animations/*.gif ${pkgdir}/usr/share/gnormalize/animations/
  install -Dm644 README ${pkgdir}/usr/share/gnormalize/README
  install -Dm644 icons/gnormalize.png ${pkgdir}/usr/share/pixmaps/gnormalize.png
  for i in 16 32 48 ; do
    install -Dm644 icons/gnormalize-${i}.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/gnormalize.png
  done
  install -Dm644 ${srcdir}/gnormalize.desktop ${pkgdir}/usr/share/applications/gnormalize.desktop
}

# vim:set ts=2 sw=2 et:
