# Maintainer: schuay <jakob.gruber@gmail.com>
# Previous Maintainer:  TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo AT gmail DOT com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix qreal usage on ARM

pkgname=openssh-askpass
pkgver=2.0.3
pkgrel=3
pkgdesc='A plasma-like passphrase dialog for ssh'
arch=('i686' 'x86_64')
url='http://hugo.pereira.free.fr/software/index.php?page=package&package_list=software_list_qt4&package=openssh-askpass&full=0'
license=('GPL')
depends=('openssh' 'qt4' 'xcb-util-keysyms')
makedepends=('cmake')
provides=('x11-ssh-askpass')
source=("http://hugo.pereira.free.fr/software/tgz/$pkgname-$pkgver.tar.gz"
        'arm.patch')
install=$pkgname.install
md5sums=('b5abdeef5f09256545f8643aca111426'
         '834cd4ee728226b219d6c747a1b6a9b2')
         
build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i "${srcdir}"/arm.patch

  mkdir -p build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" ..
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install
}
