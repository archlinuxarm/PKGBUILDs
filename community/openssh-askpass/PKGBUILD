# Maintainer: schuay <jakob.gruber@gmail.com>
# Previous Maintainer:  TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo AT gmail DOT com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix qreal usage on ARM

pkgname=openssh-askpass
pkgver=2.0.2
pkgrel=1
pkgdesc='A plasma-like passphrase dialog for ssh'
arch=('i686' 'x86_64')
url='http://hugo.pereira.free.fr/software/index.php?page=package&package_list=software_list_qt4&package=openssh-askpass&full=0'
license=('GPL')
depends=('openssh' 'qt4' 'xcb-util-keysyms')
makedepends=('cmake')
provides=('x11-ssh-askpass')
conflicts=('x11-ssh-askpass')
source=("http://hugo.pereira.free.fr/software/tgz/$pkgname-$pkgver.tar.gz"
        "$pkgname.sh"
        'arm.patch')
md5sums=('b8237a47f9145d63cd70694e3889e057'
         'baec3f0499b851aca6705968fa9e0df7'
         'c6c1f8d316bf526d1d7d959aafb4d020')
         
build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i "${srcdir}"/arm.patch

  mkdir -p build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" ..
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/etc/profile.d/$pkgname.sh"
}
