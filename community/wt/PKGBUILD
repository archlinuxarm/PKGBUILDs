# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to cflags

pkgname=wt
pkgver=3.3.4
_pkgver=3.3.4
pkgrel=6
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu' 'graphicsmagick' 'pango' 'zlib')
makedepends=('boost' 'cmake' 'postgresql-libs' 'fcgi' 'sqlite' 'mysql++' 'qt4' 'doxygen' 'mesa' 'glu')
optdepends=('openssl: for SSL support in built-in webserver'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'mysql++: for the hangman example'
	    'qt4: for the Wt/Qt interopability example (wtwithqt)')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/$pkgname-${_pkgver}.tar.gz)
md5sums=('fdbb6042c7d6901e254685966ff55fac')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}

  CFLAGS+=" -fsigned-char"
  CXXFLAGS+=" -fsigned-char"

  [[ -d build ]] && rm -r build
  mkdir -p build && cd build
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DWT_CPP_11_MODE=-std=c++11 \
      -DWT_WRASTERIMAGE_IMPLEMENTATION=GraphicsMagick \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      -DBUILD_EXAMPLES=OFF \
      -DINSTALL_FINDWT_CMAKE_FILE=ON \
      ..
  # build examples again when they are compatible with boost 1.58
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}/build

  make DESTDIR=${pkgdir} install

  rm -r ${pkgdir}/usr/cmake
  rm -rf $pkgdir/var/run
}
