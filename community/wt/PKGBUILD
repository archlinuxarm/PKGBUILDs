# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to cflags

pkgname=wt
pkgver=3.3.7
_pkgver=3.3.7
pkgrel=3
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu' 'graphicsmagick' 'pango' 'zlib')
makedepends=('boost' 'cmake' 'postgresql-libs' 'fcgi' 'sqlite' 'mysql++' 'qt4' 'doxygen' 'mesa' 'glu')
optdepends=('openssl: for SSL support in built-in webserver'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'mysql++: for the hangman example'
	    'qt4: for the Wt/Qt interopability example (wtwithqt)')
backup=('etc/wt/wt_config.xml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kdeforche/wt/archive/${_pkgver}.tar.gz")
sha512sums=('f179b42eedcfd2e61f26ef92c6aad40c55c76c9a688269c4d5bd55dd48381073d6269d9c2ab305f15ef455616d48183a3fc0ba08c740a8e18318a2ad2fb13826')

build() {
  cd "$srcdir"/${pkgname}-${_pkgver}

  CFLAGS+=" -fsigned-char"
  CXXFLAGS+=" -fsigned-char"

  [[ -d build ]] && rm -r build
  mkdir -p build && cd build
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DWT_CPP_11_MODE=-std=c++11 \
      -DCONFIGDIR=/etc \
      -DWT_WRASTERIMAGE_IMPLEMENTATION=GraphicsMagick \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      -DBUILD_EXAMPLES=ON \
      -DINSTALL_FINDWT_CMAKE_FILE=ON \
      ..
  make
}

package() {
  cd "$srcdir"/${pkgname}-${_pkgver}/build

  make DESTDIR="$pkgdir" install
}
