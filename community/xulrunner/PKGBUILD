# $Id: PKGBUILD 169809 2012-10-29 21:12:03Z ibiru $
# Maintainer: Connor Behan <connor.behan@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

# ALARM: Kevin Mihelich
#  - added --disable-elf-hack/neon to mozconfig
#  - set extra CFLAG for v5
#  - patch for skia FTBFS on ARM

pkgname=xulrunner
pkgver=37.0.1
pkgrel=1
pkgdesc="Mozilla Runtime Environment"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'nss' 'libxt' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'libpulse' 'libevent' 'libvpx' 'icu' 'python2')
makedepends=('zip' 'unzip' 'pkg-config' 'diffutils' 'yasm' 'mesa' 'autoconf2.13' 'gst-plugins-base-libs')
url="http://wiki.mozilla.org/XUL:Xul_Runner"
source=(ftp://ftp.mozilla.org/pub/mozilla.org/xulrunner/releases/$pkgver/source/xulrunner-$pkgver.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        shared-libs.patch
        rhbz-966424.patch
        48e130d69836.patch)
options=('!emptydirs' 'staticlibs')
replaces=('xulrunner-oss')
sha256sums=('b06a8c9bb04d128ab0637abf7dc1b2c36ffe8930c76e1f37e5abe18c48815952'
            '08e7c8e40ca3ab68f91b18b84a4fbbba00767c88d84c0cfcdd52fe5e36083207'
            '1aa9ebe67542a2b8c28905d070829ada5b29438c6a7961f2b0cdd6b92d8b9f5c'
            '59d9fc421bc10a5515b73e159f44a72365bf7b7e8b3fc8a8c46043ef40bd3a40'
            '746cb474c5a2c26fc474256e430e035e604b71b27df1003d4af85018fa263f4a'
            'ab2d45b70d2006b539af3ea6e6f119d8aa62ef26f9b0d9813b728f22bf7af1de')

prepare() {
  cd "$srcdir/mozilla-release"
  cp "$srcdir/mozconfig" .mozconfig

  # fix libdir/sdkdir - fedora
  patch -Np1 -i ../mozilla-pkgconfig.patch
  patch -Np1 -i ../shared-libs.patch

  # https://bugs.archlinux.org/task/41689
  patch -Np1 -i ../rhbz-966424.patch

  # https://bugzilla.mozilla.org/show_bug.cgi?id=1136958
  patch -p1 -i ../48e130d69836.patch

  # WebRTC build tries to execute "python" and expects Python 2
  # Workaround taken from chromium PKGBUILD
  mkdir "$srcdir/python2-path"
  ln -s /usr/bin/python2 "$srcdir/python2-path/python"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}

build() {
  cd "$srcdir/mozilla-release"

  [[ $CARCH == "arm" ]] && CFLAGS="$CFLAGS -D__ARM_PCS" && CXXFLAGS="$CXXFLAGS -D__ARM_PCS"

  export PATH="$srcdir/python2-path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/xulrunner-$pkgver"
  export PYTHON="/usr/bin/python2"

  make -j1 -f client.mk build
}

package() {
  cd "$srcdir/mozilla-release"
  make -j1 -f client.mk DESTDIR="$pkgdir" install

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/xulrunner-$pkgver/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/usr/lib/xulrunner-$pkgver/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/usr/lib/xulrunner-$pkgver/hyphenation"

  # add xulrunner library path to ld.so.conf
  install -d $pkgdir/etc/ld.so.conf.d
  echo "/usr/lib/xulrunner-$pkgver" > $pkgdir/etc/ld.so.conf.d/xulrunner.conf

  chmod +x "${pkgdir}/usr/lib/xulrunner-devel-$pkgver/sdk/bin/xpt.py"
  chmod +x "${pkgdir}/usr/lib/xulrunner-devel-$pkgver/sdk/bin/xpcshell"
  ln -s /usr/lib/xulrunner-devel-$pkgver/sdk/bin/xpcshell "${pkgdir}/usr/lib/xulrunner-$pkgver/xpcshell"
  sed -i 's|!/usr/bin/env python$|!/usr/bin/env python2|' \
    "$pkgdir"/usr/lib/xulrunner-devel-$pkgver/sdk/bin/{xpt,header,typelib,xpidl}.py
}
