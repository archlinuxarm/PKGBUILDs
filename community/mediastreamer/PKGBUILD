# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add --disable-strict to configure to remove -Werror

pkgname=mediastreamer
pkgver=2.11.1
pkgrel=1
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL')
depends=('ortp' 'ffmpeg' 'libxv' 'libupnp' 'bzrtp' 'glew' 'libsrtp')
# xxd from Vim is needed to build
makedepends=('intltool' 'vim')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')
md5sums=('b406b445c9e8a5c50d4f92455913bc17'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-strict \
    --enable-glx --enable-xv --enable-zrtp --enable-external-ortp
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
