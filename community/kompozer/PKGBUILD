# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: peace4all <markspost at rocketmail dot com>
# Contributor: ingamedeo <ingamedeo at gmail dot com>

# thanks to tadzio, and techlive for the investigation
# work and fix included as the NS_IMETHOD_i686.patch

# Sorry for not honouring makepkg.conf (!buildflags)
# but the source doesn't compile with makepkg's default C/CXXFLAGS

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patches from debian to fix FTBFS

pkgname=kompozer
pkgver=0.8b3
pkgrel=17
pkgdesc="A Dreamweaver style WYSIWYG web editor; Nvu unofficial bug-fix release"
arch=('x86_64' 'i686')
license=('GPL')
url="http://www.kompozer.net"
depends=('gtk2' 'nss' 'libxt' 'libidl2' 'gnome-vfs' 'desktop-file-utils')
makedepends=('zip' 'pkgconfig' 'pangox-compat' 'nss')
options=('!buildflags')
source=("http://downloads.sourceforge.net/kompozer/${pkgname}-${pkgver}-src.tar.bz2" 
        'kompozer.desktop' 
        'mozconfig.patch' 
        'kompozer-libpng15.patch' 
        'kompozer-libpng14.patch' 
        'gcc46_default_ctors.patch' 
        'kompozer_gcc_4.7.patch' 
        'any_kernel_26_plus.patch'
        'NS_IMETHOD_i686.patch'
        'freetype-pango.patch'
        'freetype-xft.patch'
        'external-nss-for-shlibsign.patch'
        '11_arm_xpcom_unused_attribute.patch'
        '12_arm_xpcom_optim.patch')
md5sums=('cd4664ecda908666b19ef0607010c627'
         'ed8edf6817892a38b8b181fc9b2caaea'
         '4778f967307cf569089daca843de1df9'
         '4852034f99e7943071946e7eedc6c2f5'
         'd5ba90f7fbaed76f77a6d9717e86f3a5'
         'ad3ec5cbc6677dd712b140148398f58f'
         '75b0e1cad65dbeb8feedac5d4f52be32'
         '58a294356b3e17ad070e4c93180bf0ad'
         '186c47586bebf83b9b76d339e3a38c95'
         'aab21bbbd12818bd38bfce466dd1053f'
         '8575801724f9aa05f50f26c5a704df69'
         '190d5c11dccb28c4b060a93270cc3d26'
         'a1bd22939deea48666abfd2df96cc886'
         '131f80138cd0e6ff99697da5725cf5ef')
install="${pkgname}.install"

prepare() {
  cd "${srcdir}/mozilla"
  patch -Np0 -i "${srcdir}/mozconfig.patch"
  patch -Np0 -i "${srcdir}/kompozer-libpng14.patch"
  patch -Np0 -i "${srcdir}/kompozer-libpng15.patch"
  patch -Np0 -i "${srcdir}/gcc46_default_ctors.patch"
  patch -Np0 -i "${srcdir}/kompozer_gcc_4.7.patch"
  patch -Np0 -i "${srcdir}/any_kernel_26_plus.patch"
  patch -Np0 -i "${srcdir}/NS_IMETHOD_i686.patch"
  # Patches for moving from freetype to freetype2 packages
  patch -Np2 -i "${srcdir}/freetype-pango.patch"
  patch -Np2 -i "${srcdir}/freetype-xft.patch"
  # Patch to exclude the integrated nss library to sign
  # the binary libraries (currently broken for i686)
  patch -Np1 -i "${srcdir}/external-nss-for-shlibsign.patch"
  # ARM fixes
  patch -Np2 -i "${srcdir}/11_arm_xpcom_unused_attribute.patch"
  patch -Np2 -i "${srcdir}/12_arm_xpcom_optim.patch"
}

build() {
  cd "${srcdir}/mozilla"
  ./configure
  make
}

package() {
  cd "${srcdir}/mozilla"

  make DESTDIR="${pkgdir}" install
  # Remove nspr.m4 because it's now provided by nspr package
  rm "${pkgdir}/usr/share/aclocal/nspr.m4"
  rmdir "${pkgdir}/usr/share/aclocal"

  install -m755 -dD "${pkgdir}/usr/share/applications/"
  install -m644  "${srcdir}/kompozer.desktop" "${pkgdir}/usr/share/applications/"

  install -d -m755 "${pkgdir}/usr/share/pixmaps/"
  ln -s "/usr/lib/kompozer/icons/mozicon256.png" "${pkgdir}/usr/share/pixmaps/kompozer.png"
  ln -s "/usr/lib/kompozer/icons/mozicon50.xpm" "${pkgdir}/usr/share/pixmaps/kompozer.xpm"
}
