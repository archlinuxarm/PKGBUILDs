# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=cinelerra-cv
pkgver=20090715
pkgrel=1
pkgdesc="A complete audio and video production environment for Linux."
arch=('i686' 'x86_64')
url="http://cinelerra.org"
license=('GPL')
depends=('e2fsprogs' 'a52dec' 'fftw' 'lame' 'libavc1394' 'libiec61883' 'libraw1394' 'libsndfile' 'libvorbis' 'libogg' 'libpng' 'libjpeg' 'libtiff' 'esound' 'mjpegtools' 'openexr' 'x264' 'libxxf86vm' 'ffmpeg' 'libxv')
makedepends=('automake>=1.7' 'autoconf>=2.57' 'libtool' 'git' 'nasm')
options=('!libtool')
source=()
md5sums=()

_gitroot="git://git.cinelerra.org/j6t/cinelerra.git"
_gitname="cinelerra"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # If you don't need OpenGL comment out the next line.
  sed -i '/\/X11R6/s///' configure.in

  ./autogen.sh

  if [ "$CARCH" = "x86_64" ]; then
	  ./configure --prefix=/usr --with-buildinfo=git/recompile --with-external-ffmpeg --disable-mmx
  else
	  ./configure --prefix=/usr --with-buildinfo=git/recompile --with-external-ffmpeg --enable-mmx
  fi

  make || return 1
  make DESTDIR=$pkgdir install
} 
