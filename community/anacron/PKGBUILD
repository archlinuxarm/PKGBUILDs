# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=anacron
pkgver=2.3
pkgrel=9
pkgdesc="A cron-like periodic command scheduler"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/anacron"
license=('GPL')
depends=('glibc' 'bash')
backup=('etc/anacrontab')
source=(http://downloads.sourceforge.net/sourceforge/anacron/${pkgname}-${pkgver}.tar.gz \
        anacrond anacrond.logrotate anacrontab run-anacron \
        00anacron.daily 00anacron.monthly 00anacron.weekly)
md5sums=('865cc1dfe1ed75c470d3e6de13763f03' '2bc29a66a143c89605ec204b8118b82b'\
         'cacfeaed940a5663ab5ed2eac8179e5c' '6dd361a519b45ee51dc2737eb5a4bdd5'\
         '5ff0cdcb9ec99778938ac6ef26800327' '0a83e5b7bb5ba032f829aebcae7a41c8'\
         'ac6e7c7ea1a20881d9fc8d9dcbeede56' 'e9496fb7b6d9c1b79ac5306bb6806c0d')
sha1sums=('d0e154123585e9faf615d67230d82d7d1b4c099a'
          '7885726671ccafa091ef868a1d62af9fa7f5f2d5'
          'dcf2aba70a4bacda40593ae052494e56df161982'
          '5f0d196d9161777bf16f7c4d5bd8e8187cfd3339'
          '7058dad9fe75b9cfcb08494b2b54f1eaaae95ef2'
          '30076cd4e8fdce88717b4eb31e67acfa06d9a406'
          '06f65004a1c3351548d2b5719c5cf1716d818ffd'
          '428245aabdceae40925a6b818acc803945444de8')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's/const int isleap;/int isleap;/' gregor.c || return 1
  make || return 1
  make PREFIX=${pkgdir} MANDIR=${pkgdir}/usr/share/man install || return 1

  install -d ${pkgdir}/etc/{rc.d,cron.daily,cron.weekly,cron.monthly}
  install -D -m755 ../run-anacron ${pkgdir}/usr/sbin/run-anacron
  install -D -m755 ../anacrond ${pkgdir}/etc/rc.d/anacrond
  install -D -m755 ../00anacron.daily ${pkgdir}/etc/cron.daily/00anacron
  install -D -m755 ../00anacron.weekly ${pkgdir}/etc/cron.weekly/00anacron
  install -D -m755 ../00anacron.monthly ${pkgdir}/etc/cron.monthly/00anacron
  install -D -m644 ../anacrontab ${pkgdir}/etc/anacrontab
  install -D -m644 ../anacrond.logrotate ${pkgdir}/etc/logrotate.d/anacrond
}
