# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Patrick Smits <mail@patricksmits.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.com>
#  - remove -m32 command from configure

pkgname=yaws
pkgver=1.96
pkgrel=2
pkgdesc='Yet Another Web Server, pure Erlang HTTP server/framework'
arch=('x86_64' 'i686')
url='http://yaws.hyber.org/'
license=('BSD')
depends=('pam' 'erlang-nox')
backup=('etc/yaws/yaws.conf')
options=('!emptydirs')
install=$pkgname.install
source=("http://yaws.hyber.org/download/$pkgname-$pkgver.tar.gz"
        "$pkgname.service")
sha256sums=('8d4f16d18937335ac021ca4c65c8ee81dc4d71f133718c7148856cc6bf1f55e2'
            '3f8347add6e3741e77f7bb9ba6b31feb1c878e2e58ecf19063891e801fa24698')

prepare() {
  cd "$pkgname-$pkgver"

  # python2 fix
  for file in cgi.yaws cgi-bin/foo.py; do
	  sed -i '0,/on/s//on2/' "www/$file"
  done
  # R16B1 fix
  sed -i 's/:sha(Salted)/:hash(sha, Salted)/' src/yaws_websockets.erl

  # remove m32
  sed -i 's/\-m32//g' configure
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/lib/erlang/lib"
  ln -s /usr/lib/yaws "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 et:
