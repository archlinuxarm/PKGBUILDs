# $Id: PKGBUILD 69781 2012-04-23 09:18:39Z ibiru $
# Maintainer: Vesa Kaihlavirta
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -m32 flag from configure

pkgname=yaws
pkgver=1.92
pkgrel=2
pkgdesc='Yet Another Web Server -- The pure Erlang HTTP server/framework'
arch=('x86_64' 'i686')
url='http://yaws.hyber.org/'
license=('BSD')
depends=('pam' 'erlang')
backup=('etc/yaws/yaws.conf')
options=('!emptydirs')
install=$pkgname.install
source=("http://yaws.hyber.org/download/$pkgname-$pkgver.tar.gz")
sha256sums=('cde83ca55d8e1b10a89094e1cfdc3e78d91ba9b1036cb5ea4e1994f5922c2357')

build() {
  cd "$pkgname-$pkgver"

  sed -i 's/\-m32//g' configure
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  for file in www/cgi.yaws www/cgi-bin/foo.py; do
    # python2 fix
	  sed -i '0,/on/s//on2/' "$file"
  done
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/lib/erlang/lib"
  ln -s /usr/lib/yaws "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
