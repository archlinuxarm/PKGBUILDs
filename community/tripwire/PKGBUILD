# $Id: PKGBUILD 921 2009-08-08 23:52:48Z mherych $
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: eliott <eliott@cactuswax.net>

pkgname=tripwire
pkgver=2.4.1.2
pkgrel=1
pkgdesc="An intrusion detection system."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/tripwire/"
license=('GPL')
depends=('openssl')
backup=('etc/tripwire/twpol.txt' 'etc/tripwire/twcfg.txt')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.tar.bz2 \
        twpol.txt twcfg.txt)

md5sums=('8a1147c278b528ed593023912c4b649a'
         '82900de7500bf6a6f90d8b9f20c4ee44'
	 '4b44d8179e247a638ee2752543405753')

build() {
  cd ${srcdir}/$pkgname-$pkgver-src

#build package
  ./configure --prefix=/usr --sysconfdir=/etc/tripwire
  make || return 1

  # This package doesn't have a typical make install, so we do it by hand.
  #make DESTDIR=${pkgdir} install || return 1
 
#create var/lib directories
  install -d ${pkgdir}/var/lib || return 1
  install -d -m700 ${pkgdir}/var/lib/tripwire || return 1
  install -d -m700 ${pkgdir}/var/lib/tripwire/report || return 1

#install binaries
  install -d ${pkgdir}/usr/sbin || return 1
  install -m755 bin/siggen ${pkgdir}/usr/sbin/ || return 1
  install -m755 bin/tripwire ${pkgdir}/usr/sbin/ || return 1
  install -m755 bin/twadmin ${pkgdir}/usr/sbin/ || return 1
  install -m755 bin/twprint ${pkgdir}/usr/sbin/ || return 1

#copy install files for use by user after install, and fix a path
  install -d ${pkgdir}/usr/share/$pkgname || return 1
  install -m755 install/install.sh ${pkgdir}/usr/share/$pkgname/ || return 1
  install -m644 install/install.cfg ${pkgdir}/usr/share/$pkgname/ || return 1
  sed -i 's#./install/install.cfg#./install.cfg#' ${pkgdir}/usr/share/$pkgname/install.sh || return 1

#install man pages - FS#13766 fixed
  install -d ${pkgdir}/usr/share/man/{man4,man5,man8} || return 1
  install -m644 man/man4/*.4 ${pkgdir}/usr/share/man/man4/ || return 1
  install -m644 man/man5/*.5 ${pkgdir}/usr/share/man/man5/ || return 1
  install -m644 man/man8/*.8 ${pkgdir}/usr/share/man/man8/ || return 1

#install configuration files
  install -d ${pkgdir}/etc/$pkgname || return 1
  install -m644 ${srcdir}/twpol.txt ${pkgdir}/etc/tripwire/twpol.txt || return 1
  install -m644 ${srcdir}/twcfg.txt ${pkgdir}/etc/tripwire/twcfg.txt || return 1
}
