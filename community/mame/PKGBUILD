# $Id: PKGBUILD 313659 2018-03-31 22:58:35Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - install 'mame' instead of 'mame64'

highmem=1

pkgname=mame
pkgver=0.197
pkgrel=1
pkgdesc="A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support."
url="http://mamedev.org/"
license=(GPL2)
arch=(x86_64)
depends=(sdl2_ttf alsa-lib qt5-base)
makedepends=(nasm mesa python2 libxinerama)
conflicts=(sdlmame)
replaces=(sdlmame)
source=("https://github.com/mamedev/mame/archive/mame${pkgver/./}.tar.gz" mame.sh)
sha256sums=('2ce7d6f79cdad2c904924db0eba90368026b6bc38ab7b0d1cc5792560b2dcedd'
            '050e7238aa504992e604fa0458d7c5b75183d435e6f8123ad320731731323f61')

build() {
  cd mame-mame${pkgver/./}
  make \
    NOWERROR=1 \
    OPTIMIZE=2 \
    PYTHON_EXECUTABLE=/usr/bin/python2 \
    TOOLS=1 \
    ARCHOPTS=-flifetime-dse=1
}

package() {
  cd mame-mame${pkgver/./}

  # Install the mame script
  install -Dm755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname

  # Install the binaries
  install -Dm755 mame "$pkgdir"/usr/lib/mame/mame
  for _i in castool chdman imgtool jedutil nltool nlwav pngcmp regrep romcmp src2html \
            split srcclean ldverify ldresample; do
    install -m755 $_i -t "$pkgdir"/usr/lib/mame
  done

  # Install the extra bits
  install -d "$pkgdir"/usr/share/$pkgname/{artwork,ctrlr,hash,keymaps,shader}
  install -d "$pkgdir"/usr/share/man/man1
  install -d "$pkgdir"/usr/share/man/man6
  install -m644 src/osd/modules/opengl/shader/glsl*.*h "$pkgdir"/usr/share/$pkgname/shader/
  install -m644 docs/man/*.1* "$pkgdir"/usr/share/man/man1/
  install -m644 docs/man/*.6* "$pkgdir"/usr/share/man/man6/

  cp -a artwork/* "$pkgdir"/usr/share/$pkgname/artwork/
  install -m644 ctrlr/* "$pkgdir"/usr/share/$pkgname/ctrlr/
  install -m644 keymaps/* "$pkgdir"/usr/share/$pkgname/keymaps/
  install -m644 hash/* "$pkgdir"/usr/share/$pkgname/hash/
  cp -a bgfx "$pkgdir"/usr/lib/$pkgname/bgfx
  cp -a plugins "$pkgdir"/usr/share/$pkgname/plugins
  cp -a language "$pkgdir"/usr/share/$pkgname/language

  # Include the license
  install -Dm644 docs/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # FS#28203
  sed -i 's|KEYCODE_2_PAD|KEYCODE_2PAD|' "$pkgdir"/usr/share/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_4_PAD|KEYCODE_4PAD|' "$pkgdir"/usr/share/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_6_PAD|KEYCODE_6PAD|' "$pkgdir"/usr/share/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_8_PAD|KEYCODE_8PAD|' "$pkgdir"/usr/share/mame/ctrlr/*.cfg

  # documentation
  install -dm0755 "$pkgdir"/usr/share/doc
  cp -a docs "$pkgdir"/usr/share/doc/$pkgname
  rm -r "$pkgdir"/usr/share/doc/$pkgname/man
}
