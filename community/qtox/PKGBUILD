# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to explicitly link against libatomic for v5/v6

pkgname=qtox
_pkgname=qTox
pkgver=1.17.4
pkgrel=1
_gitver='c0e9a3b79609681e5b9f6bbf8f9a36cb1993dc5f'
pkgdesc='a chat, voice, video, and file transfer IM client using the encrypted peer-to-peer Tox protocol'
arch=('x86_64')
url='https://github.com/qTox/qTox'
license=('GPL3')
depends=('desktop-file-utils'
         'libxss'
         'openal'
         'ffmpeg'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'libexif'
         'toxcore'
         'sonnet'
         'snorenotify'
         'libgl')
makedepends=('qt5-tools' 'git' 'check' 'cmake' 'mesa' 'libglvnd')

source=( 
        "$pkgname-$pkgver.tar.lz::https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz"
        "$pkgname-$pkgver.tar.lz.asc::https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz.asc"
        'atomic.patch'
        )
sha512sums=('a1757a5dc98153e131c9469771d2c8b5f729ceb26314c2abae5f17b0d59c8cba36b6833ddaaaf46839ebb5e19bf088750b00121805cd6713a21cf5266a52d6ca'
            'SKIP'
            'd2386cda78958e6a154a9612535d6f9f714aa80f07e8c33f9f8ad63b1f6f3599508ab184a67ba04254f9621b66ec6283f560722a3895ca3c2ab4ced9e3d7148d')

## GPG key fingerprints taken from https://github.com/qTox/qTox/blob/$_gitver/README.md
validpgpkeys=('DA262CC93C0E1E525AD21C8596775D454B8EBF44'  # sudden6 <sudden6@gmx.at>
              'BA7883E22F9D35945BA3376053137C3033F09008'  # Zetok Zalbavar <zetok@openmailbox.org>
	      '1157616BBD860C539926F8139591A163FF9BE04C'  # - antis81
	      '31039166FA902CA50D05D6085AF9F2E29107C727'  # - Diadlo
	      'C7A2552D0B250F983827742C133203A3AC399151'  # - initramfs
	      '2880C860D95C909D3DA45C687E086DD661263264'  # - tux3
          '74ADB7A7F8ECC1009916C4E42AA8E0DA1B31FB3C'  # - AnthonyBilinski subkey
          '7EB339FE881747E701B7D472EBE36E66A8429B99'  # - AnthonyBilinski
          'CA9221C5389B7C50AA5F779352A50775BE13DF17'  # - noavarice
          '141C880E8BA25B198D0F850F7C132143C1A3A7D4'  # – tox-user
             )

prepare() {
  cd $_pkgname
  # disable -Werror for release packaging
  sed -e 's|-Werror||' -i CMakeLists.txt
  if [[ $CARCH == "arm" || $CARCH == "armv6h" ]]; then
    patch -p0 -i ../atomic.patch
  fi
}

build() {
  cd $_pkgname
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
          -DDESKTOP_NOTIFICATIONS=ON \
	  -DGIT_VERSION=$_gitver \
	  -DGIT_DESCRIBE=v$pkgver
  make
}

check() {
  cd $_pkgname
  cd build
  make test
}

package() {
  cd $_pkgname
  cd build
  make DESTDIR="$pkgdir" install
}
