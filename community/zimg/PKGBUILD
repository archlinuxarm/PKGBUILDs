# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-x86simd from configure

pkgname=zimg
pkgver=3.0
pkgrel=1
pkgdesc='Scaling, colorspace conversion, and dithering library'
arch=(x86_64)
url=https://github.com/sekrit-twc/zimg
license=(custom:WTFPL)
makedepends=(git)
provides=(libzimg.so)
options=(!emptydirs)
_tag=a91c96aba15d4a255fe4187f1f0c2364dd2da98e
source=(git+https://github.com/sekrit-twc/zimg.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd zimg

  git describe --tags | sed 's/^release-//'
}

prepare() {
  cd zimg

  ./autogen.sh
}

build() {
  cd zimg

  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

package(){
  make DESTDIR="${pkgdir}" -C zimg install
  install -Dm 644 zimg/COPYING -t "${pkgdir}"/usr/share/licenses/zimg/
}

# vim: ts=2 sw=2 et:
