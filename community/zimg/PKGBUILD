# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-x86simd from configure

pkgname=zimg
pkgver=2.7.3
pkgrel=1
pkgdesc='Scaling, colorspace conversion, and dithering library'
arch=('x86_64')
url='https://github.com/sekrit-twc/zimg'
license=('custom:WTFPL')
depends=('gcc-libs' 'glibc')
provides=('libzimg.so')
options=('!emptydirs')
source=("zimg-${pkgver}.tar.gz::https://github.com/sekrit-twc/zimg/archive/release-${pkgver}.tar.gz")
sha256sums=('c0b7d719338d86bf69c688fcea451a6d81d93551fa9e305504f7e13bdbf7046c')

build() {
  cd zimg-release-${pkgver}

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --disable-static
  make
}

package(){
  cd zimg-release-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/zimg/
}

# vim: ts=2 sw=2 et:
