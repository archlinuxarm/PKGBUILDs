# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-x86simd from configure

pkgname=zimg
pkgver=2.0.3
pkgrel=1
pkgdesc='Scaling, colorspace conversion, and dithering library'
arch=('i686' 'x86_64')
url='https://github.com/sekrit-twc/zimg'
license=('custom:WTFPL')
depends=('gcc-libs' 'glibc')
provides=('libzimg.so')
options=('!emptydirs')
source=("zimg-${pkgver}.tar.gz::https://github.com/sekrit-twc/zimg/archive/release-${pkgver}.tar.gz")


build() {
  cd zimg-release-${pkgver}

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --disable-static
  make
}

package(){
  cd zimg-release-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/vapoursynth
  mv "${pkgdir}"/usr/lib/{zimg/,vapoursynth/lib}vszimg.so

  install -dm 755 "${pkgdir}"/usr/share/licenses/zimg
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/zimg/
}

# vim: ts=2 sw=2 et:
sha256sums=('521b109348d4570e5b9a313b8549caaf882062740f4a20acd036d033149ae125')
