# Maintainer: David Runge <dvzrv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !distcc, build checks for clang first

pkgname=roc-toolkit
pkgver=0.2.4
pkgrel=1
pkgdesc="Real-time audio streaming over the network"
arch=(x86_64)
url="https://github.com/roc-streaming/roc-toolkit/"
license=(
  MPL2
  custom:CC0
)
depends=(
  glibc
  gcc-libs
  libunwind
  libuv
)
makedepends=(
  alsa-lib
  cpputest
  gengetopt
  libpulse
  openfec
  ragel
  scons
  sox
  speexdsp
)
options=(!distcc)
optdepends=(
  'libpulse: for roc-conv, roc-receive and roc-send'
  'sox: for roc-conv, roc-receive and roc-send'
)
provides=(libroc.so)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('aed11c0e4187b81f5db2d797c1e5396ba7a71cb70cc362e3b562a8089242a04b4c20d68fc4dca37d74c063b61dd6b5b11c98ec31024c7d758f56f738f87db308')
b2sums=('f34e7185d4645b4b8077e982afaf6b145253ba9b950c0380fb3ed5215c4d6c53c7c6a6009150a9dc3551773c13d2fb381439ca54281a43f7a0caad1b0bd7f564')

build() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --disable-openssl  # disable as it is not yet used
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons "${scons_options[@]}"
}

check() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --disable-openssl  # disable as it is not yet used
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons test "${scons_options[@]}"
}

package() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
  )
  depends+=(
    openfec libopenfec.so
    speexdsp libspeexdsp.so
  )

  cd $pkgname-$pkgver
  scons DESTDIR="$pkgdir/" "${scons_options[@]}" install

  install -vDm 644 3rdparty/hedley/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/hedley.COPYING"
}
