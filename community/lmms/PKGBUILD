# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove wine makedepend and optdepend
#  - remove carla makedepend
#  - remove -DWINE_INCLUDE_DIR and -DWINE_LIBRARY from cmake command

_pkgver=1.2.0-rc7
pkgname=lmms
pkgver=${_pkgver/-}
pkgrel=1
pkgdesc='The Linux MultiMedia Studio'
arch=('x86_64')
url="https://lmms.io"
license=('GPL2')
groups=('pro-audio')
depends=('fftw' 'fltk' 'fluidsynth' 'lame' 'libgig' 'libsamplerate'
'qt5-x11extras' 'sdl' 'stk')
makedepends=('cmake' 'doxygen' 'extra-cmake-modules' 'freetype2' 'git'
'ladspa' 'qt5-tools')
optdepends=('pulseaudio: pulseaudio support')
source=("git+https://github.com/${pkgname}/${pkgname}.git#tag=v${_pkgver}"
        'git+https://github.com/Lukas-W/qt5-x11embed.git'
        'git+https://github.com/rampantpixels/rpmalloc.git')
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  mkdir -vp build
  git submodule init
  git config submodule.src/3rdparty/qt5-x11embed.url "${srcdir}/qt5-x11embed"
  git config submodule.src/3rdparty/rpmalloc.url "${srcdir}/rpmalloc"
  git submodule update
  # setting lib dir
  sed -e 's|lib64|lib|g' -i cmake/modules/DetectMachine.cmake
  # setting proper DESTDIR based install path for bash-completion
  sed -e 's/\(${BASHCOMP_USER\)/\\$DESTDIR\1/g' -i cmake/modules/BashCompletion.cmake
}

build() {
  cd "${pkgname}-${pkgver}/build"
  export LDFLAGS="${LDFLAGS}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DWANT_QT5=ON \
        -DWANT_SOUNDIO=OFF \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_MODULE_PATH=/usr/share/ECM/find-modules \
        -DCMAKE_PREFIX_PATH=/usr/include/wine/windows \
        ..
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
