# $Id: PKGBUILD 80173 2012-11-17 12:51:55Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove wine makedepend, no vst

pkgname=lmms
pkgver=0.4.14
pkgrel=1
pkgdesc='The Linux MultiMedia Studio.'
arch=('i686' 'x86_64')
url='http://lmms.sourceforge.net/'
license=('GPL')
depends=('sdl' 'glib2' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'qt4' 'sdl_sound'
         'libsamplerate' 'shared-mime-info')
optdepends=('fftw: SpectrumAnalyzer plugin'
            'stk: STK instruments plugins'
            'pulseaudio: PulseAudio output')
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2' 'fftw' 'stk')
[[ $CARCH = i686 ]] && makedepends+=('wine')
provides=('lmms-extras')
conflicts=('lmms-extras')
options=('!libtool' '!makeflags')
install=lmms.install
source=("http://downloads.sourceforge.net/sourceforge/lmms/${pkgname}-${pkgver}.tar.bz2"
        zynaddsubfx_unlink.patch)
md5sums=('a30e4fcc24c117a1fb4fd7bef891f7e8'
         'd2ea78ac0cf2979505f140c6845617cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake

  patch -Np1 -i ../zynaddsubfx_unlink.patch

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
