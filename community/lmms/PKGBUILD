# $Id: PKGBUILD 80173 2012-11-17 12:51:55Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove wine makedepend, no vst

pkgname=lmms
pkgver=1.0.2
pkgrel=1
pkgdesc='The Linux MultiMedia Studio.'
arch=('i686' 'x86_64')
url='http://lmms.sourceforge.net/'
license=('GPL')
depends=('sdl' 'glib2' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'qt4' 'sdl_sound'
         'libsamplerate' 'shared-mime-info' 'fltk')
optdepends=('fftw: SpectrumAnalyzer plugin'
            'stk: STK instruments plugins'
            'pulseaudio: PulseAudio output')
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2' 'fftw' 'stk')
provides=('lmms-extras')
conflicts=('lmms-extras')
options=('!makeflags')
install=lmms.install
source=("${pkgname}-${pkgver}-src.tar.bz2::http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tar.bz2/download"
        lmms-1.0.2-build-fix.patch
        zynaddsubfx_unlink.patch)
md5sums=('db1c3e432cad933c9fd124c4840c0bf1'
         '2ff166311a02cf6220dda5cac660528a'
         'd2ea78ac0cf2979505f140c6845617cf')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ../lmms-1.0.2-build-fix.patch
  patch -Np1 -i ../zynaddsubfx_unlink.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
