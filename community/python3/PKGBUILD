# $Id: PKGBUILD 1765 2009-08-31 03:38:54Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python3
pkgver=3.1.1
pkgrel=1
_pybasever=3.1
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org"
depends=('expat' 'bzip2' 'gdbm' 'openssl>=0.9.8i' 'zlib')
makedepends=('tk>=8.5.5' 'sqlite3')
optdepends=('tk: for tkinter, pynche and modulator')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
        python-internal-expat.patch
        python-gdbm.patch)
md5sums=('d1ddd9f16e3c6a51c7208f33518cd674'
         '993800dbcc0c4f199c1ceee998a06e71'
         '16af6360d8524de8f86f72c22a7165ec')

build() {
  cd ${srcdir}/Python-${pkgver}

  patch -Np0 -i ${srcdir}/python-internal-expat.patch
  patch -Np1 -i ${srcdir}/python-gdbm.patch

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --enable-shared --with-threads --with-computed-gotos \
              --enable-unicode --with-wide-unicode

  make || return 1
  make DESTDIR=${pkgdir} install

  ln -sf python${_pybasever} ${pkgdir}/usr/bin/python3
  ln -sf python${_pybasever}-config ${pkgdir}/usr/bin/python3-config

  # Fix conflicts with python
  rm ${pkgdir}/usr/bin/2to3

  # license
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
