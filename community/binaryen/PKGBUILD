# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - only move /usr/lib64 in package for v8

pkgname=binaryen
pkgver=84
pkgrel=1
pkgdesc="Compiler infrastructure and toolchain library for WebAssembly, in C++"
arch=('x86_64')
url="https://github.com/WebAssembly/binaryen"
license=('MIT')
depends=()
makedepends=(cmake ninja python)
source=("https://github.com/WebAssembly/binaryen/archive/version_${pkgver}.tar.gz"
        "binaryen.sh")
sha384sums=('450fe29c997273f7218ea72e3c8348509e8f16e69f6c0f3cc99e58c9d7591c9850b76af7bf1080aba6d1ff93c7e930d9'
            'fdf76d46e6ab9b915212bbca7c29e624e209de911b18ecaccc40b99a00360cd12b261f96c8655f02ec4fd28cedffbcac')

prepare() {
    mkdir binaryen-version_${pkgver}/build
}

build() {
    cd binaryen-version_${pkgver}/build

    cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ..
    ninja
}

package() {
    cd binaryen-version_${pkgver}/build
    DESTDIR=${pkgdir} ninja install
    [[ $CARCH == "aarch64" ]] && mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib

    install -Dm755 $srcdir/binaryen.sh $pkgdir/etc/profile.d/binaryen.sh
    install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
