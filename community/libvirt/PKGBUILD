# ${Id}: PKGBUILD 274316 2017-12-13 22:56:42Z anatolik ${}
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove numactl from depends, doesn't build on ARM
#  - drop ceph depend until it builds
#  - removed qemu makedepend

pkgname=libvirt
pkgver=4.4.0
pkgrel=1
pkgdesc="API for controlling virtualization engines (openvz,kvm,qemu,virtualbox,xen,etc)"
arch=('x86_64')
url="http://libvirt.org/"
license=('LGPL')
makedepends=('pkgconfig' 'lvm2' 'linux-api-headers' 'dnsmasq' 'lxc'
	     'libiscsi' 'open-iscsi' 'perl-xml-xpath' 'libxslt'
       'glusterfs' 'netcf' 'yajl' 'libx11' 'libpciaccess' 'parted')
options=('emptydirs')
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
source=("https://libvirt.org/sources/${pkgname}-${pkgver}.tar.xz"{,.asc}
	'libvirtd.conf.d'
	'libvirtd-guests.conf.d'
	'libvirt.sysusers.d')
#	"ae102b5d7bccd29bc6015a3e0acefeaa90d097ac.patch::https://libvirt.org/git/?p=libvirt.git;a=patch;h=ae102b5d7bccd29bc6015a3e0acefeaa90d097ac")
sha512sums=('725b9b093a2aaeab6fdcb8df341a400ab363bcd50af8400dfd0593c41d8fbc8352f89147be9fbfde2e8418cc7298e090fe07ec7255b0aff1f0d001b93322f93f'
            'SKIP'
            'fc0e16e045a2c84d168d42c97d9e14ca32ba0d86025135967f4367cf3fa663882eefb6923ebf04676ae763f4f459e5156d7221b36b47c835f9e531c6b6e0cd9d'
            'ef221bae994ad0a15ab5186b7469132896156d82bfdc3ef3456447d5cf1af347401ef33e8665d5b2f76451f5457aee7ea01064d7b9223d6691c90c4456763258'
            '7d1d535aaf739a6753f6819c49272c8d9b5f488e0a8553797499334a76b8631474e222b6048f2125b858e5ecc21e602face45dd02121f833d605b9ae58322982')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' "${file}"
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' "${file}"
  done

  sed -i 's|/sysconfig/|/conf.d/|g' \
    src/remote/libvirtd.service.in \
    tools/{libvirt-guests.service,libvirt-guests.sh,virt-pki-validate}.in \
    src/locking/virtlockd.service.in
  sed -i 's|@sbindir@|/usr/bin|g' src/locking/virtlockd.service.in
  sed -i 's|/usr/libexec/qemu-bridge-helper|/usr/lib/qemu/qemu-bridge-helper|g' \
    src/qemu/qemu{.conf,_conf.c} \
    src/qemu/test_libvirtd_qemu.aug.in

  sed -i 's|libsystemd-daemon|libsystemd|g' configure
  sed -i 's/notify/simple/' src/remote/libvirtd.service.in

#  patch -p1 -i "${srcdir}"/ae102b5d7bccd29bc6015a3e0acefeaa90d097ac.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=`which python2`
  export LDFLAGS=-lX11
  export RADVD=/usr/bin/radvd
  [ -f Makefile ] || ./configure --prefix=/usr --libexec=/usr/lib/"${pkgname}" --sbindir=/usr/bin \
	--with-storage-lvm --without-xen --with-udev --without-hal --disable-static \
	--with-init-script=systemd --with-storage-gluster \
	--with-qemu-user=nobody --with-qemu-group=kvm \
	--with-netcf --with-interface --with-lxc --with-storage-iscsi --with-storage-disk
	# --with-audit
  make
}

package_libvirt() {
  depends=('e2fsprogs' 'gnutls' 'iptables' 'libxml2' 'parted' 'polkit' 'python2'
     'avahi' 'yajl' 'libpciaccess' 'udev' 'dbus' 'libxau' 'libxdmcp' 'libpcap' 'libcap-ng'
     'curl' 'libsasl' 'libgcrypt' 'libgpg-error' 'openssl' 'libxcb' 'gcc-libs'
     'iproute2' 'libnl' 'libx11' 'gettext' 'libssh2'
     'netcf' 'fuse2')
  optdepends=('ebtables: required for default NAT networking'
        'dnsmasq: required for default NAT/DHCP for guests'
        'bridge-utils: for bridged networking'
        'netcat: for remote management over ssh'
        'radvd'
        'dmidecode'
        'parted')
  backup=('etc/conf.d/libvirt-guests'
    'etc/conf.d/libvirtd'
    'etc/libvirt/libvirt.conf'
    'etc/libvirt/virtlogd.conf'
    'etc/libvirt/libvirtd.conf'
    'etc/libvirt/lxc.conf'
    'etc/libvirt/nwfilter/allow-arp.xml'
    'etc/libvirt/nwfilter/allow-dhcp-server.xml'
    'etc/libvirt/nwfilter/allow-dhcp.xml'
    'etc/libvirt/nwfilter/allow-incoming-ipv4.xml'
    'etc/libvirt/nwfilter/allow-ipv4.xml'
    'etc/libvirt/nwfilter/clean-traffic.xml'
    'etc/libvirt/nwfilter/no-arp-ip-spoofing.xml'
    'etc/libvirt/nwfilter/no-arp-mac-spoofing.xml'
    'etc/libvirt/nwfilter/no-arp-spoofing.xml'
    'etc/libvirt/nwfilter/no-ip-multicast.xml'
    'etc/libvirt/nwfilter/no-ip-spoofing.xml'
    'etc/libvirt/nwfilter/no-mac-broadcast.xml'
    'etc/libvirt/nwfilter/no-mac-spoofing.xml'
    'etc/libvirt/nwfilter/no-other-l2-traffic.xml'
    'etc/libvirt/nwfilter/no-other-rarp-traffic.xml'
    'etc/libvirt/nwfilter/qemu-announce-self-rarp.xml'
    'etc/libvirt/nwfilter/qemu-announce-self.xml'
    'etc/libvirt/qemu-lockd.conf'
    'etc/libvirt/qemu.conf'
    'etc/libvirt/qemu/networks/default.xml'
    'etc/libvirt/virt-login-shell.conf'
    'etc/libvirt/virtlockd.conf'
    'etc/logrotate.d/libvirtd'
    'etc/logrotate.d/libvirtd.lxc'
    'etc/logrotate.d/libvirtd.qemu'
    'etc/logrotate.d/libvirtd.uml'
    'etc/sasl2/libvirt.conf')
  install="libvirt.install"
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/libvirtd.conf.d "${pkgdir}"/etc/conf.d/libvirtd
  install -D -m644 "${srcdir}"/libvirtd-guests.conf.d "${pkgdir}"/etc/conf.d/libvirt-guests
  install -D -m644 "${srcdir}"/libvirt.sysusers.d "${pkgdir}"/usr/lib/sysusers.d/libvirt.conf

  #chown 0:102 "${pkgdir}"/usr/share/polkit-1/rules.d
  #chmod 0750 "${pkgdir}"/usr/share/polkit-1/rules.d

  rm -rf \
	"${pkgdir}"/var/run \
	"${pkgdir}"/var/lib/libvirt/qemu \
	"${pkgdir}"/var/cache/libvirt/qemu \
	"${pkgdir}"/etc/sysconfig

  rm -f "${pkgdir}"/etc/libvirt/qemu/networks/autostart/default.xml
  #move glusterfs module
  mv "$pkgdir"/usr/lib/libvirt/storage-backend/libvirt_storage_backend_gluster.so "$pkgdir"/../
}

package_libvirt-glusterfs() {
  depends=("libvirt=$pkgver" 'glusterfs')

  mkdir -p "$pkgdir"/usr/lib/libvirt/storage-backend/
  cp "$pkgdir"/../libvirt_storage_backend_gluster.so "$pkgdir"/usr/lib/libvirt/storage-backend/libvirt_storage_backend_gluster.so
}
