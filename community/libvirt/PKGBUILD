# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove numactl from depends, doesn't build on ARM
#  - drop ceph depend until it builds
#  - removed qemu makedepend

pkgname=libvirt
pkgver=5.10.0
pkgrel=3
pkgdesc="API for controlling virtualization engines (openvz,kvm,qemu,virtualbox,xen,etc)"
arch=('x86_64')
url="https://libvirt.org/"
license=('LGPL')
makedepends=('lvm2' 'linux-api-headers' 'dnsmasq' 'lxc' 'libiscsi' 'open-iscsi'
             'perl-xml-xpath' 'libxslt' 'parted' 'python' 'rpcsvc-proto')
depends=('e2fsprogs' 'gnutls' 'iptables' 'libxml2' 'parted' 'polkit'
         'yajl' 'libpciaccess' 'udev' 'dbus' 'libxau' 'libxdmcp' 'libpcap'
         'libcap-ng' 'curl' 'libsasl' 'libgcrypt' 'libgpg-error' 'openssl'
         'libxcb' 'gcc-libs' 'iproute2' 'libnl' 'libx11' 'gettext'
         'libssh2' 'netcf' 'fuse2' 'glusterfs' 'libiscsi')
optdepends=('ebtables: required for default NAT networking'
            'dnsmasq: required for default NAT/DHCP for guests'
            'bridge-utils: for bridged networking'
            'netcat: for remote management over ssh'
            'radvd'
            'dmidecode'
            'parted')
backup=('etc/conf.d/libvirt-guests'
  'etc/conf.d/libvirtd'
  'etc/libvirt/libvirt.conf'
  'etc/libvirt/virtlogd.conf'
  'etc/libvirt/libvirtd.conf'
  'etc/libvirt/lxc.conf'
  'etc/libvirt/nwfilter/allow-arp.xml'
  'etc/libvirt/nwfilter/allow-dhcp-server.xml'
  'etc/libvirt/nwfilter/allow-dhcp.xml'
  'etc/libvirt/nwfilter/allow-incoming-ipv4.xml'
  'etc/libvirt/nwfilter/allow-ipv4.xml'
  'etc/libvirt/nwfilter/clean-traffic.xml'
  'etc/libvirt/nwfilter/no-arp-ip-spoofing.xml'
  'etc/libvirt/nwfilter/no-arp-mac-spoofing.xml'
  'etc/libvirt/nwfilter/no-arp-spoofing.xml'
  'etc/libvirt/nwfilter/no-ip-multicast.xml'
  'etc/libvirt/nwfilter/no-ip-spoofing.xml'
  'etc/libvirt/nwfilter/no-mac-broadcast.xml'
  'etc/libvirt/nwfilter/no-mac-spoofing.xml'
  'etc/libvirt/nwfilter/no-other-l2-traffic.xml'
  'etc/libvirt/nwfilter/no-other-rarp-traffic.xml'
  'etc/libvirt/nwfilter/qemu-announce-self-rarp.xml'
  'etc/libvirt/nwfilter/qemu-announce-self.xml'
  'etc/libvirt/nwfilter/clean-traffic-gateway.xml'
  'etc/libvirt/qemu-lockd.conf'
  'etc/libvirt/qemu.conf'
  'etc/libvirt/qemu/networks/default.xml'
  'etc/libvirt/virt-login-shell.conf'
  'etc/libvirt/virtlockd.conf'
  'etc/logrotate.d/libvirtd'
  'etc/logrotate.d/libvirtd.lxc'
  'etc/logrotate.d/libvirtd.qemu'
  'etc/sasl2/libvirt.conf')
options=('emptydirs')
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
source=("https://libvirt.org/sources/${pkgname}-${pkgver}.tar.xz"{,.asc}
        'libvirtd.conf.d'
        'libvirtd-guests.conf.d'
        'libvirt.sysusers.d'
        '0001-qemu_firmware-Pass-virDomainDef-into-qemuFirmwareFil.patch'
        '0002-qemu_firmware-Introduce-want-variable-to-qemuFirmwar.patch'
        '0003-src-Introduce-and-use-virDomainDefHasOldStyleUEFI-an.patch'
        '0004-qemu_firmware-Try-to-autofill-for-old-style-UEFI-spe.patch')
sha512sums=('bb518a6aa6731a7de4b24fc7b8ffdcd2cdd84dfe49047374e0f02265c274eb06dbe1732c9cc4954412853d36d9dd5c1124fb8a5e37f4c01654d85013773ffa23'
            'SKIP'
            'fc0e16e045a2c84d168d42c97d9e14ca32ba0d86025135967f4367cf3fa663882eefb6923ebf04676ae763f4f459e5156d7221b36b47c835f9e531c6b6e0cd9d'
            'ef221bae994ad0a15ab5186b7469132896156d82bfdc3ef3456447d5cf1af347401ef33e8665d5b2f76451f5457aee7ea01064d7b9223d6691c90c4456763258'
            '7d1d535aaf739a6753f6819c49272c8d9b5f488e0a8553797499334a76b8631474e222b6048f2125b858e5ecc21e602face45dd02121f833d605b9ae58322982'
            '58569ece7fd6ce69c4261523a985778f29e33e78c814ffb8ed674f34a32dc92aaaeb6d785ab507015f15a7480434bcfbf93b728b51539831d5fa8b4915f45a11'
            '4162a7a38d94cb98a2c9a318fb7d7b9b8dc37a07762e8f6c8e2962c9961230f901fe354c80ea83dfe6d0d8cdda5f706a7cf20fcbcaf916f854755ea9a00e435b'
            '64334505c906a256852d7d56715ce259e2881e147b290ca321d73043dd683d594b291245de36f97259b87c247852858e04e89315f929234236bb8abc39c284f2'
            '8908557893efe6f53d15907be059a410f99e4f6ffd5211979978f30ab21faaeabaf80ff98e45eef2802540dd688c862b3bea22555b968651368c629c53da39fa')
changelog=Changelog

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  #local src
  #for src in "${source[@]}"; do
  #  src="${src%%::*}"
  #  src="${src##*/}"
  #  [[ $src = *.patch ]] || continue
  #  msg2 "Applying patch $src..."
  #  patch -Np1 < "../$src"
  #done

  # FS#64175
  patch -p1 < ../0001-qemu_firmware-Pass-virDomainDef-into-qemuFirmwareFil.patch
  patch -p1 < ../0002-qemu_firmware-Introduce-want-variable-to-qemuFirmwar.patch
  patch -p1 < ../0003-src-Introduce-and-use-virDomainDefHasOldStyleUEFI-an.patch
  patch -p1 < ../0004-qemu_firmware-Try-to-autofill-for-old-style-UEFI-spe.patch

  autoreconf -vi

  sed -i 's|/sysconfig/|/conf.d/|g' \
    src/remote/libvirtd.service.in \
    tools/{libvirt-guests.service,libvirt-guests.sh,virt-pki-validate}.in \
    src/locking/virtlockd.service.in
  sed -i 's|@sbindir@|/usr/bin|g' src/locking/virtlockd.service.in
  sed -i 's|/usr/libexec/qemu-bridge-helper|/usr/lib/qemu/qemu-bridge-helper|g' \
    src/qemu/qemu{.conf,_conf.c} \
    src/qemu/test_libvirtd_qemu.aug.in

  sed -i 's|libsystemd-daemon|libsystemd|g' configure
  sed -i 's/notify/simple/' src/remote/libvirtd.service.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=$(command -v python)
  export LDFLAGS=-lX11
  export RADVD=/usr/bin/radvd
  mkdir build && cd build
  ZFS=/usr/bin/zfs ZPOOL=/usr/bin/zpool ../configure \
    --prefix=/usr \
    --libexec=/usr/lib/"${pkgname}" \
    --sbindir=/usr/bin \
    --disable-static \
    --with-init-script=systemd \
    --with-qemu-user=nobody \
    --with-qemu-group=kvm \
    --without-hal \
    --with-interface \
    --with-lxc \
    --with-netcf \
    --with-udev \
    --with-storage-disk \
    --with-storage-gluster \
    --with-storage-iscsi \
    --with-storage-lvm \
    --with-storage-zfs \
    --with-runstatedir=/run
    # --with-audit
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/libvirtd.conf.d "${pkgdir}"/etc/conf.d/libvirtd
  install -D -m644 "${srcdir}"/libvirtd-guests.conf.d "${pkgdir}"/etc/conf.d/libvirt-guests
  install -D -m644 "${srcdir}"/libvirt.sysusers.d "${pkgdir}"/usr/lib/sysusers.d/libvirt.conf

  chown 0:102 "${pkgdir}"/usr/share/polkit-1/rules.d
  chmod 0750 "${pkgdir}"/usr/share/polkit-1/rules.d

  rm -rf \
    "${pkgdir}"/var/run \
    "${pkgdir}"/var/lib/libvirt/qemu \
    "${pkgdir}"/var/cache/libvirt/qemu \
    "${pkgdir}"/etc/sysconfig

  rm -f "${pkgdir}"/etc/libvirt/qemu/networks/autostart/default.xml

  # Fix permission Fix #61977
  chmod 600 "${pkgdir}"/etc/libvirt/nwfilter/*.xml "${pkgdir}/etc/libvirt/qemu/networks/default.xml"

  # Fix firewalld rules Fix #62219
  sed -i "s|<rule priority='32767'><reject/></rule>|#<rule priority='32767'><reject/></rule>|" "${pkgdir}/usr/lib/firewalld/zones/libvirt.xml"
}
