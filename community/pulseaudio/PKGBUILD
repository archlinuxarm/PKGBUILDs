# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pulseaudio
pkgver=0.9.15
pkgrel=1
pkgdesc="A networked sound server"
arch=('i686' 'x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=('consolekit' 'gdbm' 'hal' 'libasyncns' 'libcap' 'liboil' 'libsamplerate'
         'libtool' 'policykit' 'speex')
makedepends=('autoconf' 'avahi' 'bluez' 'gconf' 'intltool' 'jack-audio-connection-kit'
             'libatomic_ops' 'lirc-utils' 'pkgconfig' 'tcp_wrappers')
optdepends=('alsa-plugins: ALSA support'
            'avahi: network support'
            'bluez: bluetooth support'
            'gconf'
            'glib'
            'libxtst'
            'jack-audio-connection-kit: jack support'
            'lirc-utils: infra-red support'
            'tcp_wrappers: network support')
provides=('polypaudio')
conflicts=('polypaudio')
replaces=('polypaudio')
options=('emptydirs' '!libtool' '!makeflags')
backup=('etc/pulse/client.conf' 'etc/pulse/daemon.conf' 'etc/pulse/default.pa' 'etc/pulse/system.pa')
install=pulseaudio.install
source=(http://0pointer.de/lennart/projects/pulseaudio/${pkgname}-${pkgver}.tar.gz
        pulseaudio.conf pulseaudio.rc)
md5sums=('4510364eeab219fd100bd1b373b1a002' 'd4a7d4ad51b406588ba7ac7931c5dd88'
         'c5aa09c1d3e2217dc3bb23138e2a5a1d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -fi || return 1
  ./configure --libexecdir=/usr/lib \
              --localstatedir=/var \
              --prefix=/usr \
              --sysconfdir=/etc
  make || return 1
  make DESTDIR=${pkgdir} install

  install -Dm755 ${srcdir}/pulseaudio.rc ${pkgdir}/etc/rc.d/pulseaudio || return 1
  install -Dm644 ${srcdir}/pulseaudio.conf ${pkgdir}/etc/conf.d/pulseaudio || return 1
  install -dm755 ${pkgdir}/var/run/pulse || return 1

  # Ugly fix for pulseaudio system users
  install -dm755 ${pkgdir}/var/pulse || return 1
  echo -e '\n### Automatically restore volumes\nload-module module-volume-restore table="/var/pulse/volume-restore.table"' \
    >> ${pkgdir}/etc/pulse/system.pa || return 1
}

# vim:set ts=2 sw=2 et:
