# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Foster McLane <fkmclane@gmail.com>
# Contributor: Jonathan Thomas <jonathan@openshot.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=libopenshot-audio
pkgver=0.2.2
pkgrel=1
pkgdesc="A high-quality audio editing and playback library used by libopenshot."
arch=('x86_64')
url="https://openshot.org/"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'zlib')
makedepends=('alsa-lib' 'cmake' 'doxygen' 'freetype2' 'libx11' 'python')
provides=('libopenshot-audio.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenShot/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('9960c6225dd9b843d18134e851761bf12861220a2c63970055bc73d554725e56abd6d35339f7490b672faaf0699f58e3701ff8a966ab07af3264325a9d0dc472')
b2sums=('fcf242bdd678d2e79e2775f3aff7507520a4e8b9ddc7696e1506beca0c1f8e0687d66dfa35a31748e2e622def6e1c22c160e9424c1c27527854ebcafc24413ca')

build() {
  cd "${pkgname}-${pkgver}"
  [[ $CARCH == "armv6h" || $CARCH == "arm" ]] && echo "target_link_libraries(openshot-audio PUBLIC atomic)" >> CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {AUTHORS,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
