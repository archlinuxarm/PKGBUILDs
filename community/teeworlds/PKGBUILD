# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Armin 'IsSuE' Praher [issue.at.gmx.dot.at]
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>

pkgname=teeworlds
pkgver=0.5.1
_bamver=0.2.0
pkgrel=2
pkgdesc="a multiplayer 2D shooter"
arch=('i686' 'x86_64')
url="http://teeworlds.com"
license=('custom')
depends=('alsa-lib' 'mesa' 'sdl')
makedepends=('python')
replaces=('teewars')
source=(http://www.teeworlds.com/files/${pkgname}-${pkgver}-src.tar.gz
        bam-${_bamver}.tar.gz::http://teeworlds.com/trac/bam/browser/releases/bam-${_bamver}.tar.gz?format=raw
        teeworlds.desktop
        teeworlds.png
	fix_datadir_search.patch)
md5sums=('5f96e73ff4c3d552de55902da9255606'
         'd4efe74591a73c1cec8b34d76ffd1049'
         '3944aaf7a8c19a83510b2b39c4cddf5f'
         '44f39e398409232a0e0a431ed46d8cc8'
         '673791ecb74274346456f0fae53b4428')

build() {
  # Build bam (used to build teewars)
  # Now it is released separately I should make a separate package...
  cd $srcdir/bam-${_bamver} || return 1
  ./make_unix.sh || return 1

  # Build teewars
  cd $srcdir/${pkgname}-${pkgver}-src/
  # fix for FS 13118 which will be applied in next release
  patch -p1 -i ../fix_datadir_search.patch || return 1
  ../bam-${_bamver}/src/bam server_release client_release || return 1

  # Install data files  
  mkdir -p $pkgdir/usr/share/${pkgname}/data
  cp -r $srcdir/${pkgname}-${pkgver}-src/data/* \
    $pkgdir/usr/share/$pkgname/data || return 1

  install -Dm755 $srcdir/${pkgname}-${pkgver}-src/${pkgname} \
    $pkgdir/usr/bin/${pkgname} || return 1
  install -Dm755 $srcdir/${pkgname}-${pkgver}-src/${pkgname}_srv \
    $pkgdir/usr/bin/${pkgname}_srv || return 1
 
  install -Dm644 $srcdir/${pkgname}.desktop \
    $pkgdir/usr/share/applications/${pkgname}.desktop || return 1
  install -Dm644 $srcdir/${pkgname}.png \
    $pkgdir/usr/share/pixmaps/${pkgname}.png || return 1
  install -Dm644 $srcdir/${pkgname}-${pkgver}-src/license.txt \
    $pkgdir/usr/share/licenses/${pkgname}/license.txt || return 1
}
