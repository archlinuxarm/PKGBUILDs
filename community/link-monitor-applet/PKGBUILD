# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Roman Kyrylych <roman@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=link-monitor-applet
pkgver=3.0
pkgrel=1
pkgdesc="GNOME Panel Applet displaying the round-trip time to one or more hosts in a bar graph."
arch=('i686' 'x86_64')
url="http://www.nongnu.org/link-monitor/"
license=('GPL')
depends=('eel' 'gnome-panel' 'geoip' 'librsvg' 'gconf>=2.18.0.1-4')
makedepends=('intltool' 'gnome-doc-utils>=0.11.2')
install="link-monitor-applet.install"
source=(http://savannah.nongnu.org/download/link-monitor/$pkgname-$pkgver.tar.bz2)
options=('!emptydirs')
md5sums=('64d89b8b9bafa443d2aaa99f274a56d9')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./jb configure prefix=/usr sysconfdir=/etc localstatedir=/var \
    libexecdir=/usr/lib/link-monitor-applet destdir=$pkgdir install-gconf-schemas=no || return 1
  ./jb build || return 1
  ./jb install || return 1 

  mkdir -p $pkgdir/usr/share/gconf/schemas
  gconf-merge-schema $pkgdir/usr/share/gconf/schemas/${pkgname}.schemas \
    $pkgdir/etc/gconf/schemas/*.schemas
  rm -f $pkgdir/etc/gconf/schemas/*.schemas
}
