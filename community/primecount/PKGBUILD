# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DWITH_FLOAT128=OFF

pkgname=primecount
pkgver=6.3
pkgrel=1
pkgdesc="Fast C++ prime counting function implementation"
arch=(x86_64)
url="https://github.com/kimwalisch/primecount"
license=(BSD)
depends=(openmpi primesieve)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/kimwalisch/primecount/archive/v$pkgver.tar.gz")
sha256sums=('407333bb67683a13af5c5e9749649aef3dcc77f59c334a3f0c86b64ad5c1abf8')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_POPCNT=OFF \
    -DWITH_FLOAT128=OFF \
    -DWITH_MPI=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
