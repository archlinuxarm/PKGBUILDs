# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DWITH_FLOAT128=OFF

pkgname=primecount
pkgver=7.6
pkgrel=1
pkgdesc='Fast C++ prime counting function implementation'
arch=(x86_64)
url='https://github.com/kimwalisch/primecount'
license=(BSD)
depends=(primesieve)
makedepends=(cmake)
source=(https://github.com/kimwalisch/primecount/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e9a1fa2c41b9a7b84f2bead21b53cc9f7e2a5a0a34ddd818431a4e789aa44230')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_POPCNT=OFF \
    -DWITH_FLOAT128=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
