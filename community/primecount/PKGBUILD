# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DWITH_FLOAT128=OFF

pkgname=primecount
pkgver=6.0
pkgrel=1
pkgdesc="Fast C++ prime counting function implementation"
arch=(x86_64)
url="https://github.com/kimwalisch/primecount"
license=(BSD)
depends=(openmpi primesieve)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/kimwalisch/primecount/archive/v$pkgver.tar.gz")
sha256sums=('9f135e82c25e6ffa5223963dfc6978657966ac37c67d3ee6d10ab2362c889863')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_POPCNT=OFF \
    -DWITH_FLOAT128=OFF \
    -DWITH_MPI=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
