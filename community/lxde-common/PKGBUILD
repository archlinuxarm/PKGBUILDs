# $Id: PKGBUILD 53456 2011-08-03 20:00:26Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - rebuild for libpng - remove when bumped upstream

plugrel=1

pkgname=lxde-common
pkgver=0.5.5
pkgrel=1.1
pkgdesc="Common files of the LXDE Desktop"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('lxde')
depends=('libx11' 'glib2' 'lxde-icon-theme')
url="http://lxde.org/"
# startlxde is useless without lxsession 
makedepends=(automake)
optdepends=(lxsession-lite)
backup=(etc/xdg/lxsession/LXDE/{desktop.conf,autostart})
source=("http://downloads.sourceforge.net/lxde/${pkgname}-${pkgver}.tar.gz")
md5sums=('e51a6b2a815a89fda1f497b509465a97')


build() {
    cd "$srcdir/$pkgname-$pkgver"
    autoreconf
    ./configure --sysconfdir=/etc --prefix=/usr
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm644 lxde-logout.desktop \
      ${pkgdir}/usr/share/applications/lxde-logout.desktop
}
