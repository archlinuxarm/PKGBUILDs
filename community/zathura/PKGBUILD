# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix FTBFS on ARM

pkgname=zathura
pkgver=0.3.7
pkgrel=1

pkgdesc="Minimalistic document viewer"
url="http://pwmt.org/projects/zathura/"
arch=('i686' 'x86_64')
license=('custom')

makedepends=('python2-docutils' 'texlive-bin')
depends=('girara>=0.2.7' 'sqlite' 'desktop-file-utils' 'file' 'libsynctex')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support')

source=(https://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.gz
        bash-completion
        makefile.patch)

sha1sums=('51935ae1781712b9dc8baed7698c2fa900bcf4fa'
          '94a8236c483626a7f3acee053a1ea885aed45a82'
          'be57203c6709951635981936d0a7907db5672663')

prepare() {
  patch -p1 -d zathura-$pkgver -i ../makefile.patch
}

build() {
  cd zathura-$pkgver
  make WITH_SYNCTEX=1
}

package() {
  cd zathura-$pkgver

  make install DESTDIR="$pkgdir"
  install -D -m664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm0644 "$srcdir"/bash-completion "$pkgdir"/usr/share/bash-completion/completions/zathura
}
