# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v7 with -fPIC

pkgname=unshield
pkgver=1.3
pkgrel=2
pkgdesc="Extracts CAB files from InstallShield installers"
arch=('i686' 'x86_64')
url="https://github.com/twogood/unshield"
license=('custom')
depends=('zlib' 'openssl')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/twogood/unshield/archive/$pkgver.tar.gz")
md5sums=('13b716e0a3f45fe74ca24c6aaf4e5bb0')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  [[ $CARCH == "armv7h" ]] && CFLAGS+=" -fPIC" && CXXFLAGS+=" -fPIC"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    .
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/unshield/LICENSE
}
