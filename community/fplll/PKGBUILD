# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=fplll
pkgver=5.4.0
pkgrel=1
pkgdesc="Lattice algorithms using floating-point arithmetic"
arch=(x86_64)
url="https://github.com/fplll/fplll"
license=(LGPL)
depends=(mpfr qd)
source=("https://github.com/fplll/fplll/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('fe192a65a56439b098e26e3b7ee224dda7c2c73a58f36ef2cc6f9185ae8c482b')

build() {
  cd $pkgname-$pkgver
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
