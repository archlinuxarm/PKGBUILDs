# $Id: PKGBUILD 182562 2016-07-09 10:36:14Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: lagrange <flkazemakase@gmail.com>
# Contributor: mid-kid <esteve.varela@gmail.com>

# ALARM: Joseph Kogut <joseph.kogut@gmail.com>
# - patch to find libMali.so

pkgbase=python-kivy
pkgname=('python-kivy' 'python2-kivy')
pkgver=1.9.1
pkgrel=2
pkgdesc="A software library for rapid development of hardware-accelerated multitouch applications."
arch=('i686' 'x86_64')
url="http://kivy.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'gstreamer' 'sdl2_ttf'
             'sdl2_mixer' 'sdl2_image')
checkdepends=('python-nose' 'python2-nose' 'python2-mock' 'python-coverage' 'python2-coverage'
              'python-dbus' 'python2-dbus' 'python-gobject' 'python2-gobject' 'xorg-server-xvfb'
              'git' 'mtdev' 'xclip' 'xsel' 'gst-plugins-base' 'gst-plugins-good')
source=("https://pypi.python.org/packages/source/K/Kivy/kivy-$pkgver.tar.gz"
	"autodetect-libmali.patch")

sha256sums=('29bc45be34c26a8acb1dafdd329145f997a473be344cd052659f821f6478637e'
            '8261f9a9208ea26082a9fad1f836e2b1effa4d79e16933b3fbd5444458ee3642')

prepare() {
  # For better metadata
  export KIVY_USE_SETUPTOOLS=1

  export LC_CTYPE=en_US.UTF-8

  pushd "$srcdir"/kivy-$pkgver
  patch -p1 -i ../autodetect-libmali.patch
  popd

  cp -r kivy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/kivy-$pkgver
  python setup.py build build_ext --inplace

  cd "$srcdir"/kivy-$pkgver-py2
  python2 setup.py build build_ext --inplace
}

check() {
  cd "$srcdir"/kivy-$pkgver
  PYTHONPATH="$PWD:$PYTHONPATH" xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" make test

  cd "$srcdir"/kivy-$pkgver-py2
  PYTHONPATH="$PWD:$PYTHONPATH" xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" make PYTHON=python2 test
}

package_python-kivy() {
  depends=('python' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')

  cd kivy-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-kivy() {
  depends=('python2' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')

  cd kivy-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/share/kivy{,2}-examples
}
