# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: hm_b <holger@music-nerds.net>
# Contributor: Leslie P. Polzer <polzer@stardawn.org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=jack_capture
pkgver=0.9.73
pkgrel=6
pkgdesc="Simple command line tool to record JACK audio output to a file"
arch=('x86_64')
url="https://github.com/kmatheussen/jack_capture"
license=('GPL2')
groups=('pro-audio')
depends=('jack' 'lame' 'liblo')
optdepends=('meterbridge: levels monitor'
            'zenity: gtk2 gui')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kmatheussen/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('eecab2e26d5e83b65adf386d6fb38f2d9169cf1574e462ec455367f7d98f4f4d5a9d0766b9c572cfdcb43c55ae914bb8a2222267842c9912e2afe13c1f7b0038')

build() {
  cd "${pkgname}-${pkgver}"
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LDFLAGS+=" -latomic"
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make PREFIX='/usr' DESTDIR="${pkgdir}" install
  # gtk2 gui
  install -vDm 755 "${pkgname}_gui" "${pkgdir}/usr/bin/${pkgname}_gui"
  # doc
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
