# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=higan
pkgver=103
pkgrel=2
pkgdesc='Nintendo multi-system emulator'
arch=('i686' 'x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('alsa-lib' 'cairo' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'gtksourceview2' 'libao' 'libgl' 'libpulse' 'libx11' 'libxext' 'libxv'
         'openal' 'pango' 'sdl'
         'libudev.so')
makedepends=('mesa')
conflicts=('higan-gtk' 'higan-qt')
replaces=('higan-gtk' 'higan-qt')
install='higan.install'
source=("https://download.byuu.org/higan_v${pkgver}-source.7z"
        'higan-flags.patch'
        '0001-AArch64-fixes.patch')
sha256sums=('e63a6ab6a4131f9a05382c0c8c67d226aeaec2ec51f139260c87fa1f5e994276'
            '6ee3489a3cf3e8d09beaa5c1f809824593aa08b61dc9a8c9ddc12a09c89ae6cb'
            '2c5af5006f0438a7d7bfe62aa0c540b9e6ee526d45dfcafd65bb8598d50d83d7'
            '91739473c602d421533fb79c51311ad8d3a0acbbfb95b1b62d85feb2dd6fcb94')

prepare() {
  cd higan_v${pkgver}-source

  patch -Np1 -i ../higan-flags.patch
  patch -Np1 -i ../0001-AArch64-fixes.patch
}

build() {
  cd higan_v${pkgver}-source

  make -C icarus
  make -C higan
}

package() {
  cd higan_v${pkgver}-source

  install -Dm 755 icarus/out/icarus -t "${pkgdir}"/usr/bin/
  install -Dm 755 higan/out/higan -t "${pkgdir}"/usr/bin/
  install -Dm 644 higan/data/higan.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 higan/data/higan.png -t "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' higan/systems "${pkgdir}"/usr/share/higan
}

# vim: ts=2 sw=2 et:
