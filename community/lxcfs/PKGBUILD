# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -D_FILE_OFFSET_BITS=64 to CPPFLAGS

pkgname=lxcfs
pkgver=5.0.0
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man' 'meson' 'python-jinja')
options=('emptydirs' 'debug')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc}
        "lxcfs-fix-man.patch::https://patch-diff.githubusercontent.com/raw/lxc/lxcfs/pull/524.patch"
        "lxcfs-fix-libdir.patch::https://patch-diff.githubusercontent.com/raw/lxc/lxcfs/pull/527.patch"
        "lxcfs.service")
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('d131b2284437165eabdcdf3c392b96fc95e6ee4f7bf04a7e9999f63997a995ee'
            'SKIP'
            '5aaab31a34994c4496d7c1fa8d6e885c6c5d68e31f90bb04223da8d62afde5bc'
            'ff88ab96041e63aaede9af280d7ee7d847ae6ba8ec98832e2f42267ee5ed46bc'
            '3549e83ebbd6ec5934be0c3137b0dbb1101cec098bc49d135fea29f733b9e2a1')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 < "$srcdir/lxcfs-fix-man.patch"
    patch -Np1 < "$srcdir/lxcfs-fix-libdir.patch"
}

build() {
    cd "$pkgname-$pkgver"
    CPPFLAGS+=" -D_FILE_OFFSET_BITS=64"
    arch-meson ../build
    meson compile -C ../build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" meson install -C ../build
    install -dm0755 "$pkgdir"/var/lib/lxcfs
    install -Dm0644 "$srcdir"/lxcfs.service "$pkgdir"/usr/lib/systemd/system/lxcfs.service
}
