# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -D_FILE_OFFSET_BITS=64 to CPPFLAGS

pkgname=lxcfs
pkgver=5.0.3
pkgrel=2
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man' 'meson' 'python-jinja' 'systemd')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc}
	https://github.com/lxc/lxcfs/commit/4ab95514ad1042b124cbcd039a691fb3156ae840.patch)
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('fa0923539c1275c000879483f727326f5d65ab548f57e501140cd3c1ea0c5395'
            'SKIP'
            'be123820fe62dcc4edb5bb0f8898751f56fe6b52f306c5fd89d71f73434abf92')

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 <"$srcdir"/4ab95514ad1042b124cbcd039a691fb3156ae840.patch
}

build() {
    cd "$pkgname-$pkgver"
    CPPFLAGS+=" -D_FILE_OFFSET_BITS=64"
    arch-meson ../build
    meson compile -C ../build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" meson install -C ../build
    install -dm0755 "$pkgdir"/var/lib/lxcfs
}
