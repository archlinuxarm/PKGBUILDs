# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -D_FILE_OFFSET_BITS=64 to CPPFLAGS

pkgname=lxcfs
pkgver=4.0.12
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man')
options=('emptydirs' 'debug')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc}
	"lxcfs.service")
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('f502b7c99b5013e58f7d5c4117778c0bffe98532a42e023308eb72a287f0c969'
            'SKIP'
            '3549e83ebbd6ec5934be0c3137b0dbb1101cec098bc49d135fea29f733b9e2a1')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    CPPFLAGS+=" -D_FILE_OFFSET_BITS=64"
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-pamdir=/usr/lib/security \
        --localstatedir=/var
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -dm0755 "$pkgdir"/var/lib/lxcfs
    install -Dm0644 "$srcdir"/lxcfs.service "$pkgdir"/usr/lib/systemd/system/lxcfs.service
}
