# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -D_FILE_OFFSET_BITS=64 to CPPFLAGS

pkgname=lxcfs
pkgver=5.0.2
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man' 'meson' 'python-jinja' 'systemd')
options=('emptydirs' 'debug')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc}
        "lxcfs.service")
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('c3df03629ee8955de60a28da7490350ad1eb7aa3134b6917d40ec68ef092d039'
            'SKIP'
            '3549e83ebbd6ec5934be0c3137b0dbb1101cec098bc49d135fea29f733b9e2a1')

prepare() {
    cd "$pkgname-$pkgver"
#    patch -p1 <../lxcfs-5.0.1-fix-reinitialization-with-fuse3.patch
}

build() {
    cd "$pkgname-$pkgver"
    CPPFLAGS+=" -D_FILE_OFFSET_BITS=64"
    arch-meson ../build
    meson compile -C ../build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" meson install -C ../build
    install -dm0755 "$pkgdir"/var/lib/lxcfs
    install -Dm0644 "$srcdir"/lxcfs.service "$pkgdir"/usr/lib/systemd/system/lxcfs.service
}
