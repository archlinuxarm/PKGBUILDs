# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Tilman Blumenbach <tilman (at) ax86 (dot) net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove libsmbios depend

pkgname=netperf
pkgver=2.7.0
pkgrel=3
pkgdesc='Benchmarking tool for many different types of networking'
url='https://github.com/HewlettPackard/netperf/'
license=('custom')
arch=('i686' 'x86_64')
depends=('glibc' 'lksctp-tools')
source=("https://github.com/HewlettPackard/netperf/archive/$pkgname-$pkgver.tar.gz"
        "$pkgname.service"
        '001-fix-inlining.patch')
md5sums=('e0d45b5bca1eee2aef0155de82366202'
         '7bce5056b2a24d76fcc0c20e4b527416'
         '0160c47c86ff1fca8df64cfb4cd4d9f2')

prepare() {
  cd netperf-netperf-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
}

build() {
  cd netperf-netperf-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-demo \
    --enable-unixdomain \
    --enable-dccp \
    --enable-sctp
  make
}

package() {
  cd netperf-netperf-$pkgver
  make DESTDIR="$pkgdir" install
  # license
  install -D -m 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  # systemd
  install -D -m 644 ../$pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 et:
