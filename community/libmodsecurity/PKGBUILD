# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with -fsigned-char

pkgname=libmodsecurity
pkgver=20170611
pkgrel=1
_commit=20134ef242f374fd37f3224ea98cecded9abd2e2

pkgdesc='ModSecurity v3 library component'
arch=('i686' 'x86_64')
depends=('yajl' 'curl' 'libxml2' 'pcre' 'geoip')
makedepends=('git')
url="https://github.com/SpiderLabs/ModSecurity/tree/v3/master"
license=('APACHE')
source=("git+https://github.com/SpiderLabs/ModSecurity.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
	cd "$srcdir"/ModSecurity
	git submodule update --init
	CXXFLAGS+=" -fsigned-char"
}

build() {
	cd "$srcdir"/ModSecurity
	./build.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir"/ModSecurity
	make DESTDIR="$pkgdir" install
}
