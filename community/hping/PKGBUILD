# $Id: PKGBUILD 57877 2011-11-04 07:45:07Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kevin Piche <kevin@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added ARM and AArch64 to bytesex.h.patch

pkgname=hping
pkgver=3.0.0
pkgrel=3
pkgdesc='A command-line oriented TCP/IP packet assembler/analyzer.'
arch=('i686' 'x86_64')
url='http://www.hping.org'
license=('GPL2' 'BSD')
depends=('libpcap')
source=("http://www.hping.org/hping3-20051105.tar.gz"
        'Makefile.patch'
        'bytesex.h.patch')
md5sums=('ca4ea4e34bcc2162aedf25df8b2d1747'
         '339a38f86169d1b9344287a85201afbe'
         '27f390e387b95bc6e7316e4e6616a9a6')

build() {
  cd "${srcdir}/hping3-20051105"

  patch -Np1 -i ../bytesex.h.patch

  MANPATH=/usr/share/man ./configure --no-tcl
  make
}

package() {
  cd "${srcdir}/hping3-20051105"

  patch -p1 < ../Makefile.patch

  make DESTDIR="${pkgdir}" install
  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
