# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=nestopia
pkgver=1.40h
pkgrel=2
pkgdesc='An NES emulator featuring cycle exact emulation, a ridiculous number of mappers, and lots of custom sound chips.'
url='http://rbelmont.mameworld.info/?page_id=200'
license=('GPL')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.12' 'alsa-lib' 'gtk2>=2.4' 'mesa')
makedepends=('unzip')
# rbelmont.mameworld.info blocks some user-agents
DLAGENTS=('http::/usr/bin/wget -c -t 3 --waitretry=3 --user-agent=Mozilla/5.0')
source=("http://downloads.sourceforge.net/${pkgname}/Nestopia${pkgver//[.a-z]/}src.zip"
	"http://rbelmont.mameworld.info/nst${pkgver//[a.a-z]/}_lnx_release_${pkgver:${#pkgver}-1:1}.zip"
	"nestopia"
	"nestopia_bogus_error_on_save_settings.patch")
md5sums=('526c99a06d2b257135e7047b0ed95ae0'
         'f9a9a905bada67e11dac1364612d0b35'
         'abc2f030dd291f58d65c9095ef024225'
         '0160078c9afcbb1a6ac389c3989929f7')

build() {
  cd ${srcdir}

  # remove a bogus error message on exit
  patch -p0 < nestopia_bogus_error_on_save_settings.patch

  make || return 1

  # Install the nestopia script that copies required files to ~/.nestopia
  install -Dm755 ${srcdir}/${pkgname} ${pkgdir}/usr/bin/${pkgname} || return 1

  # Older releases used a .sh file. Make a symlink to make everyone happy
  cd ${pkgdir}/usr/bin && \
  ln -s ${pkgname} ${pkgname}.sh || return 1

  cd ${srcdir}

  # Install the required files and executable in /usr/share
  install -Dm755 nst ${pkgdir}/usr/bin/${pkgname}-bin && \
  install -dm775 ${pkgdir}/usr/share/${pkgname}/ && \
  install -m644 nstcontrols ${pkgdir}/usr/share/${pkgname}/ && \
  install -m644 NstDatabase.xml ${pkgdir}/usr/share/${pkgname}/
}
