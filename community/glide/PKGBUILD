# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: fanningert <thomas@fanninger.at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with go instead of gcc-go

pkgname=glide
pkgver=0.13.2
pkgrel=1
pkgdesc='Dependency management and vendoring for Go projects'
arch=('x86_64')
url='https://github.com/Masterminds/glide'
license=('MIT')
makedepends=('go' 'git')
options=('!strip' '!emptydirs')
source=("git+$url#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  mkdir -p build/go && cd build/go
  for f in "/usr/lib/go/"*; do ln -s "$f"; done
  rm pkg && mkdir pkg && cd pkg
  for f in "/usr/lib/go/pkg/"*; do ln -s "$f"; done
  export GOPATH="$srcdir/build"
  export GOROOT="$GOPATH/go"
  mkdir -p "$GOPATH/src/${url#https://}"
  mv "$srcdir/$pkgname"/* "$GOPATH/src/${url#https://}"
}

build() {
  cd "$GOPATH/src/${url#https://}"

  go build -o glide -ldflags "-X main.version=$pkgver" glide.go
}

package() {
  cd "$GOPATH/src/${url#https://}"

  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/Masterminds/glide
# vim: ts=2 sw=2 et:
