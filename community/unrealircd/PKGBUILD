# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=3.2.8.1
pkgrel=1
pkgdesc="Open Source IRC Server."
arch=('i686' 'x86_64')
url="http://unrealircd.com"
license=('GPL2')
depends=(openssl curl)
conflicts=(ircd)
provides=(ircd)
backup=(etc/unrealircd/unrealircd.conf)
source=(http://unrealircd.icedslash.com/Unreal$pkgver.tar.gz
	unrealircd.rc)
md5sums=('7b741e94e867c0a7370553fd01506c66'
         '0d728240132a97a2cef61be9558750b1')

build() {
  mkdir -p $startdir/pkg/etc/unrealircd
  mkdir -p $startdir/pkg/usr/bin
  cd $startdir/src/Unreal3.2

  ./configure --with-showlistmodes \
      --enable-hub --enable-prefixaq --with-listen=5 \
      --with-dpath=/etc/unrealircd \
      --with-spath=/usr/bin \
      --with-nick-history=2000 --with-sendq=3000000 \
      --with-bufferpool=18 --with-hostname=archlinux \
      --with-permissions=0600 --with-fd-setsize=1024 \
      --enable-dynamic-linking \
      --enable-ziplinks \
      --enable-ssl \
      --enable-libcurl=/usr

  make || return 1
  make IRCDDIR=$startdir/pkg/etc/unrealircd BINDIR=$startdir/pkg/usr/bin install || return 1

  mv $startdir/src/Unreal3.2/src/ircd $startdir/pkg/usr/bin/unrealircd && \
  mv $startdir/src/Unreal3.2/doc/example.conf $startdir/pkg/etc/unrealircd/unrealircd.conf && \
  mkdir -p $startdir/pkg/usr/lib/unrealircd && \
  mv $startdir/pkg/etc/unrealircd/modules $startdir/pkg/usr/lib/unrealircd/ && \
  ln -s /usr/lib/unrealircd/modules $startdir/pkg/etc/unrealircd/modules && \
  install -D -m0755 $startdir/src/unrealircd.rc $startdir/pkg/etc/rc.d/unrealircd || return 1

  # log
  mkdir -p $startdir/pkg/var/log/unrealircd/ && \
  touch $startdir/pkg/var/log/unrealircd/ircd.log && \
  ln -s /var/log/unrealircd/ircd.log $startdir/pkg/etc/unrealircd/ircd.log || return 1

  find $startdir/pkg/usr -type f -exec chmod ugo+r {} \; && \
  find $startdir/pkg/usr -type d -exec chmod ugo+rx {} \;
}
