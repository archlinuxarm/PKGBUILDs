# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

# ALAMR: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix AArch64 FTBFS

pkgname=unrealircd
pkgver=4.0.0
pkgrel=4
pkgdesc="Open Source IRC Server"
arch=('i686' 'x86_64')
url="http://unrealircd.com"
license=('GPL2')
depends=('openssl' 'curl' 'c-ares')
conflicts=('ircd')
provides=('ircd')
backup=('etc/unrealircd/unrealircd.conf')
install=unrealircd.install
source=(https://www.unrealircd.org/unrealircd4/unrealircd-$pkgver.tar.gz
	unrealircd.service
	unrealircd.tmpfiles.d
	config.settings
	install.pl
	0001-run-autoreconf-on-TRE-library.patch)
md5sums=('ea5922554cda40e39a2d6fd7520284f1'
         '677d8de0bae770488a2c1730f9475a51'
         '3ec519ea7dbe99696eb6c51dfc0d382a'
         '11ce231048c92651e76fdeb58609d0ba'
         '3a23792b03420fc578cf6688b1574ae3'
         '28bd33be158f5c494601b6b3026803fa')

prepare() {
  cd $srcdir/unrealircd-$pkgver
  cp $srcdir/config.settings .
  sed -i \
    -e 's|$(INSTALL) -m 0700|$(INSTALL) -m 0755|g' \
    -e 's|$(INSTALL) -m 0600|$(INSTALL) -m 0644|g' \
    Makefile.in
  sed -i 's|ARG=" "|ARG="--with-pidfile=/run/unrealircd/ircd.pid "|' Config
  patch -p1 -i ../0001-run-autoreconf-on-TRE-library.patch
}

build() {
  cd $srcdir/unrealircd-$pkgver
  ./Config -quick
  make
}

package() {
  cd $srcdir/unrealircd-$pkgver

  export pkgdir
  make INSTALL=$srcdir/install.pl install
  mv $pkgdir/usr/unrealircd $pkgdir/etc/unrealircd/unrealircd
  cp $pkgdir/etc/unrealircd/examples/example.conf $pkgdir/etc/unrealircd/unrealircd.conf
  rm -rf $pkgdir/tmp

  install -Dm0644 $srcdir/unrealircd.service $pkgdir/usr/lib/systemd/system/unrealircd.service
  install -Dm0644 $srcdir/unrealircd.tmpfiles.d $pkgdir/usr/lib/tmpfiles.d/unrealircd.conf
}
