# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-sse
#  - explicitly link v5/v6 with libatomic

pkgname=audacity
pkgver=2.2.2
pkgrel=1
epoch=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="https://audacityteam.org"
license=('GPL2' 'CCPL')
groups=('pro-audio')
# upstream enquired via mail about:
# vendors/requires older version of portaudio
# vendors/doesn't detect system version of portmidi
depends=('libid3tag' 'libmad' 'libsoxr' 'lilv' 'portsmf' 'soundtouch' 'suil'
'twolame' 'vamp-plugin-sdk' 'wxgtk3')
makedepends=('cmake' 'ffmpeg')
optdepends=('ffmpeg: additional import/export capabilities')
source=("https://github.com/${pkgname}/${pkgname}/archive/Audacity-${pkgver}.tar.gz")
sha512sums=('311a5f3413e7faf40dc5639b2b9f8b0d03d1a410c50d894ca7abf3fe5733a29b710ccf7e3215d97dd9142e93c0ab5e13955dfd13146b03f927f74465459a6457')

prepare() {
  mv -v "${pkgname}-Audacity-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LIBS="-latomic"
  WX_CONFIG=wx-config-gtk3 LIBS=$LIBS ./configure --prefix="/usr" \
    --with-libsndfile="system" \
    --with-ffmpeg="system" \
    --with-expat="system" \
    --with-lame="system" \
    --with-libsoxr="system" \
    --with-portaudio \
    --with-portmidi \
    --with-libflac \
    --with-libid3tag \
    --with-libsamplerate \
    --with-sbsms \
    --with-soundtouch \
    --with-libtwolame \
    --with-libvorbis \
    --with-lv2 \
    --disable-sse
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
