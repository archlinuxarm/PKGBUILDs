# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-sse

pkgname=audacity
pkgver=2.2.1
pkgrel=2
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="https://audacityteam.org"
license=('GPL2' 'CCPL')
depends=('libmad' 'libid3tag' 'wxgtk3' 'lame' 'soundtouch'
         'ffmpeg' 'vamp-plugin-sdk' 'portsmf' 'twolame' 'suil' 'lilv') # sbsms - needs unreleased version
makedepends=('cmake')
source=("https://github.com/${pkgname}/${pkgname}/archive/Audacity-${pkgver}.tar.gz")
sha512sums=('5f3e17aa5f39210f1440f49e19835bc6f2b6ab1bb58cee989dd2aa46cbcaf82eaa01037a8ee9e3a419741a34ae00c0325da33f017d83be531654032797ac7edd')

prepare() {
  cd "${pkgname}-Audacity-${pkgver}"
  autoreconf -vi
}

build() {
  cd "${pkgname}-Audacity-${pkgver}"
  WX_CONFIG=wx-config-gtk3 ./configure --prefix="/usr" \
    --with-libsndfile="system" \
    --with-ffmpeg="system" \
    --with-expat="system" \
    --with-lame="system" \
    --with-libsoxr="system" \
    --with-libclac \
    --with-libid3tag \
    --with-libsamplerate \
    --with-sbsms \
    --with-soundtouch \
    --with-libtwolame \
    --with-libvorbis \
    --with-lv2 \
    --with-portaudio \
    --with-portmidi \
    --disable-sse
  make
}

package() {
  cd "${pkgname}-Audacity-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
