# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-sse
#  - explicitly link v5/v6 with libatomic

pkgname=audacity
pkgver=2.4.0
pkgrel=1
epoch=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="https://audacityteam.org"
license=('GPL2' 'CCPL')
groups=('pro-audio')
# upstream hard codes/vendors portaudio/portmidi
# https://github.com/audacity/audacity/issues/522
depends=('expat' 'gcc-libs' 'gdk-pixbuf2' 'glibc' 'libmad'
'libsoxr' 'soundtouch' 'wxgtk3')
makedepends=('alsa-lib' 'cmake' 'ffmpeg' 'flac' 'glib2' 'gtk3' 'jack' 'lame'
'libid3tag' 'libogg' 'libsndfile' 'libvorbis' 'lilv' 'lv2' 'portsmf' 'suil'
'twolame' 'vamp-plugin-sdk')
optdepends=('ffmpeg: additional import/export capabilities')
source=("https://github.com/${pkgname}/${pkgname}/archive/Audacity-${pkgver}.tar.gz")
sha512sums=('a9f926e3a539c3a052249a7a1e968b629a8a807fa2c74d940bd18df5744c36cb4f18b4b78c56fe7fbabce12ffa732cb413418a512cf91bb5c3c111766c5ba0e3')

prepare() {
  mv -v "${pkgname}-Audacity-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  # the cmake setup is still more complicated and error-prone than the
  # autotools setup:
  # https://github.com/audacity/audacity/issues/519
  # https://github.com/audacity/audacity/issues/520
  # https://github.com/audacity/audacity/issues/521
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LIBS="-latomic"
  WX_CONFIG=wx-config-gtk3 LIBS=$LIBS ./configure --prefix="/usr" \
    --with-libsndfile="system" \
    --with-ffmpeg="system" \
    --with-expat="system" \
    --with-lame="system" \
    --with-libsoxr="system" \
    --with-portaudio \
    --with-portmidi \
    --with-libflac \
    --with-libid3tag \
    --with-libsamplerate \
    --with-sbsms \
    --with-soundtouch \
    --with-libtwolame \
    --with-libvorbis \
    --with-lv2 \
    --disable-sse
  make
}

package() {
  depends+=('libFLAC.so' 'libFLAC++.so' 'libasound.so' 'libgdk-3.so'
  'libglib-2.0.so' 'libgobject-2.0.so' 'libgtk-3.so' 'libid3tag.so'
  'libjack.so' 'liblilv-0.so' 'libogg.so' 'libsndfile.so' 'libsuil-0.so'
  'libtwolame.so' 'libvamp-hostsdk.so' 'libvorbis.so' 'libvorbisenc.so'
  'libvorbisfile.so' 'libportSMF.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
