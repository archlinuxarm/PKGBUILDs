# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Pulphix <crimea.v@libero.it>

pkgname=gamin
pkgver=0.1.10
pkgrel=1
pkgdesc='Gamin is a file and directory monitoring system defined to be a subset of the FAM (File Alteration Monitor) system.'
url='http://www.gnome.org/~veillard/gamin'
license=('GPL')
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('python')
options=('libtool')
optdepends=('python: If you want to use the python module, that is.')
provides=('fam')
conflicts=('fam')
source=("http://www.gnome.org/~veillard/gamin/sources/${pkgname}-${pkgver}.tar.gz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --disable-static --with-threads \
       	  --disable-debug-api --disable-debug --libexecdir=/usr/lib/gamin
	make || return 1
	make DESTDIR="$pkgdir" install || return 1
}
md5sums=('b4ec549e57da470c04edd5ec2876a028')
