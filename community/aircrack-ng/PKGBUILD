# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch out NEON variants

pkgname=aircrack-ng
_pkgver=1.2
pkgver=${_pkgver//-/}
pkgrel=2
pkgdesc="Key cracker for the 802.11 WEP and WPA-PSK protocols"
arch=('x86_64')
url="https://www.aircrack-ng.org"
license=('GPL2')
depends=('openssl' 'sqlite' 'iw' 'net-tools' 'wireless_tools' 'ethtool')
conflicts=('aircrack-ng-scripts')
replaces=('aircrack-ng-scripts')
provides=('aircrack-ng-scripts')
source=(https://download.aircrack-ng.org/$pkgname-$_pkgver.tar.gz
        0001-no-neon.patch)
md5sums=('bb11ec14e1fe505d8d0d51cee0c54df9')

prepare() {
  cd $pkgname-$_pkgver
  if [[ $CARCH != "aarch64" ]]; then
    patch -p1 -i ../0001-no-neon.patch
  fi
}

build() {
  cd $pkgname-$_pkgver

  ./autogen.sh

  ./configure --prefix=/usr

  make sqlite=true experimental=true
}

package() {
  cd $pkgname-$_pkgver

  make DESTDIR="$pkgdir" sqlite=true experimental=true \
    bindir=/usr/bin sbindir=/usr/bin mandir=/usr/share/man/man1/ \
    smandir=/usr/share/man/man8 pkglibexecdir=/usr/lib/aircrack-ng install
}
md5sums=('bb11ec14e1fe505d8d0d51cee0c54df9'
         '061f085f09de3da97521d7a98b1ddfe3')
md5sums=('bb11ec14e1fe505d8d0d51cee0c54df9'
         'ffc5b66caa339222d0af56e615c94bd1')
