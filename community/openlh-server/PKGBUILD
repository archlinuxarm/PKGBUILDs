# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: Wilson Pinto JÃºnior <n3rd3x@guake-terminal.org>

pkgname=openlh-server
pkgver=0.2
pkgrel=1
pkgdesc="A Free OpenLanhouse Client, Server module"
arch=('i686' 'x86_64')
url="http://openlanhouse.org"
depends=('openlh-core' 'pygtk' 'gnome-python' 'python-sqlalchemy')
makedepends=('intltool')
license=('GPL3')
source=(http://openlanhouse.org/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('83f73617d24ac358ac7646fa5748738c')
install=openlh-server.install

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
  
  install -m755 -d ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

