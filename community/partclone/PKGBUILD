# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove fail-mbr from build

pkgname=partclone
pkgver=0.3.18
pkgrel=1
pkgdesc="Utilities to save and restore used blocks on a partition"
arch=('x86_64')
url="https://partclone.org"
license=('GPL')
depends=('progsreiserfs' 'ntfs-3g' 'nilfs-utils')
source=($pkgname-$pkgver.tar.gz::https://github.com/Thomas-Tsai/partclone/archive/$pkgver.tar.gz)
md5sums=('011fd357ceb590d6fc1b8d0e8d4652fc')

build() {
  cd $pkgname-$pkgver

  ./autogen
  sed -i 's/fail-mbr//' Makefile.in
  ./configure --prefix=/usr --enable-extfs --enable-reiserfs --enable-fat \
    --enable-hfsp --enable-btrfs --enable-ncursesw --enable-ntfs \
    --enable-exfat --enable-f2fs --enable-minix --enable-nilfs2 --enable-xfs \
    --sbindir=/usr/bin

  make
}

package() {
  cd $pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
