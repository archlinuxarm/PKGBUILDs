# $Id: PKGBUILD 90410 2013-05-12 09:03:51Z jsteel $
# Maintainer: Jonathan Steel <mail at jsteel dot org>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove fail-mbr from build

pkgname=partclone
pkgver=0.2.58
pkgrel=2
pkgdesc="Utilities to save and restore used blocks on a partition"
arch=('i686' 'x86_64')
url="http://partclone.org"
license=('GPL')
depends=('progsreiserfs' 'ntfs-3g')
source=(https://downloads.sourceforge.net/project/$pkgname/stable/$pkgver/${pkgname}_$pkgver.orig.tar.gz)
md5sums=('8d8edeab8a0ce430d2e00efcc25f4ce7')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  sed -i 's/fail-mbr//' Makefile.in
  ./configure --prefix=/usr --enable-extfs --enable-reiserfs --enable-fat \
    --enable-hfsp --enable-btrfs --enable-ncursesw --enable-ntfs --sbindir=/usr/bin

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir"/ install
}
