# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Philippe Proulx <pproulx@efficios.com>
# Contributor: Manuel Mendez <mmendez534 at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on numactl, configure with --disable-numa

pkgbase='lttng-ust'
pkgname=('lttng-ust' 'python-lttngust')
pkgver=2.11.0
pkgrel=2
arch=('x86_64')
url="https://lttng.org/"
license=('LGPL2.1' 'GPL2' 'MIT')
makedepends=('asciidoc' 'xmlto' 'libxml2' 'python' 'liburcu>=0.7.2' 'util-linux')
source=("https://lttng.org/files/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('fb7258961a7e913487806a7e0129c95becff8c631382b119ff1d78f95cdd7460ec24e62671642160fdb006a3a292b2b10f35c8424f0367ce7c6a41cbcca77a09'
            'SKIP')
validpgpkeys=('2A0B4ED915F2D3FA45F5B16217280A9781186ACF')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # build lttng-ust
    PYTHON="/usr/bin/python" ./configure \
        --prefix='/usr' \
        --enable-man-pages \
        --disable-numa
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}

package_lttng-ust() {
    pkgdesc="LTTng user space tracing libraries for LTTng"
    depends=('liburcu>=0.7.2' 'util-linux' 'python' 'numactl')
    optdepends=('babeltrace: trace viewer')
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-lttngust() {
    pkgdesc='LTTng-UST Python Bindings'
    depends=('lttng-ust' 'python')
    cd "${srcdir}/${pkgbase}-${pkgver}/python-lttngust"
    make DESTDIR="${pkgdir}" PYTHON="/usr/bin/python" install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
