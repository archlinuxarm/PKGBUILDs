# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fixes for AArch64 from debian

pkgname=hardinfo
pkgver=0.5.1
pkgrel=8
pkgdesc="A system information and benchmark tool."
arch=('x86_64')
url="https://github.com/lpereira/hardinfo"
license=('GPL2')
depends=('gtk2' 'libsoup')
options=('!emptydirs')
source=(http://sourceforge.net/projects/hardinfo.berlios/files/${pkgname}-${pkgver}.tar.bz2
        fixsensors.patch hardinfo.distro hardinfo-gnu89.patch
        aarch64.patch)
sha1sums=('983f0445aa60e02156bc5a5eaedeffb30b8e4d64'
          'd2151dce76dca238102937d0e15541f172bca530'
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c'
          '7807fb252ad8391b61dfe1c3883ecdc232814091'
          '4573fca8bb686b621596afe9d4ee79ac8f3acf4f')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/fixsensors.patch"
  sed -i 's|/usr/lib64|/usr/lib|' configure
  sed -i 's|lib64|lib|' binreloc.c
  sed -i 's|{ DB_PREFIX "debian_version",	"deb"  },|{ DB_PREFIX "hardinfo.distro",       "arch"  },{ DB_PREFIX "debian_version",	"deb"  },|' computer.h
  patch -p1 -i ../hardinfo-gnu89.patch # Fix build (Debian)
  ln -sf armv4l arch/linux/aarch64
  patch -p1 -i ../aarch64.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/hardinfo.distro" "${pkgdir}/etc/hardinfo.distro"
}
