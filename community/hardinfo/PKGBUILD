# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=hardinfo
pkgver=0.5.1
pkgrel=1
pkgdesc="A system information and benchmark tool."
arch=('i686' 'x86_64')
url="http://hardinfo.berlios.de/wiki/index.php/Main_Page"
license=('GPL2')
depends=('gtk2' 'libsoup>=2.4.0' 'gnutls>=2.4.1')
options=('!emptydirs')
source=(http://download.berlios.de/hardinfo/${pkgname}-${pkgver}.tar.bz2 \
        fixsensors.patch hardinfo.distro)
md5sums=('6fb38992e140f2fab16518ae1f38e188' '05c212db07b9f152cdea052ff7bb28fa'\
         'ab8a557d10f74bec5c94ed6cae34729c')
sha1sums=('983f0445aa60e02156bc5a5eaedeffb30b8e4d64'
          'd2151dce76dca238102937d0e15541f172bca530'
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../fixsensors.patch || return 1
  sed -i 's|/usr/lib64|/usr/lib|' configure || return 1
  ./configure --prefix=/usr || return 1
  sed -i 's|lib64|lib|' binreloc.c || return 1
  sed -i 's|/sbin/lsmod|/bin/lsmod|g' arch/linux/{parisc,ppc,m68k,x86,common,mips,sparc,x86_64}/modules.h || return 1
  sed -i 's|/sbin/lsmod|/bin/lsmod|g' arch/this/modules.h || return 1
  sed -i 's|{ DB_PREFIX "debian_version",	"deb"  },|{ DB_PREFIX "hardinfo.distro",       "arch"  },{ DB_PREFIX "debian_version",	"deb"  },|' computer.h || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 ../hardinfo.distro ${pkgdir}/etc/hardinfo.distro || return 1
}
