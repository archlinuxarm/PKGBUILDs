# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Gergely Imreh <imrehg@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't patch PATCH-added--msse2-to-compile-flags-for.patch

pkgname=io
pkgver=2015.11.11
pkgrel=7
pkgdesc='IO programming language'
arch=('x86_64' 'i686')
url='http://iolanguage.com/'
license=('BSD')
depends=('pcre' 'libsndfile' 'libxmu' 'freetype2' 'libedit' 'libxml2' 'libtiff' 'libevent' 'yajl' 'libmariadbclient' 'lzo' 'libpng' 'python2' 'util-linux' 'freeglut' 'libjpeg-turbo' 'libtheora')
makedepends=('cmake' 'git')
options=('!makeflags' '!buildflags')
source=("git://github.com/stevedekorte/io#tag=$pkgver"
        'PATCH-Fix-for-inlining-in-C11-mode.patch'
        'PATCH-added--msse2-to-compile-flags-for.patch')
md5sums=('SKIP'
         'a3bab046cbc4be88ec95bb22c28abab3'
         '83552f6cabb4224cbcdcc01cb3bd4b90')

prepare() {
  cd io
  patch -p1 -i../PATCH-Fix-for-inlining-in-C11-mode.patch
  #patch -p1 -i../PATCH-added--msse2-to-compile-flags-for.patch
}

build() {
  cd io
  sed -i "s:20140919:${pkgver//./}:" libs/iovm/source/IoVersion.h
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd io
  make -C build DESTDIR="$pkgdir" install
  install -Dm644 license/bsd_license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/bsd_license.txt"
}

# vim:set ts=2 sw=2 et:
