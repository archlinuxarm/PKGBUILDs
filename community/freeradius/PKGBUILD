# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=freeradius
pkgver=3.0.25
pkgrel=4
pkgdesc='The premier open source RADIUS server'
arch=('x86_64')
url="https://freeradius.org/"
license=('GPL')
depends=('krb5' 'pth' 'net-snmp' 'postgresql-libs' 'mariadb-libs' 'talloc'
         'libpcap' 'libxcrypt' 'libcrypt.so' 'smbclient')
makedepends=('curl' 'freetds' 'hiredis' 'json-c' 'libmemcached' 'perl'
             'python' 'samba' 'sqlite' 'unixodbc' 'yubico-c-client')
optdepends=('curl: for REST'
            'freetds: for Sybase and MS SQL'
            'hiredis: for redis support'
            'json-c: rlm_rest module'
            'libmemcached: for memcached'
            'perl: for Perl'
            'python: for Python'
            'sqlite: for sqlite'
            'unixodbc: for ODBC'
            'yubico-c-client: for yubicloud')
options=('!makeflags')
validpgpkeys=('BF2384EC6938B9744B03E2A620E37C25995B4F85') # FreeRADIUS - Package Signing [...] <packages@freeradius.org>
source=("ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2"{,.sig}
        'python310.patch'
        'freeradius-sysusers.conf'
        'freeradius-tmpfiles.conf'
        'freeradius.service')
sha256sums=('fc158cdab4a705b179b1a91cd72473006ef4dfb570b0d097db6c9c34049a4509'
            'SKIP'
            '58f415a4d05f2446d2514b98f5fd5d740d7bee400745d43a82d024a3d23ab253'
            '573c05df429405d8647f5b52f68f7ad13bf7392314d30154df770ab170d793f8'
            'f536a9aa972e3e42a6b1a6d8ee17166eb721c7cba2e80f60473811497c7bd8bc'
            'c469e1a3f9edad769da01c324779babe783ee85a9b53ce4638d3d2d09c7c8d4b')

prepare() {
  cd "$srcdir"/freeradius-server-$pkgver
  patch -Np1 -i ../python310.patch
}

build() {
  cd "$srcdir"/freeradius-server-$pkgver

  [[ $CARCH == arm || $CARCH == armv6h ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib/freeradius \
    --localstatedir=/var \
    --enable-heimdal-krb5 \
    --enable-reproducible-builds \
    --with-system-libltdl \
    --with-system-libtool \
    --with-udpfromto
  make PACKAGE='archlinux'
}

package() {
  cd "$srcdir"/freeradius-server-$pkgver

  make install R="$pkgdir" PACKAGE='archlinux'
  chmod o+r "$pkgdir"/etc/raddb/*
  mv "$pkgdir"/etc/raddb "$pkgdir"/etc/raddb.default
  rm -rf "$pkgdir"/var/run

  install -D -m0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -D -m0644 "$srcdir"/$pkgname-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -D -m0644 "$srcdir"/$pkgname-tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf
}
