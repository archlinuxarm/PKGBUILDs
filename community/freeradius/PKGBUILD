# $Id: PKGBUILD 2560 2009-09-19 16:23:45Z ibiru $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>

pkgname=freeradius
pkgver=2.1.6
pkgrel=3
pkgdesc="The premier open source RADIUS server."
arch=('i686' 'x86_64')
url="http://www.freeradius.org/"
license=("GPL")
depends=('openssl' 'heimdal' 'pth' 'libldap>=2.4.18' 'net-snmp' 'pam' \
	 'postgresql-libs>=8.4.1' 'libtool' 'libmysqlclient' 'gdbm' 'shadow')
options=(!libtool emptydirs !makeflags)
install=$pkgname.install
source=(ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2 \
	radiusd)
md5sums=('645ac631505cde46e93c47e273bdec19'
         '655d2d6764e0730d64e5d92e75794b51')

build() {
  cd $startdir/src/freeradius-server-$pkgver

  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  ./configure --with-system-libtool \
	      --prefix=/usr --enable-heimdal-krb5 \
	      --localstatedir=/var \
	      --sysconfdir=/etc \
	      --libdir=/usr/lib/freeradius

  make || return 1
  make install R=$startdir/pkg || return 1

  install -D -m755 ../radiusd $startdir/pkg/etc/rc.d/radiusd && \
  chmod o+r $startdir/pkg/etc/raddb/* && \
  mv $startdir/pkg/etc/raddb $startdir/pkg/etc/raddb.default
}
