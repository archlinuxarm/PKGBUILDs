# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix fPIC on AArch64

pkgname=clementine
pkgver=1.3.1
pkgrel=15
pkgdesc='A modern music player and library organizer'
url='http://www.clementine-player.org/'
license=('GPL')
arch=('x86_64')
depends=('chromaprint' 'crypto++' 'fftw' 'gcc-libs' 'glew' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'libcdio' 'libechonest' 'libgl'
         'libgpod' 'liblastfm' 'libmtp' 'libpulse' 'libx11' 'protobuf' 'qjson'
         'qt4' 'sqlite' 'taglib' 'zlib')
makedepends=('boost' 'cmake' 'mesa' 'sparsehash')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'gst-libav: Libav plugin'
            'gvfs: Various devices support')
source=("clementine-${pkgver}.tar.gz::https://github.com/clementine-player/Clementine/archive/${pkgver}.tar.gz"
        'clementine-gcc6.patch'
        'add-missing-functional-includes-5630.patch'
        'clementine-1.3.1-chromaprint-1.4.0.patch'
        'clementine-cryptopp6.patch')
sha256sums=('f885931a9ab7c88607d07b50c64fcce46fc05f13dd2c0a04188c94eff938f37c'
            '05bd4dc0138eed084332fa1a688a96858418731f337f54e0d8ab0853123f40ee'
            '213a1c2eb3f3c1d2904ed855ad851c10705d04cfa20b36b843b60003469fdc1a'
            '4cc9c9df599df4a595c6acf4e129a54296c0a8e537627435e3df14e1e95447b0'
            'e9c09dca76ed8fae2ecb089571396c9293f546871fb2f27cc37010dd5cba9e46')

prepare() {
  cd Clementine-${pkgver}

  # Fix build with GCC 6
  patch -p1 -i ../clementine-gcc6.patch
  # https://github.com/clementine-player/Clementine/pull/5630
  patch -p1 -i ../add-missing-functional-includes-5630.patch
  # Fix build with chromaprint 1.4.0
  patch -p1 -i ../clementine-1.3.1-chromaprint-1.4.0.patch
  # Fix build with crypto++ 6.0.0 due to https://github.com/weidai11/cryptopp/issues/442
  patch -p1 -i ../clementine-cryptopp6.patch

  [[ $CARCH == "aarch64" ]] && sed -i 's/fpic/fPIC/' 3rdparty/vreen/vreen/cmake/CommonUtils.cmake

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd Clementine-${pkgver}/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd Clementine-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
