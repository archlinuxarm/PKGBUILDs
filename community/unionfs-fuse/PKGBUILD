# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Smith Dhumbumroong <zodmaner at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -D_FILE_OFFSET_BITS=64 to CPPFLAGS

pkgname=unionfs-fuse
pkgver=3.2
pkgrel=1
pkgdesc="A user space unionfs implementation"
arch=('x86_64')
url="https://github.com/rpodgorny/unionfs-fuse"
license=('BSD')
depends=('fuse3')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rpodgorny/unionfs-fuse/archive/v${pkgver}.tar.gz")
sha256sums=('16a4de448dd696443dd508c4217c2bf13a829f3497e1a0e9fa6f09487ac466c1')

build() {
  cd $pkgname-$pkgver
  CPPFLAGS+=" -D_FILE_OFFSET_BITS=64"
  make
}

package() {
  cd $pkgname-$pkgver
  make SBINDIR="/bin" DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

