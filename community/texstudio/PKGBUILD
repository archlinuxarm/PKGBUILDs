# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patch, use NO_CRASH_HANDLER=1 for AArch64

pkgname=texstudio
pkgver=2.10.4
pkgrel=3
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('i686' 'x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11')
makedepends=('qt5-tools')
optdepends=('evince: pdf reader'
            'kdegraphics-okular: alternate pdf reader')
replaces=('texmakerx')
install=$pkgname.install

source=(http://downloads.sourceforge.net/project/texstudio/texstudio/TeXstudio%20${pkgver}/${pkgname}-${pkgver}.tar.gz
        texstudio-debughelper-arm-fix.patch)
md5sums=('96b34746e5a2b7b41111cc7ac83d7a92'
         '54eba96da2c93980fd9d85f751a56905')

prepare() {
    cd "$srcdir/$pkgname$pkgver"
    patch -p1 -i ../texstudio-debughelper-arm-fix.patch
}

build() {
    cd "$srcdir/$pkgname$pkgver"
    [[ $CARCH == "aarch64" ]] && EXTRA="NO_CRASH_HANDLER=1"
    qmake-qt5 CONFIG-="debug" $EXTRA texstudio.pro
    make
}

package() {
    cd "$srcdir/$pkgname$pkgver"
    make INSTALL_ROOT="$pkgdir" install
}
