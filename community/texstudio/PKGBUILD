# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patch, use NO_CRASH_HANDLER=1 for AArch64

pkgname=texstudio
pkgver=2.12.4
pkgrel=2
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('i686' 'x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'mercurial' 'imagemagick' 'librsvg')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("hg+http://hg.code.sf.net/p/texstudio/hg#tag=${pkgver}"
        texstudio-debughelper-arm-fix.patch)
sha512sums=('SKIP'
            'dcb5ce7cd3e83fc221903fa895e6c428c54ada7468e74bb0a3e6bfac01c9e9575e4780d300b67f44064f2d25e10dd1aef531b847cdee71fbfbe7337808a0c8f4')

prepare() {
    cd hg
    patch -p1 -i ../texstudio-debughelper-arm-fix.patch
}

build() {
    cd hg
    [[ $CARCH == "aarch64" ]] && EXTRA="NO_CRASH_HANDLER=1"
    qmake-qt5 CONFIG-="debug" $EXTRA texstudio.pro
    make
}

package() {
    cd hg
    make INSTALL_ROOT="${pkgdir}" install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done
}
