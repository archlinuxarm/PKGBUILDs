# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: javonera <javonera@gmail.com>

pkgname=screenlets
pkgver=0.1.2
pkgrel=2
pkgdesc="Screenlets are small owner-drawn applications"
arch=('i686' 'x86_64')
url="http://screenlets.org/"
license=('GPL3')
depends=('pyxdg' 'dbus-python' 'libnotify' 'gnome-python-desktop'
         'notification-daemon' 'xdg-utils')
install=screenlets.install
source=(https://code.launchpad.net/screenlets/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('8bab8052ff5555481fdbe8a5a6310706')

build() {
  cd "$srcdir/$pkgname"
  sed -i "s;/usr/local;/usr;g" \
    src/lib/__init__.py setup.py \
    src/bin/screenlets-* desktop-menu/applications/* \
    desktop-menu/*.desktop \
    desktop-menu/desktop-directories/Screenlets.directory 

  python setup.py install --root="$pkgdir" || return 1
  install -D -m644 desktop-menu/desktop-directories/Screenlets.directory \
    "$pkgdir/usr/share/desktop-directories/Screenlets.directory"
  install -D -m644 desktop-menu/screenlets.svg "$pkgdir/usr/share/icons/screenlets.svg"

  cd desktop-menu/
  for i in $(find -name '*.desktop'); do install -D -m644 $i $pkgdir/usr/share/applications/$(echo $i | sed 's|./applications||') ; done
}
