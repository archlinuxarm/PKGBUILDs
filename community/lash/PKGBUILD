# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix FTBFS on ARM

pkgname=lash
pkgver=0.6.0~rc2
_relver=0.6.0.594
pkgrel=7
pkgdesc="A session management system for JACK and ALSA"
arch=('i686' 'x86_64')
#url="http://lash-audio.org"
url="http://lash.nongnu.org/"
license=('GPL')
depends=('dbus-core' 'gtk2' 'jack' 'libxml2' 'python2')
install=$pkgname.install
source=("http://download.savannah.gnu.org/releases/lash/${pkgname}-${pkgver}.tar.bz2" "makefile.patch"
        '04_disable_stacktrace.patch')
md5sums=('af1dc4f4ceb284b1b0845de4f4c2fe47'
         '24ceb7e3d008c25e1490102983165612'
         '5bca5868120205e93caffb30c877e946')

build() {
  cd "${srcdir}/${pkgname}-${_relver}"
  patch -p0 < "${srcdir}/makefile.patch"
  patch -p1 -i ../04_disable_stacktrace.patch

  # Python2 fixes
  export PYTHON="python2"
  sed -i "s#env python#&2#" clients/lash_control

  export LDFLAGS="-ldl -lm"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_relver}"
  make DESTDIR=${pkgdir} install
}
