# Maintainer: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Muhammad Qadri <Muhammad dot A dot Qadri at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=julius
pkgver=4.4.2.1
pkgrel=3
pkgdesc="A high-performance, two-pass large vocabulary continuous speech recognition decoder software"
arch=("x86_64")
url="http://julius.sourceforge.jp/"
license=("custom")
depends=("libpulse" "sdl2")
optdepends=("voxforge-am-julius: acoustic models")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/julius-speech/julius/archive/v${pkgver}.tar.gz"
        "configure.patch"
        '0001-AArch64-cpuid-fix.patch')
md5sums=('9760ad16eb643267c3101f105deec2ff'
         '9fe2a29fdec779104f69539ad4d1cce0'
         '5e62d26ac5b95aa8f53ecf9c40e2a75a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # configure error
  patch -p1 -i "${srcdir}/configure.patch"

  # aarch64 fix
  patch -p1 -i "${srcdir}/0001-AArch64-cpuid-fix.patch"
}

build() {
  # redefine makeflags, coz it could not build in parallel
  export MAKEFLAGS="-j1"

  cd "${srcdir}/${pkgname}-${pkgver}/"
  ./configure \
    --prefix="/usr/" \
    --mandir='${prefix}/share/man'\
    --enable-words-int \
    --enable-msd \
    --enable-gmm-vad \
    --enable-decoder-vad \
    --enable-power-reject \
    --enable-wpair
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  # leave static libraries since julius does not support
  # shared ones at the moment. See also FS#50599
  make prefix="${pkgdir}/usr" install
  install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/julius/LICENSE"
}

# vim:set ts=2 sw=2 et:
