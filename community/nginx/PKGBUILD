# $Id: PKGBUILD 2307 2009-09-14 20:44:03Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Miroslaw Szot <mss@czlug.icis.pcz.pl>

_doc_root=/srv/http/nginx
_server_root=/etc/nginx
_conf_path=${_server_root}/conf
_tmp_path=/var/spool/nginx
_log_path=/var/log/nginx
_user=http
_group=http

pkgname=nginx
pkgver=0.7.62
pkgrel=1
pkgdesc="lightweight HTTP server and IMAP/POP3 proxy server"
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl')
url="http://nginx.net/"
license=('custom')
backup=("${_conf_path#/}/nginx.conf" \
	"${_conf_path#/}/koi-win" \
	"${_conf_path#/}/koi-utf" \
	"${_conf_path#/}/win-utf" \
	"${_conf_path#/}/mime.types" \
	"${_conf_path#/}/fastcgi_params" \
	"etc/logrotate.d/nginx")
source=(http://sysoev.ru/nginx/nginx-${pkgver}.tar.gz \
	nginx)
md5sums=('ab22f1b7f098a90d803a3abb94d23f7e'
         '0eb8611bf5bc1192ee7ee606fce772e2')

build() {
	local _src_dir=$startdir/src/nginx-${pkgver}
	local _build_dir=$_src_dir/objs

	cd $_src_dir
	./configure \
		--prefix=${_server_root} \
		--sbin-path=/usr/sbin/nginx \
		--pid-path=/var/run/nginx.pid \
		--lock-path=/var/lock/nginx.lock \
		--http-client-body-temp-path=${_tmp_path}/client_body_temp \
		--http-proxy-temp-path=${_tmp_path}/proxy_temp \
		--http-fastcgi-temp-path=${_tmp_path}/fastcgi_temp \
		--http-log-path=${_log_path}/access.log \
		--error-log-path=${_log_path}/error.log \
		--user=${_user} --group=${_group} \
		--with-imap --with-imap_ssl_module --with-http_ssl_module \
                --with-http_stub_status_module

	make || return 1
	make DESTDIR=$startdir/pkg install || return 1

	install -d $startdir/pkg/etc/logrotate.d/
	cat <<- EOF > $startdir/pkg/etc/logrotate.d/nginx
	$_log_path/*log {
		create 640 http log
		compress
		postrotate
			/bin/kill -HUP `cat /var/run/nginx.pid 2>/dev/null` 2> /dev/null || true
		endscript
	}
	EOF

	sed -i -e "s/\<user\s\+\w\+;/user $_user;/g" $startdir/pkg/$_conf_path/nginx.conf 

	install -d $startdir/pkg/$_tmp_path

	# move default document root outside server root
	install -d $startdir/pkg/$_doc_root
	mv $startdir/pkg/$_server_root/html/* $startdir/pkg/$_doc_root/
	rm -rf $startdir/pkg/$_server_root/html
	rm -f $startdir/pkg/$_doc_root/index.html

	# let's create links for relative paths in config file
	ln -s $_log_path ${startdir}/pkg/$_server_root/logs && \
	ln -s $_doc_root ${startdir}/pkg/$_server_root/html || return 1

	install -D -m755 $startdir/src/nginx $startdir/pkg/etc/rc.d/nginx && \
	install -D -m644 LICENSE $startdir/pkg/usr/share/licenses/nginx/LICENSE && \
	mkdir -p $pkgdir/etc/conf.d && \
	echo "NGINX_CONFIG=/etc/nginx/conf/nginx.conf" >$pkgdir/etc/conf.d/nginx
}
