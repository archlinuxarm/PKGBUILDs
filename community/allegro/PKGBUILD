# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc
#  - comment fixing man path

pkgname=allegro
pkgver=5.2.8.0
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=(x86_64)
url='https://liballeg.org/'
license=(custom)
makedepends=(cmake glu libtheora mesa-libgl ninja opusfile xorgproto)
depends=(dumb gtk3 jack libgl libpulse libtheora libwebp libxpm opusfile physfs)
source=("https://github.com/liballeg/allegro5/releases/download/$pkgver/allegro-$pkgver.tar.gz")
b2sums=('57c71bdc7fb37c75f10def64ffc9bfaa55443e5b6a0910d1c698c936318eed273873aacddbf49ba3a7e6d8c36d332e35f84db1156cc667a50f8f98313af23828')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D WANT_DOCS_HTML=OFF \
    -G Ninja
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$pkgname-$pkgver/LICENSE.txt" \
    -t "$pkgdir/usr/share/licenses/$pkgname"
  # Fix man path
  #mv -v -f "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}
