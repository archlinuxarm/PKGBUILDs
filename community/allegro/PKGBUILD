# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc
#  - comment fixing man path

pkgname=allegro
pkgver=5.2.6.0
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=(x86_64)
url='https://liballeg.org/'
license=(custom)
makedepends=(cmake glu libtheora mesa-libgl opusfile xorgproto)
depends=(dumb gtk2 jack libgl libpulse libtheora libwebp libxpm opusfile physfs)
source=("https://github.com/liballeg/allegro5/releases/download/$pkgver/allegro-$pkgver.tar.gz")
sha256sums=('5de8189ec051e1865f359654f86ec68e2a12a94edd00ad06d1106caa5ff27763')

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS_HTML=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname

  # Fix man path
  #mv -v -f "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}
