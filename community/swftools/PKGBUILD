# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=swftools
pkgver=0.9.0
pkgrel=2
pkgdesc="A collection of SWF manipulation and creation utilities."
arch=('i686' 'x86_64')
url="http://www.swftools.org/"
license=('GPL')
depends=('avifile' 'giflib' 'freeglut' 'lame' 't1lib' 'libjpeg' 'fontconfig')
makedepends=('bison' 'flex' 'zlib')
source=(http://www.swftools.org/$pkgname-$pkgver.tar.gz
	swftools-const.patch)

md5sums=('518e94894a6b1be584ba79b6ab1b2a1a'
         '485778a637486d32b6b2f753e3c3d3a2')
build() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -Np1 -i $srcdir/swftools-const.patch || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make prefix=${pkgdir}/usr install

  cd ${pkgdir}/usr/share/$pkgname/swfs
  rm -f default_loader.swf default_viewer.swf
  ln -s tessel_loader.swf default_loader.swf
  ln -s simple_viewer.swf default_viewer.swf
}
