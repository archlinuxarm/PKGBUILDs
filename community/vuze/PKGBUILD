# $Id: PKGBUILD 1831 2009-09-05 08:28:10Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=vuze
pkgver=4.2.0.8
pkgrel=1
pkgdesc="BitTorrent client with GUI, written in Java, formerly known as Azureus"
arch=('i686' 'x86_64')
url="http://azureus.sourceforge.net/"
license=('GPL')
depends=('bash' 'java-runtime')
optdepends=('xulrunner: needed for browsing the vuze content')
provides=('azureus')
conflicts=('azureus' 'java-gcj-compat')
replaces=('azureus')
source=("http://downloads.sourceforge.net/azureus/Vuze_${pkgver}_linux.tar.bz2"
	'vuze.desktop')
md5sums=('77bd4fd6bd498605f0f59dee53acae08'
        '345ad45b972009dcd672c4eaf52fb336')                                     
[ "${CARCH}" = "x86_64" ] && source[0]="http://downloads.sourceforge.net/azureus/Vuze_${pkgver}_linux-x86_64.tar.bz2"
[ "${CARCH}" = "x86_64" ] && md5sums[0]='127e6f18e5670162cef9165f09baff6a'

build() {
  cd ${srcdir}

  install -d "$pkgdir/usr/share"
  cp -r vuze "$pkgdir/usr/share" || return 1

  install -D -m755 vuze/vuze "$pkgdir/usr/bin/vuze" || return 1
  sed -i 's%#PROGRAM_DIR="/home/username/apps/azureus"%PROGRAM_DIR="/usr/share/vuze"%' "$pkgdir/usr/bin/vuze" || return 1

  install -D -m644 vuze/vuze.png "$pkgdir/usr/share/pixmaps/vuze.png"|| return 1
  install -D -m644 vuze.desktop  "$pkgdir/usr/share/applications/vuze.desktop" || return 1
}
