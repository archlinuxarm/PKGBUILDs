# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Roman Kyrylych <roman@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=wzdftpd
pkgver=0.8.3
pkgrel=2
pkgdesc="A portable, modular and efficient ftp server"
arch=('i686' 'x86_64')
url="http://www.wzdftpd.net"
license=('GPL')
depends=('avahi' 'gnutls' 'tcl' 'pam')
makedepends=('cmake')
options=('!libtool')
source=(http://downloads.sourceforge.net/wzdftpd/wzdftpd-$pkgver.tar.gz
        wzd.cfg.sample wzdftpd)
install=$pkgname.install
md5sums=('6114c32fc91786d9485fbc10c6306082'
         '28227e7b8df1836230bdc685817f6970'
         'b58091cd089af812aad0511e146e4d00')

build() {
	cd $srcdir
	# Add to fix TCL 8.5 library path
	sed -i 's|tcl84 |tcl8.5 tcl84 |g;
		s|tk84 |tk8.5 tk84 |' \
		$pkgname-$pkgver/cmake/FindTCLDev.cmake || return 1
	mkdir build
	cd build
	cmake ../$pkgname-$pkgver \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DLOCALSTATE_INSTALL_DIR=/var \
		-DCONF_INSTALL_PATH=/etc/wzdftpd \
		-DSKIP_BUILD_RPATH=ON \
		-DSKIP_INSTALL_RPATH=ON \
 		-DWITH_MySQL=OFF \
		-DWITH_PAM=ON \
		-DWITH_PostgreSQL=OFF \
		-DWITH_SQLite3=OFF \
		-DWITH_Zeroconf=ON \
		-DWITH_TCLDev=ON \
		-DWITH_PERLDev=OFF \
		-DWITH_GnuTLS=ON \
		-DWITH_SFV=ON
	make || return 1
	make DESTDIR=$pkgdir install
  
	cp $srcdir/wzd.cfg.sample $pkgdir/etc/$pkgname
	mkdir -p $pkgdir/usr/var/log/$pkgname
	install -D -m755 ../wzdftpd $pkgdir/etc/rc.d/wzdftpd
}

