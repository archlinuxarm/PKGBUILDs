# $Id: PKGBUILD 746 2009-08-05 09:43:45Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Bergstrom <stefan.bergstrom@gmail.com>
pkgname=gnump3d
pkgver=3.0
pkgrel=4
url="http://www.gnu.org/software/gnump3d/"
arch=(any)
pkgdesc="A streaming server for MP3s, OGG vorbis files, movies and other media formats."
license=("GPL")
depends=('sh' 'perl')
backup=('etc/gnump3d/gnump3d.conf')
options=('force')
install=gnump3d.install
source=("http://savannah.gnu.org/download/$pkgname/$pkgname-$pkgver.tar.bz2" \
	'gnump3d')
md5sums=('41786650bbc591484c08014a89478bf9'
         '4ac4fd5f3637613a5b5a191081437e23')

build() {
    cd $startdir/src/$pkgname-$pkgver

    install -d $startdir/pkg/var/log/gnump3d && \
    install -d $startdir/pkg/var/cache/gnump3d/serving || return 1

    install -d $startdir/pkg/usr/share/perl5/vendor_perl/gnump3d/plugins && \
    install -d $startdir/pkg/usr/share/perl5/vendor_perl/gnump3d/lang && \
    install -m 644 lib/gnump3d/*.pm         $startdir/pkg/usr/share/perl5/vendor_perl/gnump3d && \
    install -m 644 lib/gnump3d/plugins/*.pm $startdir/pkg/usr/share/perl5/vendor_perl/gnump3d/plugins && \
    install -m 644 lib/gnump3d/lang/*.pm    $startdir/pkg/usr/share/perl5/vendor_perl/gnump3d/lang || return 1

    install -d $startdir/pkg/usr/bin && \
    install -m 755 bin/gnump3d2 $startdir/pkg/usr/bin/gnump3d && \
    install -m 755 bin/gnump3d-top bin/gnump3d-index $startdir/pkg/usr/bin || return 1

    install -d $startdir/pkg/usr/share/man/man1 && \
    install -m 644 man/gnump3d-top.1 man/gnump3d-index.1 man/gnump3d.1 man/gnump3d.conf.1 $startdir/pkg/usr/share/man/man1 || return 1

    install -d $startdir/pkg/usr/share/gnump3d && \
    cp -R templates/* $startdir/pkg/usr/share/gnump3d && \
    chmod -R a+r $startdir/pkg/usr/share/gnump3d && \
    chmod +rx $startdir/pkg/usr/share/gnump3d/*/ || return 1

    sed -i "s|PLUGINDIR|/usr/share/perl5/vendor_perl|g" etc/gnump3d.conf && \
    install -d $startdir/pkg/etc/gnump3d && \
    install -m 644 etc/gnump3d.conf etc/mime.types $startdir/pkg/etc/gnump3d && \
    install -m 644 etc/gnump3d.conf etc/file.types $startdir/pkg/etc/gnump3d || return 1

    mkdir -p $startdir/pkg/srv/mp3 && \
    sed -i "s|/home/mp3|/srv/mp3|" $startdir/pkg/etc/gnump3d/gnump3d.conf || return 1

    install -D -m 755 $startdir/gnump3d $startdir/pkg/etc/rc.d/gnump3d
}
