# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sekenre <sekenre at ukfsn dot org>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>

pkgname=metamail
pkgver=2.7
pkgrel=4
pkgdesc="Used to display and process MIME messages"
arch=('i686' 'x86_64')
url="http://packages.debian.org/stable/mail/metamail"
license=('custom')
depends=('ncurses')
source=("http://ftp.de.debian.org/debian/pool/main/m/metamail/metamail_$pkgver.orig.tar.gz" \
	"http://ftp.de.debian.org/debian/pool/main/m/metamail/metamail_$pkgver-52.diff.gz" \
	license)
noextract=(metamail_$pkgver-52.diff.gz)
md5sums=('c6967e9bc5d3c919764b02df24efca01'
         '43d21022f048b6610932cf6f6f46b516'
         '38be502570f3ce5b5a2bafd6a876373f')

build() {
  cd $srcdir/metamail-$pkgver.orig

  zcat $srcdir/metamail_$pkgver-52.diff.gz | patch -p1

  touch NEWS AUTHORS ChangeLog
  automake --add-missing
  autoconf

  sh ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -D -m0644 $srcdir/license $pkgdir/usr/share/licenses/$pkgname/license
}
