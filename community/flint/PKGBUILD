# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>
# Contributor: Alessandro "jakedust" Andrioni <jakedust@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO via -DIPO_SUPPORTED=FALSE

pkgname=flint
pkgver=2.6.0
pkgrel=1
pkgdesc="A C library for doing number theory"
arch=(x86_64)
url="http://www.flintlib.org"
license=(GPL)
depends=(mpfr ntl)
makedepends=(cmake python)
source=("http://www.flintlib.org/flint-$pkgver.tar.gz")
sha256sums=('9089edadd12cbbda4533ab6f58efb7565fd973b5b82a89f53f64203bc3510840')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NTL=ON \
    -DIPO_SUPPORTED=FALSE
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
