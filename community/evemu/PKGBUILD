# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Nicolas Quiénot < niQo at aur >
# Contributor: Damián Nohales <damiannohales at gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - autoreconf for v5/v6
 
pkgname=evemu
pkgver=2.7.0
pkgrel=3
pkgdesc='Tools and bindings for kernel input event device emulation and data capture and replay'
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Evemu'
license=(GPL3)
depends=(libevdev)
makedepends=(asciidoc python python2 xmlto)
optdepends=('python: Python 3 bindings'
            'python2: Python 2 bindings')
source=(https://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=(0A75E35E0FAEE97EC769103E2F2670AC164DB36F) # Benjamin Tissoires <benjamin.tissoires@gmail.com>
sha1sums=('16c6e1484253e1610a019dcca22b1698002824ca'
          'SKIP')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}
 
build() {
  cp -r $pkgname-$pkgver python2

  cd $pkgname-$pkgver
  PYTHON=python3 ./configure --prefix=/usr
  make

  cd ../python2
  PYTHON=python2 ./configure --prefix=/usr --disable-tests
}

check() {
  cd $pkgname-$pkgver
  # Tests have to be run as 'root'
  # make check
}
 
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd ../python2
  make -C python DESTDIR="$pkgdir" install
}
