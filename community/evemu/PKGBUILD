# Maintainer: Anatol Pomozov
# Contributor: Nicolas Quiénot < niQo at aur >
# Contributor: Damián Nohales <damiannohales at gmail.com>
 
# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - autoreconf for v5/v6
 
pkgname=evemu
pkgver=2.7.0
pkgrel=4
pkgdesc='Tools and bindings for kernel input event device emulation and data capture and replay'
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Evemu'
license=(GPL3)
depends=(libevdev)
makedepends=(asciidoc python xmlto)
optdepends=('python: Python 3 bindings')
source=(https://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=(0A75E35E0FAEE97EC769103E2F2670AC164DB36F) # Benjamin Tissoires <benjamin.tissoires@gmail.com>
sha1sums=('16c6e1484253e1610a019dcca22b1698002824ca'
          'SKIP')
 
prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}
 
build() {
  cd $pkgname-$pkgver
  PYTHON=python3 ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  # Tests have to be run as 'root'
  # make check
}
 
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
