# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for qreal on ARM

pkgname=scribus
pkgver=1.4.3
pkgrel=3
pkgdesc='Desktop publishing software'
url="http://www.scribus.net/"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake' 'boost' 'mesa')
depends=('qt4' 'cairo' 'lcms2' 'libcups' 'libxml2' 'hyphen'
         'ghostscript' 'python2' 'podofo' 'boost-libs'
         'desktop-file-utils' 'shared-mime-info')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'CalendarWizard.patch'
        'arm.patch')
sha1sums=('30c111762790e4407c13236bbb4b06f04e907f71'
          'a92629a98bc960a6f7f946faf2d6f06e9d1e9452'
          'efb1fbf23b29527397f5fd7fe999bbeef50e5129')

options=('!libtool')
install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p2 -i ../CalendarWizard.patch
	patch -p1 -i "${srcdir}"/arm.patch
	sed 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' -i scribus.desktop
	sed \
		-e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
		-e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
		-i scribus/plugins/scriptplugin/{samples,scripts}/*
	install -d ../build
}

build() {
	cd "${srcdir}/build"
	cmake "../${pkgname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DLIB_SUFFIX=""
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
	install -Dm644 "../${pkgname}-${pkgver}/scribus.desktop" "${pkgdir}/usr/share/applications/scribus.desktop"
}
