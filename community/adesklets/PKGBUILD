# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Fubar

pkgname=adesklets
pkgver=0.6.1
pkgrel=9
pkgdesc="An imlib2-based system to have interactive 'desklets'."
arch=('i686' 'x86_64')
url="http://adesklets.sourceforge.net/"
license=('GPL2')
depends=('imlib2' 'fontconfig' 'python>=2.6' 'perl' 'ttf-bitstream-vera' 'libx11')
install=$pkgname.install
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2 \
        fontconfig-2.4.patch)

md5sums=('cd390c9398449c5566033e2e4792bccb'
         '0374aec97670c90713fcabc2710e2160')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

#patch for fontconfig => 2.4
  patch -Np0 -i ${srcdir}/fontconfig-2.4.patch || return 1

  autoreconf || return 1
  ./configure --prefix=/usr || return 1

#makefile Fix
  sed -i 's/\/bin\/sh//' doc/Makefile || return 1

  make || return 1
  make DESTDIR=${pkgdir} INSTALLDIRS=vendor install || return 1

  rm ${pkgdir}/usr/share/info/dir

#remove the font stuff
  rm -f ${pkgdir}/usr/share/adesklets/{FONT_LICENSE,Vera.ttf}

  find ${pkgdir} -name '.packlist' -exec rm  '{}' \;
  find ${pkgdir} -name 'perllocal.pod' -exec rm  '{}' \;
}
