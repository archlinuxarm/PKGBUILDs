# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Maarten Baert

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DENABLE_32BIT_GLINJECT="FALSE" -DENABLE_X86_ASM="FALSE" -DWITH_GLINJECT="FALSE"

pkgname=simplescreenrecorder
pkgver=0.3.10
pkgrel=1
pkgdesc="A feature-rich screen recorder that supports X11 and OpenGL."
arch=("x86_64")
url="http://www.maartenbaert.be/simplescreenrecorder/"
license=("GPL3")
# upstream reports crashes with Qt5, reverting FS#45104
# trying qt5 again with 0.3.9-3
#depends=("qt4"
depends=("qt5-base" "qt5-x11extras"
    "ffmpeg" "alsa-lib" "libpulse" "jack" "libgl" "glu"
    "libx11" "libxext" "libxfixes" "libxi"
    "desktop-file-utils" "gtk-update-icon-cache")
optdepends=("lib32-simplescreenrecorder: OpenGL recording of 32-bit applications")
makedepends=("git" "cmake" "qt5-tools")
source=("git+https://github.com/MaartenBaert/ssr.git#tag=$pkgver")
md5sums=("SKIP")

install=simplescreenrecorder.install

prepare() {
  cd ssr
  mkdir -p build
}

build() {
  cd ssr/build
  # fPIC is only required for qt5 + gcc5
  #CXXFLAGS="$CXXFLAGS -fPIC"
  #./configure --prefix=/usr --disable-assert --with-qt5
  #./configure --prefix=/usr --disable-assert
  #  -DLRELEASE='/usr/bin/lrelease-qt4' \
  cmake -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_BUILD_TYPE=Release \
    -DWITH_QT5=on \
    -DCMAKE_INSTALL_LIBDIR='lib' ../ \
    -DENABLE_32BIT_GLINJECT="FALSE" -DENABLE_X86_ASM="FALSE" -DWITH_GLINJECT="FALSE"
  make
}
package() {
  cd ssr/build
  make DESTDIR="$pkgdir" install
}
