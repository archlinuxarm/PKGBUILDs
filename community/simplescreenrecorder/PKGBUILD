# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Maarten Baert

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DENABLE_32BIT_GLINJECT="FALSE" -DENABLE_X86_ASM="FALSE" -DWITH_GLINJECT="FALSE"

pkgname=simplescreenrecorder
pkgver=0.4.4
pkgrel=1
pkgdesc="A feature-rich screen recorder that supports X11 and OpenGL."
arch=("x86_64")
url="https://www.maartenbaert.be/simplescreenrecorder/"
license=("GPL3")
depends=("qt5-base" "qt5-x11extras"
    "ffmpeg" "alsa-lib" "libpulse" "jack" "libgl" "glu"
    "libx11" "libxext" "libxfixes" "libxi" "libxinerama"
    "desktop-file-utils" "gtk-update-icon-cache")
optdepends=("lib32-simplescreenrecorder: OpenGL recording of 32-bit applications")
makedepends=("git" "cmake" "qt5-tools")
source=("git+https://github.com/MaartenBaert/ssr.git#tag=$pkgver"
         ffmpeg5.patch::https://patch-diff.githubusercontent.com/raw/MaartenBaert/ssr/pull/934.patch)
md5sums=('SKIP'
         '4ef035d6c45d146a06b9953075173185')

install=simplescreenrecorder.install

prepare() {
  cd ssr
  mkdir -p build

  patch -p1 -i ../ffmpeg5.patch # Fix build with ffmpeg 5
}

build() {
  cd ssr/build
  # fPIC is only required for qt5 + gcc5
  #CXXFLAGS="$CXXFLAGS -fPIC"
  #./configure --prefix=/usr --disable-assert --with-qt5
  #./configure --prefix=/usr --disable-assert
  #  -DLRELEASE='/usr/bin/lrelease-qt4' \
  cmake -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_BUILD_TYPE=Release \
    -DWITH_QT5=on \
    -DCMAKE_INSTALL_LIBDIR='lib' ../ \
    -DENABLE_32BIT_GLINJECT="FALSE" -DENABLE_X86_ASM="FALSE" -DWITH_GLINJECT="FALSE"
  make
}
package() {
  cd ssr/build
  make DESTDIR="$pkgdir" install
}
