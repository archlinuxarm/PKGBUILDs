# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Maarten Baert

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-x86-asm --disable-glinjectlib

pkgname=simplescreenrecorder
pkgver=0.3.3
pkgrel=2
pkgdesc="A feature-rich screen recorder that supports X11 and OpenGL."
arch=("i686" "x86_64")
url="http://www.maartenbaert.be/simplescreenrecorder/"
license=("GPL3")
depends=("qt5-base" "qt5-x11extras" "qt5-tools" "ffmpeg" "alsa-lib" "libpulse" "jack" "libgl" "glu" "libx11" "libxext" "libxfixes" "libxi")
optdepends=("lib32-simplescreenrecorder: OpenGL recording of 32-bit applications")
makedepends=("git")
source=("git+https://github.com/MaartenBaert/ssr.git#tag=$pkgver")
md5sums=("SKIP")

options=("!libtool")
install=simplescreenrecorder.install

build() {
  cd ssr
  # fPIC is only required for qt5 + gcc5
  CXXFLAGS="$CXXFLAGS -fPIC"
  ./configure --prefix=/usr --disable-assert --with-qt5 --disable-x86-asm --disable-glinjectlib
  make
}
package() {
  cd ssr
  make DESTDIR="$pkgdir" install
}
