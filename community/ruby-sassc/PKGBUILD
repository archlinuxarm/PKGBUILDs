# Maintainer: Anatol Pomozov

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - gem install with --disable-lto

_gemname=sassc
pkgname=ruby-$_gemname
pkgver=2.3.0
pkgrel=2
pkgdesc="Use libsass with Ruby!"
arch=(x86_64)
url='https://github.com/sass/sassc-ruby'
license=(MIT)
depends=(ruby ruby-ffi)
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha1sums=('53868c8146264e70c532ec5d683d014436eaf922')

package() {
  local _gemdir=$(gem env gemdir)
  local _extdir=$(ruby -e'puts File.join(Gem.default_dir, "extensions", Gem::Platform.local.to_s, Gem.extension_api_version)')/$_gemname-$pkgver
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem -- --disable-march-tune-native --disable-lto
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  ln -s "/$_extdir/sassc/libsass.so" "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/lib/sassc/"

  # remove junk
  rm -r "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/"{ext,test}
  rm "$pkgdir/$_extdir"/gem_make.out
}
