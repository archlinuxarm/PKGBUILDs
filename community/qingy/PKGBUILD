# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=qingy
pkgver=0.9.9
pkgrel=2
pkgdesc="Qingy is a DirectFB getty replacement"
arch=('i686' 'x86_64')
url="http://qingy.sourceforge.net"
license=('GPL')
depends=('directfb>=1.4.1' 'pam' 'openssl' 'ncurses>=5.6-7' 'libxss')
makedepends=('pkgconfig')
backup=('etc/qingy/settings' 'etc/qingy/welcomes')
options=('!libtool')
install=qingy.install
source=(http://downloads.sourceforge.net/sourceforge/qingy/${pkgname}-${pkgver}.tar.bz2 qingy.logrotate README)
md5sums=('34f40172a71e9a6c2c77ddce11d2a63a' 'a9bdfd0ac9f29380eafa97a4a056789d'\
         'f54865d71ea34e2f68e53ef60248701b')
sha1sums=('b4a09e61f91e46c42bc50024ec225a485edf385f'
          '21676cf0b71334c666f7b3e4f3d8b4f319266c81'
          '66520ee4bd61121f1f776067b465af5df7bbf542')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|/usr/share/xsessions|/etc/X11/sessions|' src/libraries/session.c || return 1
  sed -i 's|/usr/X11R6/bin|/usr/X11R6/bin:/sbin:/usr/sbin:/opt/kde/bin:/opt/plt/bin:/usr/share/eclipse:/opt/java/bin:/opt/java/jre/bin:/opt/openoffice/program:/opt/NX/bin:/opt/qt/bin:/opt/plan9/bin:/opt/firebird/bin:/opt/glest/bin:/opt/grass/bin:/opt/maven/bin:/usr/share/netbeans/bin:/opt/qgis/bin:/opt/texlive/bin:/opt/torcs/bin:/opt/wildfire/bin|' src/libraries/session.c || return 1

  ./configure --prefix=/usr --sbindir=/sbin --sysconfdir=/etc \
              --disable-optimizations --enable-pam \
              --disable-static-build --enable-gpm-lock --enable-emacs || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm "${pkgdir}/usr/share/info/dir" || return 1
  gzip "${pkgdir}/usr/share/info/qingy.info" || return 1


  install -d "${pkgdir}/var/lib/qingy" || return 1
  install -D -m644 "${srcdir}/qingy.logrotate" "${pkgdir}/etc/logrotate.d/qingy" || return 1
  install -D -m644 "${srcdir}/README" "${pkgdir}/usr/share/doc/qingy/README" || return 1

# fix settings for Arch
  sed -i 's|/var/lib/misc|/var/lib/qingy|' "${pkgdir}/etc/qingy/settings" || return 1
  sed -i 's|/usr/X11R6/bin/XFree86|/usr/bin/Xorg|' "${pkgdir}/etc/qingy/settings" || return 1
  sed -i 's|/usr/X11R6/bin/xinit|/usr/bin/xinit|' "${pkgdir}/etc/qingy/settings" || return 1
  sed -i 's|session    optional|#session    optional|' "${pkgdir}/etc/pam.d/qingy" || return 1
}
