# $Id: PKGBUILD 3472 2009-10-03 18:33:24Z andrea $
# Maintainer: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>

pkgname=hsoconnect
pkgver=1.2.18
pkgrel=3
pkgdesc="HSO command line connection utility"
arch=('i686' 'x86_64')
url="http://www.pharscape.org/forum/index.php?topic=743.0"
license=('GPL2')
depends=('pygtk' 'hsolink' 'ozerocdoff' 'dbus-python')
backup=('usr/share/hsoconnect/HSOconnect.cfg')
source=(ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz
	'hsoconnect.sh' 'HSOconnect.cfg' 'hsoconnect.desktop')
md5sums=('ed7147b66838ca19114657ccffe441c7'
         '3ed520143bbb8de243cde7f2f503ce32'
         'ba44b2e3cae9e2c17408b109744d98cd'
         '54fa6503ea415d7bbf897af23e7055a9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -D -m755 -gnetwork ${srcdir}/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname} || return 1
  
  install -d ${pkgdir}/usr/share/man/man1
  install -m644 hsoconnect/HSOconnect.1 ${pkgdir}/usr/share/man/man1 || return 1

  install -d ${pkgdir}/usr/share/hsoconnect
  cp -r hsoconnect/hsoc hsoconnect/hsoconf ${pkgdir}/usr/share/hsoconnect || return 1
  rm ${pkgdir}/usr/share/hsoconnect/hsoconf/HSOconnect.cfg.def
  chown -R :network ${pkgdir}/usr/share/hsoconnect

  sed -i 's|dialout|network|' ${pkgdir}/usr/share/hsoconnect/hsoc/HSOconsole.py
  sed -i 's|PIDPATH =.*$|PIDPATH ="/tmp"|g' \
    ${pkgdir}/usr/share/hsoconnect/hsoc/HSOconnect.py

  install -D -m644 -gnetwork ${srcdir}/HSOconnect.cfg \
    ${pkgdir}/usr/share/hsoconnect/hsoconf/HSOconnect.cfg || return 1

  touch ${pkgdir}/usr/share/hsoconnect/hsoconf/{hsoconnect.log,transferlog} || return 1
  chmod 664 ${pkgdir}/usr/share/hsoconnect/hsoconf/{hsoconnect.log,transferlog}
  chown :network ${pkgdir}/usr/share/hsoconnect/hsoconf/{hsoconnect.log,transferlog}

  install -d ${pkgdir}/usr/share/applications
  install -m644 ${srcdir}/hsoconnect.desktop ${pkgdir}/usr/share/applications/ || return 1

  install -D hsoconnect/hsoconf/images/pengie/pengie-2-3232.png ${pkgdir}/usr/share/pixmaps/hsoconnect.png || return 1

  install -d ${pkgdir}/etc/
  ln -sf /usr/share/hsoconnect/hsoconf/HSOconnect.cfg ${pkgdir}/etc/hsoconnect || return 1
}
