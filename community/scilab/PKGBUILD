# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>
# Paulo Matias
pkgname=scilab
pkgver=5.1.1
pkgrel=3
pkgdesc="Scilab is a scientific software package for numerical computations"
arch=('i686' 'x86_64')
url="http://www.scilab.org/"
provides=('scilab=$pkgver')
conflicts=('scilab4' 'scilab5')
license=('custom:CeCILL')
depends=('libxml2' 'pcre' 'java-saxon6<7' 'lapack' 'java-batik' \
  'java-flexdock' 'jogl' 'fop' 'jeuclid-core' 'fftw' 'libmatio' \
  'bash' 'tk' 'jrosetta' 'ocaml' 'javahelp2')
makedepends=('apache-ant')
source=("http://www.scilab.org/download/$pkgver/scilab-$pkgver-src.tar.gz" \
  'java.patch' 'scilab.desktop')
md5sums=('acc396708aaa7c703288301801714378' '7cf31756e3c995e56ec657a0f9930be7'\
         'ad6286f324891fe8b86c60e4012a36b7')
options=('!libtool')

build() {
	cd $srcdir/scilab-$pkgver
	
	patch -p1 < ../java.patch || return 1
	./configure --prefix=/usr --disable-build-help --with-install-help-file \
	  --enable-build-localization --without-pvm \
	  --with-matio --without-umfpack --with-fftw \
	  --with-gfortran  # Needed for 64 bits, don't hurt for 32 bits
	make all doc 
	make DESTDIR=$pkgdir install install-html || return 1
	
	install -d $pkgdir/usr/share/scilab/thirdparty &&
	install -d $pkgdir/usr/share/licenses/$pkgname &&
	install --mode=644 $srcdir/scilab-$pkgver/COPYING \
	  $pkgdir/usr/share/licenses/$pkgname || return 1
	#fix icon
	install -Dm644 $pkgdir/usr/share/scilab/icons/scilab.xpm \
	  $pkgdir/usr/share/pixmaps/scilab.xpm || return 1
	install -Dm644 $srcdir/$pkgname.desktop \
	  $pkgdir/usr/share/applications/$pkgname.desktop|| return 1
	sed -i 's#/jni##' $pkgdir/usr/share/scilab/etc/librarypath.xml
}
