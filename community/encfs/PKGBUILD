# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Sven Kauber, <celeon@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

# remove when bumped upstream

pkgname=encfs
pkgver=1.9.4
pkgrel=1.1
pkgdesc='Encrypted filesystem in user-space'
arch=('x86_64')
url='https://vgough.github.io/encfs/'
license=('LGPL')
depends=('openssl' 'fuse2' 'tinyxml2')
makedepends=('cmake')
source=(https://github.com/vgough/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
# validpgpkeys=('E583187B2E26D6EF8BF4556333C65E29813C14DF') # Valient Gough
validpgpkeys=('FFF3E01444FED7C316A3545A895F5BC123A02740') # Jakob Unterwurzacher <jakobunt@gmail.com>
sha256sums=('20656b4ead58ebd8d5f49a5c346b59e70dc2dc31220159e5b5a115bfa1bc40d6'
            'SKIP')

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
      -DUSE_INTERNAL_TINYXML=OFF \
      -DINSTALL_LIBENCFS=ON \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_INSTALL_PREFIX=/usr
  make
}

# check() {
#   cd "${srcdir}"/build
# 
#   make test
# }

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
