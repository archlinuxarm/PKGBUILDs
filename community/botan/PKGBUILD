# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon on !AArch64

pkgname=botan
pkgver=2.12.1
pkgrel=1
pkgdesc='Crypto library written in C++'
arch=(x86_64)
url='https://botan.randombit.net/'
license=(BSD)
makedepends=(python)
optdepends=('python: for using botan2.py')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')
source=("https://botan.randombit.net/releases/Botan-${pkgver}.tar.xz"{,.asc})
sha256sums=('7e035f142a51fca1359705792627a282456d49749bf62a37a8e48375d41baaa9'
            'SKIP')

build() {
  cd "${pkgname^}-$pkgver"

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"

  # botan benefits from -O3. Upstream is using it when testing.
  CXXFLAGS="$CXXFLAGS -O3" \
    ./configure.py \
    --prefix=/usr \
    --with-bzip \
    --with-lzma \
    --with-zlib \
    --with-os-feature=getrandom \
    $CONFIG

  make
}

check() {
  cd "${pkgname^}-$pkgver"

  ./botan-test
}

package() {
  DESTDIR="$pkgdir" make -C "${pkgname^}-$pkgver" install
  install -Dm644 "${pkgname^}-$pkgver/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/randombit/botan
# vim: ts=2 sw=2 et:
