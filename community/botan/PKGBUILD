# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon on !AArch64

pkgname=botan
pkgver=2.15.0
pkgrel=2
pkgdesc='Crypto library written in C++'
arch=(x86_64)
url='https://botan.randombit.net/'
license=(BSD)
depends=(xz sqlite)
makedepends=(python boost openssl)
optdepends=('python: for using botan2.py' 'boost-libs: for the botan executable')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')
source=("https://botan.randombit.net/releases/Botan-${pkgver}.tar.xz"{,.asc}
         botan-ldflags.patch)
sha256sums=('d88af1307f1fefac79aa4f2f524699478d69ce15a857cf2d0a90ac6bf2a50009'
            'SKIP'
            'a6e85d537f9a1299be8a6763df09c1b90680f5efd92c42a53ff0f985c82b3000')

prepare() {
  cd ${pkgname^}-$pkgver
  patch -p1 -i ../botan-ldflags.patch # Fix --as-needed
}

build() {
  cd "${pkgname^}-$pkgver"

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"

  ./configure.py \
    --prefix=/usr \
    --with-bzip \
    --with-lzma \
    --with-zlib \
    --with-boost \
    --with-openssl \
    --with-sqlite3 \
    --with-os-feature=getrandom \
    $CONFIG

  make
}

check() {
  cd "${pkgname^}-$pkgver"

  LD_LIBRARY_PATH="$PWD" ./botan-test
}

package() {
  DESTDIR="$pkgdir" make -C "${pkgname^}-$pkgver" install
  install -Dm644 "${pkgname^}-$pkgver/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
