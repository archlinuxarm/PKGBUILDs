# $Id: PKGBUILD 87983 2013-04-09 20:48:46Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - compiler flags adjustment to build correctly
#  - --cpu in configure set to correct architectures, autodetect fails

pkgname=botan
pkgver=1.11.13
pkgrel=1
pkgdesc='Crypto library written in C++'
license=('BSD')
arch=('x86_64' 'i686')
url='http://botan.randombit.net/'
depends=('gcc-libs' 'sh' 'asio')
makedepends=('python2' 'asio')
source=("http://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc})
sha256sums=('81f826a609ec1eed8b0ede1b870b8dea1294f5314de807cf33cd7073c634e3c9'
            'SKIP')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC') # Botan Distribution Key

prepare() {
  cd "Botan-$pkgver/src"

  # Use python2 for the installation scripts
  find scripts -name '*.py' -exec sed -i -e '1s,python$,python2,' {} +
}

build() {
  cd "Botan-$pkgver"

  if [[ $CARCH == 'armv7h' ]]; then
    sed -i 's/lang_flags "/lang_flags "-mfpu=vfpv3-d16 -mfloat-abi=hard /' src/build-data/cc/gcc.txt
    CPU='arm/armv7-a'
  elif [[ $CARCH == 'armv6h' ]]; then
    sed -i 's/lang_flags "/lang_flags "-mfpu=vfp -mfloat-abi=hard /' src/build-data/cc/gcc.txt
    CPU='arm/armv6'
  elif [[ $CARCH == 'arm' ]]; then
    CPU='arm/armv5te'
  fi

  python2 configure.py --prefix=/usr --enable-modules=cvc --destdir="$pkgdir/usr" --cpu=$CPU
  make 
}

package() {
  cd "Botan-$pkgver"

  make DESTDIR="$pkgdir/usr" install
  find "$pkgdir/usr/share/doc" -type f -exec chmod 0644 {} \;
  install -Dm644 doc/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
