# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - AArch64 patch
#  - disable neon on !AArch64

pkgname=botan
pkgver=2.6.0
pkgrel=1
pkgdesc='Crypto library written in C++'
arch=('x86_64')
url='https://botan.randombit.net/'
license=('BSD')
depends=('xz')
makedepends=('python')
optdepends=('python: for using botan2.py')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')
source=("https://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc}
        '0001-aarch64-support.patch')
sha256sums=('c1f261555bba702c73608dde7bd743ef2d6377a41a1c295915b25c5babaf5cc5'
            'SKIP'
            '0e279e3b16e115e26f5dc1edf02a4082f063134513aaf6ad1fbd10a1d2624e37')

prepare() {
  cd "${pkgname^}-$pkgver"

  patch -p1 -i ../0001-aarch64-support.patch
}

build() {
  cd "${pkgname^}-$pkgver"

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"

  # botan benefits from -O3, and upstream devs are running benchmarks with it
  CXXFLAGS="$CXXFLAGS -O3" ./configure.py \
    --prefix=/usr \
    --with-bzip \
    --with-zlib \
    --with-lzma \
    $CONFIG
  make
}

check() {
  cd "${pkgname^}-$pkgver"

  ./botan-test
}

package() {
  DESTDIR="$pkgdir" make -C "${pkgname^}-$pkgver" install
  install -Dm644 "${pkgname^}-$pkgver/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/randombit/botan
# vim: ts=2 sw=2 et:
