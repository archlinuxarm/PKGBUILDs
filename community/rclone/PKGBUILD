# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

# ALARM: Lorenz Steinert <lsteinert@uraziel.de>
#  - remove makedepends pandoc
#  - remove rclone.1 MANUAL.html and MANUAL.txt targets from make in build()

pkgname=rclone
pkgver=1.59.2
pkgrel=1
pkgdesc="Sync files to and from Google Drive, S3, Swift, Cloudfiles, Dropbox and Google Cloud Storage"
arch=('x86_64')
url="https://rclone.org/"
license=('MIT')
depends=('glibc')
optdepends=('fuse2: for rclone mount')
makedepends=('python' 'go' 'git' 'fuse2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rclone/rclone/archive/v$pkgver.tar.gz")
sha512sums=('5741e1e1b4d2d0dc0f80baf2f7c318cdda995c2f15b7b9e0dd1c9be7372ab21cedba377c40adcc0edd0482aec7c2a27e0144a6edb957840b9422a8bbff922342')

prepare() {
  cd "rclone-$pkgver"
  sed -i "1s/python$/&2/" bin/make_manual.py bin/make_backend_docs.py
}

build() {
  cd "rclone-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  PATH="/build/go/bin:$PATH" make TAG=v$pkgver rclone || true
  PATH="/build/go/bin:$PATH" rclone genautocomplete bash rclone.bash_completion
  PATH="/build/go/bin:$PATH" rclone genautocomplete zsh rclone.zsh_completion
}

check() {
  cd "rclone-$pkgver"
  PATH="/build/go/bin:$PATH" make TAG=v$pkgver test
}

package() {
  cd "rclone-$pkgver"

  install -Dm755 rclone "$pkgdir"/usr/bin/rclone

  install -Dm644 rclone.bash_completion "$pkgdir"/usr/share/bash-completion/completions/rclone
  install -Dm644 rclone.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_rclone

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  install -Dm644 rclone.1 "$pkgdir"/usr/share/man/man1/rclone.1
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -t "$pkgdir"/usr/share/doc/$pkgname -m644 MANUAL.html MANUAL.txt
}
