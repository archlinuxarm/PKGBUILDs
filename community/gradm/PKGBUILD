# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: s1gma <s1gma@mindslicer.com>
# Contributor: Ahmad24 <myitrayan@gmail.com>
# Contributor: maxrp <maxp@pdx.edu>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - comment LIBS mistreatment of ARM in makefile

pkgname=gradm
_version=3.0
_timestamp=201407222118
pkgver=3.0.$_timestamp
pkgrel=2
pkgdesc="Administration utility for grsecurity's Role Based Access Control (RBAC)"
arch=(i686 x86_64)
url=https://grsecurity.net/
license=(GPL2)
depends=(pam)
source=(https://grsecurity.net/stable/$pkgname-$_version-$_timestamp.tar.gz
        https://grsecurity.net/stable/$pkgname-$_version-$_timestamp.tar.gz.sig
        learn_config
        policy)
sha256sums=('6c29274d63293540646be8c8c2c131654ec307b17674c25085b352305562e7e8'
            'SKIP'
            '704ea6ba7f748761735cbe1cf52ef04f53eab1a1e9ea1bdcb6abaaf4a641e44d'
            '1ddc7eede746da2ac321a2a46facefbe98992123d950b1c1240aa4d360cbc7c4')

prepare() {
  cd $pkgname
  sed -i -e 's/^CFLAGS :=/CFLAGS +=/' -e 's:sbin:usr/bin:' Makefile
  sed -i 's/^LIBS/#LIBS/' Makefile
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  cp "$srcdir"/{learn_config,policy} "$pkgdir/etc/grsec"
  rm -r "$pkgdir/dev"
}
