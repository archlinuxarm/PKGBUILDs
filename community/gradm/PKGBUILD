# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: s1gma <s1gma@mindslicer.com>
# Contributor: Ahmad24 <myitrayan@gmail.com>
# Contributor: maxrp <maxp@pdx.edu>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - comment LIBS mistreatment of ARM in makefile

pkgname=gradm
_version=3.0
_timestamp=201408301734
pkgver=3.0.$_timestamp
pkgrel=2
pkgdesc="Administration utility for grsecurity's Role Based Access Control (RBAC)"
arch=(i686 x86_64)
url=https://grsecurity.net/
license=(GPL2)
depends=(pam)
source=(https://grsecurity.net/stable/$pkgname-$_version-$_timestamp.tar.gz
        https://grsecurity.net/stable/$pkgname-$_version-$_timestamp.tar.gz.sig
        learn_config
        policy)
sha256sums=('b190e5afecdf3ac5020a4e5e4b698645f1c01b20d036129dd8b609c4bd0c319c'
            'SKIP'
            '61c3042879ec2303b713f57f751fb66a95e2cc4737fbbd6d95879829c7b7d3c0'
            '73cf31add3da55b539777d736764a40c6b30041cc259e1d0372c867b87070440')
validpgpkeys=(
              'DE9452CE46F42094907F108B44D1C0F82525FE49' # Bradley Spengler
)

prepare() {
  cd $pkgname
  sed -i -e 's/^CFLAGS :=/CFLAGS +=/' -e 's:sbin:usr/bin:' Makefile
  sed -i 's/^LIBS/#LIBS/' Makefile
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  cp "$srcdir"/{learn_config,policy} "$pkgdir/etc/grsec"
  rm -r "$pkgdir/dev"
}
