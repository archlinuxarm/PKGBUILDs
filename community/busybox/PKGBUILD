# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with musl-gcc

pkgname=busybox
pkgver=1.26.1
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
install=busybox.install
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"
	"config")
sha256sums=('0fbbe487fa35cc3258749cf79c259578c1a221013cc4fb60d6a3542590f781f2'
            'ee3f947d8194a85b0adc150db8000d09c3fc31827ab259954a2c1ea9e646df6c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir"/config .config
  sed '1,1i#include <sys/resource.h>' -i include/libbb.h
  # if you want to run menuconfig uncomment the following line:
#  make menuconfig ; return 1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 busybox "$pkgdir"/usr/bin/busybox
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
