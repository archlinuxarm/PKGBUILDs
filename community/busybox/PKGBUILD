# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with musl-gcc

pkgname=busybox
pkgver=1.28.4
pkgrel=2
pkgdesc="Utilities for rescue and embedded systems"
arch=("x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("ncurses")
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"
        "config")
sha256sums=('e3c14a3699dc7e82fed397392957afc78e37bdf25398ac38ead6e84621b2ae6a'
            '7a067356b7e0f85ce1ba0aa6d2fd4f14de14872e80f7dd2f3cedd7e31d6c5a5b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir"/config .config
  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0755 busybox "$pkgdir"/usr/bin/busybox

  # docs
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
