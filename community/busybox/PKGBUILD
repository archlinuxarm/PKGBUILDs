# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with musl-gcc

pkgname=busybox
pkgver=1.26.2
pkgrel=2
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
install=busybox.install
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"
	"config")
sha256sums=('da3e44913fc1a9c9b7c5337ea5292da518683cbff32be630777f565d6036af16'
            '42e3d2756774c038e86c9fc6b228f6db4f6b75217ff403ac64d71a8bd0543286')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir"/config .config
  sed '1,1i#include <sys/resource.h>' -i include/libbb.h
  # if you want to run menuconfig uncomment the following line:
#  make menuconfig ; return 1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 busybox "$pkgdir"/usr/bin/busybox
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
