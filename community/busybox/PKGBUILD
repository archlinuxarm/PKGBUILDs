# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with musl-gcc

pkgname=busybox
pkgver=1.28.3
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
install=busybox.install
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"
	"config")
sha256sums=('ad0d22033f23e696f9a71a4c2f9210194dda39b024a79151f4ac278995332a6e'
            '5ac1b35aa9fee3be15a4ce10e533de35ea9e51f888186a45222e102f35c36bc6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir"/config .config
  sed '1,1i#include <sys/resource.h>' -i include/libbb.h
  # if you want to run menuconfig uncomment the following line:
#  make menuconfig ; return 1
  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 busybox "$pkgdir"/usr/bin/busybox
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
