# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with musl-gcc

pkgname=busybox
pkgver=1.27.1
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
install=busybox.install
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"
	"config")
sha256sums=('c890ac53fb218eb4c6ad9ed3207a896783b142e6d306f292b8d9bec82af5f936'
            '632e18699a9dd8d76341162f57de2ad3920b9a809645fe710b0b656b9552dfbc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir"/config .config
  sed '1,1i#include <sys/resource.h>' -i include/libbb.h
  # if you want to run menuconfig uncomment the following line:
#  make menuconfig ; return 1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 busybox "$pkgdir"/usr/bin/busybox
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
