# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

pkgname=tesseract
pkgver=2.04
pkgrel=2
pkgdesc="An OCR programm"
arch=(x86_64 i686)
url="http://code.google.com/p/tesseract-ocr"
license=("APACHE")
depends=(libtiff libjpeg zlib gcc-libs)
source=(http://$pkgname-ocr.googlecode.com/files/$pkgname-$pkgver.tar.gz
        http://$pkgname-ocr.googlecode.com/files/$pkgname-2.00.nld.tar.gz
        http://$pkgname-ocr.googlecode.com/files/$pkgname-2.00.spa.tar.gz
        http://$pkgname-ocr.googlecode.com/files/$pkgname-2.00.deu.tar.gz
        http://$pkgname-ocr.googlecode.com/files/$pkgname-2.00.ita.tar.gz
        http://$pkgname-ocr.googlecode.com/files/$pkgname-2.01.por.tar.gz
        http://$pkgname-ocr.googlecode.com/files/$pkgname-2.00.fra.tar.gz
        http://$pkgname-ocr.googlecode.com/files/$pkgname-2.00.eng.tar.gz)
md5sums=('b44eba1a9f4892ac62e484c807fe0533'
         'b2f6ede182cea4bbfffd3b040533ce58'
         'bc26a777b2384613895677cb8e61ca75'
         '609d91b1ae3759a756b819b5d8403653'
         '2759e1dae91a989a43490ff4c2253a4b'
         'eca21fed3ce1f8ded8dfcedbd1351af7'
         '64896b462e62572a3708bb461820126c'
         'b8291d6b3a63ce7879d688e845e341a9')

build() {
  cd $startdir/src/$pkgname-$pkgver
  mv ../tessdata/* tessdata
  ./configure --prefix=/usr

  patch -p0 <<EOF tessdata/Makefile
174a175,176
>             por.DangAmbigs por.freq-dawg por.inttemp por.normproto \\
>             por.pffmtable por.user-words por.word-dawg por.unicharset \\
526a530,531
> por.DangAmbigs por.freq-dawg por.inttemp por.normproto por.pffmtable por.user-words por.word-dawg por.unicharset : makedummies
> 	\$(top_srcdir)/tessdata/makedummies por
EOF

  patch -p1 <<EOF viewer/scrollview.h
diff -r tesseract-2.03/viewer/scrollview.h tesseract-2.03.my/viewer/scrollview.h
35a36,37
> #include <string.h>
> #include <limits.h>
EOF

  patch -p1 <<EOF viewer/svutil.cpp
diff -r tesseract-2.03/viewer/svutil.cpp tesseract-2.03.my/viewer/svutil.cpp
23a24,27
> #include <string.h>
> #include <limits.h>
> #include <stdlib.h>
> #include <stdio.h>
EOF

  make || return 1
  rm -f java/makefile
  make DESTDIR=$startdir/pkg install || return 1
  find $startdir/pkg/usr/share/tessdata -type f -exec chmod 0644 {} \;
}
