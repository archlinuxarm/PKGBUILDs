# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-sse from configure

pkgname=calf
pkgver=0.90.2
pkgrel=1
pkgdesc='LV2 plug-in suite and standalone Jack host'
arch=('x86_64')
url="https://calf-studio-gear.org"
groups=('lv2-plugins' 'pro-audio')
license=('LGPL')
depends=('fftw' 'fluidsynth' 'gtk2')
makedepends=('lv2')
source=("https://calf-studio-gear.org/files/$pkgname-$pkgver.tar.gz")
sha256sums=('f0e9d36990ece7ae6334166ced5a2e02bbd8bcd2aa2ecad0efce0b4e6790c42e')

prepare(){
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-lv2 \
              --enable-experimental
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
