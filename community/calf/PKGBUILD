# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-sse from configure

pkgname=calf
pkgver=0.90.3
pkgrel=1
pkgdesc='LV2 plug-in suite and standalone Jack host'
arch=('x86_64')
url="https://calf-studio-gear.org"
groups=('lv2-plugins' 'pro-audio')
license=('LGPL2.1')
depends=('fftw' 'fluidsynth' 'gtk2')
makedepends=('lv2')
source=("https://calf-studio-gear.org/files/$pkgname-$pkgver.tar.gz")
sha256sums=('e32bc23fb68f88729bdef361614fd9ac38627c774c35cd3c4eb611fca887a49f')

prepare(){
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-lv2 \
              --enable-experimental
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,ChangeLog,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
