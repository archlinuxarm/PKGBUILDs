# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Dan McGee <dan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix AArch64 FTBFS with LLVM 21

pkgbase=postgresql-old-upgrade
pkgname=(
  postgresql-old-upgrade
)
pkgver=17.6
pkgrel=2
pkgdesc="Older PostgreSQL for migrating major versions with pg_upgrade"
url="https://www.postgresql.org/"
arch=(x86_64)
license=(PostgreSQL)
depends=(
  gcc-libs
  glibc
  icu
  krb5
  libldap
  libxml2
  llvm-libs
  lz4
  openssl
  pam
  systemd-libs
  util-linux-libs
  zlib
  zstd
)
makedepends=(
  clang
  llvm
  perl
  python
  systemd
  tcl
  util-linux
)
source=(
  https://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2
  0001-jit-fix-build-with-LLVM-21.patch
  0002-llvm-21-aarch64.patch
)
b2sums=('9287eab85e17a65333d970bf7d4d254e25295615b4787072ff2a6c705cb66b7775dbe3a0c6b6fa3e2ab37c3278d204ccfdd3e587226d4c1d6363868569149216'
        '392403ece1c5886d6e0d7062091dc71ec1cb085d1da2e0d4b91638c875c5d84ce2b2e4faca05ee336f57a9482e81aeccf882c9ced8dd56091095f5f612d86e03'
        'd730f79f14ec6c0e4b3b8cbe7b885643de4830c6ce19c20d5e349e4009cf81b017168324595a4d72841f07da2fc8bbbcd2c3d310163f8e76358203d90f34ce99')

# PostgreSQL releases are unsigned and only provide these sums
md5sums=('e72b7e5dc22d44d56b113ed1f74e4084'
         '0d34dd948e5b031d9ae4312a1eb10bd6'
         '58fdb5a17290bde6f9d45c908ea00b38')
sha256sums=('e0630a3600aea27511715563259ec2111cd5f4353a4b040e0be827f94cd7a8b0'
            'ee6a8220d846c3064ca93dbdc656a0ffbcfad8736333fae17b40e5e65d4ed558'
            'b19e69d83736b3e0144d422df59f34505ce60ebb0e6bc429985e7fb11a40d67c')

prepare() {
  cd postgresql-${pkgver}
  patch -Np1 < ../0001-jit-fix-build-with-LLVM-21.patch
  patch -Np1 < ../0002-llvm-21-aarch64.patch
}

build() {
  local configure_options=(
    --prefix=/opt/pgsql-${pkgver%%.*}
    --disable-rpath
    --disable-nls
    --disable-tap-tests
    --with-gssapi
    --with-icu
    --with-ldap
    --with-libxml
    --without-libxslt
    --with-llvm
    --with-lz4
    --with-openssl
    --with-pam
    --with-perl
    --with-python
    --without-readline
    --with-system-tzdata=/usr/share/zoneinfo
    --with-systemd
    --with-tcl
    --with-uuid=e2fs
    --with-zstd
  )

  # use fat LTO objects for static libraries
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  mkdir -p build; cd build
  ../postgresql-${pkgver}/configure "${configure_options[@]}"
  make world-bin
}

package_postgresql-old-upgrade() {
  depends+=("postgresql-libs>=${pkgver}")
  optdepends=(
    'perl: for PL/Perl support'
    'python: for PL/Python 3 support'
    'tcl: for PL/Tcl support'
  )

  make -C build DESTDIR="${pkgdir}" install-world-bin


  install -Dm644 postgresql-${pkgver}/COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set sw=2 sts=-1 et:
