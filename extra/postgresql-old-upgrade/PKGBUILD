# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Dan McGee <dan@archlinux.org>

# remove when bumped upstream

pkgbase=postgresql-old-upgrade
pkgname=(
  postgresql-old-upgrade
)
pkgver=17.8
pkgrel=1
pkgdesc="Older PostgreSQL for migrating major versions with pg_upgrade"
url="https://www.postgresql.org/"
arch=(x86_64)
license=(PostgreSQL)
depends=(
  glibc
  icu libicui18n.so libicuuc.so
  krb5 libgssapi_krb5.so
  libgcc libgcc_s.so
  libldap
  libstdc++ libstdc++.so
  libxml2 libxml2.so
  llvm-libs
  lz4 liblz4.so
  openssl libcrypto.so libssl.so
  pam libpam.so
  systemd-libs libsystemd.so
  util-linux-libs
  zlib libz.so
  zstd libzstd.so
)
makedepends=(
  clang
  llvm
  perl
  python
  systemd
  tcl
  util-linux
)
source=(
  https://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2
)
b2sums=('9fcae43caf765d841c03d154e4992abbacaa734e0cf7524b59acc0103fa0b3e1888d8a46e9e6b2ccf9329f49701e19cf4b69878794b24c6306388bdb489e8401')

# PostgreSQL releases are unsigned and only provide these sums
md5sums=('c045db1c921592960ffc4aafdfc4537c')
sha256sums=('a88d195dd93730452d0cfa1a11896720d6d1ba084bc2be7d7fc557fa4e4158a0')

prepare() {
  cd postgresql-${pkgver}
}

build() {
  local configure_options=(
    --prefix=/opt/pgsql-${pkgver%%.*}
    --disable-rpath
    --disable-nls
    --disable-tap-tests
    --with-gssapi
    --with-icu
    --with-ldap
    --with-libxml
    --without-libxslt
    --with-llvm
    --with-lz4
    --with-openssl
    --with-pam
    --with-perl
    --with-python
    --without-readline
    --with-system-tzdata=/usr/share/zoneinfo
    --with-systemd
    --with-tcl
    --with-uuid=e2fs
    --with-zstd
  )

  # use fat LTO objects for static libraries
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  mkdir -p build; cd build
  ../postgresql-${pkgver}/configure "${configure_options[@]}"
  make world-bin
}

package_postgresql-old-upgrade() {
  depends+=("postgresql-libs>=${pkgver}")
  optdepends=(
    'perl: for PL/Perl support'
    'python: for PL/Python 3 support'
    'tcl: for PL/Tcl support'
  )

  make -C build DESTDIR="${pkgdir}" install-world-bin


  install -Dm644 postgresql-${pkgver}/COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set sw=2 sts=-1 et:
