# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Dan McGee <dan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix AArch64 FTBFS with LLVM 21

pkgbase=postgresql-old-upgrade
pkgname=(
  postgresql-old-upgrade
)
pkgver=17.7
pkgrel=1
pkgdesc="Older PostgreSQL for migrating major versions with pg_upgrade"
url="https://www.postgresql.org/"
arch=(x86_64)
license=(PostgreSQL)
depends=(
  gcc-libs
  glibc
  icu
  krb5
  libldap
  libxml2
  llvm-libs
  lz4
  openssl
  pam
  systemd-libs
  util-linux-libs
  zlib
  zstd
)
makedepends=(
  clang
  llvm
  perl
  python
  systemd
  tcl
  util-linux
)
source=(
  https://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2
  0002-llvm-21-aarch64.patch
)
b2sums=('245f866c7375566ca772171b89eb2277a17175b599973e740ce599605292a5847e68b857f83b208a24d0ab6938740faf00ab26758d72c7e119131745f8ab63cb'
        'd730f79f14ec6c0e4b3b8cbe7b885643de4830c6ce19c20d5e349e4009cf81b017168324595a4d72841f07da2fc8bbbcd2c3d310163f8e76358203d90f34ce99')

# PostgreSQL releases are unsigned and only provide these sums
md5sums=('a4fa04d16e511e068736d154ca74752d'
         '58fdb5a17290bde6f9d45c908ea00b38')
sha256sums=('ef9e343302eccd33112f1b2f0247be493cb5768313adeb558b02de8797a2e9b5'
            'b19e69d83736b3e0144d422df59f34505ce60ebb0e6bc429985e7fb11a40d67c')

prepare() {
  cd postgresql-${pkgver}
  patch -Np1 < ../0002-llvm-21-aarch64.patch
}

build() {
  local configure_options=(
    --prefix=/opt/pgsql-${pkgver%%.*}
    --disable-rpath
    --disable-nls
    --disable-tap-tests
    --with-gssapi
    --with-icu
    --with-ldap
    --with-libxml
    --without-libxslt
    --with-llvm
    --with-lz4
    --with-openssl
    --with-pam
    --with-perl
    --with-python
    --without-readline
    --with-system-tzdata=/usr/share/zoneinfo
    --with-systemd
    --with-tcl
    --with-uuid=e2fs
    --with-zstd
  )

  # use fat LTO objects for static libraries
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  mkdir -p build; cd build
  ../postgresql-${pkgver}/configure "${configure_options[@]}"
  make world-bin
}

package_postgresql-old-upgrade() {
  depends+=("postgresql-libs>=${pkgver}")
  optdepends=(
    'perl: for PL/Perl support'
    'python: for PL/Python 3 support'
    'tcl: for PL/Tcl support'
  )

  make -C build DESTDIR="${pkgdir}" install-world-bin


  install -Dm644 postgresql-${pkgver}/COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set sw=2 sts=-1 et:
