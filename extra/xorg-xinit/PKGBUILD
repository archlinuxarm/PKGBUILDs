# $Id: PKGBUILD 26870 2009-02-13 17:13:44Z andyrtr $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xinit
pkgver=1.1.1
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=(arm)
license=('GPL')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth')
makedepends=('pkgconfig')
backup=('etc/skel/.xinitrc'
	'etc/skel/.xsession'
	'etc/X11/xinit/xserverrc'
	'etc/X11/xinit/xinitrc')
groups=('xorg')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2
	xinitrc
	xsession
	xserverrc)
options=(!emptydirs)
md5sums=('93c73705ed9eb1e1a6b6938405672f2b'
         'cee2d70748a089fbc7db64a4a6e8fdb1'
         '7873d97db30d6c279ed37e3559e7c59d'
         'bb550d3b9a2d2b6cbe6e2667a3a7cd03')

build() {
  cd "${srcdir}/xinit-${pkgver}"
  ./configure --prefix=/usr || return 1
  make XINITDIR=/etc/X11/xinit || return 1
  make XINITDIR=/etc/X11/xinit DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/etc/skel"
  install -m755 -d "${pkgdir}/etc/X11/xinit"
  install -m755 "${srcdir}/xinitrc" "${pkgdir}/etc/skel/.xinitrc" || return 1
  install -m755 "${srcdir}/xsession" "${pkgdir}/etc/skel/.xsession" || return 1
  install -m755 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc" || return 1
}
