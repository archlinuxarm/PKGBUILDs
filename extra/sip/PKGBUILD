# Maintainer: Antonio Rojas <arojas@archlinux.org>

# removed when bumped upstream

pkgname=sip
pkgver=6.8.0
pkgrel=1
arch=(any)
pkgdesc='A tool that makes it easy to create Python bindings for C and C++ libraries'
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
depends=(python
         python-packaging
         python-setuptools
         python-tomli)
makedepends=(python-build
             python-installer
             python-wheel)
source=(https://pypi.python.org/packages/source/s/sip/$pkgname-$pkgver.tar.gz)
sha256sums=('2ed1904820cb661b7207eb1dccfaebec1a5463dcad903ba448ad1945502d089c')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
