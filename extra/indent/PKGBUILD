# $Id: PKGBUILD 53908 2009-10-03 20:30:12Z eric $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: aurelien <aurelien@archlinux.org>
# Contributor: Herb Rose (hrose56@yahoo.com)

pkgname=indent
pkgver=2.2.10
pkgrel=4
pkgdesc="The GNU Pretty-printer"
arch=('i686' 'x86_64')
url="http://indent.isidore-it.eu/beautify.html"
depends=('glibc' 'texinfo')
license=('GPL3')
install=indent.install
source=(http://indent.isidore-it.eu/$pkgname-$pkgver.tar.gz)
md5sums=('be35ea62705733859fbf8caf816d8959')
sha1sums=('20fa8a7a4af6670c3254c8b87020291c3db37ed1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # Fix doc install path
  sed -i 's|^docdir = .*$|docdir = @docdir@|' doc/Makefile.in || return 1
  
  # Fix manpage installation
  sed -i 's|\(info texinfo2man\)\.c  Makefile\.am|\1|' man/Makefile.in || return 1
  rm man/indent.1

  ./configure --prefix=/usr --datadir=/usr/share --docdir=/usr/share/doc/indent || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}
