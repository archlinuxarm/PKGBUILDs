# $Id: PKGBUILD 52846 2009-09-22 12:18:37Z allan $

pkgname=gnash-gtk
pkgver=0.8.5
pkgrel=3
pkgdesc="A GNU Flash movie player (gtk)"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gnash/"
license=('GPL3')
depends=("gnash-common=$pkgver-5" 'gtk2' 'gtkglext' 'libxmu')
makedepends=('pkgconfig')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnu.org/gnu/gnash/${pkgver}/gnash-${pkgver}.tar.bz2)
md5sums=('366f703c2eb1747e4109602b75439ff4')

build() {
	cd "$srcdir/gnash-$pkgver"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-gui=gtk \
		--with-plugins-install=system \
		--with-npapi-plugindir=/usr/lib/mozilla/plugins \
		--enable-renderer=agg \
		--enable-z \
		--enable-jpeg \
		--enable-png \
		--enable-gif \
		--enable-speex \
		--enable-fontconfig \
		--enable-Xft \
		--enable-expat \
		--enable-gstreamer \
		--enable-media=gst || return 1

	make || return 1
	make DESTDIR="$pkgdir" install install-plugin || return 1
	rm -rf "$pkgdir"/usr/{lib/gnash,share}
	rm -f "$pkgdir"/usr/bin/{gnash,gprocessor,dumpshm,soldumper,flvdumper}
	rm -rf "$pkgdir/etc"
}
