# $Id: PKGBUILD 15368 2008-10-13 11:40:52Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=fast-user-switch-applet
pkgver=2.24.0
pkgrel=1
pkgdesc="An applet for the GNOME panel which provides a menu to switch between users."
arch=(i686 x86_64)
license=('GPL')
url="http://ignore-your.tv/fusa/"
depends=('gnome-panel>=2.24.0' 'libxmu' 'gdm>=2.20.8')
makedepends=('perlxml' 'pkgconfig')
options=(!emptydirs)
groups=('gnome-extra')
install=fusa.install
source=(http://ftp.gnome.org/pub/GNOME/sources/fast-user-switch-applet/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('001ef5a6a03ba6a0f4857bc3b05ec8f1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/fast-user-switch-applet \
              --localstatedir=/var --disable-scrollkeeper \
              --with-gdm-setup="/usr/bin/gksu /usr/sbin/gdmsetup" \
              --with-gdm-config=/etc/gdm/custom.conf || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
