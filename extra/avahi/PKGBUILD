# $Id: PKGBUILD 118159 2011-04-05 16:56:06Z bisson $
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - removed makedepend on mono

plugrel=1

pkgname=avahi
pkgver=0.6.30
pkgrel=1
pkgdesc='A multicast/unicast DNS-SD framework'
arch=('i686' 'x86_64')
url='http://www.avahi.org/'
license=('LGPL')
depends=('expat' 'libdaemon' 'glib2' 'dbus' 'libcap' 'gdbm')
optdepends=('gtk2: avahi-discover-standalone, bshell, bssh, bvnc'
            'qt3: qt3 bindings'
            'qt: qt bindings'
            'pygtk: avahi-bookmarks, avahi-discover'
            'twisted: avahi-bookmarks'
            'dbus-python: avahi-discover'
            'nss-mdns: NSS support for mDNS')
makedepends=('qt' 'qt3' 'pygtk' 'intltool' 'dbus-python'
             'gtk-sharp-2' 'gobject-introspection')
backup=(etc/avahi/avahi-daemon.conf etc/avahi/services/{sftp-,}ssh.service)
install=avahi.install
conflicts=('howl' 'mdnsresponder')
provides=('howl' 'mdnsresponder')
replaces=('howl' 'mdnsresponder')
options=('!libtool')
source=(http://www.avahi.org/download/avahi-${pkgver}.tar.gz
        avahi-daemon-dbus.patch
        gnome-nettool.png)
sha1sums=('5b77443537600a00770e4c77e3c443eeb5861d06'
          '41e9f23efa0b5a5f7b0f14a86cfb0677ece84c61'
          'cf56387c88aed246b9f435efc182ef44de4d52f3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's/netdev/network/g' avahi-daemon/avahi-dbus.conf
  patch -Np0 -i "${srcdir}/avahi-daemon-dbus.patch"

  # pygtk requires python2; make it explicit in case other python are installed: FS#21865
  PYTHON=python2 \
  PKG_CONFIG_PATH=/opt/qt/lib/pkgconfig \
    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-gtk3 \
    --disable-monodoc \
    --disable-doxygen-doc \
    --disable-xmltoman \
    --enable-compat-libdns_sd \
    --enable-compat-howl \
    --with-distro=archlinux \
    --with-avahi-priv-access-group=network \
    --with-autoipd-user=avahi \
    --with-autoipd-group=avahi \
    --with-systemdsystemunitdir=/lib/systemd/system # See FS#20999

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # howl and mdnsresponder compatability
  cd "${pkgdir}"/usr/include
  ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h
  ln -s avahi-compat-howl howl
  cd "${pkgdir}"/usr/lib/pkgconfig
  ln -s avahi-compat-howl.pc howl.pc

  sed -i '1c #!/usr/bin/python2' "$pkgdir"/usr/bin/avahi-{bookmarks,discover}

  install -D -m 644 "${srcdir}"/gnome-nettool.png "${pkgdir}"/usr/share/pixmaps/gnome-nettool.png
}
