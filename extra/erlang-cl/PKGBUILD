# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: kappa <kappacurve@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -m32 and -m64 flags from makefile

pkgname=erlang-cl
pkgver=1.2.4
pkgrel=7
arch=(x86_64)
pkgdesc='OpenCL binding for Erlang'
url='https://github.com/tonyrog/cl'
license=(MIT)
replaces=(cl)
depends=(erlang opencl-icd-loader)
makedepends=(git opencl-headers rebar)
source=("git+$url#commit=f0a5631fdcf4e1c56e6bf1568c6c2c8fff0d66df") # tag: cl-1.2.4
b2sums=('SKIP')

prepare() {
  cd cl
  sed -i -e 's/ -m32//' -e 's/ -m64//' c_src/Makefile
}

build() {
  cd cl
  CFLAGS+=' -D_FORTIFY_SOURCE=2 -fPIE'
  rebar compile
}

package() {
  cd cl
  install -d "$pkgdir/usr/lib/erlang/lib/cl-$pkgver"
  cp -r * "$pkgdir/usr/lib/erlang/lib/cl-$pkgver/"
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
