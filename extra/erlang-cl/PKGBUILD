# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: kappa <kappacurve@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -m32 and -m64 flags from makefile

pkgname=erlang-cl
pkgver=1.2.4
pkgrel=10
arch=(x86_64)
pkgdesc='OpenCL binding for Erlang'
url='https://github.com/tonyrog/cl'
license=(MIT)
replaces=(cl)
depends=(
  erlang
  glibc
  opencl-icd-loader
)
makedepends=(
  git
  opencl-headers
  rebar3
)
source=("git+$url#tag=cl-$pkgver")
b2sums=('68bae78fd520551bafb4dde229deceb8d35ee99464116af16e99e041ec1d59d7dacbfd32bf9587295167c48ca190eb686407f9dfc3b57be556e7f3b5eadfa7e0')

prepare() {
  cd cl
  sed -i -e 's/ -m32//' -e 's/ -m64//' c_src/Makefile
}

build() {
  cd cl
  CFLAGS+=' -D_FORTIFY_SOURCE=2 -fPIE'
  rebar3 compile
}

package() {
  cd cl
  install -vdm755 "$pkgdir/usr/lib/erlang/lib/cl-$pkgver"
  cp -va \
    _build/default/lib/cl/ebin/ \
    _build/default/lib/cl/include/ \
    _build/default/lib/cl/priv/ \
    "$pkgdir/usr/lib/erlang/lib/cl-$pkgver/"
  install -vDm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
