# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fabien Dubosson <fabien.dubosson@gmail.com>
# Contributor: Konstantin Gizdov <arch@kge.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set DISPATCH=0, x86 only

pkgname=xxhash
pkgver=0.8.2
pkgrel=1
pkgdesc='Extremely fast non-cryptographic hash algorithm'
arch=(x86_64)
url=https://cyan4973.github.io/xxHash/
license=(
  GPL2
  BSD
)
depends=(glibc)
makedepends=(git)
provides=(libxxhash.so)
_tag=bbb27a5efb85b92a0486cf361a8635715a53f6ba
source=(git+https://github.com/Cyan4973/xxHash.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd xxHash
  git describe --tags | sed 's/^v//'
}

build() {
  make PREFIX=/usr DISPATCH=0 -C xxHash
}

package() {
  make PREFIX=/usr DISPATCH=0 DESTDIR="${pkgdir}" -C xxHash install
  install -Dm 644 xxHash/LICENSE -t "${pkgdir}"/usr/share/licenses/xxhash
}

# vim: ts=2 sw=2 et:
