# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DBUILD_CUBE=OFF

pkgname=vulkan-tools
pkgver=1.1.79+6811+b7c389c9
pkgrel=1
arch=(x86_64)
url="https://www.khronos.org/vulkan/"
pkgdesc="Vulkan Utilities and Tools"
license=('custom')
depends=(gcc-libs libxcb vulkan-icd-loader)
makedepends=(git cmake python vulkan-headers libx11 wayland)
source=("git+https://github.com/KhronosGroup/Vulkan-Tools.git#commit=b7c389c9d3efe5cea722eb7395fb70529dd27566")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/Vulkan-Tools"

  echo 1.1.79+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/Vulkan-Tools"

  rm -rf build ; mkdir build ; cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/usr/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DBUILD_WSI_MIR_SUPPORT=Off \
    -DBUILD_CUBE=OFF \
    -DBUILD_VULKANINFO=ON \
    -DBUILD_ICD=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  groups=('vulkan-devel')
  
  cd "${srcdir}/Vulkan-Tools/build"
  
  make DESTDIR="${pkgdir}" install

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
