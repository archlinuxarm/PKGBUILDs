# $Id: PKGBUILD 30802 2009-03-22 00:12:37Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=2.26.0
pkgrel=1
pkgdesc="The Control Center for GNOME"
arch=(i686 x86_64)
depends=('metacity>=2.26.0' 'libxss' 'gnome-panel>=2.26.0' 'desktop-file-utils' 'gnome-settings-daemon>=2.26.0' 'libgnomekbd>=2.26.0' 'libcanberra>=0.10' 'sound-theme-freedesktop')
makedepends=('gnome-doc-utils>=0.16.0' 'pkgconfig' 'intltool')
url="http://www.gnome.org"
groups=('gnome')
replaces=('control-center')
conflicts=('control-center')
provides=('control-center')
install=gnome-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('eed8feccc37712e34d88bd3b20e46962')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-gstreamer=0.10 --enable-aboutme \
	      --disable-scrollkeeper --disable-update-mimedb || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-control-center-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  # --disable-update-mimedb?
  rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
