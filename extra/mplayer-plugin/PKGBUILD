# $Id: PKGBUILD 3695 2008-06-29 12:20:34Z jgc $
# Mantainer: Alexander Baldeck <alexander@archlinux.org>
pkgname=mplayer-plugin
pkgver=3.55
pkgrel=1
pkgdesc="mplayerplug-in is a browser plugin that uses mplayer to play videos from websites."
arch=(i686 x86_64)
license=('GPL')
url="http://mplayerplug-in.sourceforge.net/"
depends=('gtk2>=2.12.10' 'mozilla-common>=1.2' 'mplayer' 'libxpm')
makedepends=('xulrunner>=1.9' 'pkgconfig')
backup=(etc/mplayerplug-in.conf)
install=mplayer-plugin.install
source=(http://downloads.sourceforge.net/mplayerplug-in/mplayerplug-in-${pkgver}.tar.gz
	mplayerplug-in_xulrunner-1.9.patch)
md5sums=('cb59d32221cfbd04b6a8b3bb55593484'
         'fc8dfd9dfad841febf8a649f6caab4a2')

build() {
  cd ${startdir}/src/mplayerplug-in-${pkgver}
  patch -Np0 -i ${startdir}/src/mplayerplug-in_xulrunner-1.9.patch || return 1
  aclocal || return 1
  autoconf || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-wmp --enable-qt --enable-rm \
    --enable-gmp --enable-dvx --enable-gtk2 || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
}
