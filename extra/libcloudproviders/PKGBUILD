# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fina Wilke <fina@felinira.net>

# remove when bumped upstream

pkgname=libcloudproviders
pkgver=0.3.1
pkgrel=2
pkgdesc="DBus API that allows cloud storage sync clients to expose their services"
url="https://gitlab.gnome.org/World/libcloudproviders"
arch=(x86_64)
license=(LGPL3)
depends=(glib2)
makedepends=(git gobject-introspection meson vala gtk-doc)
_commit=4672d39320f4ef6091a4977b8e6a4ac079296c26  # tags/0.3.1^0
source=("git+https://gitlab.gnome.org/World/libcloudproviders.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
