# $Id: PKGBUILD 47806 2009-07-27 23:32:46Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcsh
pkgver=6.17.00
pkgrel=1
pkgdesc="Enhanced version of the Berkeley C shell"
arch=('i686' 'x86_64')
url="http://www.tcsh.org/Welcome"
license=('BSD')
depends=('ncurses')
install=tcsh.install
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz csh.cshrc \
        tcsh-6.17.00-ls-colors-var.patch csh.login usr_csh.sh usr_tcsh.sh)
md5sums=('c47de903e3d52f6824c8dd0c91eeb477' '7ca0fe6d1a1b9a0093f632499d4fb112'\
         '5febe6965b9426417c10348404bfcfd5' '4869b9da87c79854e2cc97241f125853'\
         '2b672c67b5e08ab81fbb03d8588d25e3' '8758b20326f00a1057bd95a300c28355')
sha1sums=('9bf67d5ce1b406178e9ba535ecd34553fe0d4d36' 'f156f79f67c2cc1d96617095230dc90e75ea1e43'\
         '12425a655b80366db5b1f188d737f053a3115bc3' 'dbbcfacc078789104f75cbf5a99640ce7251c99e'\
         '7668da494d053a00199f8e2c857bf3998c74f0be' '31da3a6aaa00cd8d66a6c1fba42e19fbc99f2c7c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../tcsh-6.17.00-ls-colors-var.patch || return 1
  ./configure --prefix=/ --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR="${pkgdir}" mandir=/usr/share/man install install.man || return 1

  install -D -m644 "${srcdir}/csh.cshrc" "${pkgdir}/etc/csh.cshrc" || return 1
  install -D -m644 "${srcdir}/csh.login" "${pkgdir}/etc/csh.login" || return 1
  ln -s tcsh "${pkgdir}/bin/csh" || return 1
  install -D -m 755 "${srcdir}/usr_tcsh.sh" "${pkgdir}/usr/bin/tcsh" || return 1
  install -D -m 755 "${srcdir}/usr_csh.sh" "${pkgdir}/usr/bin/csh" || return 1
  install -D -m644 Copyright "${pkgdir}/usr/share/licenses/${pkgname}/Copyright" || return 1
}
