# $Id: PKGBUILD 45273 2009-07-09 00:00:45Z eric $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=xsane
pkgver=0.996
pkgrel=2
pkgdesc="A GTK-based X11 frontend for SANE and plugin for Gimp."
arch=(i686 x86_64)
url="http://www.xsane.org"
license=('GPL2')
makedepends=('gimp')
depends=('gtk2' 'lcms' 'sane' 'zlib' 'libjpeg>=7')
install=$pkgname.install
source=(http://www.xsane.org/download/$pkgname-$pkgver.tar.gz MissingCapsFlag.patch)
md5sums=('7dcbcdf0e83322d7c0a543672d9ca2d8' '720796dd649b0f244ce9d7189c90f55c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 < ../MissingCapsFlag.patch || return 1

  ./configure --prefix=/usr --mandir=/usr/share/man --disable-gimp
  /usr/bin/make || return 1
  /usr/bin/make DESTDIR="$pkgdir" install || return 1
  # build xsane with gimp
  make clean
  ./configure --prefix=/usr
  /usr/bin/make || return 1
  install -D -m755 "$srcdir/$pkgname-$pkgver/src/xsane" "$pkgdir/usr/bin/xsane-gimp" || return 1
  ## For making Gimp Plugin available 
  /bin/mkdir -p "$pkgdir/usr/lib/gimp/2.0/plug-ins"
  cd "$pkgdir/usr/lib/gimp/2.0/plug-ins/"
  /bin/ln -s ../../../../bin/xsane-gimp xsane
}
