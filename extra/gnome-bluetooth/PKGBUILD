# $Id: PKGBUILD 33018 2009-04-03 21:17:37Z jgc $
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=0.12.0
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem."
arch=('i686' 'x86_64')
url="http://www.usefulinc.com/software/gnome-bluetooth/"
license=('GPL' 'LGPL')
depends=('libbtctl>=0.10.0' 'libgnomeui>=2.24.1')
makedepends=('gob2' 'intltool' 'pygtk>=2.14.1')
options=('!libtool')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-bluetooth/0.12/${pkgname}-${pkgver}.tar.bz2)
md5sums=('881a01ce964b8c02b5d40be8b5aedcaf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  aclocal || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var || return 1
  sed -e 's|^pythondir = /usr|pythondir = |' \
      -e 's|^pydir = /usr|pydir = |' \
      -i src/Makefile || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
