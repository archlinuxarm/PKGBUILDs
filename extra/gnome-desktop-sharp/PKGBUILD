# $Id: PKGBUILD 30958 2009-03-22 19:54:58Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-desktop-sharp
pkgver=2.26.0
pkgrel=1
pkgdesc="GNOME desktop bindings for C#"
arch=(i686 x86_64)
license=(LGPL)
url="http://gtk-sharp.sourceforge.net"
depends=('gnome-sharp>=2.24.1' 'vte>=0.20.0' 'librsvg>=2.26.0' 'gnome-panel>=2.26.0' 'libwnck>=2.26.0' 'gtkhtml>=3.26.0' 'gtksourceview2>=2.6.0')
makedepends=('pkgconfig' 'monodoc' 'nautilus-cd-burner>=2.24.0' 'libgnomeprintui>=2.18.4')
optdepends=('nautilus-cd-burner'
	    'libgnomeprintui')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('4bc990900bb318b2ba0b0e7998bb47d1')

build() {
 # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"


  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make GACUTIL="/usr/bin/gacutil /root ${pkgdir}/usr/lib" \
     	 DESTDIR="${pkgdir}" install || return 1
}
