# $Id$
# Maintainer:  Tom Gundersen <teg@jklm.no>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - create correct config_types based on arch

pkgname=libogg
pkgver=1.3.3
pkgrel=1
pkgdesc='Ogg bitstream and framing library'
arch=('x86_64')
url='http://www.xiph.org/ogg/'
license=('BSD')
depends=('glibc')
source=("https://downloads.xiph.org/releases/ogg/libogg-${pkgver}.tar.xz"
        'config_types.h')
md5sums=('87ed742047f065046eb6c36745d871b8'
         'a4d786b1b1955cb85b64fd16cbbee3f9')

build() {
  cd libogg-${pkgver}

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd libogg-${pkgver}

  make DESTDIR="${pkgdir}" install

# install BSD license
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libogg/

# Resolve multilib conflict
  if [[ $CARCH == "aarch64" ]]; then
    mv "${pkgdir}"/usr/include/ogg/config_types{,-64}.h
  else
    mv "${pkgdir}"/usr/include/ogg/config_types{,-32}.h
  fi
  install -m 644 ../config_types.h -t "${pkgdir}"/usr/include/ogg/
}

# vim: ts=2 sw=2 et:
