# $Id: PKGBUILD 38641 2009-05-09 18:52:41Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben Mazer <blm@groknil.org>

pkgname=muine
pkgver=0.8.10
pkgrel=4
pkgdesc="A music player written in C#"
arch=(i686 x86_64)
license=('GPL')
url="http://www.muine-player.org/"
depends=('gnome-sharp>=2.24.1' 'gstreamer0.10-good-plugins' 'gstreamer0.10-base-plugins' 'desktop-file-utils' 'libgnomeui>=2.24.1' 'libid3tag' 'faad2>=2.7' 'ndesk-dbus-glib')
makedepends=('intltool' 'pkgconfig' 'gnome-icon-theme')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
	    'gstreamer0.10-ffmpeg: Extra media codecs')
options=('!libtool' '!emptydirs' '!makeflags')
install=muine.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.8/${pkgname}-${pkgver}.tar.bz2
	bgo560077.patch)
md5sums=('28e561388de1deebc6336f936bc74a73'
	 '282873898d8ffce96c1b3dcb5c88c428')

build() {

  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
 
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/bgo560077.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-gstreamer || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -m755 plugins/TrayIcon.{dll,xml} "${pkgdir}/usr/lib/muine/plugins/" || return 1
  install -m644 plugins/muine-tray-*.png "${pkgdir}/usr/lib/muine/plugins/" || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
