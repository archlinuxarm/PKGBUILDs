# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - python2 variant that's still needed by packages

pkgname=python2-pyparsing
pkgver=2.4.7
pkgrel=6.1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
makedepends=('python2-setuptools')
license=('MIT')
source=("https://github.com/pyparsing/pyparsing/archive/pyparsing_$pkgver.tar.gz")
sha512sums=('c7a546729f86a2b5176e2482b566b9fd715b03e495aaef4d720b21307bb03f385dbc849247f8d266cb3d92be0a83c34ce4995b655ce85318355d5a0d42d6991e')

build() {
  cd pyparsing-pyparsing_$pkgver
  python2 setup.py build
}

check() {
  cd pyparsing-pyparsing_$pkgver
  python2 unitTests.py
}

package() {
  depends=('python2')

  cd pyparsing-pyparsing_$pkgver

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
