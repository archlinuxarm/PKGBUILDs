# Maintainer: tobias [tobias.archlinux.org]
# Contributor: tobias [tobias.justdreams.de]

pkgname=cinepaint
_srcver=0.22-1
pkgver=${_srcver/-/.}
pkgrel=5
pkgdesc="sophisticated graphics manipulation programm supporting >8bit pictures"
arch=('arm')
license=('LGPL' 'GPL' 'MIT-OSI')
url=(http://cinepaint.org.sourceforge.net/)
depends=('gtk2>=2.16.1' 'openexr>=1.6.1' 'lcms>=1.18' 'libxmu>=1.0.4' 'libxpm>=3.5.7' 'fltk>=1.1.9' 'desktop-file-utils' 'ftgl>=2.1.3rc5' 'libjpeg>=7')
makedepends=('python>=2.6' 'gutenprint>=5.2.3')
optdepends=('python' 'gutenprint')
options=('!libtool')
install=cinepaint.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${_srcver}.tar.gz
        cinepaint-0.22.1-gcc43.patch
        cinepaint-0.22.1-multiple_parameters_named.patch
        cinepaint-0.22.1-ambiguousawake.patch
	cinepaint-0.22-gcc44.patch)
md5sums=('f360587240db6b6d2f0bfd94c420c492'
		 'f05e17a16d0018e5301aff0e1fa597b0'
         '0876510a5ce3d581ebdde7e18199c09f'
         '4586aa153a0d3cac36120afc1f95a880'
         '368e820c27f525e3569d43388f1c119a')

build() {
  cd "${srcdir}/${pkgname}-${_srcver}"
 # FIXES
  patch -Np1 -i "${srcdir}/cinepaint-0.22.1-gcc43.patch" || return 1
  patch -Np1 -i "${srcdir}/cinepaint-0.22-gcc44.patch" || return 1
  patch -Np1 -i "${srcdir}/cinepaint-0.22.1-multiple_parameters_named.patch" || return 1
  patch -Np0 -i "${srcdir}/cinepaint-0.22.1-ambiguousawake.patch" || return 1
  find plug-ins/print -type f -exec \
    sed -i "s:gutenprintui/gutenprintui:gutenprintui2/gutenprintui:" {} \;
  # Fix insecure rpath
  sed '/-rpath/d' -i plug-ins/icc_examin/icc_examin/configure

 # build
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --enable-gtk2 --enable-pygimp \
    --with-python=/usr/bin/python2.6
 # FIXES
  sed -i 's/^\(X_LIBS.*\)$/\1 -lpthread/' plug-ins/openexr/Makefile
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 cinepaint.desktop "${pkgdir}/usr/share/applications/" || return 1
  install -m644 cinepaint.png "${pkgdir}/usr/share/pixmaps/" || return 1
}
