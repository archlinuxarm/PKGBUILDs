# $Id: PKGBUILD 53477 2009-10-01 04:20:45Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : shining <shiningxc.at.gmail.com>
# Contributor : cyberdune <cyberdune@gmail.com>

pkgname=octave
pkgver=3.2.3
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw>=3.2.2' 'pcre' 'curl' 'lapack' 'libx11' 'graphicsmagick>=1.3.6')
makedepends=('texinfo' 'graphicsmagick')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting')
source=("ftp://ftp.octave.org/pub/octave/octave-$pkgver.tar.bz2"
	'octave-3.2.0_as_needed.patch'  'octave-3.2.0_parallel_make.patch')
options=('!emptydirs')
install=octave.install
md5sums=('eecaa1a3b2da3b94fa7bcebbc4f1a1f2'
         '33c8886cd908ace40f8b60334df1c34f'
         'd59d783a8d7e8d8306caed4b2b8671a4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # gentoo patches
  patch -Np1 -i ${srcdir}/octave-3.2.0_as_needed.patch || return 1
  patch -Np1 -i ${srcdir}/octave-3.2.0_parallel_make.patch || return 1
  
  # http://www.nabble.com/Random-rounding-errors-td16010966.html
  FFLAGS="-O -ffloat-store" \

  ./configure --prefix=/usr --libexecdir=/usr/lib \
  --without-hdf5 --without-umfpack \
  --enable-shared --disable-static

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

}
