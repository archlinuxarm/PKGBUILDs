# $Id: PKGBUILD 42805 2009-06-18 20:31:42Z tpowa $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python24
pkgver=2.4.6
pkgrel=2
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org"
depends=('db4.5' 'bzip2' 'gdbm' 'openssl' 'zlib' 'gcc-libs' 'readline>=6.')
makedepends=('tk')
source=(http://www.python.org/ftp/python/$pkgver/Python-$pkgver.tar.bz2 
	db4.5-setup.py-detection.patch db_fix.patch)
options=('!makeflags')
md5sums=('76083277f6c7e4d78992f36d7ad9018d'
         'f9620f756b43f46b28b2986de21285dc'
         '1c178479d17ae1e59cccb0a3f7be043b')

build() {
  [ -e /usr/lib/libdb.so ] && echo "make sure you have no other db* pkg installed" && return 1

  cd ${srcdir}/Python-${pkgver}
  patch -Np0 -i ../db4.5-setup.py-detection.patch || return 1
  patch -Np0 -i ../db_fix.patch || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --enable-shared --with-threads --enable-unicode
  make || return 1
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/bin/python

  # Rename all the scripts
  pushd ${pkgdir}/usr/bin
  mv idle idle2.4
  mv pydoc pydoc2.4
  mv smtpd.py smtpd2.4.py
  mv ${pkgdir}/usr/share/man/man1/python.1 ${pkgdir}/usr/share/man/man1/python${pkgver}
  popd

  # license
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
