# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=4.3.2
pkgrel=1
pkgdesc='KDE PIM Runtime Environment'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdepimlibs' 'kdebase-runtime' 'kdelibs-experimental')
replaces=('kdepim-akonadi' 'kdepim-akonadiconsole')
conflicts=('kdepim-akonadi' 'kdepim-akonadiconsole')
provides=("kdepim-akonadi=${pkgver}" "kdepim-akonadiconsole=${pkgver}")
makedepends=('pkgconfig' 'cmake' 'automoc4')
install='kdepim-runtime.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a998b2d6e62e79a80fefc2b3c632a3b8')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}
