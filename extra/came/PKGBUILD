# $Id: PKGBUILD 53618 2009-10-03 05:02:00Z eric $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Curtis Campbell <curtisjamescampbell@hotmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=came
pkgver=1.9
pkgrel=5
pkgdesc="Replacement for xawtv webcam application"
arch=(i686 x86_64)
license=('GPL')
url='http://linuxbrit.co.uk/camE/'
depends=('giblib' 'curl>=7.19.6')
source=(ftp://ftp.archlinux.org/other/came/camE-${pkgver}.tar.gz
        came-1.9-curl-friendly.patch)
md5sums=('8bdc049b01cd32088eef8cccf3c096ab' 'e0f1375b7a554e11c1d161d5112dd53d')
sha1sums=('1718ff0662a3183fd8386711bb4b9195abbd170f' '9b9615d07081331ba51ca8d460b82fa6f6e3a3e3')

build() {
  cd "${srcdir}/camE-${pkgver}"
  patch -Np1 -i "${srcdir}/came-1.9-curl-friendly.patch" || return 1
  sed -i 's|#define VERSION "1.7"|#define VERSION "1.7"\n#define TRUE 1\n#define FALSE 0|' webcam.c || return 1
  make || return 1

  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/came"

  install -m755 camE "${pkgdir}/usr/bin/" || return 1
  install -m644 example.camErc "${pkgdir}/usr/share/came/camErc" || return 1
  install -m644 example.camErc.ssh "${pkgdir}/usr/share/came/camErc.ssh" || return 1
}
