# $Id: PKGBUILD 38897 2009-05-10 18:14:10Z jgc $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=imake
pkgver=1.0.2
pkgrel=4
pkgdesc="X.Org imake program and related utilities"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(glibc)
makedepends=(pkgconfig xproto perl)
source=(http://xorg.freedesktop.org/releases/individual/util/imake-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/lndir-1.0.1.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.1.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.2.tar.bz2
	linuxconfig.patch
	xorg-cf-files-1.0.2-xprint.patch
	LICENSE)
md5sums=('02fea5a02ba2857c7d81820c8e8b8e6f'
         'b533c0771dbbaf9b041ff35bb941d3a2'
         'e274ea9f55dfd62afa0a7b1e1ab4ba96'
         '54ab2549d723e51ff2b9cff4d8bf44d1'
         '5f62dd5545b782c74f6e4e70d0e6552c'
         'b3385d8efbbe7c7fc50ed3a96ce37a16'
         'eae2925d0bc39c3b1dc0872ccbff99ed'
         'f5767133ef7a98a0bfa15139e0e3d32e')

build() {
  cd ${srcdir}/xorg-cf-files-*
  patch -Np1 -i "${srcdir}/linuxconfig.patch" || return 1
  patch -Np1 -i "${srcdir}/xorg-cf-files-1.0.2-xprint.patch" || return 1
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      popd
    fi
  done
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
