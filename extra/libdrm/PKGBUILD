# $Id: PKGBUILD 40207 2009-05-28 21:02:08Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.11
pkgrel=2
pkgdesc="Userspace interface to kernel DRM services"
arch=(arm)
license=('custom')
depends=('glibc')
options=('!libtool' 'force')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	no-pthread-stubs.patch
	intel-bo-cache.patch
	COPYING)
md5sums=('e0e66fae165d0b665b61e9516bf33ade'
         'e5f8f3b9f6357c2f82950cdfb8e9c9ec'
	 '53e7c214f6fb9fea8deb1ba3bf9b13ed'
	 'ba65e71c481b94ef0fb6c23c7f21ffa1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch" || return 1
  patch -Np1 -i "${srcdir}/intel-bo-cache.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --enable-nouveau-experimental-api || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
