# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora to fix FTBFS on ARM

pkgname=kdegames-ksirk
pkgver=16.12.0
pkgrel=1
pkgdesc="A computerized version of a well known strategy game"
url="http://kde.org/applications/games/ksirk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksirk-${pkgver}.tar.xz"{,.sig}
        'ksirk-15.12.1-iris_base64.patch')
sha1sums=('bca4aecd268182fe2a2fe25f6b2d2ea2361158a1'
          'SKIP'
          '78dbd3d4e70d2afb917ba86a6254a775de2bc2dd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  patch -d ksirk-${pkgver} -p1 -i ../ksirk-15.12.1-iris_base64.patch
}

build() {
  mkdir -p build
  cd build
  cmake ../ksirk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
