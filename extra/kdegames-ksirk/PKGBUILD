# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora to fix FTBFS on ARM

pkgname=kdegames-ksirk
pkgver=17.08.0
pkgrel=1
pkgdesc="A computerized version of a well known strategy game"
url="https://kde.org/applications/games/ksirk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'qca-qt4')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/${pkgver}/src/ksirk-${pkgver}.tar.xz"{,.sig}
        'ksirk-15.12.1-iris_base64.patch')
sha256sums=('02b1621215c667b057ffd92352958d9ba6c1ac53c23d75965fed0799e9c98be1'
            'SKIP'
            '04a8cc75458bf55f40073cbffabd8fada59d65eea9d389e62f029e3e868b1e6c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d ksirk-${pkgver} -p1 -i ../ksirk-15.12.1-iris_base64.patch
}

build() {
  mkdir -p build
  cd build
  cmake ../ksirk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
