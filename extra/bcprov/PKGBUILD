# $Id: PKGBUILD 49813 2009-08-16 15:39:34Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=bcprov
pkgver=1.43
_pkgver=143
pkgrel=1
pkgdesc="Java cryptography APIs (Bouncy Castle)"
arch=(any)
url="http://www.bouncycastle.org/java.html"
license=('custom')
depends=('java-runtime')
makedepends=('openjdk6' 'junit')
source=(http://www.bouncycastle.org/download/bcprov-jdk16-${_pkgver}.tar.gz)
md5sums=('d5b3d6edaacc332251341a79c3f86590')

build() {
  cd "${srcdir}/bcprov-jdk16-${_pkgver}"
  jar xf src.zip || return 1

  mkdir classes
  find . -name '*.java' > src.list
  javac -cp /usr/share/java/junit.jar -d classes @src.list || return 1

  cd classes
  find . -path '*test/*.class' -delete
  find . -depth -type d -empty -delete
  jar cf ../bcprov.jar * || return 1
  install -m755 -d "${pkgdir}/usr/share/java"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../bcprov.jar "${pkgdir}/usr/share/java/" || return 1
  install -m644 ../LICENSE.html "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
