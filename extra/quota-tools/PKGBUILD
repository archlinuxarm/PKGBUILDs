# $Id: PKGBUILD 23363 2009-01-08 00:26:14Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd <judd@archlinux.org>

pkgname=quota-tools
pkgver=3.17
pkgrel=1
pkgdesc="Tools to manage kernel-level quotas in Linux"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/linuxquota/"
license=('GPL' 'BSD')
depends=('e2fsprogs' 'tcp_wrappers')
backup=('etc/warnquota.conf' 'etc/quotatab' 'etc/quotagrpadmins')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/linuxquota/quota-${pkgver}.tar.gz LICENSE)
md5sums=('cb3e98a039c0cf98e7c1ad4b50a5bda7' 'dd792440e684043e4e4ef80963d0237b')
sha1sums=('8dc04ea803a43496dfe275a8260757f558a2d762' '57297bdc9e638c500506169bbbe12eb89bcf7d07')

build() {
  cd ${srcdir}/${pkgname}
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make ROOTDIR=${pkgdir} install || return 1
  install -D -m644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  # remove conflicts
  # glibc
  rm ${pkgdir}/usr/include/rpcsvc/rquota.h || return 1
  rm ${pkgdir}/usr/include/rpcsvc/rquota.x || return 1
  # man-pages
  rm ${pkgdir}/usr/share/man/man2/quotactl.2 || return 1
}
