# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on valgrind

pkgname=pipewire
pkgver=0.1.9
pkgrel=3
pkgdesc="Server and user space API to deal with multimedia pipelines"
url="https://pipewire.org"
license=(LGPL2.1)
arch=(x86_64)
depends=(gstreamer gst-plugins-base v4l-utils sbc ffmpeg)
makedepends=(git meson doxygen graphviz xmltoman)
install=pipewire.install
_commit=62f113dbba7c706fad18c715cfad57ca7e102cb3  # tags/0.1.9
source=("git+https://github.com/PipeWire/pipewire#commit=$_commit"
        unhide-providers.diff)
sha256sums=('SKIP'
            '8692ddbfc433bb4a56e3e4594683f9a7047b6cce539c7322dd3b74157a727f99')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # the pipewireprovider is broken: the devices provided have no caps
  # at least don't hide other providers until this is fixed
  patch -Np1 -i ../unhide-providers.diff

  # Reduce docs size
  printf '%s\n' >>doc/Doxyfile.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  arch-meson $pkgname build \
    -D enable_docs=true \
    -D enable_man=true \
    -D enable_gstreamer=true
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
