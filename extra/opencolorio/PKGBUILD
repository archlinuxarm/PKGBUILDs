# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dumitru Ursu <dima@ceata.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream PR to fix FTBFS

pkgname=opencolorio
pkgver=2.2.0
pkgrel=2
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('freeglut' 'glew' 'lcms2' 'tinyxml' 'yaml-cpp' 'imath' 'pystring'
         'minizip-ng' 'openimageio' 'openshadinglanguage')
makedepends=('cmake' 'python' 'pybind11' 'ninja')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz
        use-system-minizip.patch
        https://patch-diff.githubusercontent.com/raw/AcademySoftwareFoundation/OpenColorIO/pull/1721.patch)
sha512sums=('f9fc6991f99f28bb2117ecf6af6ea907310c9ae118d17e54c1bf642ec99e35bf899463d80bccdbaca6cd66bae62e17fdd0417e2fb42c8b8f80c6892e7cbe8770'
            'de760fa88f9680e9bd02c3810957f68f82ef461591763de47e4ffa31739aebd1ebf0793dd7e93582aaef11afd9d4ba088f8911259c749dc6a74b9cf4b163470e'
            'f4662310ca52fb617cdf83f711718b02385de3dc42ea72583f626b64bfd71d48bf8c0314d3ee56d55697e424ee34b089ed80dd5c6c9d080e7c91f40a3b864219')

prepare() {
  cd OpenColorIO-$pkgver

  # We don't care about the failures of this patch in this current release
  patch -Np1 -i "$srcdir"/use-system-minizip.patch

  patch -Np1 -i "$srcdir"/1721.patch
}

build() {
  cd OpenColorIO-$pkgver

  CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja -C build
}

package() {
  cd OpenColorIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
