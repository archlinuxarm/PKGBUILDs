# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dumitru Ursu <dima@ceata.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added -fsigned-char to CXXFLAGS

pkgname=opencolorio
pkgver=2.3.1
pkgrel=2
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('freeglut' 'glew' 'lcms2' 'tinyxml' 'yaml-cpp' 'imath' 'pystring' 'minizip-ng' 'openexr')
makedepends=('cmake' 'python' 'pybind11' 'ninja')
checkdepends=('openimageio')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz)
sha512sums=('a4951b530bc10737feea2dcc0143a595cb6a8f71ad0475ddefbb50606d5316fcdeddea93843c3d295ae5edbdbc3d5974f6524e715d7322a3061c702ca9a72c43')

build() {
  cd OpenColorIO-$pkgver

  CXXFLAGS+=" -ffat-lto-objects -fsigned-char"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_INSTALL_RPATH=YES \
    -Wno-dev
  ninja -C build
}

check() {
  cd OpenColorIO-$pkgver/build

  ctest -E test_gpu
}

package() {
  cd OpenColorIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
