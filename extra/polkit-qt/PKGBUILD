# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=polkit-qt
pkgver=0.9.2
pkgrel=2
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('policykit' 'qt')
makedepends=('cmake')
source=("http://download.kde.org/stable/apps/KDE4.x/admin/${pkgname}-${pkgver}.tar.bz2"
        'link.patch')
md5sums=('14cb821f8917c13ed4154a3ab8e596be'
         '850a0ed23a86d048b35b288fdef3aae2')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -p1 -i $srcdir/link.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	make
	make DESTDIR=$pkgdir install
}
