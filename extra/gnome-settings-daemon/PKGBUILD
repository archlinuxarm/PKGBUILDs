# $Id: PKGBUILD 36056 2009-04-19 20:03:59Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-settings-daemon
pkgver=2.26.1
pkgrel=1
pkgdesc="The GNOME Settings daemon"
arch=(i686 x86_64)
license=('GPL')
depends=('libgnomekbd>=2.26.0' 'gnome-desktop>=2.26.1' 'libnotify>=0.4.5' 'libxxf86misc' 'gstreamer0.10-base>=0.10.22')
makedepends=('intltool' 'pkgconfig')
conflicts=('gnome-control-center<2.22.0')
options=(!emptydirs !libtool)
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2
	07_use_gstreamer_volume.patch)
md5sums=('7be383de54c0707c64843ae51563c43a'
	 'fb186df7c9b31fd8ab03e93aa3af7c78')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/07_use_gstreamer_volume.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
 	--libexecdir=/usr/bin --disable-static || return 1

  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-settings-daemon ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
