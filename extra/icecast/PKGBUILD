# $Id: PKGBUILD 22244 2008-12-24 10:41:47Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributer: Jason Chu <jchu@xentac.net>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=icecast
pkgver=2.3.2
pkgrel=1
pkgdesc="Streaming OGG and MP3 server. Replacement for shoutcase."
arch=(i686 x86_64)
license=('GPL')
url="http://www.icecast.org/"
depends=('libxslt' 'libvorbis' 'curl>=7.19.2' 'speex' 'libtheora')
makedepends=('sed')
backup=('etc/icecast.xml')
source=(http://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz
        icecast icecast.logrotate)
md5sums=('ff516b3ccd2bcc31e68f460cd316093f'
         'fe65f4d6826a750a868227b9d86f1be3'
         '8fad3bf3283fa2bd651b71fdf505eae9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install man-page
  sed -i -e 's/icecast2/icecast/g' debian/icecast2.1
  install -D -m 644 debian/icecast2.1 ${pkgdir}/usr/share/man/man1/icecast.1

  # init file
  install -D -m755 ${srcdir}/icecast ${pkgdir}/etc/rc.d/icecast
  # rotate the logs (taken from Fedora
  install -D -m644 ${srcdir}/icecast.logrotate ${pkgdir}/etc/logrotate.d/icecast
}


