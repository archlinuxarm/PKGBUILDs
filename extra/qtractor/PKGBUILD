# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=1.5.11
pkgrel=2
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libmad
  libxcb
  qt6-base
  qt6-svg
  zlib
)
makedepends=(
  alsa-lib
  clap
  cmake
  dssi
  git
  jack
  ladspa
  liblo
  libogg
  libsamplerate
  libsndfile
  libvorbis
  lilv
  lv2
  qt6-tools
  rubberband
  suil
  vst3sdk
)
optdepends=(
  'new-session-manager: for session management'
)
provides=(
  clap-host
  dssi-host
  ladspa-host
  lv2-host
  vst-host
  vst3-host
)
source=(
  $pkgname::git+https://github.com/rncbc/qtractor.git#tag=v$pkgver
  minibpm::git+https://github.com/breakfastquay/minibpm.git
)
sha512sums=('16478fb338c32d539db415a51072f8b55459b7734e93fdaf24c892aa3684704a24e08240e1cfd11d1d5c8cbdef2c46b6939805d2bafd729178799ade81f7730d'
            'SKIP')
b2sums=('f462fb8edffeadae987c61103498d313719920fd5238a29ddf52cfe517e10608ef9e59089e61a87c30a202ef870f65e5e157aaae0f9cbb6f16ac62cd32b99737'
        'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.src/minibpm.url "$srcdir/minibpm"
  git submodule deinit src/clap
  git submodule deinit src/vst3/base
  git submodule deinit src/vst3/pluginterfaces
  git submodule deinit src/vst3/public.sdk
  git -c protocol.file.allow=always submodule update

  # symlink vst3sdk into place
  rm -frv src/vst3
  ln -fsv /usr/src/vst3sdk/ src/vst3
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -D CONFIG_SSE=OFF
    -D CONFIG_LV2_UI_GTK2=OFF
    -D CONFIG_LV2_UI_GTKMM2=OFF
    -S $pkgname
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
    liblo liblo.so
    libogg libogg.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
    libvorbis libvorbisenc.so libvorbisfile.so libvorbis.so
    lilv liblilv-0.so
    rubberband librubberband.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/README* -t "$pkgdir/usr/share/doc/$pkgname/"
}
