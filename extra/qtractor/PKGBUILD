# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=0.9.36
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libmad
  libxcb
  qt6-base
  qt6-svg
  zlib
)
makedepends=(
  alsa-lib
  aubio
  clap
  cmake
  dssi
  jack
  ladspa
  liblo
  libogg
  libsamplerate
  libsndfile
  libvorbis
  lilv
  lv2
  qt6-tools
  rubberband
  suil
  vst3sdk
)
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=(
  clap-host
  dssi-host
  ladspa-host
  lv2-host
  vst-host
  vst3-host
)
source=(https://github.com/rncbc/$pkgname/archive/refs/tags/${pkgname}_${pkgver//./_}.tar.gz)
sha512sums=('fdbf83d215466fe791d33794c96eb59627305545d7e06a74f9712f086a4b3747e4baecafc2ba01717af8efe2f309dd45e244de64c304d31cab4496f62aa8ec66')
b2sums=('9a8ff51a848e65e496dbd9be64a04601a6fd5b4165add83b55ce4595cb6cffbab10811f5410eae6214c9b60dc0b2f67bfc42d4f7e9540c7aba2684a8644d04fa')

prepare() {
  # symlink vst3sdk into place
  rm -frv $pkgname-${pkgname}_${pkgver//./_}/src/vst3
  ln -fsv /usr/src/vst3sdk $pkgname-${pkgname}_${pkgver//./_}/src/vst3
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -D CONFIG_SSE=OFF
    -D CONFIG_LV2_UI_GTK2=OFF
    -D CONFIG_LV2_UI_GTKMM2=OFF
    -S $pkgname-${pkgname}_${pkgver//./_}
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    aubio libaubio.so
    jack libjack.so
    liblo liblo.so
    libogg libogg.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
    libvorbis libvorbisenc.so libvorbisfile.so libvorbis.so
    lilv liblilv-0.so
    rubberband librubberband.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-${pkgname}_${pkgver//./_}/{README,README.VST} -t "$pkgdir/usr/share/doc/$pkgname/"
}
