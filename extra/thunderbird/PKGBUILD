# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Anders Bostrom <anders.bostrom@home.se>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - mozconfig changes for ARM

buildarch=20
highmem=1
noautobuild=1

pkgname=thunderbird
pkgver=24.2.0
pkgrel=1
pkgdesc="Standalone Mail/News reader"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird/"
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libnotify' 'libvpx' 'libxt' 'mime-types' 'mozilla-common' 'nss' 'sqlite' 'startup-notification')
makedepends=('unzip' 'zip' 'pkg-config' 'python2' 'wireless_tools' 'yasm' 'mesa' 'autoconf2.13')
optdepends=('libcanberra: for sound support')
install=thunderbird.install
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2{,.asc}
        mozconfig
        thunderbird.desktop
        thunderbird-install-dir.patch
        vendor.js)
options=(!emptydirs)
sha256sums=('66474132bd6ebbb8a913c3f4acd4ecc9bec011e4c7ee49475f29558801a905cf'
            'SKIP'
            '1e5fad181194155f65e4496759ff94523de8bcbc9838d257d65c303a9c409292'
            'd506b771e765e09e2e039b975e25befade8eec97e8950813a32463604955ab4e'
            '929a7ceca0e90d0635a2099b7df24033e865fb5b5b8c5824468c4561c21c377d'
            'e4ea8e6788163d9f8db8f1f40023db3ea0a1358f9a4510169f2d4c4fe6a887ed')

prepare() {
  cd comm-esr24
  patch -Np1 -i "$srcdir/thunderbird-install-dir.patch"

  cp "$srcdir/mozconfig" .mozconfig

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' mozilla/configure
}

build() {
  cd comm-esr24

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/thunderbird"
  export PYTHON="/usr/bin/python2"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd comm-esr24
  make -j1 -f client.mk DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/vendor.js "$pkgdir/usr/lib/thunderbird/defaults/preferences/vendor.js"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 other-licenses/branding/thunderbird/mailicon${i/x*/}.png \
          "$pkgdir/usr/share/icons/hicolor/$i/apps/thunderbird.png"
  done

  install -Dm644 "$srcdir/thunderbird.desktop" \
      "$pkgdir/usr/share/applications/thunderbird.desktop"

  rm -rf "$pkgdir"/usr/lib/thunderbird/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/usr/lib/thunderbird/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/usr/lib/thunderbird/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/thunderbird-devel,share/idl}
}
