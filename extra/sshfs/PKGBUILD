# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use -D_FILE_OFFSET_BITS=64 with 32-bit ARM

pkgname=sshfs
pkgver=3.7.3
pkgrel=1
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=('x86_64')
url="https://github.com/libfuse/sshfs"
license=(GPL)
depends=(fuse3 glib2 openssh)
makedepends=('meson' 'python-docutils')
source=(https://github.com/libfuse/sshfs/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('5218ce7bdd2ce0a34137a0d7798e0f6d09f0e6d21b1e98ee730a18b0699c2e99'
            'SKIP')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F') # Nikolaus Rath <Nikolaus@rath.org>

prepare() {
  cd $pkgname-$pkgver

  [[ $CARCH == 'arm' || $CARCH == 'armv6h' || $CARCH == 'armv7h' ]] && export CFLAGS+=" -D_FILE_OFFSET_BITS=64"
  rm -rf build
  mkdir build
  cd build
  arch-meson ..
}

build() {
  cd $pkgname-$pkgver/build
  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
