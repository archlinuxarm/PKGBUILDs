# $Id: PKGBUILD 52122 2009-09-16 07:33:27Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.28.1
pkgrel=1
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb' 'libxpm' 'bluez' 'libical' 'postgresql-libs>=8.4.1')
makedepends=('gtk2' 'libmysqlclient>=5.1' 'perlxml' 'intltool')
optdepends=('gtk2: xgnokii GUI'
            'libmysqlclient: smsd mysql backend'
	    'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
options=('!libtool' '!makeflags')
source=(http://www.gnokii.org/download/$pkgname/$pkgname-${pkgver}.tar.bz2
	'gnokii.patch')
md5sums=('520d300f1838460d374410ea99ff9a11'
         '4d764727686ee34bc73489c8b1321c82')

build() {
  cd "${srcdir}/${pkgname}-0.6.28"

  # FS#13249
  sed -i 's|cellphone|phone|' xgnokii/xgnokii.desktop.in || return 1

  patch -Np1 -i "${srcdir}/gnokii.patch" || return 1

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-security \
    --disable-unix98test || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc" || return 1
}
