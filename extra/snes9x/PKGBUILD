# $Id: PKGBUILD 51836 2009-09-12 08:37:47Z allan $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>

pkgname=snes9x
pkgver=1.51
pkgrel=2
pkgdesc="A portable Emulator for the Super Nintento Entertainment System"
arch=('i686' 'x86_64')
url="http://www.snes9x.com"
license=('custom')
# remove nasm on non x86 platforms
depends=('zlib' 'libpng' 'sdl' 'nasm' 'mesa')
source=(http://www.geocities.co.jp/SiliconValley-PaloAlto/2560/release/${pkgname}-${pkgver}-src.tar.bz2
        snes9x-1.51-glibc2.10.patch
        LICENSE)
md5sums=('a1038cc761f400318cdced960972f8a7'
         '181db31b84b8f3f7f16b4fb11dc043cc'
         '9b69c926c88c5d9b26c69b9437d6cf1d')

build() {
  install -dm755 ${pkgdir}/usr/bin

  cd ${srcdir}/${pkgname}-${pkgver}-src
  patch -Np0 -i ${srcdir}/snes9x-1.51-glibc2.10.patch || return 1

  # build snes9x with opengl
  ./configure --prefix=/usr \
              --with-sdd1-decomp \
              --with-opengl \
              --with-netplay
  make || return 1
  install -Dm755 ${srcdir}/${pkgname}-${pkgver}-src/osnes9x ${pkgdir}/usr/bin/osnes9x

  # build snes9x with glide
  #./configure --prefix=/usr --with-sdd1-decomp --with-glide
  #make || return 1
  #install -Dm755 ${srcdir}/${pkgname}-${pkgver}-src/osnes9x ${pkgdir}/usr/bin/3dfxsnes9x

  # build snes9x without opengl
  ./configure --prefix=/usr \
              --with-sdd1-decomp \
              --with-netplay
  make clean || return 1
  make || return 1
  install -Dm755 ${srcdir}/${pkgname}-${pkgver}-src/snes9x ${pkgdir}/usr/bin/snes9x

  install -Dm644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/snes9x/LICENSE
}

