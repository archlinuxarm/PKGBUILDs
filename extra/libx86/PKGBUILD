# $Id: PKGBUILD 21250 2008-12-11 16:58:44Z thayer $
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=libx86
pkgver=1.1
pkgrel=2
pkgdesc="Provides an lrmi interface that works on x86, am64 and alpha"
arch=('i686' 'x86_64')
url="http://www.codon.org.uk/~mjg59/libx86/"
license=('custom')
depends=('glibc')
source=(http://www.codon.org.uk/~mjg59/libx86/downloads/libx86-$pkgver.tar.gz 
        libx86-ifmask.patch)
md5sums=('41bee1f8e22b82d82b5f7d7ba51abc2a'
         '573897186eb8670d8d97c64ea7614001')

build() {
  cd $srcdir/$pkgname-$pkgver

  # lrmi.c patch courtesy of Gentoo
  patch -Np0 -i $srcdir/libx86-ifmask.patch || return 1

  # compensate for x86_64
  if [ "$CARCH" = "x86_64" ]; then
		make BACKEND=x86emu || return 1
    else
	  make || return 1
  fi

  make DESTDIR=$pkgdir install || return 1
  chmod 644 $pkgdir/usr/lib/libx86.a || return 1

  install -D -m 644 COPYRIGHT $pkgdir/usr/share/licenses/$pkgname/COPYRIGHT || return 1
}
