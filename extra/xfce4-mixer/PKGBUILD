# $Id: PKGBUILD 36344 2009-04-21 04:53:49Z andyrtr $
# Maintainer: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-mixer
pkgver=4.6.1
pkgrel=1
pkgdesc="The volume control plugin for the Xfce panel"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("xfce4-panel>=$pkgver" 'gstreamer0.10-base>=0.10.22' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
optdepends=('gstreamer0.10-base-plugins:  to support basic audio hardware' \
            'gstreamer0.10-good-plugins:  well supported hardware'\
            'gstreamer0.10-bad-plugins:   not so well supported hardware'\
            'gstreamer0.10-ugly-plugins:  might contain questionable license hardware')
options=('!libtool')
install=${pkgname}.install
source=(http://www.xfce.org/archive/xfce-${pkgver}/src/${pkgname}-${pkgver}.tar.bz2)
md5sums=('a99e2455445480ef5081fe69454a46fc')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static \
    --with-sound=alsa || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
