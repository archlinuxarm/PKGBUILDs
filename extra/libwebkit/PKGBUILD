# $Id: PKGBUILD 109463 2011-02-09 20:43:10Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - libtool is disabled for x86 because another tool takes its place for webkit.
#    that tool doesn't run on arm, we have to use libtool.

plugrel=1

pkgname=libwebkit
pkgver=1.2.7
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.6' 'gstreamer0.10-base' 'libsoup' 'enchant')
makedepends=('gperf' 'gtk-doc' 'gobject-introspection')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
#options=('!libtool')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz
        introspection.patch)
md5sums=('25c7e548b65aeb6d83c0182c32ef0927'
         'e7e83312618cb30cc9a1567a21cd0e06')


build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -Np0 -i "${srcdir}/introspection.patch"

  ./configure --prefix=/usr \
      --enable-video --enable-introspection \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu
  #disable makeflags if parallel building fails
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/webkit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
