# $Id: PKGBUILD 104228 2010-12-30 22:22:42Z ibiru $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# Plugbox changes: None, but this package has to be built by running:
# CPPFLAGS="-I/usr/include/cairo -I/usr/include/freetype2 \
# -I/usr/include/gtk-2.0 -I/usr/include/pango-1.0 -I/usr/lib/gtk-2.0/include \
# -I/usr/include/gdk-pixbuf-2.0 -I/usr/include/atk-1.0 \
# -I/usr/include/gail-1.0" makepkg --asroot -Acs

plugrel=1

pkgname=libwebkit
pkgver=1.2.6
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.6' 'gstreamer0.10-base' 'libsoup' 'enchant')
makedepends=('gperf' 'gtk-doc' 'gobject-introspection')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
#options=('!libtool')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz
        introspection.patch)
md5sums=('7afb510040b2a49c65514a06bb8200fc'
         'e7e83312618cb30cc9a1567a21cd0e06')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -Np0 -i "${srcdir}/introspection.patch"

  ./configure --prefix=/usr \
      --enable-video --enable-introspection \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu
  #disable makeflags if parallel building fails
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/webkit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
