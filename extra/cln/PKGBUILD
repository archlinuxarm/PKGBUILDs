# $Id: PKGBUILD 127750 2011-06-18 05:06:20Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - Redefined our CFLAGS and CXXFLAGS to remove -O2, and define -DNO_ASM.
#    Optimization kills the builds, NO_ASM prevents it using asm that doesn't work for us.
#  - Removed building and installing pdf docs, it always fails.

plugrel=1

pkgname=cln
pkgver=1.3.2
pkgrel=1
pkgdesc="Class library for numbers"
arch=('i686' 'x86_64')
url="http://www.ginac.de/CLN/"
license=('GPL')
depends=('gmp')
makedepends=('texlive-core')
options=('!libtool')
install=cln.install
source=(ftp://ftpthep.physik.uni-mainz.de/pub/gnu/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d897cce94d9c34d106575ed4ec865d71')
sha1sums=('c30dca80e75f45e2107f233075e6d0339ea884b0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "$CARCH" = "armv7h" ]; then
    export CFLAGS="-march=armv7-a -pipe -DNO_ASM"
    export CXXFLAGS="-march=armv7-a -pipe -DNO_ASM"
  else
    export CFLAGS="-march=armv5te -pipe -DNO_ASM"
    export CXXFLAGS="-march=armv5te -pipe -DNO_ASM"
  fi
  ./configure --prefix=/usr
  make all html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-html
}
