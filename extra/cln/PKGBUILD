# $Id: PKGBUILD 127750 2011-06-18 05:06:20Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Redefined our CFLAGS and CXXFLAGS to remove -O2, and define -DNO_ASM.
#    Optimization kills the builds, NO_ASM prevents it using asm that doesn't work for us.
#  - Removed building and installing pdf docs, it always fails.

pkgname=cln
pkgver=1.3.4
pkgrel=1
pkgdesc="Class library for numbers"
arch=('i686' 'x86_64')
url="http://www.ginac.de/CLN/"
license=('GPL')
depends=('gmp')
makedepends=('texlive-core')
install=cln.install
source=(http://www.ginac.de/CLN/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('76f73071236ead72ba5c9ee892f29ca24e557b8c')

build() {
  cd ${pkgname}-${pkgver}
  if [ "$CARCH" = "armv7h" ]; then
    export CFLAGS="-march=armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16 -pipe -DNO_ASM"
    export CXXFLAGS="-march=armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16 -pipe -DNO_ASM"
  elif [ "$CARCH" = "armv6h" ]; then
    export CFLAGS="-march=armv6 -mfloat-abi=hard -mfpu=vfp -pipe -DNO_ASM"
    export CXXFLAGS="-march=armv6 -mfloat-abi=hard -mfpu=vfp -pipe -DNO_ASM"
  else
    export CFLAGS="-march=armv5te -pipe -DNO_ASM"
    export CXXFLAGS="-march=armv5te -pipe -DNO_ASM"
  fi
  ./configure --prefix=/usr
  make all html
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-html
}
