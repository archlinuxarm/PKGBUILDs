# $Id: PKGBUILD 81927 2010-06-07 10:44:11Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

# Plugbox changed only the gcc-lib depends to be 4.4.3-5
# instead of 4.5.0

pkgbase=libsigc++
pkgname=('libsigc++' 'libsigc++-docs')
pkgver=2.2.8
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
url="http://libsigc.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.2/${pkgname}-${pkgver}.tar.bz2)
makedepends=('gcc-libs>=4.4.3-5')
options=(!libtool !emptydirs)
sha256sums=('0fd75fd8249e09ae2fd3ffd20c3fbaea11b47d71ba5ba2340ed98b6bdca40ea8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package_libsigc++() {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2"
  depends=('gcc-libs>=4.4.3-5')
  provides=("libsigc++2.0=${pkgver}")
  replaces=('libsigc++2.0')
  conflicts=('libsigc++2.0')
  cd "${srcdir}/libsigc++-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile || return 1
  make DESTDIR="${pkgdir}" install || return 1
}

package_libsigc++-docs() {
  pkgdesc="Developer documentation for libsigc++"
  provides=("libsigc++2.0-docs=${pkgver}")
  replaces=('libsigc++2.0-docs')
  conflicts=('libsigc++2.0-docs')
  cd "${srcdir}/libsigc++-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install || return 1
}
