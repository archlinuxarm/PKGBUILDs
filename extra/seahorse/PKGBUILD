# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=2.26.2
pkgrel=3
pkgdesc="GNOME application for managing PGP keys."
arch=('arm')
license=('GPL')
url="http://seahorse.sourceforge.net/"
depends=('gnome-keyring>=2.26.1' 'libglade>=2.6.4' 'avahi>=0.6.25' 'libnotify>=0.4.5' \
         'libsoup>=2.26.2' 'gpgme' 'desktop-file-utils' 'zip')
makedepends=('gettext' 'libldap>=2.4.18' 'intltool' 'pkgconfig' 'gnome-doc-utils>=0.16.0' 
             'openssh' 'libsm')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
replaces=('gnome-keyring-manager')
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/seahorse/2.26/seahorse-${pkgver}.tar.bz2
        seahorse-2.26.2-gpgme-lfs.patch)
md5sums=('c5126bd7ddfda3317daa71c06f261034'
         '20d527f7531cc7182964ef92895c9b28')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/seahorse-2.26.2-gpgme-lfs.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-ldap --enable-hkp \
	      --enable-libnotify \
	      --disable-scrollkeeper --disable-update-mime-database || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain seahorse ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
