# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Remi Gacogne <rgacogne[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc

pkgname=powerdns-recursor
pkgver=5.3.1
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('gcc-libs' 'boost-libs' 'libcap' 'libcurl.so' 'libsodium' 'systemd-libs' 'luajit' 'openssl'
         'libboost_context.so' 'fstrm' 'net-snmp')
makedepends=('boost' 'meson' 'python' 'python-yaml' 'ragel' 'rust' 'systemd')
options=(!lto)
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.xz{,.asc}
        sysusers.conf)
sha512sums=('e595f9dc7d7b05ef7b80e905b5536341bc6a5267a37786e73a0fdb0f52970d41568b01717a5f6900b1ee297c01063d3edccad3d143c72c94409bb1457b901f41'
            'SKIP'
            '7aada0f186bc1cd6af9a4af598511e7f517f0c103973397d260796c8e83c789d720990579d6ccba53e4aa4a8a6c5b2107f4d32d09ce9a72118ae5030b03bb6ab')
validpgpkeys=('FBAE0323821C7706A5CA151BDCF513FA7EED19F3'  # Peter van Dijk <peter.van.dijk@powerdns.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF') # Winkels, Erik <erik.winkels@open-xchange.com>

build() {
  meson setup "pdns-recursor-${pkgver}" build \
    --prefix        /usr \
    --libexecdir    lib \
    --sbindir       bin \
    --sysconfdir    /etc/powerdns \
    --auto-features enabled \
    --wrap-mode     nodownload \
    -Ddebug=true \
    -Dhardening-fortify-source=disabled \
    -Doptimization=2 \
    -Db_lto=false \
    -Db_pie=true \
    -Dauto-var-init=zero \
    -Ddns-over-tls=enabled \
    -Dreproducible=true \
    -Dsigners-libsodium=enabled \
    -Dsnmp=enabled \
    -Dsystemd-service=enabled \
    -Dunit-tests=true \
    -Dsigners-libcrypto-path=/usr # workaround for https://github.com/PowerDNS/pdns/issues/14084
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  mv "${pkgdir}/etc/powerdns/recursor.yml-dist" "${pkgdir}/etc/powerdns/recursor.conf"
  install -Dm 644 "${srcdir}/sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/powerdns-recursor.conf"
}

# vim: ts=2 sw=2 et:
