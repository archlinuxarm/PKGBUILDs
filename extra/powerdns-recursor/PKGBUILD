# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Remi Gacogne <rgacogne[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc

pkgname=powerdns-recursor
pkgver=5.0.4
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('gcc-libs' 'boost-libs' 'libcap' 'libcurl.so' 'libsodium' 'systemd-libs' 'luajit' 'openssl'
         'libboost_context.so' 'fstrm' 'net-snmp')
makedepends=('boost' 'ragel' 'rust' 'systemd')
options=(!lto)
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.bz2{,.asc}
        sysusers.conf)
sha512sums=('a78422917df91a8f8c49bc5c2bdf599f92f6c3359f049653c6623a05da8117445c04400321cc4e2128cfaedb7b345cbec003339c35e4f2c76bb43d16d07b00d4'
            'SKIP'
            '63b3f0664d38fbbcb6b1c914cee050a27eca0a15550962973743580dd5d3ace2e68272ca2d856d53cc74001fe9246d61207ebbc74524c096736777ed08c88e72')
validpgpkeys=('FBAE0323821C7706A5CA151BDCF513FA7EED19F3'  # Peter van Dijk <peter.van.dijk@powerdns.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF') # Winkels, Erik <erik.winkels@open-xchange.com>

prepare() {
  cd pdns-recursor-${pkgver}
  autoreconf -i
}

build() {
  cd pdns-recursor-${pkgver}
  LIBS=$LIBS ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/powerdns \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-systemd \
    --enable-dns-over-tls \
    --with-libsodium \
    --with-lua=luajit \
    --with-net-snmp \
    --enable-auto-var-init=zero \
    --with-service-user=pdns-recursor \
    --with-service-group=pdns-recursor
  make
}

package() {
  cd pdns-recursor-${pkgver}
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/powerdns/recursor.conf"{-dist,}
  install -Dm 644 "${srcdir}/sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/powerdns-recursor.conf"
}

# vim: ts=2 sw=2 et:
