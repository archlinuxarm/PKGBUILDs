# $Id: PKGBUILD 113430 2011-03-08 17:02:40Z ibiru $
# Maintainer : Ionut Biru <ibiru@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - add 'rtmpdump' to depends

plugrel=1

pkgbase=transmission
pkgname=('transmission-cli' 'transmission-gtk' 'transmission-qt')
pkgver=2.22
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
makedepends=('intltool' 'curl' 'libnotify' 'desktop-file-utils' 'qt' 'libevent' 'libcanberra')
source=(http://mirrors.m0k.org/transmission/files/${pkgbase}-${pkgver}.tar.bz2
        transmissiond transmissiond.conf)
md5sums=('6499986bf769276310b00bda1090090d'
         '50b7c17300bc8b38f44fb6f681ba05d0'
         'be39806c35b7544856fa4070b00fc960')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  ./configure --prefix=/usr --disable-gconf2
  make
  pushd qt
  qmake qtr.pro
  make
}
package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent' 'rtmpdump')
  backup=('etc/conf.d/transmissiond')
  install=transmission-cli.install

  cd "${srcdir}/${pkgbase}-${pkgver}"

  for dir in daemon cli web utils
  do
      make -C "${dir}" DESTDIR="${pkgdir}" install
  done

  install -D -m755 "${srcdir}/transmissiond" "${pkgdir}/etc/rc.d/transmissiond"
  install -D -m644 "${srcdir}/transmissiond.conf" "${pkgdir}/etc/conf.d/transmissiond"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-cli/COPYING"
}

package_transmission-gtk() {
  pkgdesc="Fast, easy, and free BitTorrent client (GTK+ GUI)"
  depends=('curl' 'libevent' 'libnotify' 'desktop-file-utils' 'hicolor-icon-theme'
           'gtk2' 'dbus-glib' 'libcanberra' 'rtmpdump')
  optdepends=('notification-daemon: Desktop notification support'
  	      'transmission-cli: daemon and web support')
  install=transmission-gtk.install

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make -C gtk DESTDIR="${pkgdir}" install
  make -C po DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-gtk/COPYING"
}

package_transmission-qt() {
  pkgdesc="Fast, easy, and free BitTorrent client (Qt GUI)"
  depends=('curl' 'qt' 'libevent' 'rtmpdump')
  optdepends=('transmission-cli: daemon and web support')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make -C qt INSTALL_ROOT="${pkgdir}"/usr install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-qt/COPYING"
  install -D -m644 qt/icons/transmission.png "${pkgdir}/usr/share/pixmaps/transmission-qt.png"
  install -D -m644 qt/transmission-qt.desktop "${pkgdir}/usr/share/applications/transmission-qt.desktop"
}
