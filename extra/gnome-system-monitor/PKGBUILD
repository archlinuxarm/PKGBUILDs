# $Id: PKGBUILD 41833 2009-06-07 13:22:09Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=2.26.2
pkgrel=1
pkgdesc="A system monitor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgksu>=2.0.5' 'libwnck>=2.26.1' 'gconf>=2.26.0-2' 'libgtop>=2.26.1' 'gtkmm>=2.16.0' 'librsvg>=2.26.0' 'gnome-icon-theme>=2.26.0')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.16.0' 'intltool')
options=(!emptydirs)
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-system-monitor.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d69ddb569e51ba381b6bf2d8d32f3b78')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper \
	      --enable-pcrecpp || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
