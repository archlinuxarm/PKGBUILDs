# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop docs package for now, hangs build
#  - patch for AArch64

pkgname=doxygen
pkgver=1.8.10
pkgrel=4
license=('GPL')
arch=(i686 x86_64)
url="http://www.doxygen.org/"
makedepends=('cmake' 'gcc-libs' 'flex' 'qt4' 'texlive-core' 'ghostscript'
             'texlive-latexextra' 'graphviz' 'python2')
source=(http://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz
        0001-Partial-revert-of-commit-080a465b1321.patch
        flex-2.6.patch::"https://github.com/heirecka/doxygen/commit/5fcb1357.patch"
        doxygen-1.8.10-drop-qt-arch-x86-64-definition.patch)
md5sums=('79767ccd986f12a0f949015efb5f058f'
         '93cc5bb3f18c73a3e2b53d6276d87a11'
         'b282c91bac1888f607a3676a236d2828'
         '7039d4a63c3a0e8ead83d1f828d5a271')
sha1sums=('6bad93cbdb1b2efacba14769d39bcc7bc9c0753b'
          'aa03a6a51ff691ffef11d96bb25236f2f7655b6b'
          'aeeb45547de0f659789588b563210d9511c6a291'
          '5aea21a336015675075b3b8744e4444b0dc6c3d5')

prepare() {
  mkdir build

  cd "${srcdir}/${pkgname}-${pkgver}"
  # Install the man pages in the right place
  sed -i 's:DESTINATION man/man1:DESTINATION "${CMAKE_INSTALL_PREFIX}/share/man/man1":g' \
    doc/CMakeLists.txt

  # https://bugzilla.gnome.org/show_bug.cgi?id=759241
  patch -Np1 -i ../0001-Partial-revert-of-commit-080a465b1321.patch

  # Fix build with flex 2.6
  patch -p1 -i ../flex-2.6.patch

  # Fix AArch64 build
  patch -p1 -i ../doxygen-1.8.10-drop-qt-arch-x86-64-definition.patch
}

build() {
  cd build

  cmake "../${pkgbase}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DDOC_INSTALL_DIR:PATH=share/doc/doxygen \
    -DQT_QMAKE_EXECUTABLE:FILE=/usr/lib/qt4/bin/qmake \
    -DPYTHON_EXECUTABLE:FILE=/usr/bin/python2 \
    -Dbuild_doc:BOOL=OFF \
    -Dbuild_wizard:BOOL=ON

  make
}

check(){
  cd build
  make tests
}

package_doxygen() {
  pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation'
              'qt4: for doxywizard')

  cd build
  make DESTDIR="$pkgdir" install

  # Docs are in 'doxygen-docs'
  rm -rf "${pkgdir}/usr/share/doc"
}

