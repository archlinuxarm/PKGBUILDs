# $Id: PKGBUILD 52437 2009-09-19 15:16:23Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-exchange
pkgver=2.26.3
_majorver=2.26
pkgrel=2
pkgdesc="Ximian Connector Exchange plugin for Evolution"
arch=(i686 x86_64)
license=('GPL')
url="http://www.ximian.com"
depends=('evolution-data-server>=2.26.3' 'libgnomeui>=2.24.1' 'gvfs>=1.2.3')
makedepends=('pkgconfig' 'intltool' 'evolution>=2.26.3' 'libsm')
replaces=('ximian-connector')
conflicts=('ximian-connector')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=evolution-exchange.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_majorver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('6ee035bfcafeef0aba01b459092eeec7')

build() {
  # do not use --as-needed in LDFLAGS
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  # Look for correct DB, we want 4.1
  sed -e 's|DB_CFLAGS="-I$withval/include"|DB_CFLAGS="-I$withval/include/db4.1"|' \
      -e 's|DB_LIBS="-L$withval/lib -ldb"|DB_LIBS="-L$withval/lib -ldb-4.1"|' \
      -i configure
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  	--libexecdir=/usr/lib/evolution --disable-static --with-libdb=/usr  || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain evolution-exchange-${_majorver} ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
