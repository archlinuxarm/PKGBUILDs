# $Id: PKGBUILD 46264 2009-07-15 00:34:53Z giovanni $
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=deluge
pkgver=1.1.9
pkgrel=2
pkgdesc="A bittorrent client written with python and pygtk"
arch=('i686' 'x86_64')
url="http://deluge-torrent.org/"
license=('GPL3')
depends=('pygtk' 'boost>=1.39.0' 'pyxdg' 'dbus-python' 'librsvg' 
'setuptools' 'libtorrent-rasterbar>=0.14.2')
makedepends=('subversion' 'intltool')
optdepends=('python-chardet: used to load non-UTF8 encoded torrents')
source=(http://download.deluge-torrent.org/source/${pkgname}-${pkgver}.tar.bz2 deluged)
md5sums=('38b9dbf255ed9620760d546dbb757882' '6e1585bdc79243b1cd22ed9423cc4ec2')
install="deluge.install"

build() {

  cd ${srcdir}/${pkgname}-${pkgver}/

  python setup.py build || return 1
  python setup.py install --prefix=/usr --root=${pkgdir} || return 1

  install -d -m755 ${pkgdir}/srv/deluge || return 1
  install -D -m644 deluge/data/pixmaps/deluge.svg ${pkgdir}/usr/share/pixmaps/deluge.svg || return 1
  install -D -m755 ${srcdir}/deluged ${pkgdir}/etc/rc.d/deluged || return 1

}

