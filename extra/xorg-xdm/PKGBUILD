# $Id: PKGBUILD 36929 2009-04-28 20:31:34Z eric $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xdm
pkgver=1.1.8
pkgrel=2
pkgdesc="X Display Manager"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('pam' 'libxaw' 'libxinerama' 'xorg-server-utils' 'libxft')
makedepends=('pkgconfig')
backup=(etc/X11/xdm/Xaccess etc/X11/xdm/Xresources etc/X11/xdm/Xservers etc/X11/xdm/xdm-config etc/pam.d/xdm etc/X11/xdm/Xsetup_0 etc/X11/xdm/Xsession)
options=('!libtool')
source=(${url}/releases/individual/app/xdm-${pkgver}.tar.bz2
	Xsession-loginshell.patch
	Xsession-xsm.patch
	xdm.pam
        LICENSE)
md5sums=('cc1816bc62a3722ad509373b0b7f30fe' '234ae568e7b22cbc09433bb396ff3d80'\
         '2c30078c50bc653d72eb5114506eb313' '419d6289ba6f851135f5c70c0e3cbec4'\
         'dbba206a043cabe3cd28199ad67e9cdc')

build() {
  cd ${srcdir}/xdm-${pkgver}
  patch -Np0 -i ${srcdir}/Xsession-loginshell.patch || return 1
  patch -Np1 -i ${srcdir}/Xsession-xsm.patch || return 1
  ./configure --prefix=/usr \
              --disable-static \
	      --disable-xprint \
	      --with-xdmconfigdir=/etc/X11/xdm \
	      --with-xdmscriptdir=/etc/X11/xdm \
	      --with-pixmapdir=/usr/share/xdm/pixmaps || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m755 -d ${pkgdir}/etc/pam.d
  install -m644 ${srcdir}/xdm.pam ${pkgdir}/etc/pam.d/xdm || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  #Get rid of /usr/X11R6
  cd ${pkgdir}/etc/X11/xdm
  sed -i -e 's/\/X11R6//g' *
}
