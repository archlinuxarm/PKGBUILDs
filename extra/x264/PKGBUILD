# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure needs --disable-asm to build on non-AArch64
#  - --disable-lto

pkgname=x264
pkgver=0.159.r2999.296494a
pkgrel=1
epoch=3
pkgdesc='Open Source H264/AVC video encoder'
arch=(x86_64)
url=https://www.videolan.org/developers/x264.html
license=(GPL)
depends=(
  liblsmash.so
)
makedepends=(
  git
  l-smash
  nasm
)
provides=(
  libx264
  libx264.so
)
conflicts=(
  libx264
  libx264-10bit
  libx264-all
)
replaces=(
  libx264
  libx264-10bit
  libx264-all
)
source=(git+https://code.videolan.org/videolan/x264.git#commit=296494a4011f58f32adc54304a2654627558c59a)
sha256sums=(SKIP)

pkgver() {
  cd x264

  ./version.sh | grep X264_POINTVER | sed -r 's/^#define X264_POINTVER "([0-9]+\.[0-9]+)\.([0-9]+) (.*)"$/\1.r\2.\3/'
}

build() {
  cd x264

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-asm"

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-pic \
    --disable-lto \
    --disable-avs $CONFIG
  make
}

package() {
  make -C x264 DESTDIR="${pkgdir}" install-cli install-lib-shared
}

# vim: ts=2 sw=2 et:
