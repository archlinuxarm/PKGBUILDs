# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure needs --disable-asm to build on ARM

pkgname=('x264' 'x264-10bit' 'x264-dev')
pkgver=142.20140311
pkgrel=3
epoch=1
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git')
source=(git://git.videolan.org/x264.git#commit=b7a50c16)
md5sums=('SKIP')

prepare() {
  sed -i 's/install-lib-dev$//g' $pkgname/Makefile
  cp -r $pkgname $pkgname-10bit
}

pkgver() {
  cd $pkgname
  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _date=$(git log -1 --format="%cd" --date=short | tr -d -)

  echo ${_ver}.${_date}
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --enable-shared --enable-pic --disable-asm
  make

  cd ../$pkgname-10bit
  ./configure --prefix=/usr \
    --enable-shared --enable-pic \
    --bit-depth=10 --disable-asm
  make
}

package_x264() {
  pkgdesc='Library for encoding H264/AVC video streams'
  depends=('glibc')

  install -d "$pkgdir"/usr/lib
  make -C $pkgname DESTDIR="$pkgdir" install-cli install-lib-shared
}

package_x264-10bit() {
  pkgdesc='Library for encoding H264/AVC video streams. 10bit-depth.'
  depends=('glibc')
  provides=('x264')
  conflicts=('x264')

  install -d "$pkgdir"/usr/lib
  make -C $pkgname DESTDIR="$pkgdir" install-cli install-lib-shared
}

package_x264-dev() {
  pkgdesc='Development files for library for encoding H264/AVC video streams'
  depends=('x264')

  make -C $pkgbase DESTDIR="$pkgdir" install-lib-dev
}
