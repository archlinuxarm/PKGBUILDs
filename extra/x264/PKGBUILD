# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure needs --disable-asm to build on non-AArch64

pkgname=('x264' 'libx264' 'libx264-10bit')
pkgver=144.20150223
pkgrel=1
epoch=2
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git' 'ffmpeg')
source=(git://git.videolan.org/x264.git#commit=c8a773ebf
        'aarch64.patch')
md5sums=('SKIP'
         'cfea784ff527edab0972a540660f4000')

pkgver() {
  cd $pkgname
  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _date=$(git log -1 --format="%cd" --date=short | tr -d -)

  echo ${_ver}.${_date}
}

prepare() {
  cd $pkgname
  patch -p1 -i ../aarch64.patch
  cd ..
  cp -r $pkgname $pkgname-10bit
}

build() {
  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-asm"

  cd $pkgname
  ./configure --prefix=/usr \
    --enable-shared --enable-pic $CONFIG
  make

  cd ../$pkgname-10bit
  ./configure --prefix=/usr \
    --enable-shared --enable-pic \
    --bit-depth=10 $CONFIG
  make
}

package_x264() {
  pkgdesc='CLI tools for encoding H264/AVC video streams'
  depends=('glibc' 'ffmpeg')
  provides=('x264-10bit')
  conflicts=('x264-10bit')
  replaces=('x264-10bit')

  make -C $pkgbase DESTDIR="$pkgdir" install-cli
  install -Dm755 $pkgbase-10bit/x264 "$pkgdir"/usr/bin/x264-10bit
}

package_libx264() {
  pkgdesc='Library for encoding H264/AVC video streams'
  depends=('glibc')
  provides=('libx264.so' 'x264-dev')
  conflicts=('x264-dev')
  replaces=('x264-dev')

  install -d "$pkgdir"/usr/lib
  make -C $pkgbase DESTDIR="$pkgdir" install-lib-shared
}

package_libx264-10bit() {
  pkgdesc='Library for encoding H264/AVC video streams. 10bit-depth.'
  depends=('glibc')
  provides=('libx264.so' 'libx264' 'x264-dev')
  conflicts=('libx264' 'x264-dev')

  install -d "$pkgdir"/usr/lib
  make -C $pkgbase-10bit DESTDIR="$pkgdir" install-lib-shared
}
