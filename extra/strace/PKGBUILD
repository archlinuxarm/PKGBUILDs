# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --enable-mpers=check to fix AArch64 build

pkgname=strace
pkgver=4.21
pkgrel=1
pkgdesc='A diagnostic, debugging and instructional userspace tracer'
arch=(x86_64)
url='http://sourceforge.net/projects/strace/'
license=(BSD)
depends=(perl libunwind)
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}-${pkgver}.tar.xz{,.asc})
md5sums=('2f3d225b4de54ffe49a392f39923c5fd'
         'SKIP')
validpgpkeys=('296D6F29A020808E8717A8842DB5BD89A340AEB7') # Dmitry V. Levin <ldv@altlinux.org>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --with-libunwind --enable-mpers=check
  make
}

check() {
  # tests do not work in chroot environment. TODO: fixit.
  # make -C $pkgname-$pkgver check
  true
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
