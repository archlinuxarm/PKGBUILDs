# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hold for toolchain update, only build for v7/v8

buildarch=12
#noautobuild=1

pkgname=valgrind
pkgver=3.12.0
pkgrel=2
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc=2.25' 'perl')
makedepends=(gdb openmpi)
optdepends=('openmpi: MPI support')
options=(!emptydirs)
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2)
sha1sums=('7a6878bf998c60d1e377a4f22ebece8d9305bda4')

build() {
  # valgrind does not like stack protector flags
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fstack-protector-strong/}
  CXXFLAGS=${CXXFLAGS/-fstack-protector-strong/}

  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
