# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hold for toolchain update, only build for v7

buildarch=4
noautobuild=1

pkgname=valgrind
pkgver=3.10.1
pkgrel=3
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.22' 'glibc<2.23' 'perl')
makedepends=('gdb' 'openmpi')
optdepends=('openmpi: MPI support')
options=('!emptydirs')
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2
        valgrind-glibc.patch
	valgrind-kernel-version.patch)
sha1sums=('15518427de842547ffa84eff8b2b67f3b16581a8'
          '1fa127ef24630ad5f4901ea0e37ea2acb12d3d17'
          '08b71e1ebc3e3906d0ff273db0aba2177a857c47')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/valgrind-glibc.patch
  patch -p1 -i "$srcdir"/valgrind-kernel-version.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
