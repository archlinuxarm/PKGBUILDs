# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hold for toolchain update, only build for v7/v8

buildarch=12
#noautobuild=1

pkgname=valgrind
pkgver=3.11.0
pkgrel=1
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.22' 'glibc<2.23' 'perl')
makedepends=(gdb openmpi)
optdepends=('openmpi: MPI support')
# valgrind does not like stack protector flags
options=(!emptydirs)
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2)
sha1sums=('340757e91d9e83591158fe8bb985c6b11bc53de5')

build() {
  CFLAGS=${CFLAGS/-fstack-protector/} # remove stack protector flag

  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
