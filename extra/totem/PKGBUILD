# $Id: PKGBUILD 44713 2009-07-03 18:13:46Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=totem
pkgver=2.26.3
pkgrel=1
url="http://www.hadess.net/totem.php3"
pkgdesc="A GNOME2 integrated movie player based on Gstreamer."
arch=(i686 x86_64)
license=('GPL2' 'custom')
depends=('gstreamer0.10-good-plugins' 'totem-plparser>=2.26.2' 'libxxf86vm' 'libxtst' 'desktop-file-utils' 'iso-codes>=3.10.1' 'libepc>=0.3.10' 'python>=2.6' 'startup-notification' 'python-gdata')
makedepends=('nautilus>=2.26.3' 'xulrunner>=1.9.1' 'pkgconfig' 'bluez>=4.30' 'lirc-utils' 'gnome-doc-utils>=0.16.0' 'gnome-control-center>=2.26.0' 'libtracker' 'intltool' 'pygtk')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
options=('!libtool' '!emptydirs')
conflicts=('totem-xine')
replaces=('totem-xine')
groups=('gnome-extra')
install=totem.install
source=(http://ftp.gnome.org/pub/gnome/sources/totem/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f8f7390f5d93268cd200086a1b4f765a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  LD_PRELOAD="" ./configure --enable-python || return 1
  LD_PRELOAD="" ./configure --prefix=/usr --sysconfdir=/etc \
                    --libexecdir=/usr/lib/totem \
                    --localstatedir=/var --disable-static \
                    --enable-python --enable-nautilus \
                    --with-dbus --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
       DESTDIR="${pkgdir}" install || return 1

  rm -f ${pkgdir}/usr/share/totem/vanity.*
  rm -f "${pkgdir}/usr/share/pixmaps/vanity.png"

  rm -rf "${pkgdir}/usr/lib/mozilla"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain totem ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
