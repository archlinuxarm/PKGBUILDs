# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Conributor: MÃ©lanie Chauvel (ariasuni) <perso at hack-libre dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc

pkgname=nuspell
pkgver=4.1.0
pkgrel=2
pkgdesc='Fast and safe spellchecking C++ library'
arch=('x86_64')
url='https://nuspell.github.io/'
license=('LGPL3')
depends=('boost-libs' 'icu')
makedepends=('cmake' 'boost' 'catch2')
source=("https://github.com/nuspell/nuspell/archive/v${pkgver}.tar.gz")
sha512sums=('d0c01dd73fefd8b9c800e6419fe916e55a53b8b19f5848d3766cdcb50383fdcd9e9bb49362b5cea4d9d2d7fda21aed055f28cb47b006e2049b2a904355f7f9ed')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
