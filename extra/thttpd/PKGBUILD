# $Id: PKGBUILD 26493 2009-02-08 05:39:36Z eric $
# Maintainer: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=thttpd
pkgver=2.25b
pkgrel=2
pkgdesc="tiny/turbo/throttling HTTP server"
arch=(i686 x86_64)
url="http://www.acme.com/software/thttpd/"
license=('BSD')
depends=('bash')
backup=('usr/www/thttpd_config')
conflicts=('apache')
source=(http://www.acme.com/software/thttpd/$pkgname-$pkgver.tar.gz \
        thttpd thttpd_config LICENSE)
md5sums=('156b249b3b0bcd48b06badd2db0d56c5' '256b995472c8f0fea235b46fdd97e6ec'\
         'b899c226895dcaa3fb131e98720843c1' '0c5762c2c34dcbe9eb18815516502872')

build() {
  cd $startdir/src/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man || return 1
  make || return 1
  install -d  $startdir/pkg/usr/sbin
  install -d  $startdir/pkg/usr/share/man/man{1,8}
  install -d  $startdir/pkg/usr/www/data/log
  make BINDIR=$startdir/pkg/usr/sbin \
    MANDIR=$startdir/pkg/usr/share/man WEBDIR=$startdir/pkg/usr/www \
    WEBGROUP=root install || return 1
  chown root:root $startdir/pkg/usr/share/man/man8/thttpd.8
  install -D -m755 ../thttpd $startdir/pkg/etc/rc.d/thttpd
  install -D -m755 ../thttpd_config  $startdir/pkg/usr/www/thttpd_config
  install -D -m644 ../LICENSE $startdir/pkg/usr/share/licenses/$pkgname/LICENSE
}

