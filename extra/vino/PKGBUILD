# $Id: PKGBUILD 44556 2009-07-02 17:55:31Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=2.26.2
pkgrel=2
pkgdesc="a VNC server for the GNOME desktop"
arch=(i686 x86_64)
license=('GPL')
depends=('libgnomeui>=2.24.1' 'libnotify>=0.4.5' 'libxtst' 'gnutls>=2.8.1' 'libsoup>=2.26.3' 'libunique>=1.0.8' 'gtk2>=2.16.4')
makedepends=('pkgconfig' 'intltool')
groups=('gnome-extra')
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('580a8f79130273f03cb57c32bba59120')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/vino \
              --localstatedir=/var || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain vino ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
