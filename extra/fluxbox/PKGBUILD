# $Id: PKGBUILD 12779 2008-09-20 06:42:07Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor:  John Proctor <jproctor@prium.net>

pkgname=fluxbox
pkgver=1.1.1
pkgrel=1
pkgdesc="A lightweight and highly-configurable window manager"
arch=('i686' 'x86_64')
url="http://www.fluxbox.org"
license=('MIT')
depends=('libxft' 'libxpm' 'libxinerama' 'libxrandr' 'gcc-libs' 'bash' 'imlib2')
makedepends=('pkgconfig')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2 \
        fluxbox.desktop)
md5sums=('fa9fa8fe9a44f86522de5754f8b285ca' 'eeae9c88a2526ac0bd8afcd9ce97f9ed')
sha1sums=('6d49deb6c4c7d73f7567e1cf7d2575c7edc4825f' 'f3f83b8ce84d79c2f8670ef687e0dd89ab0552b8')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-kde --enable-gnome \
    --enable-xft --enable-xinerama \
    --enable-imlib2 --enable-nls || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
 # this is a fix to allow fluxbox to shutdown, but this can be changed by the user :-(
  sed -i 's|^.*slit\.autoHide.*$|session.screen0.slit.autoHide:  True|' ${pkgdir}/usr/share/fluxbox/init || return 1
  install -D -m644 ../fluxbox.desktop ${pkgdir}/etc/X11/sessions/fluxbox.desktop || return 1
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING || return 1
}
