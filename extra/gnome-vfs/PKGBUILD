# $Id: PKGBUILD 30314 2009-03-18 21:34:12Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-vfs
pkgver=2.24.1
pkgrel=2
pkgdesc="The GNOME Virtual File System"
arch=(i686 x86_64)
license=('LGPL')
depends=('fam' 'gconf>=2.26.0-2' 'hal>=0.5.11' 'bzip2' 'avahi>=0.6.24' 'smbclient>=3.3.1' 'gnome-mime-data>=2.18.0-2' 'heimdal>=1.2.1' 'gnutls>=2.6.4')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
conflicts=(gnome-vfs-samba)
provides=(gnome-vfs-samba)
replaces=(gnome-vfs-samba gnome-vfs-extras)
url="http://www.gnome.org"
install=gnome-vfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/gnome-vfs-${pkgver}.tar.bz2
	hal-show-volume-names.patch)
md5sums=('100f9af16dc659e375ee8da45175a28d' '109d07aa1dc8cb6523c47c4984047dce')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #Archlinux patch (b.g.o #321498)
  patch -Np1 -i ${startdir}/src/hal-show-volume-names.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
	--libexecdir=/usr/lib/gnome-vfs-2.0 \
  	--enable-samba --enable-hal \
        --enable-avahi --disable-howl \
	--disable-openssl --enable-gnutls || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-vfs-2.0 ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
