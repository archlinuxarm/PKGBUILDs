# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to add libm to fix v7 FTBFS

pkgname=upower
pkgver=0.99.14
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
url="https://upower.freedesktop.org"
arch=(x86_64)
license=(GPL)
depends=(systemd libimobiledevice libgudev)
makedepends=(docbook-xsl gobject-introspection python git gtk-doc meson)
checkdepends=(python-{dbus,dbusmock,gobject} umockdev)
backup=(etc/UPower/UPower.conf)
_commit=ab9520a7aad3795be7975e021577093a6e85946c  # tags/v0.99.14^0
source=("git+https://gitlab.freedesktop.org/upower/upower.git#commit=$_commit"
        0001-build-Fix-default-udevrulesdir.patch
        0002-build-Fix-version-macros.patch
        math.patch)
sha256sums=('SKIP'
            '8891e9baddca739f41a9323da94fd38c61f42e9267b0b42597f69fb3f4d8bf69'
            '68a916b1058dcc7b21e8e7568e6998bf929bb586b94500124739c1059107e256'
            '83ade36649dcc98c175d9a5a0e7e74cc8392b613c93d1655c292a405cad29873')

pkgver() {
  cd upower
  git describe --tags | sed -e 's/^v\|^UPOWER_//;s/_/\./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd upower

  # meson fixes
  git apply -3 ../0001-build-Fix-default-udevrulesdir.patch
  git apply -3 ../0002-build-Fix-version-macros.patch

  patch -p1 -i ../math.patch
}

build() {
  arch-meson upower build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libupower-glib.so)

  meson install -C build --destdir "$pkgdir"
}
