# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depends on awe packages, set -DARROW_S3=OFF

pkgname=arrow
pkgver=23.0.0
pkgrel=2
# parquet-testing and arrow-testing projects have no releases, commits may need to be updated on pkgver bumps
_parquet_testing_commit=92d45b0752487a4b55fb7f1581c8126ee3e73b0d
_arrow_testing_commit=7b641152dcb0f9e197ebe24a1986151849250959
pkgdesc="Columnar in-memory analytics layer for big data."
arch=(x86_64)
url="https://arrow.apache.org"
license=(Apache-2.0)
depends=(
  abseil-cpp
  apache-orc
  brotli
  bzip2
  glibc
  google-glog
  grpc
  libgcc
  libstdc++
  libutf8proc
  lz4
  openssl
  protobuf
  re2
  snappy
  thrift
  zlib
  zstd
)
makedepends=(
  boost
  cmake
  flatbuffers
  git
  gmock
  ninja
  rapidjson
  xsimd
)
checkdepends=(
  python
)
provides=(parquet-cpp)
conflicts=(parquet-cpp)
source=(
  https://archive.apache.org/dist/$pkgname/$pkgname-$pkgver/apache-$pkgname-$pkgver.tar.gz{,.asc}
  git+https://github.com/apache/parquet-testing.git#commit=$_parquet_testing_commit
  git+https://github.com/apache/arrow-testing.git#commit=$_arrow_testing_commit
)
sha512sums=('ddaaf31e7f3f8246ce3aa6e77c812c55a7993d037aaebc8ca9d79d54c701717a135eebfede731a3239becfaed2fa2395fcb3d68b14c32b9de72b3f5598d9df4a'
            'SKIP'
            'c7995b54e4aaa69c6a2173e057a58d7cf477b78a06787cb50c1ebe38aeba56c8da138fa1370f0187419a3ee3bd1ac7867f3e3379b0a454c7f55e5c1e335b4fb5'
            '9b85bb0ce492894631ec3a234899dd9b5c22181945ee8682ebd5f279cf275ada21d3e3e74e8509b7c045ee34fc417824a0629993d3b373c813a5069916a2a84c')
validpgpkeys=(265F80AB84FE03127E14F01125BCCA5220D84079  # Krisztian Szucs (apache) <szucs.krisztian@gmail.com>
              08D3564B7C6A9CAFBFF6A66791D18FCF079F8007  # Kouhei Sutou <kou@cozmixng.org>
              AF6AADA4C9835B75973FF5DA275C532289DD0F4A  # Raúl Cumplido Domínguez (CODE SIGNING KEY) <raulcd@apache.org>
              A2AC7132B5DA7C273A7A147665F4A8CA9769ECD7) # Apache Arrow Automated Release Signing <private@arrow.apache.org>

prepare() {
  cd apache-$pkgname-$pkgver
  # Patch out unused AWS-SDK component https://github.com/apache/arrow/pull/45191
  sed -i 's|COMPONENTS config|COMPONENTS|' cpp/cmake_modules/FindAWSSDKAlt.cmake
}

build() {
  # workaround for cmake 4.0 - arrow bundles several old dependencies https://github.com/apache/arrow/issues/45985
  # (note that ExternalProject does not propagate this as a CMake variable so we need to export an environment variable)
  export CMAKE_POLICY_VERSION_MINIMUM=3.5

  # Arrow options are defined in https://github.com/apache/arrow/blob/main/cpp/cmake_modules/DefineOptions.cmake
  # Documentation for building from source: https://arrow.apache.org/docs/developers/cpp/building.html
  local cmake_options=(
    -B build
    -S apache-$pkgname-$pkgver/cpp
    -G Ninja
    -W no-dev
    # one pyarrow test fails when the build type is none of 'debug', 'release', 'minsizerel', 'relwithdebinfo'
    -DCMAKE_BUILD_TYPE=RelWithDebInfo
    -DCMAKE_INSTALL_PREFIX="/usr"
    -DCMAKE_INSTALL_LIBDIR="lib"
    -DARROW_SIMD_LEVEL=NONE
    -DARROW_RUNTIME_SIMD_LEVEL=MAX
    -DARROW_DEPENDENCY_SOURCE=SYSTEM
    -DARROW_BUILD_STATIC=OFF
    -DARROW_BUILD_TESTS=ON
    -DARROW_BUILD_UTILITIES=ON
    -DARROW_COMPUTE=ON
    -DARROW_CSV=ON
    -DARROW_FILESYSTEM=ON
    -DARROW_FLIGHT=ON
    -DARROW_FLIGHT_SQL=ON
    -DARROW_GANDIVA=OFF  # depends on LLVMAlt
    -DARROW_HDFS=ON
    -DARROW_IPC=ON
    -DARROW_JEMALLOC=ON
    -DARROW_JSON=ON
    -DARROW_ORC=ON
    -DARROW_PARQUET=ON
    -DARROW_S3=OFF
    -DARROW_SUBSTRAIT=ON
    -DARROW_TENSORFLOW=ON
    -DARROW_USE_GLOG=ON
    -DARROW_WITH_BROTLI=ON
    -DARROW_WITH_BZ2=ON
    -DARROW_WITH_LZ4=ON
    -DARROW_WITH_SNAPPY=ON
    -DARROW_WITH_ZLIB=ON
    -DARROW_WITH_ZSTD=ON
    -DPARQUET_REQUIRE_ENCRYPTION=ON
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  local excluded_tests=""
  # arrow-s3fs-test requires minio (dropped to AUR)
  excluded_tests+="|arrow-s3fs-test"

  local ctest_options=(
    --test-dir build
    # show the stdout and stderr when the test fails
    --output-on-failure
    # execute tests in parallel
    --parallel $(nproc)
    # limit execution time for each test
    --timeout 200
    # exclude problematic tests
    --exclude-regex "$excluded_tests"
  )
  PARQUET_TEST_DATA="$srcdir"/parquet-testing/data \
  ARROW_TEST_DATA="$srcdir"/arrow-testing/data \
  ctest "${ctest_options[@]}"
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  # remove testing libraries
  find "$pkgdir"/usr/lib/ -name '*testing*' -delete
}
