# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Ren√© Wagner < rwagner at rw-net dot de >
# Contributor: Diab Neiroukh <lazerl0rd@thezest.dev>

# ALARM: SandaruKasa <sandarukasa plus archarm at ya dot ru>
#  - set `MI_NO_OPT_ARCH` to fix https://github.com/microsoft/mimalloc/issues/1095

pkgname=mimalloc
pkgver=2.2.3
pkgrel=1
pkgdesc='General-purpose allocator with excellent performance characteristics'
arch=('x86_64')
url='https://github.com/microsoft/mimalloc'
license=('MIT')
depends=('glibc')
makedepends=('git' 'cmake')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'remove-staticlib-refs.patch'
)
sha512sums=('69917fdee99f818e653e3cdb6ae3ad4c3131d715ec7752972ea28b0a801d077e30d6202f9c13e2114050b67553ea9c758fe0311ba3c2359f62f67501b53cd3f7'
            'e8a32f066f269d449a765ddc54c192ce7b615e034753b1ffdc66153374e9b7f1973ebc7acf45a90f8ccf05962708f9288e4c5f3819abfe2c909530152e24437a')
b2sums=('9fba0e21b5c6f87c562c40d1c0028318aeb114ce3e59fd055496769da1b2d35b3564b029db5260fca601523976d4ceecf1967e789696211162be8ad172e2bb36'
        'b34f447b1cf74110c97404fe815329cf84ae8ff798766eefc0f4e451cf52211e5745463c3d99209eafa8d3e1bc02ca66b5e526c04773ec5a21b626b428942f1c')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=Release \
    -D MI_NO_OPT_ARCH=ON \
    -D MI_BUILD_OBJECT=OFF \
    -D MI_INSTALL_TOPLEVEL=ON

  cmake --build build
}

check() {
  cd build

  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  pushd "$pkgdir/usr/lib/cmake/mimalloc"
  patch -p1 -i "$srcdir/remove-staticlib-refs.patch"
  popd

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
