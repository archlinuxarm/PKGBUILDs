# $Id: PKGBUILD 26878 2009-02-14 06:18:41Z eric $
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=acidrip
pkgver=0.14
pkgrel=6
pkgdesc="A Gtk2::Perl application for ripping and encoding DVD's"
license=('GPL')
arch=(i686 x86_64)
url="http://untrepid.com/acidrip/"
depends=('mplayer' 'lsdvd' 'lame' 'libdvdcss' 'gtk2-perl' 'perl>=5.10.0')
source=(ftp://ftp.archlinux.org/other/acidrip/$pkgname-$pkgver.tar.gz
        http://untrepid.com/acidrip/acidrip-icon4bit.png
        acidrip.desktop crop-fixes.patch)
md5sums=('c1c27bbf658e5c30f43e067a8ae283de' '7498602e2a3838cf35b94af85d9954d3'\
         'f6896612df224a325d66c5f8fa8553e7' '1a8470702e2e0eda8ecbc473141aa08f')
options=(!emptydirs)

build()
{
  cd $startdir/src/$pkgname-$pkgver
  patch -up1 < ../crop-fixes.patch || return 1
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor || return 1
  make  || return 1
  make install DESTDIR=${startdir}/pkg || return 1

  install -D -m644 ${startdir}/src/acidrip-icon4bit.png \
      ${startdir}/pkg/usr/share/pixmaps/acidrip.png
  install -D -m644 ${startdir}/src/acidrip.desktop \
      ${startdir}/pkg/usr/share/applications/acidrip.desktop

  # remove perllocal.pod and .packlist
  find ${startdir}/pkg -name perllocal.pod -delete
  find ${startdir}/pkg -name .packlist -delete
}
