# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop julia make/optdepends, doesn't build on ARM

pkgname=cantor
pkgver=20.12.3
pkgrel=3
pkgdesc="KDE Frontend to Mathematical Software"
url="https://kde.org/applications/education/cantor/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(analitza libspectre kpty ktexteditor knewstuff libqalculate hicolor-icon-theme qt5-xmlpatterns poppler-qt5)
makedepends=(extra-cmake-modules python kdoctools luajit r)
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend'
            'luajit: LUA backend'
            'python: Python 3 backend'
            'sagemath: SageMath backend'
            'julia: Julia backend')
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         cantor-sage-completions.patch::https://invent.kde.org/education/cantor/-/merge_requests/26.patch)
sha256sums=('4b4689d301963959fa112abfd3447ecdc1228db2aafb8403938b14fb2b69ca38'
            'SKIP'
            'be9f501d85e25884338815a199fed3690213546be481d9f92663914038fa128c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../cantor-sage-completions.patch # Fix tab completion in Sage
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
