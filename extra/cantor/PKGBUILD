# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop julia make/optdepends, doesn't build on ARM

pkgname=cantor
pkgver=16.12.3
pkgrel=2
pkgdesc="KDE Frontend to Mathematical Software"
url="https://kde.org/applications/education/cantor/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('analitza' 'libspectre' 'kpty' 'ktexteditor' 'knewstuff' 'libqalculate' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'python2' 'kdoctools' 'luajit' 'r')
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend'
            'luajit: LUA backend'
            'python: Python 3 backend'
	    'python2: Python 2 backend'
            'sagemath: SageMath backend')
conflicts=('kdeedu-cantor')
replaces=('kdeedu-cantor')
source=("https://download.kde.org/stable/applications/${pkgver}/src/cantor-${pkgver}.tar.xz"{,.sig})
sha256sums=('888677129bf30474a2f2bfe46931770f411cf8823c944e90ba4ea16d9d4eefdb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build

  if [[ $CARCH == 'i686' ]]; then
    export CFLAGS+=" -march=pentium4"
    export CXXFLAGS+=" -march=pentium4"
  fi

  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
