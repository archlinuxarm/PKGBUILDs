# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop julia make/optdepends, doesn't build on ARM

pkgname=cantor
pkgver=25.08.3
pkgrel=1
pkgdesc='KDE Frontend to Mathematical Software'
url='https://apps.kde.org/cantor/'
arch=(x86_64)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         knewstuff
         kparts
         ktexteditor
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libqalculate
         libspectre
         libxml2
         libxslt
         poppler-qt6
         qt6-base
         qt6-svg
         qt6-tools
         qt6-webengine
         syntax-highlighting)
makedepends=(analitza
             extra-cmake-modules
             kdoctools
             luajit
             python
             r)
optdepends=('analitza: KAlgebra backend'
            'maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend'
            'luajit: LUA backend'
            'python: Python backend'
            'sagemath: SageMath backend')
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/education/cantor/-/commit/08c7c1ff.patch)
sha256sums=('5635007fb9c50d82e88decf9264ff40fb9f4ee65b27474c5930b1b519eb24b72'
            'SKIP'
            'a1cc179c9a9e4105a427696b4b16faec5111237cb124de0bc0824f56a8eddd9f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 08c7c1ff.patch # Fix build with julia 1.12
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
