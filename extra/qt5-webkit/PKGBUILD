# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to link with libatomic on v5/v6
#  - disable jit on v5/v6
#  - use tarball source

highmem=1

pkgname=qt5-webkit
_pkgver=5.212.0-alpha3
pkgver=${_pkgver/-/}
pkgrel=3
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
source=("https://github.com/qtwebkit/qtwebkit/releases/download/qtwebkit-$_pkgver/qtwebkit-$_pkgver.tar.xz"
        'atomic.patch')
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen woff2)
makedepends=(cmake ruby gperf python2 qt5-doc qt5-tools)
optdepends=('gst-plugins-good: Webm codec support')
sha256sums=('357eee582445574472e410ebece632d06fa0ec94358fc4345f2a713922df5e17'
            '23fbd785daf116c71b3ce87d40860f0a6c56ab1065ee98ec41634626ca961129')

prepare() {
  mkdir -p build

  if [[ $CARCH == "arm" || $CARCH == "armv6h" ]]; then
    cd qtwebkit-$_pkgver
    patch -p0 -i ../atomic.patch
  fi
}

build() {
  cd build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && CONFIG="-DENABLE_JIT=OFF"

  cmake ../qtwebkit-$_pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF \
    $CONFIG
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
