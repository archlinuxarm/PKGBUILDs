# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - icu makedepend
#  - patch for soft-float armv5
#  - patch for AArch64 support in qtwebkit

pkgname=qt5-webkit
_qtver=5.7.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
depends=('qt5-location' 'qt5-sensors' 'qt5-webchannel' 'libwebp' 'libxslt' 'libxcomposite' 'gst-plugins-base')
makedepends=('ruby' 'gperf' 'python2' 'icu' 'qt5-doc' 'qt5-tools')
optdepends=('gst-plugins-good: Webm codec support')
conflicts=('qt')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-opensource-src-${pkgver}"
source=("http://download.qt.io/community_releases/${pkgver%.*}/${pkgver}/qtwebkit-opensource-src-${pkgver}.tar.xz"
        qt5webkit-0002-Fix-QtWebKit-build-on-ARM-softfp.patch
        0001-Add-ARM-64-support.patch)
md5sums=('4fa8f47383ce36d59dcc36d701ce93cd'
         'd55ad1f1c90725834b44eada5db4e401'
         '47657bc1b3f51892d0cc88a271a8fa31')

prepare() {
  mkdir -p build

# Hack to force python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

# ARM fixes
  cd ${_pkgfqn}
  patch -p1 -i ../qt5webkit-0002-Fix-QtWebKit-build-on-ARM-softfp.patch
  patch -p1 -i ../0001-Add-ARM-64-support.patch
}

build() {
  cd build  

  export PATH="${srcdir}/bin:$PATH"
  qmake ../${_pkgfqn}
  make
  make docs
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install install_docs

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
