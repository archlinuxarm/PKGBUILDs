# $Id: PKGBUILD 52072 2009-09-15 10:44:40Z douglas $
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=postgresql-libs
pkgver=8.4.1
pkgrel=1
pkgdesc="Libraries for use with PostgreSQL"
arch=(i686 x86_64)
license=('BSD')
url="http://www.postgresql.org/"
depends=('openssl>=0.9.8b' 'zlib' 'readline>=6.0')
options=('!makeflags')
source=(ftp://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2)

build() {
  cd $startdir/src/postgresql-$pkgver || return 1

  # configure
  ./configure --prefix=/usr --with-openssl --datadir=/usr/share/postgresql \
  --with-pam \
  	|| return 1

  # make and install
  for dir in src/interfaces src/bin/pg_config; do
    pushd ${dir} || return 1
    make || return 1
    make DESTDIR=${startdir}/pkg install || return 1
    popd
  done

  cd src/include || return 1
  make || return 1

  set -e
  mkdir -p $startdir/pkg/usr/include/postgresql/{internal,internal/libpq} \
  	|| return 1
  mkdir -p $startdir/pkg/usr/include/libpq || return 1

  # These headers are needed by the public headers of the interfaces.
  install -m644 pg_config.h $startdir/pkg/usr/include/ || return 1
  install -m644 pg_config_os.h $startdir/pkg/usr/include/ || return 1
  install -m644 postgres_ext.h $startdir/pkg/usr/include/ || return 1
  install -m644 libpq/libpq-fs.h $startdir/pkg/usr/include/libpq/ || return 1
  install -m644 pg_config_manual.h $startdir/pkg/usr/include/ || return 1

  # These headers are needed by the not-so-public headers of the interfaces.
  install -m644 c.h $startdir/pkg/usr/include/postgresql/internal/ || return 1
  install -m644 port.h $startdir/pkg/usr/include/postgresql/internal/ \
  	|| return 1
  install -m644 postgres_fe.h $startdir/pkg/usr/include/postgresql/internal/ \
  	|| return 1
  install -m644 libpq/pqcomm.h $startdir/pkg/usr/include/postgresql/internal/libpq/ || return 1
 
  install -D -m644 ../../COPYRIGHT $startdir/pkg/usr/share/licenses/$pkgname/LICENSE || return 1

  set +e
}
md5sums=('f2015af17bacbbfe140daf0d1067f9c9')
