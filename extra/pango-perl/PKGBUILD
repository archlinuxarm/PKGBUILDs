# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango-perl
_realname=Pango
pkgver=1.220
pkgrel=1
pkgdesc="Perl bindings for Pango"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
depends=('pango>=1.24.0' 'glib-perl>=1.221' 'cairo-perl>=1.061')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/${_realname}-${pkgver}.tar.gz)
md5sums=('ee4884ffe436fb936b4d84ea9c9372e3')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor || return 1
  make || return 1
  make install DESTDIR="${pkgdir}" || return 1

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
