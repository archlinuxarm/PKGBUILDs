# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch> # Contributor: Christopher Reimer <c.reimer1993@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix size_t check on ARM

pkgname=texmacs
pkgver=1.99.6
pkgrel=2
pkgdesc="Free scientific text editor, inspired by TeX and GNU Emacs. WYSIWYG editor TeX-fonts and CAS-interface (Giac, GTybalt, Macaulay 2, Maxima, Octave, Pari, Qcl, R and Yacas) in one."
arch=('x86_64')
url="http://www.texmacs.org/"
license=('GPL')
depends=('perl' 'guile1.8' 'texlive-core' 'python2' 'libxext' 'freetype2' 'qt5-base')
# do not remove texlive-core dependency, as it is needed!
optdepends=('transfig: convert images using fig2ps'
            'gawk: conversion of some files'
	    'ghostscript: rendering ps files'
            'imagemagick: convert images'
            'aspell: spell checking')
makedepends=('ghostscript' 'cmake')
source=(http://www.texmacs.org/Download/ftp/tmftp/source/TeXmacs-${pkgver}-src.tar.gz
        texmacs-fusion.patch
        fix-size_t-check.patch)
options=('!emptydirs')
sha1sums=('bef4d2637ad3cff0138236f07ee0031943b8de61'
          '21bdad3180f8dca6c16f4a3a43f1d96eab718b0f'
          '57e1c2a560ad4d52f0c96f05c990f738b65d2571')
	
prepare() {
  cd TeXmacs-${pkgver}-src

  sed -i 's/env python/env python2/' \
    plugins/{mathematica/bin/realpath.py,python/bin/tm_python,sage/bin/tm_sage} \
    TeXmacs/misc/inkscape_extension/texmacs_reedit.py
  sed -i 's/"python"/"python2"/' plugins/python/progs/init-python.scm

  sed -e 's/-Wno-deprecated-register//' -i src/CMakeLists.txt # Remove wrong flag on Linux

  # Fix crash with fusion style
  patch -p2 -i ../texmacs-fusion.patch

  patch -p1 -i ../fix-size_t-check.patch
}

build() {
  cd TeXmacs-${pkgver}-src
  
  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGUILECONFIG_EXECUTABLE=/usr/bin/guile-config1.8
  make
}

package() {
  cd TeXmacs-${pkgver}-src/build
  make DESTDIR=${pkgdir} install

 # fix fig2ps script
 sed -i 's|${prefix}|/usr|' "${pkgdir}/usr/bin/fig2ps"
 # fix launch script
 sed -e 's|\@CONFIG_LIB_PATH\@|LD_LIBRARY_PATH|g' -i "$pkgdir"/usr/bin/texmacs
}
