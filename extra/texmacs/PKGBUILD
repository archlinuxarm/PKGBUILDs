# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch> # Contributor: Christopher Reimer <c.reimer1993@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix build on ARM

pkgname=texmacs
pkgver=1.99.5
pkgrel=2
pkgdesc="Free scientific text editor, inspired by TeX and GNU Emacs. WYSIWYG editor TeX-fonts and CAS-interface (Giac, GTybalt, Macaulay 2, Maxima, Octave, Pari, Qcl, R and Yacas) in one."
arch=('i686' 'x86_64')
url="http://www.texmacs.org/"
license=('GPL')
depends=('perl' 'guile1.8' 'texlive-core' 'python2' 'libxext' 'freetype2' 'qt4')
# do not remove texlive-core dependency, as it is needed!
optdepends=('transfig: convert images using fig2ps'
            'gawk: conversion of some files'
	    'ghostscript: rendering ps files'
            'imagemagick: convert images'
            'aspell: spell checking')
makedepends=('ghostscript')
source=(http://www.texmacs.org/Download/ftp/tmftp/source/TeXmacs-${pkgver}-src.tar.gz
        TeXmacs-1.99.2-qreal-double.patch)
options=('!emptydirs')
sha1sums=('be6fdc39a094db6dc547490fa89a95876dbde002'
          '3cd581e5db92ce18b3411bda6442e882b0660da0')
	
prepare() {
  cd "${srcdir}/TeXmacs-${pkgver}-src"

  patch -p1 -i ../TeXmacs-1.99.2-qreal-double.patch

  sed -i 's/env python/env python2/' \
    plugins/{mathematica/bin/realpath.py,python/bin/tm_python,sage/bin/tm_sage} \
    TeXmacs/misc/inkscape_extension/texmacs_reedit.py
  sed -i 's/"python"/"python2"/' plugins/python/progs/init-python.scm
  sed -i '/^LDPATH/d' src/makefile.in

  autoreconf -vi
  sed -i -e 's/guile18 /guile1.8 /g' \
         -e 's/guile18-config/guile-config1.8/g' \
         configure
  sed -i -e 's|#include <libguile18.h>|#include <guile/gh.h>|g' src/Scheme/Guile/guile_tm.hpp

 # Don't generate icon-cache and mime-database (namcap tells that they should not be in a package)
  sed -i '/update-mime-database/d' Makefile.in
  sed -i '/gtk-update-icon-cache/d' Makefile.in
  sed -i '\/icons\/gnome 2>\/dev\/null/d' Makefile.in

# Remove stray backup files that break build
  find -name '.*.d' -o -name '.*.cpp' -exec rm {} \;
}

build() {
  cd "${srcdir}/TeXmacs-${pkgver}-src"
  export QMAKE=qmake-qt4
  export MOC=moc-qt4
  export UIC=uic-qt4
  export LDFLAGS="$LDFLAGS -lz"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --libexecdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/TeXmacs-${pkgver}-src"
  make DESTDIR=${pkgdir} install

 # fix fig2ps script
 sed -i 's|${prefix}|/usr|' "${pkgdir}/usr/bin/fig2ps"
 # fix launch script
 sed -e 's|\@CONFIG_LIB_PATH\@|LD_LIBRARY_PATH|g' -i "$pkgdir"/usr/bin/texmacs
}
