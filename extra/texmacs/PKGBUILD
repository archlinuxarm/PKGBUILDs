# $Id: PKGBUILD 52910 2009-09-23 05:55:30Z eric $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=texmacs
pkgver=1.0.7
pkgrel=3
pkgdesc="GNU TeXmacs is a free scientific text editor, which was both inspired by TeX and GNU Emacs. WYSIWYG editor TeX-fonts and CAS-interface (Giac, GTybalt, Macaulay 2, Maxima, Octave, Pari, Qcl, R and Yacas) in one."
arch=("i686" "x86_64")
url="http://www.texmacs.org/"
license=('GPL')
depends=('perl' 'guile>=1.8.7' 'texlive-core' 'python' 'libxext' 'freetype2')
# do not remove texlive-core dependency, as it is needed!
optdepends=('transfig: convert images using fig2ps')
source=("ftp://ftp.texmacs.org/pub/TeXmacs/targz/TeXmacs-${pkgver}-src.tar.gz")
md5sums=('4b58579250a697cbf68ed83ef3805030')
sha1sums=('aea77447930d847e5419814b3b3e5719be18c72b')

build() {
  cd ${srcdir}/TeXmacs-${pkgver}-src
  sed -i '/^LDPATH/d' src/makefile.in
  ./configure --prefix=/usr --mandir=/usr/share/man --libexecdir=/usr/lib
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

 # fix fig2ps script
 sed -i 's|${prefix}|/usr|' ${pkgdir}/usr/bin/fig2ps
}

