# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build aarch64 with crypto extension

pkgbase=xet-core
pkgname=(python-hf-xet)
pkgver=1.2.0
pkgrel=3
pkgdesc="xet client tech, used in Huggingface Hub"
arch=(x86_64)
url=https://github.com/huggingface/xet-core
license=(Apache-2.0)
makedepends=(
  cargo
  maturin
  python-installer
  openssl
)
source=($pkgbase-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
        $pkgbase-pyproject.patch::https://github.com/huggingface/xet-core/commit/4b8ed4b85b068518f2bc85aa9330b6bf0c9242f2.patch)
b2sums=('f54088890975e906ef63b5c54ff08d0f4fbbc67b8d5dd10360b97b5b20137553ccd5b8559d059fc19d193200c6f97d4a5d6ceabc8a28e4eddf5d7ce5018fc484'
        'b770660d35af862290c8ec9141a1b98b5dbac68737677e8b7bfa07ecb635ee8d33927b3b47f81575a782fe615ee5b01a73034360c06b2e666eb5b33043a90f76')

prepare() {
  cd $pkgbase-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"

  # Fix build https://github.com/huggingface/xet-core/pull/659
  patch -p1 < ../$pkgbase-pyproject.patch
}

build() {
  # fix building with LTO
  CFLAGS+=' -ffat-lto-objects'

  # build aarch64 with crypto extension
  [[ $CARCH == "aarch64" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/-march=armv8-a/-march=armv8-a+crypto/'` && CXXFLAGS="$CFLAGS"

  # always use system openssl https://docs.rs/openssl/latest/openssl/
  export OPENSSL_NO_VENDOR=1

  cd $pkgbase-$pkgver
  # there don't seem to be any useful tools for users...
  #cargo build --frozen --release

  # build Python bindings
  cd hf_xet
  local maturin_options=(
    --locked
    --release
    --target "$(rustc --print host-tuple)"
    --strip
    # wheel compatibility tag - native linux ensures we don't bundle openssl libs
    # https://github.com/PyO3/maturin/issues/2038
    --compatibility linux
    # use native-tls instead of native-tls-vendored
    --features native-tls
  )
  maturin build "${maturin_options[@]}"
}

package_python-hf-xet() {
  depends=(
    glibc
    libgcc
    openssl libssl.so libcrypto.so
    python
  )

  cd $pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  cd hf_xet
  python -m installer --destdir "$pkgdir" target/wheels/*.whl
}
