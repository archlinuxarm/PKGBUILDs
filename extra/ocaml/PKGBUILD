# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix ARM build

pkgbase='ocaml'
pkgname=('ocaml' 'ocaml-compiler-libs')
pkgver=4.02.0
pkgrel=1
pkgdesc="A functional language with OO extensions"
arch=('i686' 'x86_64')
license=('LGPL2.1' 'custom: QPL-1.0')
url="http://caml.inria.fr/"
depends=('gdbm')
makedepends=('tk>=8.6.0' 'ncurses>=5.6-7' 'libx11')
optdepends=('ncurses: advanced ncurses features' 'tk: advanced tk features')
source=(http://caml.inria.fr/distrib/ocaml-4.02/${pkgname}-${pkgver}.tar.gz
        arm.patch)
options=('!makeflags' '!emptydirs' 'staticlibs')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../arm.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure -prefix /usr -x11include /usr/include 
  make world.opt
}

package_ocaml() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make PREFIX="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man" install
   
  # Save >10MB with this one, makepkg only strips debug symbols.
  #find "${pkgdir}/usr/lib" -type f -name '*.so.*' -exec strip --strip-unneeded {} \;

  # install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ocaml-compiler-libs() {
pkgdesc="Several modules used internally by the OCaml compiler"
license=('custom: QPL-1.0')
depends=('ocaml')
optdepends=()

  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Install compiler libraries
  local compiler_libs="${pkgdir}/usr/lib/ocaml/compiler-libs"
  mkdir -p "$compiler_libs"/{parsing,typing,utils}
  cp parsing/*.{cmi,cmo,cmx,ml,mli,mll,o} "$compiler_libs"/parsing
  cp typing/*.{cmi,cmo,cmx,ml,mli,o} "$compiler_libs"/typing
  cp utils/*.{cmi,cmo,cmx,ml,mli,o} "$compiler_libs"/utils
  # duplicated by installation
  rm -f "$compiler_libs"/typing/outcometree.{cmi,mli}

  # install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
md5sums=('8bba7e7d872083af1723dd450e07a5f4'
         'b76dff01b9152e7c829dfdfe4c0ebfd6')
