# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DAOM_TARGET_CPU=generic for !AArch64

pkgname=aom
pkgver=1.0.0
pkgrel=1
pkgdesc="Alliance for Open Media video codec"
url="https://aomedia.org/"
arch=(x86_64)
license=(BSD custom:PATENTS)
depends=(gcc-libs)
makedepends=(git cmake ninja doxygen graphviz yasm)
_commit=d14c5bb4f336ef1842046089849dee4a301fbbf0  # tags/v1.0.0^0
source=("git+https://aomedia.googlesource.com/aom#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  # Reduce docs size
  printf '%s\n' >>libs.doxy_template \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  cd build
  [[ $CARCH != "aarch64" ]] && CONFIG="-DAOM_TARGET_CPU=generic"
  cmake -G Ninja ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=1 \
    -DENABLE_TESTS=0 $CONFIG
  cmake --build .
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install

  mkdir -p "$pkgdir/usr/share/doc"
  cp -r build/docs/html "$pkgdir/usr/share/doc/$pkgname"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/{LICENSE,PATENTS}
}

# vim:set et sw=2:
