# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DAOM_TARGET_CPU=generic for !AArch64

pkgbase=aom
pkgname=(
  aom
  aom-docs
)
pkgver=3.13.0
pkgrel=1
pkgdesc="Alliance for Open Media video codec"
url="https://aomedia.org/"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cmake
  doxygen
  ninja
  yasm
)
source=(
  https://storage.googleapis.com/aom-releases/libaom-$pkgver.tar.gz{,.asc}
)
b2sums=('2e9eddc1ad18991d31c7af3345c4e5426212055e926b6c7ab1f90867e0ad7c3994f6fba6e8000067753df2b5247b364db7c9a50053c01a7ba07f060e1c665333'
        'SKIP')
validpgpkeys=(
  B002F08B74A148DAA01F7123A48E86DB0B830498 # AOMedia release signing key <av1-discuss@aomedia.org>
)

prepare() {
  cd libaom-$pkgver
}

build() {
  [[ $CARCH != "aarch64" ]] && CONFIG="-DAOM_TARGET_CPU=generic"
  local cmake_options=(
    # Upstream would like Release, adding -O3 and removing assertions
    # https://gitlab.archlinux.org/archlinux/packaging/packages/aom/-/issues/1
    -D CMAKE_BUILD_TYPE=Release

    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_SHARED_LIBS=1
    -D ENABLE_TESTS=0
    $CONFIG
  )

  # upstream wants this off
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=0}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=0}"

  cmake -S libaom-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package_aom() {
  provides=(libaom.so)

  DESTDIR="$pkgdir" cmake --install build

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libaom-$pkgver/{LICENSE,PATENTS}
}

package_aom-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "$pkgdir/usr/share/doc"
  cp -r build/docs/html "$pkgdir/usr/share/doc/$pkgbase"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libaom-$pkgver/{LICENSE,PATENTS}
}

# vim:set sw=2 sts=-1 et:
