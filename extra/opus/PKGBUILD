# $Id: PKGBUILD 200804 2013-12-05 18:41:22Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: pumbur

# ALARM: Matthias Grosser <mtgrosser[at]gmx[dot]net>
#  - added --enable-fixed-point to configure for armel

pkgname=opus
pkgver=1.1
pkgrel=1.1
pkgdesc="Codec designed for interactive speech and audio transmission over the Internet"
arch=(i686 x86_64)
url="http://www.opus-codec.org/"
license=(BSD)
depends=(glibc)
source=(http://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('c5a8cf7c0b066759542bc4ca46817ac6')

build() {
  cd $pkgname-$pkgver
  [[ $CARCH == 'arm' ]] && CONFIG='--enable-fixed-point'
  ./configure --prefix=/usr --enable-custom-modes --disable-static $CONFIG
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
