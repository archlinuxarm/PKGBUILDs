# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop valgrind makedepend

pkgname=orc
pkgver=0.4.27
pkgrel=1
pkgdesc="Optimized Inner Loop Runtime Compiler"
url="https://cgit.freedesktop.org/gstreamer/orc"
arch=(i686 x86_64)
license=(custom)
depends=(glibc)
makedepends=(git gtk-doc meson)
_commit=1163fd1027010ce16ff25bc5448948f4a5073844  # tags/orc-0.4.27^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/orc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^orc-//;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

check() {
  cd build
  mesontest
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
