# $Id: PKGBUILD 165577 2012-08-25 16:01:55Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with pulseaudio as sound server, not jack (x86 asm)

pkgname=snd
pkgver=17.3
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('i686' 'x86_64')
url="http://ccrma.stanford.edu/software/snd/"
license=('custom:free')
depends=('pulseaudio' 'gamin' 'gsl' 'fftw' 'gtk2' 'libmpc')
makedepends=('ladspa' 'wavpack' 'flac' 'speex' 'gmp'
             'timidity++' 'mpg123' 'vorbis-tools')
optdepends=('wavpack' 'flac' 'speex' 'mpg123'
            'timidity++' 'vorbis-tools')
source=("ftp://ccrma-ftp.stanford.edu/pub/Lisp/${pkgname}-${pkgver}.tar.gz")
md5sums=('cb45d5b2ddef4f0b47fa2c803b11ed0a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # execstack is probably due to gmp; this is here just for a reminder
  LDFLAGS+=" -Wl,-z,noexecstack"

  ./configure --prefix=/usr \
              --with-alsa \
              --with-pulseaudio \
              --with-gtk \
              --with-ladspa \
              --with-gmp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
