# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove make/optdepends on p7zip

pkgname=file-roller
pkgver=44.5
pkgrel=1
pkgdesc="Create and modify archives"
url="https://wiki.gnome.org/Apps/FileRoller"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libarchive
  libnautilus-extension
  pango
)
makedepends=(
  appstream-glib
  arj
  binutils
  bzip3
  cdrtools
  cpio
  dpkg
  git
  glib2-devel
  lhasa
  lrzip
  meson
  python
  rpmextract
  squashfs-tools
  unace
  unrar
  unzip
  yelp-tools
  zip
)
optdepends=(
  'arj: arj archive support'
  'binutils: ar archive support'
  'bzip3: bzip3 compression support'
  'cdrtools: ISO image support'
  'cpio: cpio archive support'
  'dpkg: deb archive support'
  'lhasa: LHA/LZA archive support'
  'lrzip: lrzip compression support'
  'rpmextract: rpm archive support'
  'squashfs-tools: squashfs image support'
  'unace: ACE archive support'
  'unrar: better RAR archive support'
  'unzip: better ZIP archive support'
  'zip: better ZIP archive support'
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/file-roller.git#tag=$pkgver")
b2sums=('90547d4577041f7e7add35078ece373bb48b01f526d638e7d0a81000d8273da91120ff681995c319c257922278ff01382d11001dc3f0cf24d8337d509aae4b77')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
