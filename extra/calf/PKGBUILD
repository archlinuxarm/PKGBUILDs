# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-sse from configure

pkgname=calf
pkgver=0.90.4
pkgrel=1
pkgdesc='LV2 plug-in suite and standalone Jack host'
arch=(x86_64)
url='https://calf-studio-gear.org'
groups=(lv2-plugins pro-audio)
license=(LGPL2.1)
depends=(cairo expat fluidsynth gcc-libs gdk-pixbuf2 glib2 glibc gtk2 hicolor-icon-theme jack)
makedepends=(git lv2)
source=("git+https://github.com/calf-studio-gear/calf#tag=$pkgver")
b2sums=('a5d2ccca675c21e807b4851d26e519beafce5ce159be8e5c711d1dd1febbf17f164dd9e6bc4e776295bfc2132c6ccb67bf4dc0a975acf49f6094bd0ef8f914b1')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --enable-experimental --prefix=/usr --with-lv2
  # prevent libtool from overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname
  install -Dm644 $pkgname/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
