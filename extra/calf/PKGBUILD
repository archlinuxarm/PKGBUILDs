# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-sse from configure

pkgname=calf
pkgver=0.90.3
pkgrel=6
pkgdesc='LV2 plug-in suite and standalone Jack host'
arch=(x86_64)
url="https://calf-studio-gear.org"
groups=(
  lv2-plugins
  pro-audio
)
license=(LGPL2.1)
depends=(
  cairo
  expat
  fluidsynth
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk2
  hicolor-icon-theme
  jack
)
makedepends=(lv2)
source=(https://calf-studio-gear.org/files/$pkgname-$pkgver.tar.gz)
sha512sums=('a17be4f2e753c5426ff6995ebf4e88f1d4916629f624fcb576e09e5cafd56474fc8944d9defba31561a3476c55d78dd69189cedb11197a09cc3af6e59aa2a867')
b2sums=('c34eebe4a80492f54f4af0ce136f489554959a0a605cd729a3138e368976aafb0fcf4827050aea6de3b276d1e6d5a67283eed88dd342aa8cf65ffcb3ff1cd86f')

prepare(){
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --enable-experimental
    --prefix=/usr
    --with-lv2
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent libtool from overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
