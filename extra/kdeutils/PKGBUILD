# $Id: PKGBUILD 54321 2009-10-08 13:44:52Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeutils
pkgname=('kdeutils-ark'
         'kdeutils-kcalc'
         'kdeutils-kcharselect'
         'kdeutils-kdelirc'
         'kdeutils-kdessh'
         'kdeutils-kdf'
         'kdeutils-kfloppy'
         'kdeutils-kgpg'
         'kdeutils-ktimer'
         'kdeutils-kwallet'
         'kdeutils-okteta'
         'kdeutils-printer-applet'
         'kdeutils-superkaramba'
         'kdeutils-sweeper')
pkgver=4.3.2
pkgrel=4
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeutils')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdelibs-experimental' 'kdepimlibs'
             'kdebase-lib' 'kdebase-runtime' 'kdebindings-python' 'qca' 
             'system-config-printer-common' 'libarchive' 'qimageblitz')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
	'fix-printer-applet-segfault.patch')
md5sums=('b5e72292ed1504f8ee5a82a388af3b04'
         '4dbb643c5598a81d07503d8088ca5d17')

build() {
	cd $srcdir

	# now this patch is not on 4.3.3
	patch -Np0 -i ${srcdir}/fix-printer-applet-segfault.patch || return 1

	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeutils-ark() {
	pkgdesc='Archiving Tool'
	depends=('kdebase-runtime' 'kdebase-lib' 'libarchive')
	optdepends=('p7zip' 'zip' 'unzip' 'unrar')
	cd $srcdir/build/ark
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ark
	make DESTDIR=$pkgdir install
}

package_kdeutils-kcalc() {
	pkgdesc='Scientific Calculator'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/kcalc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcalc
	make DESTDIR=$pkgdir install
}

package_kdeutils-kcharselect() {
	pkgdesc='Character Selector'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/kcharselect
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcharselect
	make DESTDIR=$pkgdir install
}

package_kdeutils-kdelirc() {
	pkgdesc='KDE Linux infrared remote control server.'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kdelirc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kdelirc
	make DESTDIR=$pkgdir install
}

package_kdeutils-kdessh() {
	pkgdesc='front end to ssh'
	depends=('kdebase-runtime' 'kdelibs' 'kde-agent')
	cd $srcdir/build/kdessh
	make DESTDIR=$pkgdir install
}

package_kdeutils-kdf() {
	pkgdesc='View Disk Usage'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kdf
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kdf
	make DESTDIR=$pkgdir install
}

package_kdeutils-kfloppy() {
	pkgdesc='Floppy Formatter'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kfloppy
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfloppy
	make DESTDIR=$pkgdir install
}

package_kdeutils-kgpg() {
	pkgdesc='A GnuPG frontend'
	depends=('kdebase-runtime' 'kdelibs-experimental' 'kdepimlibs' 'kde-agent')
	install='kdeutils.install'
	cd $srcdir/build/kgpg
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kgpg
	make DESTDIR=$pkgdir install
}

package_kdeutils-ktimer() {
	pkgdesc='Countdown Launcher'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/ktimer
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ktimer
	make DESTDIR=$pkgdir install
}

package_kdeutils-kwallet() {
	pkgdesc='Wallet Management Tool'
	depends=('kdebase-runtime' 'kdelibs-experimental')
	install='kdeutils.install'
	cd $srcdir/build/kwallet
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kwallet
	make DESTDIR=$pkgdir install
}

package_kdeutils-okteta() {
	pkgdesc='Hex Editor'
	depends=('kdebase-runtime' 'kdelibs' 'qca')
	install='kdeutils.install'
	cd $srcdir/build/okteta
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/okteta
	make DESTDIR=$pkgdir install
}

package_kdeutils-printer-applet() {
	pkgdesc='System tray icon for managing print jobs'
	depends=('kdebase-runtime' 'kdelibs' 'kdebindings-python' 'system-config-printer-common')
	cd $srcdir/build/printer-applet
	make DESTDIR=$pkgdir install
}

package_kdeutils-superkaramba() {
	pkgdesc='An engine for cool desktop eyecandy.'
	depends=('kdebase-runtime' 'kdelibs' 'kdebindings-python' 'qimageblitz')
	install='kdeutils.install'
	cd $srcdir/build/superkaramba
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/superkaramba
	make DESTDIR=$pkgdir install
}

package_kdeutils-sweeper() {
	pkgdesc='System Cleaner'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/sweeper
	make DESTDIR=$pkgdir install
}
