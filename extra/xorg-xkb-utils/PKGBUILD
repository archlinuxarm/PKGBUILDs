# $Id: PKGBUILD 36737 2009-04-27 03:32:23Z eric $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xkb-utils
pkgver=7.4
pkgrel=2
pkgdesc="X.org keyboard utilities"
arch=(arm)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw>=1.0.5' 'libxkbfile')
makedepends=(pkgconfig)
source=(${url}/releases/individual/app/xkbutils-1.0.1.tar.bz2
        ${url}/releases/individual/app/xkbcomp-1.0.5.tar.bz2
        ${url}/releases/individual/app/xkbevd-1.0.2.tar.bz2
        ${url}/releases/individual/app/setxkbmap-1.0.4.tar.bz2
        LICENSE.setxkbmap LICENSE.xkbevd LICENSE.xkbutils)
md5sums=('84396a3dd75337caaae29d8fa5616fb1' '6cc96c3e4ed5d9802fe717beac008f19'\
         '68f2a143716c23b566f8509d9498f516' '6fab3bb176be9e510c5613d054ef1ca4'\
         '7c20baa6a26c32a04b1b1d6cbaa626a7' '7b1fc39455375a25afe6f9594ff979ac'\
         '7b1fc39455375a25afe6f9594ff979ac')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}  || return 1
      popd
    fi
  done
# Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/"  || return 1
}
