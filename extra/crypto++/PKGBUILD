# $Id: PKGBUILD 143721 2011-11-28 17:06:53Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix_build_system.patch to use libtool for compilation
#  - removed making libcryptopp.so, adjusted packaging for .libs

pkgname=crypto++
pkgver=5.6.2
_srcver=562
pkgrel=2
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('i686' 'x86_64')
url="http://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
source=("http://www.cryptopp.com/cryptopp${_srcver}.zip"
        'libcrypto++.pc'
        'fix_build_system.patch')
md5sums=('7ed022585698df48e65ce9218f6c6a67'
         '46de3847c59ad16456fa863eb33e73b2'
         '7ba22254fea73c1a9bd308ff6e034856')

build() {
  # ALARM
  patch -p1 -i fix_build_system.patch

  sed -i -e 's/^CXXFLAGS/#CXXFLAGS/' GNUmakefile
  export CXXFLAGS="${CXXFLAGS} -DNDEBUG -fPIC"
  make -f GNUmakefile  
}

package() {
  install -d ${pkgdir}/usr/{lib/pkgconfig,include/cryptopp}
  install -m644 *.h ${pkgdir}/usr/include/cryptopp/
  install -m644 .libs/libcryptopp.so "${pkgdir}/usr/lib/libcryptopp.so"
  install -m644 ${srcdir}/libcrypto++.pc "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
