# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=qt6-3d
_qtver=6.0.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='C++ and QML APIs for easy inclusion of 3D graphics'
depends=(qt6-base)
makedepends=(cmake qt6-declarative qt6-shadertools vulkan-headers) # assimp
optdepends=('qt6-shadertools: RHI renderer' 'qt6-declarative: QML bindings')
groups=(qt6)
_pkgfn="${pkgname/6-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/additional_libraries/${pkgname/6-/}/${pkgver%.*}/$_qtver/$_pkgfn.tar.xz")
sha256sums=('ebca4357d6f1ebe70b344d1e94b5b81bae00fc421da4af8fcacb1f82740ce3c7')

build() {
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(3DCore PRIVATE atomic)" >> ${_pkgfn}/src/core/CMakeLists.txt
  cmake -B build -S $_pkgfn
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
