# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=qt6-3d
_qtver=6.2.1
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='C++ and QML APIs for easy inclusion of 3D graphics'
depends=(qt6-base)
makedepends=(cmake ninja qt6-declarative qt6-shadertools assimp)
optdepends=('qt6-shadertools: RHI renderer' 'qt6-declarative: QML bindings' 'assimp: assimp importer plugin')
groups=(qt6)
_pkgfn="${pkgname/6-/}-everywhere-src-$_qtver"
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz)
sha256sums=('730c0e8e1a1a59c4acbeca68e206bab14ef770f5dacb94b84103a82243cfeeb3')

build() {
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(3DCore PRIVATE atomic)" >> ${_pkgfn}/src/core/CMakeLists.txt
  cmake -B build -S $_pkgfn -G Ninja
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
