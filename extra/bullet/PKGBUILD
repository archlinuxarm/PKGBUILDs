# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to cxxflags

pkgbase=bullet
pkgname=('bullet' 'bullet-docs')
pkgver=2.87
pkgrel=2
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and animation"
arch=('x86_64')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'glu' 'python' 'python-numpy')
source=("https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz"
        bullet3_examplebrowser.sh)
sha512sums=('649e470223295666eda6f7ff59d03097637c2645b5cd951977060ae14b89f56948ce03e437e83c986d26876f187d7ee34e790bc3882d5d66da9af89a4ab81c21'
            '8741ad94b6c46c226d89aebc8ab06d8a11bac3c04d3f0a2bf7a7524792a3375aa7bf7d295410b16fbeb4c348a31057b4570acdebe9bbaea251f44daca8d9fe81')

prepare() {
  cd bullet3-${pkgver}

}

build() {
  cd bullet3-${pkgver}

  CXXFLAGS+=" -fsigned-char"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  cmake .. \
       -DCMAKE_INSTALL_PREFIX=/usr \
       -DBUILD_SHARED_LIBS=1 \
       -DINSTALL_LIBS=1 \
       -DINSTALL_EXTRA_LIBS=1 \
       -DBUILD_PYBULLET=ON \
       -DBUILD_PYBULLET_NUMPY=ON \
       -DBUILD_OPENGL3_DEMOS=ON \
       -DCMAKE_BUILD_TYPE=Release

  make

  cd ..
  doxygen
}

package_bullet() {
  optdepends=('glu: for the example browser'
              'python: python bindings'
              'python-numpy: python bindings'
              'bullet-docs: documentation')

  cd bullet3-${pkgver}/build

  make DESTDIR=${pkgdir} install

  install -Dm755 examples/pybullet/pybullet.so.${pkgver} ${pkgdir}/usr/lib/libpybullet.so.${pkgver}
  install -Dm755 examples/ExampleBrowser/libBulletExampleBrowserLib.so.${pkgver} ${pkgdir}/usr/lib/libBulletExampleBrowserLib.so.${pkgver}
  install -Dm755 examples/OpenGLWindow/libOpenGLWindow.so ${pkgdir}/usr/lib/libOpenGLWindow.so
  install -Dm755 examples/ThirdPartyLibs/Gwen/libgwen.so ${pkgdir}/usr/lib/libgwen.so
  install -Dm755 examples/ThirdPartyLibs/BussIK/libBussIK.so ${pkgdir}/usr/lib/libBussIK.so
  install -Dm755 ${srcdir}/bullet3_examplebrowser.sh ${pkgdir}/usr/bin/bullet3_examplebrowser
  install -Dm755 examples/ExampleBrowser/App_ExampleBrowser ${pkgdir}/opt/bullet/App_ExampleBrowser
  cp -r data ${pkgdir}/opt/bullet/

  # install license
  install -Dm644 ../LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE
}

package_bullet-docs() {
  pkgdesc="Documentation for bullet"
  depends=()

  cd bullet3-${pkgver}

  # install docs
  install -Dm644 docs/GPU_rigidbody_using_OpenCL.pdf ${pkgdir}/usr/share/doc/bullet/GPU_rigidbody_using_OpenCL.pdf
  install -Dm644 docs/Bullet_User_Manual.pdf ${pkgdir}/usr/share/doc/bullet/Bullet_User_Manual.pdf
  install -Dm644 docs/BulletQuickstart.pdf ${pkgdir}/usr/share/doc/bullet/BulletQuickstart.pdf
  cp -r html ${pkgdir}/usr/share/doc/bullet/html
}
# vim: sw=2 ts=2 et:
