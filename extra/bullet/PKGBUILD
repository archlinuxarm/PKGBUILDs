# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBUILD_BULLET2_DEMOS=OFF, -DBUILD_UNIT_TESTS=OFF

pkgbase=bullet
pkgname=('bullet' 'bullet-docs')
pkgver=2.88
pkgrel=1
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and animation"
arch=('x86_64')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'glu' 'python' 'python-numpy')
source=("https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz"
        bullet3_examplebrowser.sh)
sha512sums=('15face1940d496c96fd19a44139d11d2cbb629526c40432be4a0eef5fa9a532c842ec7318248c0359a080f2034111bf1a3c2d3a6fd789bec675bd368fac7bd93'
            '8741ad94b6c46c226d89aebc8ab06d8a11bac3c04d3f0a2bf7a7524792a3375aa7bf7d295410b16fbeb4c348a31057b4570acdebe9bbaea251f44daca8d9fe81')

prepare() {
  mkdir bullet3-${pkgver}/build
}

build() {
  cd bullet3-${pkgver}/build

  cmake .. \
       -DCMAKE_INSTALL_PREFIX=/usr \
       -DBUILD_SHARED_LIBS=1 \
       -DINSTALL_LIBS=1 \
       -DINSTALL_EXTRA_LIBS=1 \
       -DBUILD_PYBULLET=ON \
       -DBUILD_PYBULLET_NUMPY=ON \
       -DCMAKE_BUILD_TYPE=Release \
       -DBUILD_BULLET2_DEMOS=OFF \
       -DBUILD_UNIT_TESTS=OFF

  make

  cd ..
  doxygen
}

package_bullet() {
  optdepends=('glu: for the example browser'
              'python: python bindings'
              'python-numpy: python bindings'
              'bullet-docs: documentation')

  cd bullet3-${pkgver}/build

  make DESTDIR=${pkgdir} install

  #install -Dm755 examples/pybullet/pybullet.so.${pkgver} ${pkgdir}/usr/lib/libpybullet.so.${pkgver}
  #install -Dm755 examples/ExampleBrowser/libBulletExampleBrowserLib.so.${pkgver} ${pkgdir}/usr/lib/libBulletExampleBrowserLib.so.${pkgver}
  #install -Dm755 examples/OpenGLWindow/libOpenGLWindow.so ${pkgdir}/usr/lib/libOpenGLWindow.so
  #install -Dm755 examples/ThirdPartyLibs/Gwen/libgwen.so ${pkgdir}/usr/lib/libgwen.so
  #install -Dm755 examples/ThirdPartyLibs/BussIK/libBussIK.so ${pkgdir}/usr/lib/libBussIK.so
  #install -Dm755 ${srcdir}/bullet3_examplebrowser.sh ${pkgdir}/usr/bin/bullet3_examplebrowser
  #install -Dm755 examples/ExampleBrowser/App_ExampleBrowser ${pkgdir}/opt/bullet/App_ExampleBrowser
  #cp -r data ${pkgdir}/opt/bullet/

  install -Dm644 ../LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE
}

package_bullet-docs() {
  pkgdesc="Documentation for bullet"

  cd bullet3-${pkgver}

  # install docs
  install -Dm644 docs/GPU_rigidbody_using_OpenCL.pdf ${pkgdir}/usr/share/doc/bullet/GPU_rigidbody_using_OpenCL.pdf
  install -Dm644 docs/Bullet_User_Manual.pdf ${pkgdir}/usr/share/doc/bullet/Bullet_User_Manual.pdf
  install -Dm644 docs/BulletQuickstart.pdf ${pkgdir}/usr/share/doc/bullet/BulletQuickstart.pdf
  cp -r html ${pkgdir}/usr/share/doc/bullet/html
}
# vim: sw=2 ts=2 et:
