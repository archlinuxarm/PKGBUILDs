# Maintainer: judd <jvinet@zeroflux.org>

pkgname=dhcp
pkgver=4.1.0.p1
_pkgver=4.1.0p1
pkgrel=1
pkgdesc="A DHCP server, client, and relay agent"
arch=('arm')
license=('custom:isc-dhcp')
url="https://www.isc.org/software/dhcp"
depends=('openssl>=0.9.8a')
backup=('etc/dhcpd.conf' 'etc/conf.d/dhcp')
install=dhcp.install
source=(http://ftp.isc.org/isc/${pkgname}/${pkgname}-${_pkgver}.tar.gz
		dhcpd
		dhcp.conf.d
		dhcp-4.1.0-missing-ipv6-not-fatal.patch)
md5sums=('325ff8338c5a21f89d5304ac13ffebdf'
		 'df22cffa7d7415ece7bb025b7bf774dd'
         '49da3192e5c885e3c7d02f447c2dea5e'
         '401eb8819fc1d10fda3f8e02ad9fe421')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  # Define _GNU_SOURCE to fix IPV6.
  sed '/^CFLAGS="$CFLAGS/ s/INGS"/INGS -D_GNU_SOURCE"/' -i configure
  # Make not having ipv6 non-fatal.
  patch -Np1 -i ${srcdir}/dhcp-4.1.0-missing-ipv6-not-fatal.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-srv-lease-file=/var/state/dhcp/dhcpd.leases \
      --with-srv6-lease-file=/var/state/dhcp/dhcpd6.leases
  make || return 1
  make DESTDIR=${pkgdir} install

  install -D -m755 ${srcdir}/dhcpd ${pkgdir}/etc/rc.d/dhcpd
  install -D -m644 ${srcdir}/dhcp.conf.d ${pkgdir}/etc/conf.d/${pkgname}
  mkdir -p ${pkgdir}/var/state/dhcp
  rm -f ${pkgdir}/etc/dhclient.conf
  rm -f ${pkgdir}/usr/sbin/dhclient
  rm -f ${pkgdir}/usr/share/man/man{5,8}/dhclient*
  # install licenses
  install -m644 -D ${srcdir}/${pkgname}-${_pkgver}/LICENSE \
                   ${pkgdir}/usr/share/licenses/dhcp/LICENSE
}
