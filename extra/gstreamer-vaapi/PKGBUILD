# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepend on valgrind

pkgname=gstreamer-vaapi
pkgver=1.12.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VAAPI Plugin"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(LGPL)
depends=(gst-plugins-base gst-plugins-bad libva libxrandr libvpx)
makedepends=(gtk-doc git python)
_commit=d0054566c029286afda49f705c4554fe9a68799a  # tags/1.12.1^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/gstreamer-vaapi#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer VAAPI Plugin (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --disable-builtin-libvpx \
    --enable-experimental --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
