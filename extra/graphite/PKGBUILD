# $Id: PKGBUILD 51199 2009-09-05 20:11:42Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

# build twice that pango-graphite can pickup the new libgraphite dependency - take care when .so names happen!

pkgbase=graphite
pkgname=('libgraphite' 'pango-graphite')
pkgver=2.3.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://graphite.sil.org/"
makedepends=('pango' 'gcc-libs')
options=('!libtool' '!emptydirs')
source=(http://downloads.sourceforge.net/project/silgraphite/silgraphite/${pkgver}/silgraphite-${pkgver}.tar.gz)
md5sums=('d35724900f6a4105550293686688bbb3')

build() {
  cd "${srcdir}/silgraphite-${pkgver}"
  pushd engine
    ./configure --prefix=/usr --disable-static
  make || return 1
  popd
  
  pushd wrappers/pangographite
  ./configure --prefix=/usr --disable-static
  make || return 1
  popd  
}

package_libgraphite() {
 pkgdesc="SILGraphite - a \"smart font\" rendering engine - the libs and headers"
 license=('custom_SIL Dual license')
 depends=('gcc-libs')

  cd "${srcdir}/silgraphite-${pkgver}"/engine
  make DESTDIR="$pkgdir/" install || return 1

  # licenses
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 license/*.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_pango-graphite() {
 pkgdesc="SILGraphite support for pango"
 license=('LGPL')
 depends=('pango' 'libgraphite')
 install=pango-graphite.install
 
  cd "${srcdir}/silgraphite-${pkgver}"/wrappers/pangographite
  make DESTDIR="$pkgdir/" install || return 1
}