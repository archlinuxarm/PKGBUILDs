# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for arm

pkgname=graphite
pkgver=1.3.10
pkgrel=1
epoch=1
arch=('i686' 'x86_64')
url="https://github.com/silnrsi/graphite"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('LGPL' 'GPL' 'custom')
depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python2'
             # for documentation
             'doxygen' 'dblatex' 'graphviz' 'asciidoc')
checkdepends=('python2-fonttools')
options=('!emptydirs')
source=(https://github.com/silnrsi/graphite/releases/download/${pkgver}/graphite2-${pkgver}.tgz
        graphite-arm-nodefaultlibs.patch)
sha1sums=('668f3bce96fc02d90ea875b401ed36b2e8957d2f'
          '67b4fb81303484a65ab5bfa183da999d14169157')


prepare() {
  cd "${srcdir}"

  # python2 fixes
  sed -i "s:\/usr\/bin\/python:\/usr\/bin\/python2:" graphite2-${pkgver}/tests/{corrupt.py,defuzz,fnttxtrender,fuzzbidi,fuzztest,hbspeeds,jsoncmp}

  cd graphite2-${pkgver}
  patch -Np1 -i ${srcdir}/graphite-arm-nodefaultlibs.patch
}

build() {
  mkdir build
  cd build
  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE:STRING=Release \
	-DGRAPHITE2_COMPARE_RENDERER=OFF \

  # fix unwanted -O3 cflag (taken form Debian)
  find . -type f ! -name "rules" ! -name "changelog" -exec sed -i -e 's/\-O3//g' {} \;

  make 
  make -j1 docs
}

check() {
  cd "${srcdir}"/build
  # python2 fixes
  sed -i "s:python:python2:g" tests/CTestTestfile.cmake
  ctest || true
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install
  # install doc files
  mkdir -p "${pkgdir}"/usr/share/doc/graphite2/api
  cp -vrf doc/doxygen/{html,latex/refman.pdf} "${pkgdir}"/usr/share/doc/graphite2/api
  cp -vrf doc/{GTF,manual}.html "${pkgdir}"/usr/share/doc/graphite2

  # licenses
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}
