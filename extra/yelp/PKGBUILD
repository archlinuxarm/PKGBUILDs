# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --enable-compile-warnings=no

pkgname=yelp
pkgver=3.38.0
pkgrel=1
pkgdesc="Get help with GNOME"
url="https://wiki.gnome.org/Apps/Yelp"
license=(GPL)
arch=(x86_64)
depends=(webkit2gtk yelp-xsl libxslt xz bzip2)
makedepends=(gtk-doc itstool autoconf-archive git xorg-server-xvfb appstream-glib)
groups=(gnome)
_commit=e36454f55d5985ab889ae3c5938607b3b6db647d  # tags/3.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/yelp.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc --enable-compile-warnings=no
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  # Documentation generation segfaults without X11
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
