# $Id: PKGBUILD 44708 2009-07-03 18:12:20Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=yelp
pkgver=2.26.0
pkgrel=3
pkgdesc="A help browser for GNOME"
arch=(i686 x86_64)
depends=('xulrunner>=1.9.1' 'libgnomeui>=2.24.1' 'gnome-doc-utils>=0.16.0' 'startup-notification>=0.10' 'libxslt' 'hicolor-icon-theme' 'rarian>=0.8.1')
makedepends=('intltool' 'pkgconfig')
groups=('gnome')
license=('GPL')
options=('!emptydirs')
url="http://www.gnome.org"
install=yelp.install
source=(http://ftp.gnome.org/pub/gnome/sources/yelp/2.26/yelp-${pkgver}.tar.bz2)
md5sums=('3413fd051c5ecbfe98819a8fb4ac83dd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --with-search=basic --with-gecko=libxul-embedding || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain yelp ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
