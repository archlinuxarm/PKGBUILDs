# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --enable-compile-warnings=no

pkgname=yelp
pkgver=3.28.0
pkgrel=1
pkgdesc="Get help with GNOME"
url="https://wiki.gnome.org/Apps/Yelp"
license=(GPL)
arch=(x86_64)
depends=(webkit2gtk yelp-xsl libxslt xz bzip2)
makedepends=(intltool gtk-doc itstool autoconf-archive git xorg-server-xvfb)
groups=(gnome)
_commit=eb2e56bdf28c494558f80bee48b2c8acd5b00607  # tags/3.28.0^0
source=("git://git.gnome.org/yelp#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # Fixup missing tag for pkgver()
  git tag -f 3.26.0 f88de1d05ad1a7c04e47674e5ba56f2e83da16a5
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc --enable-compile-warnings=no
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  # Documentation generation segfaults without X11
  xvfb-run -a -n 81 -s "-extension GLX -screen 0 1280x1024x24" make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
