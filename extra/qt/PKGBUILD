# $Id: PKGBUILD 125344 2011-05-26 00:12:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - Just say NO to x86 optimizations ;)
#  - disabled distcc, ccache - bad things happen
#  - don't use -no-neon on armv7
#  - make -j1 for v5, -j2 for v7 (pandaboard builder)

plugrel=1

pkgbase=qt
pkgname=('qt' 'qt-private-headers')
pkgver=4.7.3
pkgrel=2
arch=('i686' 'x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL')
makedepends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
    'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib'
    'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'postgresql-libs'
    'mysql' 'unixodbc' 'cups' 'gtk2')
options=('!libtool' '!distcc' '!ccache')
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("ftp://ftp.qt.nokia.com/qt/source/${_pkgfqn}.tar.gz"
        'assistant.desktop' 'designer.desktop' 'linguist.desktop'
        'qtconfig.desktop'
        'qtbug-16292.patch')
md5sums=('49b96eefb1224cc529af6fe5608654fe'
         'fc211414130ab2764132e7370f8e5caa'
         '85179f5e0437514f8639957e1d8baf62'
         'f11852b97583610f3dbb669ebc3e21bc'
         '6b771c8a81dd90b45e8a79afa0e5bbfd'
         'dc7ed8c2e8c68a175f7f05a34dccc937')

build() {
	unset QMAKESPEC
	export QT4DIR=$srcdir/$_pkgfqn
	export PATH=${QT4DIR}/bin:${PATH}
	export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

	cd $srcdir/$_pkgfqn

    # Already fixed upstream
    patch -p1 -i "${srcdir}"/qtbug-16292.patch
	
    sed -i "s|-O2|$CXXFLAGS|" mkspecs/common/g++.conf
	sed -i "/^QMAKE_RPATH/s| -Wl,-rpath,||g" mkspecs/common/g++.conf
	sed -i "/^QMAKE_LFLAGS\s/s|+=|+= $LDFLAGS|g" mkspecs/common/g++.conf

	if [ "${CARCH}" = "armv7" ]; then
		useneon=""
		mymakeflags="-j2"
	else
		useneon="-no-neon"
		mymakeflags="-j1"
	fi

	./configure -confirm-license -opensource \
		-prefix /usr \
		-docdir /usr/share/doc/qt \
		-plugindir /usr/lib/qt/plugins \
		-importdir /usr/lib/qt/imports \
		-datadir /usr/share/qt \
		-translationdir /usr/share/qt/translations \
		-sysconfdir /etc \
		-examplesdir /usr/share/doc/qt/examples \
		-demosdir /usr/share/doc/qt/demos \
		-largefile \
		-plugin-sql-{psql,mysql,sqlite,odbc} \
		-system-sqlite \
		-xmlpatterns \
		-no-phonon \
		-no-phonon-backend \
		-svg \
		-webkit \
		-script \
		-scripttools \
		-system-zlib \
		-system-libtiff \
		-system-libpng \
		-system-libmng \
		-system-libjpeg \
		-nomake demos \
		-nomake examples \
		-nomake docs \
		-no-rpath \
		-openssl-linked \
		-silent \
		-optimized-qmake \
		-dbus \
		-reduce-relocations \
		-no-separate-debug-info \
		-gtkstyle \
		-opengl \
		-no-openvg \
		-glib \
		-no-mmx \
		-no-3dnow \
		-no-sse \
		-no-sse2 \
		-no-sse3 \
		-no-ssse3 \
		-no-sse4.1 \
		-no-sse4.2 \
		-no-avx ${useneon}
	make ${mymakeflags}
}

package_qt() {
    pkgdesc='A cross-platform application and UI framework'
    depends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
      'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib'
      'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils')
    optdepends=('postgresql-libs: PostgreSQL driver'
	  'libmysqlclient: MySQL driver'
	  'unixodbc: ODBC driver'
	  'libxinerama: Xinerama support'
	  'libxcursor: Xcursor support'
	  'libxfixes: Xfixes support')
    install='qt.install'
	
    cd $srcdir/$_pkgfqn
	make INSTALL_ROOT=$pkgdir install

	# install missing icons and desktop files
	for icon in tools/linguist/linguist/images/icons/linguist-*-32.png ; do
		size=$(echo $(basename ${icon}) | cut -d- -f2)
		install -p -D -m644 ${icon} ${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/linguist.png
	done
	install -p -D -m644 src/gui/dialogs/images/qtlogo-64.png ${pkgdir}/usr/share/icons/hicolor/64x64/apps/qtlogo.png
	install -p -D -m644 tools/assistant/tools/assistant/images/assistant.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/assistant.png
	install -p -D -m644 tools/designer/src/designer/images/designer.png ${pkgdir}/usr/share/icons/hicolor/128x128/apps/designer.png
	install -d ${pkgdir}/usr/share/applications
	install -m644 ${srcdir}/{linguist,designer,assistant,qtconfig}.desktop ${pkgdir}/usr/share/applications/

	# install license addition
	install -D -m644 LGPL_EXCEPTION.txt ${pkgdir}/usr/share/licenses/qt/LGPL_EXCEPTION.txt

	# Fix wrong path in pkgconfig files
	find ${pkgdir}/usr/lib/pkgconfig -type f -name '*.pc' \
		-exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;
	# Fix wrong path in prl files
	find ${pkgdir}/usr/lib -type f -name '*.prl' \
		-exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
}

package_qt-private-headers(){
    pkgdesc="Qt private headers for development"
    depends=("qt=${pkgver}")

    install -d ${pkgdir}/usr/include/phonon/private
    cd ${srcdir}/$_pkgfqn/src/3rdparty/phonon/phonon/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/phonon/private/ \; &> /dev/null
    
    install -d ${pkgdir}/usr/include/Qt3Support/private
    cd ${srcdir}/$_pkgfqn/src/qt3support/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/Qt3Support/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtCore/private
    cd ${srcdir}/$_pkgfqn/src/corelib/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtCore/private/ \; &> /dev/null
    
    install -d ${pkgdir}/usr/include/QtDBus/private
    cd ${srcdir}/$_pkgfqn/src/dbus/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtDBus/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtDeclarative/private
    cd ${srcdir}/$_pkgfqn/src/declarative/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtDeclarative/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtDesigner/private
    cd ${srcdir}/$_pkgfqn/tools/designer/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtDesigner/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtGui/private
    cd ${srcdir}/$_pkgfqn/src/gui/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtGui/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtHelp/private
    cd ${srcdir}/$_pkgfqn/tools/assistant/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtHelp/private/ \; &> /dev/null
    
    install -d ${pkgdir}/usr/include/QtMultimedia/private
    cd ${srcdir}/$_pkgfqn/src/multimedia/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtMultimedia/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtNetwork/private
    cd ${srcdir}/$_pkgfqn/src/network/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtNetwork/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtOpenGL/private
    cd ${srcdir}/$_pkgfqn/src/opengl/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtOpenGL/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtScript/private
    cd ${srcdir}/$_pkgfqn/src/script/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtScript/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtScriptTools/private
    cd ${srcdir}/$_pkgfqn/src/scripttools/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtScriptTools/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtSql/private
    cd ${srcdir}/$_pkgfqn/src/sql/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtSql/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtSvg/private
    cd ${srcdir}/$_pkgfqn/src/svg/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtSvg/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtTest/private
    cd ${srcdir}/$_pkgfqn/src/testlib/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtTest/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtUiTools/private
    cd ${srcdir}/$_pkgfqn/tools/designer/src/uitools
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtUiTools/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtWebKit/private
    cd ${srcdir}/$_pkgfqn/src/3rdparty/webkit
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtWebKit/private/ \; &> /dev/null

    install -d ${pkgdir}/usr/include/QtXmlPatterns/private
    cd ${srcdir}/$_pkgfqn/src/xmlpatterns/
    find . -name "*_p.h" -exec install -m644 {} \
      ${pkgdir}/usr/include/QtXmlPatterns/private/ \; &> /dev/null
}
