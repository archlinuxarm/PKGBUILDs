# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use --disable-jit to solve segfaults
#  - use --disable-gtk-doc to solve html docs breaking the build

pkgbase=webkitgtk
pkgname=(webkitgtk webkitgtk2)
pkgver=2.4.3
pkgrel=2
pkgdesc="GTK+ Web content engine library"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue2 gst-plugins-base-libs
         libsecret libwebp harfbuzz-icu)
makedepends=(gtk3 gtk2 gperf gobject-introspection python2 mesa ruby gtk-doc)
optdepends=('gst-plugins-base: free media decoding'
            'gst-plugins-good: media decoding'
            'gst-libav: nonfree media decoding')
options=(!emptydirs)
source=(http://webkitgtk.org/releases/$pkgbase-${pkgver}.tar.xz
        fix-pretty-quotes.patch
        webkitgtk-2.4.3-angle_symbol_table.patch)
sha256sums=('9862c3eac1d12cc6e52cc39060a1284163950b304d534529a0dbcc5958a8ceac'
            '56316228bbbf0b7ebcbe210a35120f4e72cb9c1b680dd82cc2bde0f4549245e6'
            'a2c0b96a1671d43c18baf3cbe53c84738181aa08c3ac448c423b50722953a50b')

prepare() {
  mkdir build-gtk{,2} path

  ln -s /usr/bin/python2 path/python

  cd $pkgbase-$pkgver
  patch -Np0 -i ../fix-pretty-quotes.patch
  patch -Np1 -i ../webkitgtk-2.4.3-angle_symbol_table.patch
}

_build() (
  _ver="$1"; shift
  cd build-${_ver}

  ../$pkgbase-$pkgver/configure --prefix=/usr \
    --libexecdir=/usr/lib/webkit${_ver} \
    --enable-introspection --disable-jit \
    "$@"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
)

build() {
  PATH="$srcdir/path:$PATH"

  _build gtk --disable-gtk-doc
  _build gtk2 --disable-webkit2 --with-gtk=2.0
}

package_webkitgtk() {
  depends+=(gtk3)
  optdepends+=('gtk2: Netscape plugin support')
  provides=("webkitgtk3=${pkgver}" "libwebkit3=${pkgver}")
  conflicts=(webkitgtk3 libwebkit3)
  replaces=(webkitgtk3 libwebkit3)

  make -C build-gtk -j1 DESTDIR="$pkgdir" install
  install -Dm644 $pkgbase-$pkgver/Source/WebKit/LICENSE \
    "$pkgdir/usr/share/licenses/webkitgtk/LICENSE"
}

package_webkitgtk2() {
  pkgdesc+=" for GTK2"
  depends+=(gtk2)
  provides=("libwebkit=${pkgver}")
  conflicts=(libwebkit)
  replaces=(libwebkit)

  make -C build-gtk2 -j1 DESTDIR="$pkgdir" install
  install -Dm644 $pkgbase-$pkgver/Source/WebKit/LICENSE \
    "$pkgdir/usr/share/licenses/webkitgtk2/LICENSE"
}
