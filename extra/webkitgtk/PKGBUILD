# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - re-enabled --disable-silent-rules, needed for v5 and v6 to build
#  - use --disable-jit for v5, uses incompatible ARM assembly

pkgname=webkitgtk
pkgver=2.2.4
pkgrel=1
pkgdesc="GTK+ Web content engine library"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue gtk3 gst-plugins-base-libs
         libsecret libwebp harfbuzz-icu)
makedepends=(gtk2 gperf gobject-introspection python mesa ruby gtk-doc)
optdepends=('gtk2: Netscape plugin support')
provides=("webkitgtk3=${pkgver}" "libwebkit3=${pkgver}")
conflicts=(webkitgtk3 libwebkit3)
replaces=(webkitgtk3 libwebkit3)
options=(!libtool !emptydirs)
source=(http://webkitgtk.org/releases/$pkgname-${pkgver}.tar.xz)
sha256sums=('5e1aee1bfca3602cb02920d6984c584c775c661b30c33955e35ff9a21b4c4d74')

build() {
  cd $pkgname-$pkgver

  [[ $CARCH == 'arm' ]] && CONFIG='--disable-jit'

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --enable-introspection --disable-silent-rules $CONFIG

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
