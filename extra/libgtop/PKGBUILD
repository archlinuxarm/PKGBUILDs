# $Id: PKGBUILD 41842 2009-06-07 13:39:03Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgtop
pkgver=2.26.1
pkgrel=1
pkgdesc="A library that read information about processes and the running system"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2>=2.20.1' 'libxau' 'texinfo')
makedepends=('intltool')
install=libgtop.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool')
url="http://www.gnome.org/"
md5sums=('49fe5c91a6bbc103a0a05c6854a6cc11')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --with-libgtop-smp --with-libgtop-inodedb \
	      --with-linux-table || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -f "${pkgdir}/usr/share/info/dir"
  gzip -9nf ${pkgdir}/usr/share/info/*
}
