# $Id: PKGBUILD 44550 2009-07-02 17:50:32Z jgc $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=seamonkey
pkgver=1.1.17
pkgrel=1
pkgdesc="SeaMonkey internet suite"
arch=(i686 x86_64)
license=('MPL')
depends=('gtk2>=2.16.4' 'libidl2>=0.8.13' 'mozilla-common>=1.3' 'nss>=3.12.3' 'libxt' 'gcc-libs>=4.4.0')
makedepends=('zip' 'pkgconfig' 'diffutils' 'gcc' 'libgnomeui>=2.24.1')
replaces=('mozilla')
url="http://www.mozilla.org/projects/seamonkey"
source=(ftp://ftp.mozilla.org/pub/mozilla.org/seamonkey/releases/${pkgver}/seamonkey-${pkgver}.source.tar.bz2
        mozconfig 
        seamonkey.desktop
        firefox-2.0-link-layout.patch)
md5sums=('9eba600ba6ca522b8eada9a0a8e890e2'
         '195fb49e6e39b6ca6acee0c35aec1c13'
         '448848e65efcbf1b2be2873b66b6cd48'
         'b933c00957ea793fe940f4d46a85e10e')

build() {
  cd "${srcdir}/mozilla"
  patch -Np1 -i "${srcdir}/firefox-2.0-link-layout.patch" || return 1
  cp ${srcdir}/mozconfig .mozconfig

  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/seamonkey-${pkgver}"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}" || return 1
  make -j1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 "${srcdir}/mozilla/suite/branding/icons/gtk/seamonkey.png" \
                "${pkgdir}/usr/share/pixmaps/" || return 1
  install -m644 "${srcdir}/seamonkey.desktop" "${pkgdir}/usr/share/applications/" || return 1

  rm -f ${pkgdir}/usr/lib/pkgconfig/seamonkey-ns{s,pr}.pc
}
