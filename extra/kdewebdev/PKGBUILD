# $Id: PKGBUILD 54220 2009-10-07 10:23:11Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdewebdev
pkgname=('kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-klinkstatus'
         'kdewebdev-kommander'
         'kdewebdev-kxsldbg')
pkgver=4.3.2
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdewebdev')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ruby' 'kdepimlibs' 'kdebase-runtime' 'tidyhtml')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('23af90f4d5977531ca8eca5f297d54b9')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdewebdev-kfilereplace() {
	pkgdesc='Search & Replace Tool'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdewebdev.install'
	cd $srcdir/build/kfilereplace
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfilereplace
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kimagemapeditor() {
	pkgdesc='HTML Image Map Editor'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdewebdev.install'
	cd $srcdir/build/kimagemapeditor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kimagemapeditor
	make DESTDIR=$pkgdir install
}

package_kdewebdev-klinkstatus() {
	pkgdesc='Link Checker'
	depends=('kdebase-runtime' 'kdepimlibs' 'tidyhtml')
	install='kdewebdev.install'
	cd $srcdir/build/klinkstatus
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/klinkstatus
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kommander() {
	pkgdesc='Executor for Kommander dialogs'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/kommander
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kxsldbg() {
	pkgdesc='XSLT Debugger'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdewebdev.install'
	cd $srcdir/build/kxsldbg
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kxsldbg
	make DESTDIR=$pkgdir install
}
