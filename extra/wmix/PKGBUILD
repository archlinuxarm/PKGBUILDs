# $Id: PKGBUILD 45361 2009-07-10 04:19:31Z allan $
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Kevin Piche <kevin@archlinux.org>

pkgname=wmix
pkgver=3.1
pkgrel=4
pkgdesc="Dockapp mixer for OSS or ALSA"
arch=('i686' 'x86_64')
url="http://www.ne.jp/asahi/linux/timecop/"
license=('GPL')
depends=('libxpm')
source=(http://www.ne.jp/asahi/linux/timecop/software/$pkgname-$pkgver.tar.gz
        wmix-fix-free.patch)
md5sums=('62f6e86f7558f193e081dc29444a6699'
         '07b9b42c87cbe9531c6de5eb2e8596b0')

build() {
  cd $srcdir/$pkgname-$pkgver
  install -dm755 $pkgdir/usr/bin
  # Fix version.
  sed -e '/^#define VERSION/ s/3\.0/3.1/' -e 's/wmix 3\.0/wmix 3.1/' -i wmix.c || return 1
  # Use our cflags
  sed -i "s/-O3/$CFLAGS/" Makefile
  # Fix crash (FS#15422)
  patch -Np0 -i $srcdir/wmix-fix-free.patch
  make || return 1
  make PREFIX=$pkgdir/usr install || return 1
  install -D -m644 wmix.1x.gz $pkgdir/usr/share/man/man1/wmix.1x.gz || return 1
}
