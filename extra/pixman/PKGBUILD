# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable x86 extensions (mmx, sse2, ssse3)

pkgname=pixman
pkgver=0.42.2
pkgrel=1.1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64)
url="https://cgit.freedesktop.org/pixman/"
license=('custom')
depends=('glibc')
makedepends=('meson' 'libpng')
options=('debug')
provides=('libpixman-1.so')
source=(https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz)
sha512sums=('3476e2676e66756b1af61b1e532cd80c985c191fb7956eb01702b419726cce99e79163b7f287f74f66414680e7396d13c3fee525cd663f12b6ac4877070ff4e8')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  if [ "${CARCH}" == aarch64 ]; then
    arch-meson $pkgbase-$pkgver build \
      -D loongson-mmi=disabled \
      -D vmx=disabled \
      -D arm-simd=disabled \
      -D neon=disabled \
      -D iwmmxt=disabled \
      -D mmx=disabled \
      -D sse2=disabled \
      -D ssse3=disabled \
      -D mips-dspr2=disabled \
      -D gtk=disabled
  else
    arch-meson $pkgbase-$pkgver build \
      -D loongson-mmi=disabled \
      -D vmx=disabled \
      -D a64-neon=disabled \
      -D mmx=disabled \
      -D sse2=disabled \
      -D ssse3=disabled \
      -D mips-dspr2=disabled \
      -D gtk=disabled
  fi

  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}
