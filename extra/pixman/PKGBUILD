# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable x86 extensions (mmx, sse2, ssse3)

pkgname=pixman
pkgver=0.43.0
pkgrel=2
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64)
url="https://cgit.freedesktop.org/pixman/"
license=('MIT')
depends=('glibc')
makedepends=('meson' 'libpng')
provides=('libpixman-1.so')
source=(https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz
        0001-Drop-automatic-DEBUG-define.patch)
sha512sums=('a90399b8c6aec218abb2f419fb5d6894acf9f4c7acb4fd3893b0b7c805ba47c82ee7efb363be59bb1a15b6997b2dddb7dba062a165503b035e1124fff1b271c9'
            '6d3f81f5d48f19d54db5ad37f911dba4c6c928bc1b5744b81c3aed10ba68166f2b240329e4a4164a75664f2ff76030697ba22e603197e5fe270ee7a3757f9fe4')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

prepare() {
  cd $pkgname-$pkgver
  # disable DEBUG
  # https://gitlab.archlinux.org/archlinux/packaging/packages/pixman/-/issues/1
  # https://gitlab.freedesktop.org/pixman/pixman/-/merge_requests/91
  patch -Np1 -i ../0001-Drop-automatic-DEBUG-define.patch
}

build() {
  arch-meson $pkgname-$pkgver build \
    -D loongson-mmi=disabled \
    -D vmx=disabled \
    -D arm-simd=disabled \
    -D neon=disabled \
    -D a64-neon=disabled \
    -D iwmmxt=disabled \
    -D mmx=disabled \
    -D sse2=disabled \
    -D ssse3=disabled \
    -D mips-dspr2=disabled \
    -D gtk=disabled
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}
