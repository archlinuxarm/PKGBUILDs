# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=seahorse-plugins
pkgver=2.26.2
pkgrel=1
pkgdesc="Seahorse plugins"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-panel>=2.26.2' 'seahorse>=2.26.2' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils>=0.16.0' 'pkgconfig' 'gedit>=2.26.2' 'nautilus>=2.26.2' 'epiphany>=2.26.2')
groups=('gnome-extra')
url="http://www.gnome.org"
options=(!libtool !emptydirs)
install=seahorse-plugins.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('36b51b866fc729526f1be86bb0db7370')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper \
	      --disable-update-mime-database \
	      --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain seahorse-plugins ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
