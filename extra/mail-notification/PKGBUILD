# $Id: PKGBUILD 36284 2009-04-20 21:34:26Z jgc $
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=mail-notification
pkgver=5.4
pkgrel=2
pkgdesc="Tray icon application that informs you if you have new mail"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/mailnotify/"
license=('GPL3' 'FDL')
depends=('cyrus-sasl-plugins' 'gmime>=2.2.21' 'libnotify>=0.4.5' 'gnome-keyring>=2.26.1' 'hicolor-icon-theme' 'notification-daemon' 'libgnomeui>=2.24.1')
makedepends=('gob2' 'intltool' 'evolution>=2.26.1.1' 'gnome-doc-utils>=0.16.0')
options=('!libtool' '!emptydirs')
install=mail-notification.install
source=(http://savannah.nongnu.org/download/mailnotify/${pkgname}-${pkgver}.tar.bz2
        dont-update-cache.patch
        remove-ubuntu-special-case.patch)
md5sums=('c8dc33a61251acb5474e56eab6b18f43'
         '6007bc30e789dab0a8282038e0335eb9'
         '9cadd61bbd9c324b2916ec980231e0f2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/dont-update-cache.patch" || return 1
  patch -Np0 -i "${srcdir}/remove-ubuntu-special-case.patch" || return 1

  ./jb configure prefix=/usr sysconfdir=/etc \
              localstatedir=/var destdir="${pkgdir}" \
              gconf-schemas-dir=/etc/gconf/schemas install-gconf-schemas=yes \
              cflags="${CFLAGS}" cppflags="${CXXFLAGS}" ldflags="${LDFLAGS}" \
              library-mode=0755 || return 1
  ./jb build || return 1
  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 ./jb install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain mail-notification ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
} 
