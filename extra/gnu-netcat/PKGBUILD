# Maintainer: Tom K <tomk@runbox.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for ARM fix (http://sourceforge.net/p/netcat/bugs/23/)

pkgname=gnu-netcat
pkgver=0.7.1
pkgrel=10
pkgdesc="GNU rewrite of netcat, the network piping application"
depends=(
  'glibc'
)
makedepends=(
  'texinfo'
)
arch=('x86_64')
url="http://netcat.sourceforge.net/"
license=('GPL-2.0-only')
replaces=('netcat')
provides=('netcat')
source=("https://downloads.sourceforge.net/sourceforge/netcat/netcat-$pkgver.tar.bz2"
        'gnu-netcat-flagcount.patch')
sha256sums=('b55af0bbdf5acc02d1eb6ab18da2acd77a400bafd074489003f3df09676332bb'
            '63ffd690c586b164ec2f80723f5bcc46d009ffd5e0dd78bbe56fd1b770fd0788')

prepare() {
  cd "${srcdir}/netcat-${pkgver}"
  patch -p1 -i ../gnu-netcat-flagcount.patch
}

build() {
  cd "${srcdir}/netcat-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd "${srcdir}/netcat-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
