# $Id$
# Maintainer: Tom K <tomk@runbox.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for ARM fix (http://sourceforge.net/p/netcat/bugs/23/)

pkgname=gnu-netcat
pkgver=0.7.1
pkgrel=5.1
pkgdesc="GNU rewrite of netcat, the network piping application"
depends=('glibc' 'texinfo')
arch=('i686' 'x86_64')
url="http://netcat.sourceforge.net/"
license=('GPL')
replaces=('netcat')
provides=('netcat')
install=gnu-netcat.install
source=("http://downloads.sourceforge.net/sourceforge/netcat/netcat-$pkgver.tar.bz2"
        'gnu-netcat-flagcount.patch')
md5sums=('0a29eff1736ddb5effd0b1ec1f6fe0ef'
         'fccdd10bf84b18ba4e1d9ef2677e038c')

prepare() {
  cd "${srcdir}/netcat-${pkgver}"
  patch -p1 -i ../gnu-netcat-flagcount.patch
}

build() {
  cd "${srcdir}/netcat-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd "${srcdir}/netcat-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
