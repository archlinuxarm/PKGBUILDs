# $Id: PKGBUILD 49912 2009-08-16 21:11:14Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=scribus
pkgver=1.3.5.1
pkgrel=1
pkgdesc="A desktop publishing program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.scribus.net"
install=scribus.install
depends=('libcups>=1.3.11' 'lcms>=1.18' 'qt>=4.5.2' 'ghostscript>=8.70' \
         'libart-lgpl>=2.3.20' 'python>=2.6.2' 'libxml2>=2.7.3' 'cairo' \
         'desktop-file-utils' 'shared-mime-info')
makedepends=('cmake')
options=(!libtool !makeflags)
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('4cadca0c780491ad61dc2d815776f10b')

build() {
  cd ${srcdir}/scribus-${pkgver}
  cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_SKIP_RPATH=ON \
          -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--as-needed' \
	  -DWANT_CAIRO=1 -DLIB_SUFFIX="" 
  make || return 1
  make DESTDIR=${pkgdir} install

  # fix icon path in .desktop file
  sed -i 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' scribus.desktop

  install -Dm644 scribus.desktop \
    ${pkgdir}/usr/share/applications/scribus.desktop
}
