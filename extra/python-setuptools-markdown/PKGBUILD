# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed dependency on python-pypandoc

pkgname=python-setuptools-markdown
pkgver=0.4.1
_commit=34ed6276e0c8bcefcd967bd8db138546a270bdf4
pkgrel=9
pkgdesc="Use Markdown for your project description"
arch=('any')
url="https://github.com/msabramo/setuptools-markdown"
license=('MIT')
depends=('python-setuptools')
source=("$pkgname-$_commit.tar.gz::https://github.com/msabramo/setuptools-markdown/archive/$_commit.tar.gz"
        $pkgname-pypandoc-1.8.patch::https://github.com/msabramo/setuptools-markdown/pull/21.patch)
sha512sums=('394bf37e53bcdb702f44749eb5bfa156aa11df310e603a034af38d8ca47d8f5231b76eacec6049cf3c7615031eca7b4238670555fcf9835723beb6f3b7f9496c'
            '0da7fd152ca93dc8d5b852dedc60e10a825af5adaa64afd490e3e0674431d67ad456305464db3de0e5581ecb883da59dd91b8af575d03d9dff2f3e681cc30914')

prepare() {
  mv setuptools-markdown-{$_commit,$pkgver}
  patch -d setuptools-markdown-$pkgver -p1 < $pkgname-pypandoc-1.8.patch
}

build() {
  cd setuptools-markdown-$pkgver
  python setup.py build
}

package() {
  cd setuptools-markdown-$pkgver
  python setup.py install --root "$pkgdir"
}
