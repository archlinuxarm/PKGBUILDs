# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --enable-arm-neon=no to configure for !aarch64

pkgname=libpng
pkgver=1.6.21
_apngver=1.6.21
pkgrel=1.1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz)
md5sums=('3bacb4728f6694a64ad9052769d6a4ce'
         'SKIP'
         '360346394543ec0a880f61830541608f')
validpgpkeys=('8048643BA2C840F4F92A195FF54984BFA16C640F')

prepare() {
  cd $pkgname-$pkgver

  # Add animated PNG (apng) support. Required by Firefox
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np1 -i ../libpng-$_apngver-apng.patch
}

build() {
  cd $pkgname-$pkgver

  [[ $CARCH != "aarch64" ]] && CONFIG="--enable-arm-neon=no"
  ./configure --prefix=/usr --disable-static $CONFIG
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -D -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
