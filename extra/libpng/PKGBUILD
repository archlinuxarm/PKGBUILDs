# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --enable-arm-neon=no to configure for !aarch64

pkgname=libpng
pkgver=1.6.29
_apngver=1.6.28
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
validpgpkeys=('8048643BA2C840F4F92A195FF54984BFA16C640F') # Glenn Randers-Pehrson (mozilla) <glennrp@gmail.com>
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz)
sha256sums=('4245b684e8fe829ebb76186327bb37ce5a639938b219882b53d64bd3cfc5f239'
            'SKIP'
            '4b294933e3c875d0e3331d286840b859b8a70dc338f1fefbe803a464c1d9b654')

prepare() {
  cd $pkgname-$pkgver

  # Add animated PNG (apng) support. Required by Firefox
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np1 -i ../libpng-$_apngver-apng.patch
}

build() {
  cd $pkgname-$pkgver

  [[ $CARCH != "aarch64" ]] && CONFIG="--enable-arm-neon=no"
  ./configure --prefix=/usr --disable-static $CONFIG
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -D -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
