# $Id: PKGBUILD 42059 2009-06-10 04:40:46Z allan $
# Maintainer: Thomas Baechler <thomas@archlinux.org>
pkgname="v86d"
pkgver=0.1.9
pkgrel=7
pkgdesc="userspace helper for uvesafb that runs x86 code in an emulated environment"
url="http://dev.gentoo.org/~spock/projects/uvesafb/"
license=('GPL2')
arch=("i686" "x86_64")
# The kernel is only required for building
# There is no kernel-specific dependency
makedepends=('kernel26>=2.6.29' 'kernel26<2.6.30')
_kernver="2.6.29-ARCH"
depends=('glibc' 'klibc' $(basename /lib/klibc-*.so .so))
backup=(etc/modprobe.d/uvesafb.conf)
source=(http://dev.gentoo.org/~spock/projects/uvesafb/archive/v86d-$pkgver.tar.bz2
        v86d
        v86d_hook
        modprobe.uvesafb)
md5sums=('ebbbc8e7013c9544b6ba6981add43831'
         'ef0a9f2235cc3c00eb17457fc7eb6550'
         '813958b8f6c75a7063e4858f31509694'
         '2d7cc8dc6a41916a13869212d0191147')
options=(!makeflags)

build() {
  cd $srcdir/v86d-$pkgver

  ./configure --with-klibc --with-x86emu
  make KDIR=/lib/modules/${_kernver}/build || return 1
  make DESTDIR=$pkgdir install || return 1
  mv $pkgdir/sbin/v86d $pkgdir/sbin/v86d_klibc

  make clean

  ./configure --with-x86emu
  make KDIR=/lib/modules/${_kernver}/build || return 1
  make DESTDIR=$pkgdir install || return 1

  install -D -m644 $srcdir/v86d $pkgdir/lib/initcpio/install/v86d || return 1
  install -D -m644 $srcdir/v86d_hook $pkgdir/lib/initcpio/hooks/v86d || return 1
  install -D -m644 $srcdir/modprobe.uvesafb $pkgdir/etc/modprobe.d/uvesafb.conf || return 1
}
