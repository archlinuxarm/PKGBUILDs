# $Id: PKGBUILD 158023 2012-05-01 07:24:53Z andrea $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix ARM FTBFS

pkgname=qtscriptgenerator
pkgver=0.2.0
pkgrel=3
pkgdesc="A tool that generates Qt bindings for Qt Script"
arch=("i686" "x86_64")
url="https://code.google.com/p/qtscriptgenerator/"
license=('GPL')
depends=('qtwebkit')
makedepends=('pkgconfig' 'mesa')
source=("https://qtscriptgenerator.googlecode.com/files/${pkgname}-src-${pkgver}.tar.gz"
        'phonon.patch'
        'no-phonon.patch'
        'upstream_arm_fix_ftbfs.diff')
sha1sums=('4c1078f26b196156e857c17c9d11a66cfea66f00'
          'cca1cd546628a10a91475a82fe12ad2e4066a7d0'
          'c50b26e6504f5c981a916860d9c3929e436e1067'
          '049b21e33144b738cd1a62315d251f8e45643804')

build() {
  cd "${srcdir}"/$pkgname-src-$pkgver

  patch -p1 -i "${srcdir}"/phonon.patch
  patch -p1 -i "${srcdir}"/no-phonon.patch
  patch -p1 -i "${srcdir}"/upstream_arm_fix_ftbfs.diff

  # prepare plugins
  cd generator
  qmake-qt4
  make
  QTDIR=/usr ./generator --include-paths=/usr/include/qt4

  # build plugins
  cd ../qtbindings
  qmake-qt4
  make
}

package() {
  cd "${srcdir}"/$pkgname-src-$pkgver/qtbindings
  # move plugins to correct directory
  install -d "${pkgdir}"/usr/lib/qt4/plugins/script
  cp -a "${srcdir}"/$pkgname-src-$pkgver/plugins/script/* \
    "${pkgdir}"/usr/lib/qt4/plugins/script/
}
