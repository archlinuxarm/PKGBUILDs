# $Id: PKGBUILD 34428 2009-04-09 10:43:43Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=qtscriptgenerator
pkgver=0.1.0
pkgrel=1
pkgdesc="Script generator for qt"
arch=("i686" "x86_64")
url="http://code.google.com/p/qtscriptgenerator/"
license=('GPL')
depends=('qt>=4.5.0-4' 'phonon')
makedepends=('pkgconfig')
source=(http://qtscriptgenerator.googlecode.com/files/qtscriptgenerator-src-0.1.0.tar.gz
        phonon.patch)
build() {
  cd $srcdir/$pkgname-src-$pkgver/generator
  # prepare plugins
  patch -Np2 -i $srcdir/phonon.patch || return 1
  qmake || return 1
  make || return 1
  ./generator --include-paths=/usr/include || return 1
  # build plugins
  cd "$srcdir/$pkgname-src-$pkgver/qtbindings"
  qmake || return 1
  make || return 1
  # move plugins to correct directory
  mkdir -p $pkgdir/usr/lib/qt/plugins/script
  mv $srcdir/$pkgname-src-$pkgver/plugins/script/* $pkgdir/usr/lib/qt/plugins/script/
}
md5sums=('ca4046ad4bda36cd4e21649d4b98886d'
         'c0266892ecc645b3407f2203ee11a808')
