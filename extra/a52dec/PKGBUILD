# $Id: PKGBUILD 5601 2008-07-18 22:06:17Z jgc $
# Maintainer: Thomas Baechler <thomas@archlinux.org>
# contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=a52dec
pkgver=0.7.4
pkgrel=4
pkgdesc="liba52 is a free library for decoding ATSC A/52 streams."
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc')
options=(!libtool)
source=(http://liba52.sourceforge.net/files/${pkgname}-${pkgver}.tar.gz
	a52dec-0.7.4-build.patch)
url="http://liba52.sourceforge.net/"
md5sums=('caa9f5bc44232dc8aeea773fea56be80' 'fa16f224a7dceb7613824380abef0052')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/a52dec-0.7.4-build.patch || return 1
  ./bootstrap || return 1
  ./configure --prefix=/usr --enable-shared || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m644 liba52/a52_internal.h $startdir/pkg/usr/include/a52dec/ || return 1
}
