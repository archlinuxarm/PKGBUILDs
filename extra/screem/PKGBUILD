# $Id: PKGBUILD 40227 2009-05-28 22:39:45Z jgc $
# Maintainer: Roberto Carvajal <roberto@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=screem
pkgver=0.16.1
pkgrel=7
pkgdesc="A source oriented web site development environment for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://www.screem.org"
depends=('gtksourceview>=1.8.5-2' 'libgtkhtml>=2.11.1' 'libgnomeui>=2.24.1' 'libcroco>=0.6.2' 'libgnomeprintui>=2.18.4' 'startup-notification>=0.10' 'enchant>=1.4.2' 'gnome-menus>=2.26.1' 'desktop-file-utils')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils')
options=('!libtool')
install=screem.install
source=(http://downloads.sourceforge.net/sourceforge/screem/screem-${pkgver}.tar.gz
	goption.patch
	dbus-dontclose.patch)
md5sums=('88bfc0afadb905ddbed9bdfbc869602a'
	 'd8f855a89a6f1479085cd88956d773c1'
	 '2a26231fbab056165d84bad03dfb3edd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/goption.patch" || return 1
  patch -Np1 -i "${srcdir}/dbus-dontclose.patch" || return 1

  sed -e 's/-DGNOME_DISABLE_DEPRECATED//g' \
      -e 's/-DGNOMEUI_DISABLE_DEPRECATED//g' \
      -e 's/-DGTK_DISABLE_DEPRECATED//g' \
      -i configure || return 1

  ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var \
	--disable-update-mime \
	--disable-update-desktop \
	--enable-dbus \
	--enable-enchant \
	--disable-schemas-install || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
   DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain screem ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
