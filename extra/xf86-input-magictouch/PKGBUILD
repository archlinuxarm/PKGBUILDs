# $Id: PKGBUILD 36724 2009-04-27 01:19:22Z eric $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-magictouch
pkgver=1.0.0.5
pkgrel=6
pkgdesc="X.org magictouch input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xorg-server>=1.1.1' 'xorg-util-macros')
groups=('xorg-input-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 \
        git-7f90f6a6.patch git-6dca5f57.patch git-c616070c.patch \
        git-2a6abb40.patch git-bbd27423.patch git-0ab3a572.patch LICENSE)
md5sums=('d23f2791cd634ef85b7cc5e9da8f8407' 'fcdf04ff2bea4f5dba4f1d2be77872ef'\
         '927c19be07b2de21efdb896074a6996d' 'af1f19626b18994b5b9bbb98b1029eb3'\
         '5c3b5e20dbb6b201e58b4c38c09e4885' '6d1553a3febd5238b60ce322cadc2230'\
         'f97a8f43d9975bdc6afb7b339600dcad' '6db298bec5113f63f5619567bcda9b56')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-0ab3a572.patch" || return 1
  patch -Np1 -i "${srcdir}/git-6dca5f57.patch" || return 1
  patch -Np1 -i "${srcdir}/git-bbd27423.patch" || return 1
  patch -Np1 -i "${srcdir}/git-c616070c.patch" || return 1
  patch -Np1 -i "${srcdir}/git-7f90f6a6.patch" || return 1
  patch -Np1 -i "${srcdir}/git-2a6abb40.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
