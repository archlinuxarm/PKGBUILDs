# $Id: PKGBUILD 53039 2009-09-25 10:29:53Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20090914
pkgrel=2
pkgdesc="An outline and bitmap font editor"
arch=('i686' 'x86_64')
url="http://fontforge.sourceforge.net"
license=('BSD')
depends=('libxkbui' 'libxi')
makedepends=('freetype2' 'libxml2' 'pango')
options=('!libtool' '!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/fontforge/fontforge_full-${pkgver}.tar.bz2)
md5sums=('16a744e42e8944c4298f7e004f95dd31')
sha1sums=('040eaca510980870e984719d3b77e98131b3bce3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-type3 --enable-double \
    --enable-devicetables --without-python || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
