# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.13.1
pkgrel=1
pkgdesc="Platform for audio synthesis and algorithmic composition"
arch=(x86_64)
url="https://supercollider.github.io"
license=(
  GPL-2.0-or-later
  GPL-3.0-or-later
)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libx11
  qt5-base
  qt5-svg
  qt5-webengine
  qt5-websockets
)
makedepends=(
  abletonlink
  alsa-lib
  avahi
  boost
  cmake
  emacs
  fftw
  glib2-devel
  libsndfile
  qt5-tools
  readline
  systemd-libs
  yaml-cpp
)
checkdepends=(xorg-server-xvfb)
optdepends=(
  'emacs: emacs interface'
  'gedit: gedit interface'
  'sc3-plugins: additional extension plugins for scsynth'
)
provides=(libscsynth.so "sclang=$pkgver" "scsynth=$pkgver")
source=(
    https://github.com/$pkgname/$pkgname/releases/download/Version-$pkgver/$_name-$pkgver-Source.tar.bz2{,.asc}
    boost-1.84.patch
    boost-1.85.patch
    boost-1.87-1.patch
    boost-1.87-2.patch
)
install=$pkgname.install
sha512sums=('a8d56ae1a28ce9a4559d7b6c4602c4aa97a28e3e3ac8c71a9b6f96bcd1b2b26ad87dfce9e9a832008b54f80e66fbe24b88562f255e6c845e1073b2204260cdf1'
            'SKIP'
            '1b530d60570729a0b4dd13fbb9059b45288d4ff71391db6290b5e686985ad749267252c15eb63c81bf7bc01ca1141c163e3c7e30e6de1fc43c7b26ed66ab20cf'
            'aabf9c44d5953adff7ee3f38d8d41b3b7dc2bdd4a498fb55df0aef66786f820ce5436accff44c7498743fe3ebbcc75549ec00769fe8269ea1f556419a93c16d9'
            'f470e449d9f918bc02936b588af01031fa4d986b82ec4da3468dd7844e9cb90cf62e513187fa8a9f8bf8b1447d3a4d57d804b47ab472be2efcadc13464773fb6'
            'bc472f99352670399e08cf29c2c17bd1feb791f6830d7e855d3ef63c3daa4a7d134d9001145b97fe3d0a33767d68fe32f7cd0c5b418a4b4f12e4f40c26844c70')
b2sums=('55bff1334171e8efdfdf29ea41f36b832fa013aca0f14710c72f96faef9b5b29b78408a79cca378124ddd417b0102fff745971cd7e3be458f2d33ec8932c95af'
        'SKIP'
        '3c333d4b0b63d2c67167be6abe954a843cdd4c73b3cc28a9d7e011162dbb3ddf475ad75f03a780abb7cbf8a54ee0b308e02a7a752532f5f1c110a1e3d8152753'
        '42b76de099a42643e107f8152fd6ba050771d938d89885c5e46d4e07d3e94d03c57cb4eefa0cd38c52f7cbfbf96fefa0101532cd64cd1efce8e2e76604ab99f0'
        'c7dd692ab7b463bffa785b83e55d73a57704278709366514406523e8831355a82a0e5cf2fff11f121f68fa7b233d75ed1a0edf2f4af629123d5bff8527111370'
        'fe1a0363b6f064dece2bb144fce82f5aa01f53e9ce49afa99494f9705a0de0265b321a56d7a909371f2ede38c684e24ac2c3b21f95e25d8405c51be2e7f5fb68')
validpgpkeys=('2E1C4FC2F6BB58FA157B21B8064B501EB7405F04') # Marcin PÄ…czkowski (dyfer)

prepare() {
  cd $_name-$pkgver-Source
# Patches from Gentoo
  patch -p1 -i ../boost-1.84.patch
  patch -p1 -i ../boost-1.85.patch
  patch -p1 -i ../boost-1.87-1.patch
  patch -p1 -i ../boost-1.87-2.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D Boost_NO_BOOST_CMAKE=ON
    -D LIBSCSYNTH=ON
    -D FORTIFY=ON
    -D SYSTEM_ABLETON_LINK=ON
    -D SYSTEM_BOOST=ON
    -D SYSTEM_YAMLCPP=ON
    -D SC_VIM=OFF
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5
    -S $_name-$pkgver-Source
    -W no-dev
    -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF
    -DCMAKE_C_FLAGS="${CFLAGS}"
  )
  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  xvfb-run make test ARGS="-VV -j1" -C build
}

package() {
  depends+=(
    alsa-lib libasound.so
    avahi libavahi-common.so libavahi-client.so
    boost-libs libboost_{program_options,thread}.so
    fftw libfftw3f.so
    jack libjack.so
    libsndfile libsndfile.so
    readline libreadline.so
    systemd-libs libudev.so
    yaml-cpp libyaml-cpp.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver-Source/{AUTHORS,{CHANGELOG,README,README_LINUX}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
