# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.14.0
pkgrel=3
pkgdesc="Platform for audio synthesis and algorithmic composition"
arch=(x86_64)
url="https://supercollider.github.io"
license=(
  GPL-2.0-or-later
  GPL-3.0-or-later
)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libx11
  qt6-base
  qt6-svg
  qt6-webchannel
  qt6-webengine
  qt6-websockets
)
makedepends=(
  abletonlink
  alsa-lib
  avahi
  boost
  cmake
  emacs
  fftw
  glib2-devel
  libsndfile
  qt6-tools
  readline
  systemd-libs
  yaml-cpp
)
checkdepends=(xorg-server-xvfb)
optdepends=(
  'sc3-plugins: additional extension plugins for scsynth'
)
provides=(libscsynth.so "sclang=$pkgver" "scsynth=$pkgver")
source=(
    https://github.com/$pkgname/$pkgname/releases/download/Version-$pkgver/$_name-$pkgver-Source.tar.bz2{,.asc}
    $pkgname-3.14.0-boost-1.89.patch
)
sha512sums=('79dab27ed0787ab8ae1eeabf4baa87d360a91a41b256669be6b39f651d3cb2d33225d9c556348afbbead3b85c4f9640f9af6e6a323e4742bd22d9ea7668f836d'
            'SKIP'
            'd99ddb8c430dd96d985428e89fe7b5ddb724c9488538dd306ef3ac035e22cac53329c530fdedaff0e249d20d8a5f70b1c14d90e9dec7ce050de78eecced02527')
b2sums=('50c9829fcd17ceb97f2e53d9422318b413b93ae65a02a722a03cbe405c6fa4a1ea058e527f7c4e7b800f1a544710ba31b8102b48324778505d67dd2a8bb9d1ea'
        'SKIP'
        'ff370171fe9555486b98c1390051878d0da2917a86f9dea3ac1a61d9fcba041cd484cef5b236ffa29a1f26bd8bc5e54338ed3038a889da71a7f6a7f1e8e9fdef')
validpgpkeys=('2E1C4FC2F6BB58FA157B21B8064B501EB7405F04') # Marcin PÄ…czkowski (dyfer)

prepare() {
  # backport of https://github.com/supercollider/supercollider/commit/ee3ce7319fb61676454199cfa7023b804954d31a
  patch -Np1 -d $_name-$pkgver-Source -i ../$pkgname-3.14.0-boost-1.89.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D Boost_NO_BOOST_CMAKE=ON
    -D LIBSCSYNTH=ON
    -D FORTIFY=ON
    -D SYSTEM_ABLETON_LINK=ON
    -D SYSTEM_BOOST=ON
    -D SYSTEM_YAMLCPP=ON
    -S $_name-$pkgver-Source
    -W no-dev
    -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF
    -DCMAKE_C_FLAGS="${CFLAGS}"
  )
  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  xvfb-run make test ARGS="-VV -j1" -C build
}

package() {
  depends+=(
    alsa-lib libasound.so
    avahi libavahi-common.so libavahi-client.so
    boost-libs libboost_{program_options,thread}.so
    fftw libfftw3f.so
    jack libjack.so
    libsndfile libsndfile.so
    readline libreadline.so
    systemd-libs libudev.so
    yaml-cpp libyaml-cpp.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver-Source/{AUTHORS,{CHANGELOG,README,README_LINUX}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
