# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --enable-compile-warnings=no

pkgname=gjs
pkgver=1.50.1
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(i686 x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js52 gtk3)
makedepends=(gobject-introspection git autoconf-archive xorg-server-xvfb)
checkdepends=(valgrind)
_commit=b1b7b14bd54e108fde97eea9247e2636c8d760cf  # tags/1.50.1^0
source=("git+https://git.gnome.org/browse/gjs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --libexecdir=/usr/lib --with-xvfb-tests --enable-compile-warnings=no
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
