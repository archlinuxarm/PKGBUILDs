# $Id: PKGBUILD 44972 2009-07-05 23:00:52Z giovanni $
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=commoncpp2
pkgver=1.7.3
pkgrel=1
pkgdesc="GNU Common C++ 2"
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/commoncpp/'
license=('GPL' 'custom')
depends=('gcc-libs' 'zlib' 'bash')
options=('!libtool' '!makeflags')
source=(http://ftp.gnu.org/pub/gnu/commoncpp/${pkgname}-${pkgver}.tar.gz
        commoncpp2-1.7.3-gcc44.patch)
md5sums=('ca741179a728d264eb276ae471ebaf70'
         '120dc19fa21b78a18caf2e1db8a66a97')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/commoncpp2-1.7.3-gcc44.patch || return 1
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=${pkgdir} install
  
  gzip -9 ${pkgdir}/usr/share/info/commoncpp2.info
  rm ${pkgdir}/usr/share/info/dir
  
  install -D -m644 COPYING.addendum ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
