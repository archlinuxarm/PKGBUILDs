# $Id: PKGBUILD 54202 2009-10-07 10:08:16Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeaccessibility
pkgname=('kdeaccessibility-colorschemes'
         'kdeaccessibility-iconthemes'
         'kdeaccessibility-kmag'
         'kdeaccessibility-kmousetool'
         'kdeaccessibility-kmouth'
         'kdeaccessibility-kttsd')
pkgver=4.3.2
pkgrel=1
arch=('i686' 'x86_64')
url='http://accessibility.kde.org'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-runtime' 'kdelibs' 'festival')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('52ab06324f1d47ce5d14638e74b206ed')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeaccessibility-colorschemes() {
	pkgdesc='KDE Accessibility color schemes'
	cd $srcdir/build/ColorSchemes
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-iconthemes() {
	pkgdesc='KDE Accessibility icon themes'
	cd $srcdir/build/IconThemes
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmag() {
	pkgdesc='Screen Magnifier'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeaccessibility.install'
	cd $srcdir/build/kmag
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmag
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmousetool() {
	pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeaccessibility.install'
	cd $srcdir/build/kmousetool
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmousetool
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmouth() {
	pkgdesc='Speech Synthesizer Frontend'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeaccessibility.install'
	cd $srcdir/build/kmouth
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmouth
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kttsd() {
	pkgdesc='KDE Text To Speech Daemon'
	depends=('kdelibs' 'kdebase-runtime' 'festival')
	install='kdeaccessibility.install'
	cd $srcdir/build/kttsd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kttsd
	make DESTDIR=$pkgdir install
}
