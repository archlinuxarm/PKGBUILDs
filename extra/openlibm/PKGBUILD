# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch out hard ARM march and float specification

pkgname=openlibm
pkgver=0.8.2
pkgrel=1
pkgdesc='Standalone implementation of C mathematical functions'
arch=(x86_64)
url='https://openlibm.org/'
license=(BSD)
depends=(glibc)
source=(https://github.com/JuliaLang/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        '0001-Remove-ARM-march-and-float-specification.patch')
sha256sums=('7244f9aa468584744e260cef740d57d10eab6e9c05f62084f8f2ba457f4b4b1d'
            '1d8a5781bf7d87ae955b7c4c88f76c523eb30a9f0d057be223323cae578da31c')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-Remove-ARM-march-and-float-specification.patch
  sed -e 's|/usr/local|/usr|' -i Make.inc
}

build() {
  cd $pkgname-$pkgver
  make prefix=/usr
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
