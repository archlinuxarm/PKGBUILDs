# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch out hard ARM march and float specification

pkgname=openlibm
pkgver=0.8.4
pkgrel=1
pkgdesc='Standalone implementation of C mathematical functions'
arch=(x86_64)
url='https://openlibm.org/'
license=(MIT)
depends=(glibc)
makedepends=(git)
source=(git+https://github.com/JuliaLang/$pkgname#tag=v$pkgver
        '0001-Remove-ARM-march-and-float-specification.patch')
sha256sums=('fb9105e361ddb3f9a508cb64ffa6d21b0dcb0d980919677b8708be86cdb77a38'
            '1d8a5781bf7d87ae955b7c4c88f76c523eb30a9f0d057be223323cae578da31c')

prepare() {
  cd $pkgname
  patch -p1 -i ../0001-Remove-ARM-march-and-float-specification.patch
  sed -e 's|/usr/local|/usr|' -i Make.inc
}

build() {
  cd $pkgname
  make prefix=/usr
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
