# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DWITH_FLOAT128=OFF

pkgname=primecount
pkgver=8.1
pkgrel=1
pkgdesc='Fast C++ prime counting function implementation'
arch=(x86_64)
url='https://github.com/kimwalisch/primecount'
license=(BSD-2-Clause)
depends=(gcc-libs
         glibc
         primesieve)
makedepends=(cmake
             git)
source=(git+https://github.com/kimwalisch/primecount#tag=v$pkgver)
sha256sums=('fef6554110763dfaf000b2b9453eb7003e90c741f1bc226170a30ae7b3809051')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_FLOAT128=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
