# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DWITH_FLOAT128=OFF

pkgname=primecount
pkgver=7.9
pkgrel=1
pkgdesc='Fast C++ prime counting function implementation'
arch=(x86_64)
url='https://github.com/kimwalisch/primecount'
license=(BSD)
depends=(primesieve)
makedepends=(cmake)
source=(https://github.com/kimwalisch/primecount/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('872975ba2cbb43f5cc1ff5f5fda9ec4ec3f2be1eb3e3e906abe5d0b29a997f5b')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_POPCNT=OFF \
    -DWITH_FLOAT128=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
