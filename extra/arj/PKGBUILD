# $Id: PKGBUILD 49935 2009-08-16 22:10:36Z jgc $
# Maintainer: Travis Willard <travisw@wmpub.ca>
# Contributor: Gergely Tamas <dice@mfa.kfki.hu>

pkgname=arj
pkgver=3.10.22
pkgrel=5
pkgdesc="Free and portable clone of the ARJ archiver"
url="http://arj.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc')
makedepends=('autoconf')
options=(!makeflags)
source=(http://downloads.sourceforge.net/sourceforge/arj/$pkgname-$pkgver.tar.gz
        001_arches_align.patch
        002_no_remove_static_const.patch
        003_64_bit_clean.patch
        arj-3.10.22-custom-printf.patch)
md5sums=('f263bf3cf6d42a8b7e85b4fb514336d3'
         '550bc972d825036f17f202a2b11b35c2'
         '395dfa8cc500ffae648777f8f241be88'
         '56b3cf96ec485b0d824761457417fcc0'
         '4630a7254dfc6c8d8c7e696aa4a58f4a')
sha1sums=('e8470f480e9eee14906e5485a8898e5c24738c8b'
          '7f8904f8c89bacbbeec8c431d627efbb8da2f259'
          '919a3c02a1e039803502c21175d69f7703d13026'
          'd506338e34b40ef5cac6ec14c858fd651a354aaf'
          '4c9481a4b196c4f56fb44fcd3b9362222d6cbe58')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add gentoo patches
  patch -Np1 -i "${srcdir}/001_arches_align.patch" || return 1
  patch -Np1 -i "${srcdir}/002_no_remove_static_const.patch" || return 1
  patch -Np1 -i "${srcdir}/003_64_bit_clean.patch" || return 1
  patch -Np1 -i "${srcdir}/arj-3.10.22-custom-printf.patch" || return 1

  # Build!
  cd gnu
  aclocal || return 1
  autoconf || return 1
  rm -f config.{guess,sub}
  _autover=$(automake --version | head -n1 | awk '{ print $4 }')
  cp /usr/share/automake-${_autover}/config.{guess,sub} . || return 1
  ./configure --prefix=/usr || return 1
  cd ..
  make prepare || return 1
  make || return 1

  # Install!
  make DESTDIR="${pkgdir}" install || return 1
}
