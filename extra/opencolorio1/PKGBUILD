# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dumitru Ursu <dima@ceata.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - turn off using sse

pkgname=opencolorio1
pkgver=1.1.1
pkgrel=3
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('lcms2' 'tinyxml' 'yaml-cpp')
makedepends=('boost' 'cmake')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz
        8d48ee8da42de2d878db7b42586db8b3c67f83e1.patch
        make-coinstallable.patch)
sha512sums=('bed722f9ddce1887d28aacef2882debccd7c3f3c0c708d2723fea58a097de9f02721af9e85453e089ffda5406aef593ab6536c6886307823c132aa787e492e33'
            'fd1c6e134ef898a8715333b1e85a606736685ec0fce07188c0fba68ae048edb61dc4f107eff8de79a53dbbf226a1f84bb0a7d046a15fafb981ce02568350fd49'
            'e7ed107012f94e14bc883baade73ac11c136fbd315832b2c770ca4f0e8cb39ca160da86d848f7762c5e531af88315f2e27b65ef294bf05a6a2130af7285e5369')

prepare() {
  mv OpenColorIO-* $pkgname-$pkgver

  cd $pkgname-$pkgver
  sed -i "s/-Werror//g" src/core/CMakeLists.txt
  sed -i "s/-Werror//g" src/pyglue/CMakeLists.txt
  sed -i "s/push(hidden)/push(default)/g" src/core/OCIOYaml.cpp

  patch -Np1 -i "$srcdir"/8d48ee8da42de2d878db7b42586db8b3c67f83e1.patch
  patch -p1 < "$srcdir"/make-coinstallable.patch # Make it coinstallable with OCIO2
}

build() {
  cd $pkgname-$pkgver

  mkdir -p build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOCIO_BUILD_STATIC=OFF \
    -DOCIO_BUILD_APPS=OFF \
    -DOCIO_BUILD_PYGLUE=OFF \
    -DUSE_EXTERNAL_YAML=ON \
    -DUSE_EXTERNAL_TINYXML=ON \
    -DUSE_EXTERNAL_LCMS=ON \
    -DOCIO_USE_SSE=OFF
  make
}

package() {
  cd "$pkgname-$pkgver"/build

  make DESTDIR="$pkgdir" install

  install -Dm644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  rm -r "$pkgdir"/usr/lib/{cmake,pkgconfig} # krita provides its own FindOCIO
  rm -r "$pkgdir"/usr/share/ocio
}

# vim:set ts=2 sw=2 et:
