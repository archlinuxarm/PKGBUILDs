# $Id: PKGBUILD 86795 2013-03-22 10:46:14Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - depend on botan, configure with USE_SYSTEM_BOTAN=1

pkgname=qtcreator
pkgver=3.2.0
_pkgver=v3.2.0
pkgrel=2
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://qt-project.org'
license=('LGPL')
depends=('qt5-quick1' 'qt5-tools' 'qt5-quickcontrols' 'botan')
makedepends=('git' 'clang')
options=('docs')
optdepends=('qt5-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project support'
            'openssh-askpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support'
            'clang: Clang code model'
            'valgrind: for analyze support')
install=qtcreator.install
source=("git://gitorious.org/qt-creator/qt-creator.git#tag=${_pkgver}"
        "git://gitorious.org/qt-labs/qbs.git"
        "remote-qml.patch::https://qt.gitorious.org/qt-creator/qt-creator/commit/e8305e3ed2349b95c7311c296febc6d75f1a0c2a.patch"
        'qtcreator.desktop')
md5sums=('SKIP'
         'SKIP'
         '648a54f4cf1bde2f62145da5e6c6b88e'
         '50880836fd62ccd87550940feb995f06')

prepare() {
  cd qt-creator
  git submodule init
  git config submodule.qbs.url $srcdir/qbs
  git submodule update

  patch -Np1 < $srcdir/remote-qml.patch
}

build() {
  [[ -d build ]] && rm -r build
  mkdir build && cd build

  LLVM_INSTALL_DIR=/usr qmake -r ../qt-creator/qtcreator.pro USE_SYSTEM_BOTAN=1
  make
  make docs -j1
}

package() {
  cd build

  make INSTALL_ROOT="${pkgdir}/usr/" install
  make INSTALL_ROOT="${pkgdir}/usr/" install_docs

  # Workaround for FS#40583
  mv "${pkgdir}"/usr/bin/qtcreator "${pkgdir}"/usr/bin/qtcreator-bin
  echo "#!/bin/sh" > "${pkgdir}"/usr/bin/qtcreator
  echo "QT_LOGGING_TO_CONSOLE=1 qtcreator-bin \$@" >> "${pkgdir}"/usr/bin/qtcreator
  chmod +x "${pkgdir}"/usr/bin/qtcreator

  install -Dm644 ${srcdir}/qtcreator.desktop ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator/LGPL_EXCEPTION.TXT ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}
