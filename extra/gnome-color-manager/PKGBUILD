# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to enable linking with libm to fix FTBFS

pkgname=gnome-color-manager
pkgver=3.30.0
pkgrel=1
pkgdesc="GNOME Color Profile Tools"
url="https://gitlab.gnome.org/GNOME/gnome-color-manager"
arch=(x86_64)
license=(GPL2)
depends=(libcanberra vte3 exiv2 shared-color-targets colord-gtk libexif)
makedepends=(yelp-tools git appstream-glib meson docbook-utils docbook-sgml perl-sgmls)
checkdepends=(xorg-server-xvfb)
optdepends=('argyllcms: color calibration')
groups=(gnome)
_commit=1ff61d08a37ceb994d10604eeb85de585e35c5ce  # tags/GNOME_COLOR_MANAGER_3_30_0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-color-manager.git#commit=$_commit"
        '0001-set-libm-as-required.patch')
sha256sums=('SKIP'
            '3fbf13def9b2f71c1ff4d14b205efa21d26ae7b631121e987af77ecdba128f3c')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_COLOR_MANAGER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply ../0001-set-libm-as-required.patch
}

build() {
  arch-meson $pkgname build -D packagekit=false
  ninja -C build
}

check() {
  xvfb-run meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
