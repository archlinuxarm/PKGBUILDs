# $Id: PKGBUILD 52138 2009-09-16 10:13:53Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cyrus-sasl-plugins
pkgver=2.1.23
pkgrel=1
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i686' 'x86_64')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
depends=('postgresql-libs>=8.4.1' 'heimdal>=1.2-1' 'libldap>2.4' 'libmysqlclient')
source=(ftp://ftp.andrew.cmu.edu/pub/cyrus-mail/cyrus-sasl-${pkgver}.tar.gz
        cyrus-sasl-2.1.22-gcc44.patch)
md5sums=('2eb0e48106f0e9cd8001e654f267ecbc'
         '5deb4d67b53ecba20c7887fc8fdebee1')

build() {
  cd ${srcdir}/cyrus-sasl-${pkgver}
  patch -Np1 -i $srcdir/cyrus-sasl-2.1.22-gcc44.patch || return 1

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-login \
    --disable-plain \
    --enable-sql \
    --disable-sqlite \
    --enable-gssapi=/usr/include/gssapi \
    --with-mysql=/usr \
    --with-pgsql=/usr \
    --enable-postgresql \
    --enable-ldapdb \
    --with-ldap=/usr
  cd sasldb
  make
  cd ../plugins
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  
  install -Dm644 ../COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

  rm -f ${pkgdir}/usr/lib/sasl2/libsasldb.*
}
