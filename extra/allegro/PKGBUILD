# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc

pkgname=allegro
pkgver=5.2.11.2
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64')
url='https://liballeg.org/'
license=('Zlib' 'BSD-3-Clause' 'OFL-1.1' 'LicenseRef-Custom')
depends=(
    'alsa-lib'
    'dumb'
    'flac'
    'freetype2'
    'glib2'
    'glibc'
    'gtk3'
    'libgl'
    'libjpeg'
    'libogg'
    'libopenmpt'
    'libpng'
    'libpulse'
    'libvorbis'
    'libtheora'
    'libwebp'
    'libx11'
    'libxcursor'
    'libxi'
    'libxinerama'
    'libxrandr'
    'openal'
    'opusfile'
    'physfs')
makedepends=(
    'cmake'
    'glu'
    'mesa'
    'xorgproto')
source=("https://github.com/liballeg/allegro5/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('c16d0dc0f2778ab7fc0813ce5ed8160a12262859b73f609ab0ca35fb576d8646a3e918c367b88640ffbbea82f451220b1d915efeac3fa37e4e32421afc336070')

build() {
    export CFLAGS+=' -DNDEBUG'
    cmake -B build -S "${pkgname}${pkgver%%.*}-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DWANT_DEMO:BOOL='OFF' \
        -DWANT_DOCS_HTML:BOOL='OFF' \
        -DWANT_EXAMPLES:BOOL='OFF' \
        -DWANT_TESTS:BOOL='OFF' \
        -Wno-dev
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -D -m644 "${pkgname}${pkgver%%.*}-${pkgver}/LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
