# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc
#  - comment fixing man path

pkgname=allegro
pkgver=5.2.9.0
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=(x86_64)
url='https://liballeg.org/'
license=(custom)
makedepends=(cmake glu libtheora mesa-libgl ninja opusfile xorgproto)
depends=(dumb gtk3 jack libgl libpulse libtheora libwebp libxpm opusfile physfs)
source=("https://github.com/liballeg/allegro5/releases/download/$pkgver/allegro-$pkgver.tar.gz")
b2sums=('423db21dd4ffd86a86f9e8b48cc1075c6168cf58a768f1f4b6050dd79b23a8a0d883ec0fe22c610eb1745c2b3bf06a94d1f0c45121fa6e9202df4ac0b8538d21')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D WANT_DOCS_HTML=OFF \
    -G Ninja
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$pkgname-$pkgver/LICENSE.txt" \
    -t "$pkgdir/usr/share/licenses/$pkgname"
  # Fix man path
  #mv -v -f "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}
