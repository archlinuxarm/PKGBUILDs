# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc

pkgname=allegro
pkgver=5.2.10.1
pkgrel=4
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64')
url='https://liballeg.org/'
license=('Zlib' 'BSD-3-Clause' 'OFL-1.1' 'LicenseRef-Custom')
depends=(
    'alsa-lib'
    'dumb'
    'flac'
    'freetype2'
    'glib2'
    'glibc'
    'gtk3'
    'libgl'
    'libjpeg'
    'libogg'
    'libopenmpt'
    'libpng'
    'libpulse'
    'libvorbis'
    'libtheora'
    'libwebp'
    'libx11'
    'libxcursor'
    'libxi'
    'libxinerama'
    'libxrandr'
    'openal'
    'opusfile'
    'physfs')
makedepends=(
    'cmake'
    'glu'
    'mesa'
    'xorgproto')
source=("https://github.com/liballeg/allegro5/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('bdb4fdc9b8a941e05d15973f871dbe136771a600b9eab0bd655d0402643827d01353fc54acf9f61c7dfc9ff2e65c86a31f0382121dfbd8cb778bef7647070704')

build() {
    export CFLAGS+=' -DNDEBUG'
    cmake -B build -S "${pkgname}${pkgver%%.*}-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCMAKE_POLICY_VERSION_MINIMUM:STRING='3.5.0' \
        -DMANDIR:PATH='share/man' \
        -DWANT_DEMO:BOOL='OFF' \
        -DWANT_DOCS_HTML:BOOL='OFF' \
        -DWANT_EXAMPLES:BOOL='OFF' \
        -DWANT_TESTS:BOOL='OFF' \
        -Wno-dev
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -D -m644 "${pkgname}${pkgver%%.*}-${pkgver}/LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
