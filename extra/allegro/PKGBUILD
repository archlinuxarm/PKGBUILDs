# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc

pkgname=allegro
pkgver=5.2.11.0
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64')
url='https://liballeg.org/'
license=('Zlib' 'BSD-3-Clause' 'OFL-1.1' 'LicenseRef-Custom')
depends=(
    'alsa-lib'
    'dumb'
    'flac'
    'freetype2'
    'glib2'
    'glibc'
    'gtk3'
    'libgl'
    'libjpeg'
    'libogg'
    'libopenmpt'
    'libpng'
    'libpulse'
    'libvorbis'
    'libtheora'
    'libwebp'
    'libx11'
    'libxcursor'
    'libxi'
    'libxinerama'
    'libxrandr'
    'openal'
    'opusfile'
    'physfs')
makedepends=(
    'cmake'
    'glu'
    'mesa'
    'xorgproto')
source=("https://github.com/liballeg/allegro5/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('9b4d639aab02c834424983d4d0d559bc5a3cdc7a18ed140c906b51d16308e9fd5cc1e8767f2fbabcbf8d69f79f919eb610b1c4ccd8f36c1421afba60106fb8e1')

build() {
    export CFLAGS+=' -DNDEBUG'
    cmake -B build -S "${pkgname}${pkgver%%.*}-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DMANDIR:PATH='share/man' \
        -DWANT_DEMO:BOOL='OFF' \
        -DWANT_DOCS_HTML:BOOL='OFF' \
        -DWANT_EXAMPLES:BOOL='OFF' \
        -DWANT_TESTS:BOOL='OFF' \
        -Wno-dev
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -D -m644 "${pkgname}${pkgver%%.*}-${pkgver}/LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
