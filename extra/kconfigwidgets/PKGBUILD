# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop clang makedepend, FTBFS on AArch64

pkgname=kconfigwidgets
pkgver=5.46.0
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kauth kcodecs kconfig kguiaddons ki18n kwidgetsaddons)
makedepends=(extra-cmake-modules kdoctools python-pyqt5 python2-pyqt5 doxygen qt5-tools)
optdepends=('python-pykf5: to use the Python 3 bindings' 'python2-pykf5: to use the Python 2 bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('770d7ed6d75a64b271c36f022f8a692a26eac82bc2ed908293327b2387189d34'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
