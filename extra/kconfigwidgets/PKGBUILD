# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop clang makedepend to fix v5/v6 FTBFS

pkgname=kconfigwidgets
pkgver=5.83.0
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kauth kcodecs kconfig kguiaddons ki18n kwidgetsaddons)
makedepends=(extra-cmake-modules kdoctools python-pyqt5 doxygen qt5-tools qt5-doc sip4)
optdepends=('python-pyqt5: for the Python bindings' 'perl: for preparetips5')
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d73260735be071fa6a1893ad6498d1ead1288a81bfb9a0b26d085cf7defd40c3'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
