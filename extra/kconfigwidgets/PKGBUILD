# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop clang makedepend, FTBFS on AArch64

pkgname=kconfigwidgets
pkgver=5.48.0
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kauth kcodecs kconfig kguiaddons ki18n kwidgetsaddons)
makedepends=(extra-cmake-modules kdoctools python-pyqt5 doxygen qt5-tools sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('09368a0215aafb4a971f8d1f8d1d8913eab323d133a6a8b93bbeae4f065f9676'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
