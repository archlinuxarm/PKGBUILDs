# $Id: PKGBUILD 61524 2009-12-17 23:03:33Z ibiru $
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>
 
pkgname=boost
pkgver=1.41.0
_boostver=1_41_0
pkgrel=2
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('arm')
url="http://www.boost.org/"
depends=('bzip2' 'zlib')
makedepends=('python>=2.6')
optdepends=('python: for python bindings')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}_${_boostver}.tar.gz
	fix-gzip-crc.patch
	serialization_fix.patch)
license=('custom')
options=(!ccache)

build() {
  cd "${srcdir}/${pkgname}_${_boostver}"
  patch -Np1 -i $srcdir/fix-gzip-crc.patch || return 1
  patch -Np1 -i $srcdir/serialization_fix.patch || return 1

  # build bjam
  cd ${srcdir}/${pkgname}_${_boostver}/tools/jam/src
  ./build.sh cc || return 1

  _bindir="bin.linuxarm"
  [ "${CARCH}" = "arm" ] && _bindir="bin.linuxarm"

  install -m755 -d ${pkgdir}/usr/bin
  install -m755 ${_bindir}/bjam ${pkgdir}/usr/bin/bjam || return 1
 
  # build bcp
  cd ${srcdir}/${pkgname}_${_boostver}/tools/bcp
  ../jam/src/${_bindir}/bjam || return 1
  install -m755 ${srcdir}/${pkgname}_${_boostver}/dist/bin/bcp \
                ${pkgdir}/usr/bin/bcp || return 1

  # build libs
  cd ${srcdir}/${pkgname}_${_boostver}
  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  ./tools/jam/src/${_bindir}/bjam \
            --disable-long-double \
            release debug-symbols=off threading=single,multi \
            runtime-link=shared link=shared,static \
            cflags=-fno-strict-aliasing \
            --prefix=${pkgdir}/usr \
            -sPYTHON_ROOT=/usr \
            -sPYTHON_VERSION=2.6 \
            -sTOOLS=gcc \
            --layout=tagged \
            install || return 1

  # build pyste
  cd ${srcdir}/${pkgname}_${_boostver}/libs/python/pyste/install
  python setup.py install --root=${pkgdir} || return 1

  # license
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/${pkgname}_${_boostver}/LICENSE_1_0.txt \
                ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1
}
md5sums=('ddb04c9b9164a36a91dcf36819666dd9'
         '682f124a765b2935c7352f38c08848cc'
         '01337211d40a4a4c9b83c481baade701')
