# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - changed _bindir to .linuxarm from .linuxx86
#  - removed (optional) dep to openmpi - does not build on ARM

pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.55.0
_boostver=${pkgver//./_}
pkgrel=6
url="http://www.boost.org/"
arch=('i686' 'x86_64')
license=('custom')
makedepends=('icu>=53.1' 'python' 'python2' 'bzip2' 'zlib')
source=(http://downloads.sourceforge.net/${pkgbase}/${pkgbase}_${_boostver}.tar.gz
        001-log_fix_dump_avx2.patch)
sha1sums=('61ed0e57d3c7c8985805bb0682de3f4c65f4b6e5'
          'a4a47cc5716df87d544ae7684aaf402287132d50')

prepare() {
   export _stagedir="${srcdir}/stagedir"
   cd ${pkgbase}_${_boostver}

   patch -p0 -i ../001-log_fix_dump_avx2.patch

   # Add an extra python version. This does not replace anything and python 2.x need to be the default.
   echo "using python : 3.4 : /usr/bin/python3 : /usr/include/python3.4m : /usr/lib ;" >> ./tools/build/v2/user-config.jam
}

build() {
   local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

   cd ${pkgbase}_${_boostver}

   ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=/usr/bin/python2

   _bindir="bin.linuxarm"

   install -dm755 "${_stagedir}"/bin
   install tools/build/v2/engine/${_bindir}/b2 "${_stagedir}"/bin/b2

   pushd tools
   for _tool in bcp inspect quickbook compiler_status process_jam_log wave; do
      "${_stagedir}"/bin/b2 --toolset=gcc cflags="${CPPFLAGS} ${CFLAGS} -std=gnu++11 -O3" linkflags="${LDFLAGS}" $_tool
   done
   "${_stagedir}"/bin/b2 --toolset=gcc cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" library_status
   popd
   cp -a dist/bin/* "${_stagedir}"/bin

   # boostbook is needed by quickbook
   install -dm755 "${_stagedir}"/share/boostbook
   cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   "${_stagedir}"/bin/b2 \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=gcc \
      python=2.7 \
      cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" \
      --layout=system \
      --prefix="${_stagedir}" \
      ${JOBS} \
      install
}

package_boost() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
   depends=("boost-libs=${pkgver}")
   optdepends=('python: for python bindings'
               'python2: for python2 bindings'
               'boost-build: to use boost jam for building your project.')
   options=('staticlibs')

   install -dm755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/{bin,include,share} "${pkgdir}"/usr

   install -d "${pkgdir}"/usr/lib
   find "${_stagedir}"/lib -name \*.a -exec mv {} "${pkgdir}"/usr/lib \;

   install -Dm644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt

   ln -s /usr/bin/b2 "$pkgdir"/usr/bin/bjam
}

package_boost-libs() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
   depends=('bzip2' 'zlib' 'icu')

   install -dm755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/lib "${pkgdir}"/usr

   install -Dm644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}
sha1sums=('61ed0e57d3c7c8985805bb0682de3f4c65f4b6e5'
          'a4a47cc5716df87d544ae7684aaf402287132d50')
