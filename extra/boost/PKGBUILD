# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

# Plugbox modifications: Chane bindir to linuxarm.

pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.43.0
_boostver=1_43_0
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.boost.org/"
makedepends=('python>=2.6' 'bzip2' 'zlib')
source=(http://downloads.sourceforge.net/sourceforge/${pkgbase}/${pkgbase}_${_boostver}.tar.gz)
license=('custom')
options=('!ccache')
md5sums=('734565ca4819bf04bd8e903e116c3fb1')

_stagedir="${srcdir}/stagedir"

build() {
  # build bjam
  cd "${srcdir}/${pkgbase}_${_boostver}/tools/jam/src"
  ./build.sh cc || return 1

  _bindir="bin.linuxarm"

  install -m755 -d "${_stagedir}"/usr/bin
  install -m755 ${_bindir}/bjam "${_stagedir}"/usr/bin/bjam || return 1

  # build bcp
  cd "${srcdir}/${pkgbase}_${_boostver}/tools/bcp"
  ../jam/src/${_bindir}/bjam --toolset=gcc || return 1
  install -m755 "${srcdir}/${pkgbase}_${_boostver}/dist/bin/bcp" \
                ${_stagedir}/usr/bin/bcp || return 1

  # build libs
  cd "${srcdir}/${pkgbase}_${_boostver}"
  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  ./tools/jam/src/${_bindir}/bjam \
            release debug-symbols=off threading=single,multi \
            runtime-link=shared link=shared,static \
            cflags=-fno-strict-aliasing \
            toolset=gcc \
            --prefix="${_stagedir}" \
            -sPYTHON_ROOT=/usr \
            -sPYTHON_VERSION=2.6 \
            -sTOOLS=gcc \
            --layout=tagged \
            install || return 1

  # build pyste
  cd "${srcdir}/${pkgbase}_${_boostver}/libs/python/pyste/install"
  python setup.py install --root=${_stagedir} || return 1
}

package_boost() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
    depends=("boost-libs=${pkgver}")
    optdepends=('python: for python bindings')

    install -dm 755 "${pkgdir}"/usr/{include,lib}
    # headers/source files
    cp -r "${_stagedir}"/include/ "${pkgdir}"/usr/

    # static libs
    cp -r "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/

    # utilities (bjam, bcp, pyste)
    cp -r "${_stagedir}"/usr/* "${pkgdir}"/usr/

    # license
    install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt || return 1
}

package_boost-libs() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
    depends=('gcc-libs' 'bzip2' 'zlib')

    install -dm 755 "${pkgdir}/usr/lib"
    #shared libs
    cp -r "${_stagedir}"/lib/*.so{,.*} "${pkgdir}/usr/lib/"

    # license
    install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt || return 1
}
