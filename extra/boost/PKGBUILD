# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Marius Knaust <marius.knaust@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build armv6h with -march=armv6k
#  - specify arm architecture to b2 for aarch64
#  - upstream PR to fix v5 FTFBS in regex module

pkgname=('boost' 'boost-libs')
pkgver=1.76.0
pkgrel=1
_srcname=boost_${pkgver//./_}
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="https://www.boost.org/"
license=('custom')
makedepends=('icu' 'python' 'python-numpy' 'bzip2' 'zlib' 'openmpi' 'zstd')
source=(https://boostorg.jfrog.io/artifactory/main/release/$pkgver/source/$_srcname.tar.gz
        $pkgname-ublas-c++20-iterator.patch::https://github.com/boostorg/ublas/commit/a31e5cffa85f.patch
        https://patch-diff.githubusercontent.com/raw/boostorg/regex/pull/132.patch)
sha256sums=('7bd7ddceec1a1dfdcbdb3e609b60d01739c38390a5f956385a12f3122049f0ca'
            'aa38addb40d5f44b4a8472029b475e7e6aef1c460509eb7d8edf03491dc1b5ee'
            '05590413cbefd1d64c65d29c5d7d06a54629dc2db26f09e008a18cda8a61d771')

prepare() {
  cd $_srcname

  # https://github.com/boostorg/ublas/pull/97
  patch -Np2 -i ../$pkgname-ublas-c++20-iterator.patch

  # https://github.com/boostorg/regex/pull/132
  patch -Np2 -i ../132.patch
}

build() {
  CFLAGS=`echo $CFLAGS | sed -e 's/armv6/armv6k/'` && CXXFLAGS="$CFLAGS"
  local JOBS="$(sed 's/.*\(-j *[0-9]\+\).*/\1/' <<<$MAKEFLAGS)"

  pushd $_srcname/tools/build
  ./bootstrap.sh --cxxflags="$CXXFLAGS $LDFLAGS"
  ./b2 install --prefix="$srcdir"/fakeinstall
  ln -s b2 "$srcdir"/fakeinstall/bin/bjam
  popd

  cd $_srcname
  ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=python3

  # support for OpenMPI
  echo "using mpi ;" >>project-config.jam

  # boostbook is needed by quickbook
  install -dm755 "$srcdir"/fakeinstall/share/boostbook
  cp -a tools/boostbook/{xsl,dtd} "$srcdir"/fakeinstall/share/boostbook/

  # install to $srcdir/fakeinstall in preparation for split packaging
  ./b2 install \
    architecture=arm \
    variant=release \
    debug-symbols=off \
    threading=multi \
    runtime-link=shared \
    link=shared,static \
    toolset=gcc \
    python=3.9 \
    cflags="$CPPFLAGS $CFLAGS -fPIC -O3" \
    cxxflags="$CPPFLAGS $CXXFLAGS -fPIC -O3" \
    linkflags="$LDFLAGS" \
    --layout=system \
    $JOBS \
    \
    --prefix="$srcdir"/fakeinstall
}

package_boost() {
  pkgdesc+=' (development headers)'
  depends=("boost-libs=$pkgver")
  optdepends=('python: for python bindings')
  options=('staticlibs')

  install -d "$pkgdir"/usr/lib
  cp -a fakeinstall/lib/*.a "$pkgdir"/usr/lib/
  cp -a fakeinstall/lib/cmake "$pkgdir"/usr/lib/
  cp -a fakeinstall/{bin,include,share} "$pkgdir"/usr/

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" $_srcname/LICENSE_1_0.txt
}

package_boost-libs() {
  pkgdesc+=' (runtime libraries)'
  depends=('bzip2' 'zlib' 'icu' 'zstd')
  optdepends=('openmpi: for mpi support')
  provides=(libboost_atomic.so libboost_chrono.so libboost_container.so
    libboost_context.so libboost_contract.so libboost_coroutine.so
    libboost_date_time.so libboost_fiber.so libboost_filesystem.so
    libboost_graph.so libboost_graph_parallel.so libboost_iostreams.so
    libboost_json.so libboost_locale.so libboost_log.so libboost_log_setup.so
    libboost_math_c99.so libboost_math_c99f.so libboost_math_c99l.so
    libboost_math_tr1.so libboost_math_tr1f.so libboost_math_tr1l.so
    libboost_mpi.so libboost_numpy39.so
    libboost_prg_exec_monitor.so libboost_program_options.so
    libboost_python39.so libboost_random.so
    libboost_regex.so libboost_serialization.so
    libboost_stacktrace_addr2line.so libboost_stacktrace_basic.so
    libboost_stacktrace_noop.so libboost_system.so libboost_thread.so
    libboost_timer.so libboost_type_erasure.so libboost_unit_test_framework.so
    libboost_wave.so libboost_wserialization.so)

  install -dm755 "$pkgdir"/usr/lib
  cp -a fakeinstall/lib/*.so* "$pkgdir"/usr/lib/

  # https://github.com/boostorg/mpi/issues/112
  install -d "$pkgdir"/usr/lib/python3.9/site-packages/boost
  touch "$pkgdir"/usr/lib/python3.9/site-packages/boost/__init__.py
  python -m compileall -o 0 -o 1 -o 2 "$pkgdir"/usr/lib/python3.9/site-packages/boost
  cp fakeinstall/lib/boost-python3.9/mpi.so \
    "$pkgdir"/usr/lib/python3.9/site-packages/boost/mpi.so

  # https://github.com/boostorg/python/issues/203#issuecomment-391477685
  for _lib in python numpy; do
    ln -srL "$pkgdir"/usr/lib/libboost_${_lib}3{9,}.so
  done

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" $_srcname/LICENSE_1_0.txt
}

# vim:set ts=2 sw=2 et:
