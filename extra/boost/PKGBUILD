# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - changed _bindir to .linuxarm from .linuxx86
#  - removed (optional) dep to openmpi - does not build on ARM

pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.54.0
_boostver=${pkgver//./_}
pkgrel=4
url="http://www.boost.org/"
arch=('i686' 'x86_64')
license=('custom')
makedepends=('icu>=52.1' 'python' 'python2' 'bzip2' 'zlib')
source=(http://downloads.sourceforge.net/${pkgbase}/${pkgbase}_${_boostver}.tar.gz
        boost-1.53.0-python3.patch
        boost-1.54.0-Fix-macro-for-int128-detection.patch
        fix-new-glibc.patch)
sha1sums=('069501636097d3f40ddfd996d29748bb23591c53'
          '34026072a7cb2534164f20e77bb71a5c75093307'
          'bf5177694ab8a0df6bc13aa47b05727c40febebb'
          'e3a5fac340c12b39add50070efb439b857108a0b')

build() {
   export _stagedir="${srcdir}/stagedir"
   local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"
    
   cd "${srcdir}/${pkgbase}_${_boostver}"

   patch -Np2 -i ../boost-1.54.0-Fix-macro-for-int128-detection.patch
   #patch -Np2 -i ../fix-new-glibc.patch
   
   # Fix build errors with python 3
   sed -i "/PYTHON_ROOT/s/print sys.prefix/print(sys.prefix)/g" bootstrap.sh
   patch -Np1 -i ../boost-1.53.0-python3.patch

   # Shut up strict aliasing warnings
   echo "using gcc : : : <compileflags>-fno-strict-aliasing ;" >> ./tools/build/v2/user-config.jam
   # Add an extra python version. This does not replace anything and python 2.x need to be the default.
   echo "using python : 3.3 : /usr/bin/python3 : /usr/include/python3.3m : /usr/lib ;" >> ./tools/build/v2/user-config.jam

   ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=/usr/bin/python2

   _bindir="bin.linuxarm"
   [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

   install -d -m 755 "${_stagedir}"/bin
   install "${srcdir}"/${pkgbase}_${_boostver}/tools/build/v2/engine/${_bindir}/bjam "${_stagedir}"/bin/bjam

   pushd tools
   for _tool in bcp inspect quickbook compiler_status process_jam_log wave; do
      "${_stagedir}"/bin/bjam --toolset=gcc $_tool
   done
   "${_stagedir}"/bin/bjam --toolset=gcc cflags="-std=gnu++11" library_status
   popd
   cp -a dist/bin/* "${_stagedir}"/bin
   
   #boostbook is needed by quickbook
   install -d -m 755 "${_stagedir}"/share/boostbook
   cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   "${_stagedir}"/bin/bjam \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=gcc \
      python=2.7 \
      --layout=system \
      --prefix="${_stagedir}" \
      ${JOBS} \
      install

   find ${_stagedir} -name \*.a -exec rm -f {} \;
}

package_boost() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
   depends=("boost-libs=${pkgver}")
   optdepends=('python: for python bindings'
               'python2: for python2 bindings'
               'boost-build: to use boost jam for building your project.')

   install -d -m 755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/{bin,include,share} "${pkgdir}"/usr

   install -D -m 644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
}

package_boost-libs() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
   depends=('bzip2' 'zlib' 'icu')

   install -d -m 755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/lib "${pkgdir}"/usr

   install -D -m 644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}
