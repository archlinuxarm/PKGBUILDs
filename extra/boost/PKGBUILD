# $Id: PKGBUILD 100298 2010-11-22 15:31:39Z ibiru $
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - changed _bindir to .linuxarm from .linuxx86

pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.45.0
_boostver=${pkgver//./_}
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.boost.org/"
makedepends=('python2' 'bzip2' 'zlib')
source=(http://downloads.sourceforge.net/sourceforge/${pkgbase}/${pkgbase}_${_boostver}.tar.gz)
license=('custom')
options=('!ccache')
md5sums=('739792c98fafb95e7a6b5da23a30062c')

_stagedir="${srcdir}/stagedir"

build() {
  # set python path for bjam
  cd "${srcdir}/${pkgbase}_${_boostver}/tools"
  echo "using python : 2.7 : /usr/bin/python2 ;" >> build/v2/user-config.jam

  # build bjam
  cd "${srcdir}/${pkgbase}_${_boostver}/tools/build/v2/engine/src"
  ./build.sh cc

  _bindir="bin.linuxarm"
  [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

  install -m755 -d "${_stagedir}"/usr/bin
  install -m755 ${_bindir}/bjam "${_stagedir}"/usr/bin/bjam

  # build bcp
  cd "${srcdir}/${pkgbase}_${_boostver}/tools/bcp"
  ../build/v2/engine/src/${_bindir}/bjam --toolset=gcc
  install -m755 "${srcdir}/${pkgbase}_${_boostver}/dist/bin/bcp" \
                ${_stagedir}/usr/bin/bcp

  # build libs
  cd "${srcdir}/${pkgbase}_${_boostver}"
  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  ./tools/build/v2/engine/src/${_bindir}/bjam \
      release debug-symbols=off threading=single,multi \
      runtime-link=shared link=shared,static \
      cflags=-fno-strict-aliasing \
      toolset=gcc \
      --prefix="${_stagedir}" \
      -sTOOLS=gcc \
      --layout=tagged \
      ${MAKEFLAGS} \
      install

  # build pyste
  cd "${srcdir}/${pkgbase}_${_boostver}/libs/python/pyste/install"
  python2 setup.py install --root=${_stagedir}
}

package_boost() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
    depends=("boost-libs=${pkgver}")
    optdepends=('python2: for python bindings')

    install -dm 755 "${pkgdir}"/usr/{include,lib}
    # headers/source files
    cp -r "${_stagedir}"/include/ "${pkgdir}"/usr/

    # static libs
    cp -r "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/

    # utilities (bjam, bcp, pyste)
    cp -r "${_stagedir}"/usr/* "${pkgdir}"/usr/

    # license
    install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
}

package_boost-libs() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
    depends=('gcc-libs' 'bzip2' 'zlib')

    install -dm 755 "${pkgdir}/usr/lib"
    #shared libs
    cp -r "${_stagedir}"/lib/*.so{,.*} "${pkgdir}/usr/lib/"

    # license
    install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}
