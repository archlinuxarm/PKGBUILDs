# $Id: PKGBUILD 47703 2009-07-25 14:37:19Z giovanni $
# Maintainer: kevin <kevin@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>
 
pkgname=boost
pkgver=1.39.0
_boostver=1_39_0
pkgrel=2
pkgdesc="Boost provides free peer-reviewed portable C++ source libraries."
arch=(i686 x86_64)
url="http://www.boost.org/"
depends=('python>=2.5' 'bzip2' 'zlib')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}_${_boostver}.tar.gz layout-tagged.patch translate_exception.patch)
license=('custom')
options=(!ccache)
 
build() {
  cd ${srcdir}/${pkgname}_${_boostver}
  #export CFLAGS="${CFLAGS} -fno-strict-aliasing"
  #export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  patch -Np0 -i ${srcdir}/layout-tagged.patch || return 1
  patch -Np1 -i ${srcdir}/translate_exception.patch || return 1

  # build bjam
  cd ${srcdir}/${pkgname}_${_boostver}/tools/jam/src
  ./build.sh cc || return 1

  _bindir="bin.linuxx86"
  [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

  install -m755 -d ${pkgdir}/usr/bin
  install -m755 ${_bindir}/bjam ${pkgdir}/usr/bin/bjam || return 1
 
  # build bcp
  cd ${srcdir}/${pkgname}_${_boostver}/tools/bcp
  ../jam/src/${_bindir}/bjam || return 1
  install -m755 ${srcdir}/${pkgname}_${_boostver}/dist/bin/bcp \
                ${pkgdir}/usr/bin/bcp || return 1

  # build libs
  cd ${srcdir}/${pkgname}_${_boostver}
  # default "minimal" install: "release link=shared,static
  #      runtime-link=shared threading=multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  ./tools/jam/src/${_bindir}/bjam \
            release debug-symbols=off threading=single,multi \
            runtime-link=shared link=shared,static \
            cflags=-fno-strict-aliasing \
            --prefix=${pkgdir}/usr \
            -sPYTHON_ROOT=/usr \
            -sPYTHON_VERSION=2.6 \
            -sTOOLS=gcc \
            --layout=tagged \
            install || return 1
#            --with-wave install || return 1
#            -d2 <- debug  cflags=-fno-strict-aliasing \

  # build pyste
  cd ${srcdir}/${pkgname}_${_boostver}/libs/python/pyste/install
  python setup.py install --root=${pkgdir} || return 1

  # license
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/${pkgname}_${_boostver}/LICENSE_1_0.txt \
                ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1
}
md5sums=('fcc6df1160753d0b8c835d17fdeeb0a7'
         '1f085180881155fbb85be0cb9f7d8f35'
         '1785bd2f958de7649b4e378680c44d16')
