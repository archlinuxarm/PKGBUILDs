# $Id: PKGBUILD 152229 2012-03-05 09:55:27Z ibiru $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-runtime-cpudetection from configure
#  - use kernel.org mirror for package source
#  - --enable-armv5te to use v5 edsp (fails autodetect for v5)

pkgbase=mplayer
pkgname=('mplayer' 'mencoder')
pkgver=36285
pkgrel=4
arch=('i686' 'x86_64')
makedepends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl' 'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient'
'aalib' 'jack' 'libcaca' 'x264' 'faac' 'faad2' 'lirc-utils'  'libxvmc' 'enca' 'libvdpau' 'opencore-amr' 'libdca' 'a52dec' 'schroedinger' 'libvpx'
'libpulse' 'fribidi' 'unzip' 'mesa' 'live-media' 'yasm' 'git' 'fontconfig' 'mpg123' 'ladspa' 'libass' 'libbluray' 'libcdio-paranoia' 'subversion')
license=('GPL')
url="http://www.mplayerhq.hu/"
options=('!buildflags' '!emptydirs')
source=(ftp://mirrors.kernel.org/archlinux/other/packages/$pkgbase/$pkgbase-$pkgver.tar.xz
        mplayer.desktop
        cdio-includes.patch
        0001-demux_gif-declare-missing-PrintGifError.patch)
md5sums=('5f96e829d711e7d1ea65e324710dca50'
         'c0d6ef795cf6de48e3b87ff7c23f0319'
         '7b5be7191aafbea64218dc4916343bbc'
         '12a2a5b79849d57eab60a1bfec2978c4')

prepare() {
    cd "$srcdir/$pkgbase-$pkgver"

    patch -Np0 -i ../cdio-includes.patch
    patch -Np1 -i ../0001-demux_gif-declare-missing-PrintGifError.patch
}

build() {
    cd "$srcdir/$pkgbase-$pkgver"

    ./configure --prefix=/usr \
        --enable-armv5te \
        --disable-gui \
        --disable-arts \
        --disable-liblzo \
        --disable-speex \
        --disable-openal \
        --disable-libdv \
        --disable-musepack \
        --disable-esd \
        --disable-mga \
        --disable-ass-internal \
        --disable-cdparanoia \
        --enable-xvmc \
        --enable-radio \
        --enable-radio-capture \
        --language=all \
        --confdir=/etc/mplayer
    [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

    make
}

package_mplayer() {
    pkgdesc="A movie player for linux"
    install=mplayer.install
    backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
    depends=('desktop-file-utils' 'ttf-dejavu' 'enca' 'libxss' 'a52dec' 'libvpx' 'lirc-utils' 'x264' 'libmng' 'libdca' 'aalib' 'lame' 'fontconfig'  'libgl'
    'libxinerama' 'libvdpau' 'libpulse' 'smbclient' 'xvidcore' 'opencore-amr' 'jack' 'cdparanoia' 'libmad' 'sdl' 'libtheora' 'libcaca' 'libxxf86dga' 'fribidi'
    'libjpeg' 'faac' 'faad2' 'libxvmc' 'schroedinger' 'mpg123' 'libass' 'libxxf86vm' 'libbluray' 'libcdio-paranoia')

    cd "$srcdir/$pkgbase-$pkgver"

    make DESTDIR="$pkgdir" install-mplayer install-mplayer-man
    install -Dm644 etc/{codecs.conf,input.conf,example.conf} "$pkgdir/etc/mplayer/"
    install -dm755 "$pkgdir/usr/share/mplayer/"
    ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "$pkgdir/usr/share/mplayer/subfont.ttf"
    rm -rf "$pkgdir/usr/share/mplayer/font"
    #desktop file  FS#14770
    install -Dm644 "$srcdir/mplayer.desktop" "$pkgdir/usr/share/applications/mplayer.desktop"
    install -Dm644 etc/mplayer256x256.png "$pkgdir/usr/share/pixmaps/mplayer.png"
}

package_mencoder() {
    pkgdesc="Free command line video decoding, encoding and filtering tool"
    depends=('enca' 'a52dec' 'libvpx' 'x264' 'libmng' 'libdca' 'bzip2' 'lame' 'alsa-lib' 'fontconfig' 'giflib' 'libpng' 'smbclient' 'xvidcore' 'opencore-amr' 'cdparanoia'
    'libmad' 'libtheora' 'fribidi' 'libjpeg' 'faac' 'faad2' 'schroedinger' 'mpg123' 'libass' 'libbluray' 'libcdio-paranoia' 'libvorbis')

    cd "$srcdir/$pkgbase-$pkgver"
    make DESTDIR="$pkgdir" install-mencoder install-mencoder-man
    find "$pkgdir/usr/share/man" -name mplayer.1 -exec rename mplayer.1 mencoder.1 {} +
}
