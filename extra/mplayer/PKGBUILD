# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-runtime-cpudetection from configure
#  - --enable-armv5te to use v5 edsp (fails autodetect for v5)

pkgbase=mplayer
pkgname=('mplayer' 'mencoder')
pkgver=37916
pkgrel=2
pkgdesc='Media player for Linux'
url='http://www.mplayerhq.hu/'
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('libxxf86dga' 'libmad' 'libxinerama' 'libmng' 'libxss' 'smbclient' 'aalib' 'jack'
             'libcaca' 'faac' 'faad2' 'lirc'  'libxvmc' 'enca' 'libdca' 'a52dec' 'libvpx' 'unzip'
             'mesa' 'live-media' 'yasm' 'git' 'mpg123' 'ladspa' 'libcdio-paranoia' 'subversion'
             'x264' 'libx264' 'rtmpdump' 'libdvdcss' 'libdvdread' 'libdvdnav' 'ffmpeg')
options=('!buildflags' '!emptydirs')
source=(${pkgbase}-${pkgver}::svn://svn.mplayerhq.hu/mplayer/trunk#revision=${pkgver}
        mplayer.desktop
        include-samba-4.0.patch
        revert-icl-fixes.patch)
sha512sums=('SKIP'
            'd3c5cbf0035279c6f307e4e225473d7b77f9b56566537a26aa694e68446b9e3240333296da627ad5af83b04cc8f476d1a3f8c05e4cf81cd6e77153feb4ed74bc'
            '9debb8c58b996f6c716c22c86c720bf9dc49b4ee9b76c57519f791667ae1de2cc6f5add878fbf4ac02c6b6fd1865e1bcfa6105e75de4bf7ec596c338ed0cae99'
            '0cae0b26d3d97fd4c962962c43a481de20335369cbca406cadfc9bda1a0608b32f5374e76c477cb9a85bda83a568a1ed17126df224ae61579d0a402c1824aea8')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 < "../include-samba-4.0.patch"
  patch -p0 < "../revert-icl-fixes.patch"
  ./version.sh
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr \
    --enable-armv5te \
    --disable-gui \
    --disable-arts \
    --disable-liblzo \
    --disable-speex \
    --disable-openal \
    --disable-libdv \
    --disable-musepack \
    --disable-esd \
    --disable-mga \
    --disable-ass-internal \
    --disable-cdparanoia \
    --disable-ffmpeg_a \
    --enable-xvmc \
    --enable-radio \
    --enable-radio-capture \
    --enable-smb \
    --language=all \
    --confdir=/etc/mplayer
  [[ "${CARCH}" = "i686" ]] && sed 's|-march=i486|-march=i686|g' -i config.mak
  make
}

package_mplayer() {
  pkgdesc='Media player for Linux'
  backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
  depends=('desktop-file-utils' 'ttf-font' 'enca' 'libxss' 'a52dec' 'libvpx'
           'lirc' 'libx264' 'libmng' 'libdca' 'aalib' 'libxinerama' 'smbclient'
           'jack' 'libmad' 'libcaca' 'libxxf86dga' 'faac' 'faad2' 'libxvmc' 'mpg123'
           'libcdio-paranoia' 'rtmpdump' 'libdvdnav' 'ffmpeg')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install-mplayer install-mplayer-man

  install -Dm 644 etc/{codecs.conf,input.conf,example.conf} "${pkgdir}/etc/mplayer"
  install -Dm 644 "${srcdir}/mplayer.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 etc/mplayer256x256.png "${pkgdir}/usr/share/pixmaps/mplayer.png"
}

package_mencoder() {
  pkgdesc='Free command line video decoding, encoding and filtering tool'
  depends=('enca' 'a52dec' 'libvpx' 'libx264' 'libmng' 'libdca' 'smbclient' 'libmad'
           'faac' 'faad2' 'mpg123' 'libcdio-paranoia' 'rtmpdump' 'libdvdnav' 'ffmpeg')

  make -C ${pkgbase}-${pkgver} DESTDIR="${pkgdir}" install-mencoder install-mencoder-man
  find "${pkgdir}/usr/share/man" -name mplayer.1 -exec rename mplayer.1 mencoder.1 {} +
}

# vim: ts=2 sw=2 et:
