# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-runtime-cpudetection from configure
#  - --enable-armv5te to use v5 edsp (fails autodetect for v5)
#  - patch to fix ARM build
#  - use system ffmpeg on AArch64

pkgbase=mplayer
pkgname=('mplayer' 'mencoder')
pkgver=37379
pkgrel=6
arch=('i686' 'x86_64')
makedepends=(
  'libxxf86dga' 'libxxf86vm' 'libmad' 'libxinerama' 'sdl' 'lame' 'libtheora'
  'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient' 'aalib' 'jack' 'libcaca'
  'faac' 'faad2' 'lirc'  'libxvmc' 'enca' 'libvdpau' 'opencore-amr'
  'libdca' 'a52dec' 'schroedinger' 'libvpx' 'libpulse' 'fribidi' 'unzip' 'mesa'
  'live-media' 'yasm' 'git' 'fontconfig' 'mpg123' 'ladspa' 'libass' 'libbluray'
  'libcdio-paranoia' 'opus' 'subversion' 'x264' 'libx264' 'rtmpdump' 'gnutls'
  'gsm' 'libdvdcss' 'libdvdread' 'libdvdnav'
)
license=('GPL')
url='http://www.mplayerhq.hu/'
options=('!buildflags' '!emptydirs')
source=($pkgbase-$pkgver::svn://svn.mplayerhq.hu/mplayer/trunk#revision=$pkgver
        http://ffmpeg.org/releases/ffmpeg-2.6.2.tar.bz2
        mplayer.desktop
        cdio-includes.patch
        include-samba-4.0.patch
        ffmpeg-libvpxenc-remove-some-unused-ctrl-id-mappings.patch
        revert-icl-fixes.patch
        37386.patch)
md5sums=('SKIP'
         'e75d598921285d6775f20164a91936ac'
         '62f44a58f072b2b1a3c3d3e4976d64b3'
         '7b5be7191aafbea64218dc4916343bbc'
         '868a92bdef148df7f38bfa992b26ce9d'
         '71be9bcd297cb9ce833bbc5d425c0470'
         '3579402002b7302fdf2d146639333efd'
         '9131fa5229c857174ab029ed9b0cb71b')

pkgver() {
  cd $pkgbase-$pkgver
  svnversion
}

prepare() {
  cd $pkgbase-$pkgver
  mv ../ffmpeg-*/ ./ffmpeg

  patch -Np0 -i ../cdio-includes.patch
  patch -Np1 -i ../include-samba-4.0.patch
  patch -Np0 -i ../revert-icl-fixes.patch
  patch -d ffmpeg -Np1 <../ffmpeg-libvpxenc-remove-some-unused-ctrl-id-mappings.patch
  patch -p0 -i ../37386.patch

  ./version.sh
}

build() {
  cd $pkgbase-$pkgver

  [[ $CARCH == "aarch64" ]] && CONFIG="--disable-ffmpeg_a"

  ./configure --prefix=/usr \
    --enable-armv5te \
    --disable-gui \
    --disable-arts \
    --disable-liblzo \
    --disable-speex \
    --disable-openal \
    --disable-libdv \
    --disable-musepack \
    --disable-esd \
    --disable-mga \
    --disable-ass-internal \
    --disable-cdparanoia \
    --enable-xvmc \
    --enable-radio \
    --enable-radio-capture \
    --enable-smb \
    --language=all \
    --confdir=/etc/mplayer $CONFIG
  [[ "$CARCH" = "i686" ]] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
}

package_mplayer() {
  pkgdesc='Media player for Linux'
  install=mplayer.install
  backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
  depends=(
    'desktop-file-utils' 'ttf-font' 'enca' 'libxss' 'a52dec' 'libvpx'
    'lirc' 'libx264' 'libmng' 'libdca' 'aalib' 'lame' 'fontconfig'
    'libgl' 'libxinerama' 'libvdpau' 'libpulse' 'smbclient' 'xvidcore'
    'opencore-amr' 'jack' 'libmad' 'sdl' 'libtheora' 'libcaca' 'libxxf86dga'
    'fribidi' 'libjpeg' 'faac' 'faad2' 'libxvmc' 'schroedinger' 'mpg123'
    'libass' 'libxxf86vm' 'libbluray' 'libcdio-paranoia' 'opus' 'rtmpdump'
    'gsm' 'gnutls' 'libdvdnav'
  )
  depends_aarch64=('ffmpeg')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-mplayer install-mplayer-man

  install -Dm644 etc/{codecs.conf,input.conf,example.conf} \
    "$pkgdir/etc/mplayer/"

  # desktop file (FS#14770)
  install -Dm644 ../mplayer.desktop \
    "$pkgdir"/usr/share/applications/mplayer.desktop
  install -Dm644 etc/mplayer256x256.png \
    "$pkgdir"/usr/share/pixmaps/mplayer.png
}

package_mencoder() {
  pkgdesc='Free command line video decoding, encoding and filtering tool'
  depends=(
    'enca' 'a52dec' 'libvpx' 'libx264' 'libmng' 'libdca' 'bzip2' 'lame'
    'alsa-lib' 'fontconfig' 'giflib' 'libpng' 'smbclient' 'xvidcore'
    'opencore-amr' 'libmad' 'libtheora' 'fribidi' 'libjpeg' 'faac' 'faad2'
    'schroedinger' 'mpg123' 'libass' 'libbluray' 'libcdio-paranoia'
    'libvorbis' 'opus' 'rtmpdump' 'gsm' 'libdvdnav'
  )

  make -C $pkgbase-$pkgver DESTDIR="$pkgdir" install-mencoder install-mencoder-man
  find "$pkgdir"/usr/share/man -name mplayer.1 -exec rename mplayer.1 mencoder.1 {} +
}
