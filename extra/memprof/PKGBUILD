# $Id: PKGBUILD 18773 2008-11-09 00:15:54Z aaron $
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=memprof
pkgver=0.6
pkgrel=3
pkgdesc="A tool for profiling memory usage and finding memory leaks"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/memprof/"
depends=('libgnomeui>=2.18.1-2' 'perlxml')
makedepends=('pkgconfig')
license=('GPL')
source=(http://ftp.gnome.org/pub/GNOME/sources/memprof/0.6/${pkgname}-${pkgver}.tar.gz
        binutils.patch)
install=memprof.install
options=('!libtool')
md5sums=('3fa53d937e1e703c1b2d4cec0c6c9645' 'bb554fb98897f47f8390cd24a752c0e5')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  [ "$CARCH" = "x86_64" ] && (patch -Np0 -i ${startdir}/src/binutils.patch || return 1)

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static
  # fix this just in the Makefile for now... memprof bugzilla knows about it
  sed -i "s|cd \$(bindir)|cd \$(DESTDIR)\$(bindir)|" Makefile

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install
   
  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
