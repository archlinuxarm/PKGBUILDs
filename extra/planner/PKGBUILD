# $Id: PKGBUILD 46598 2009-07-18 22:33:01Z jgc $
# Contributor: herb  <herb@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=planner
pkgver=0.14.4
pkgrel=2
pkgdesc="Gnome Project Management System"
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/Planner"
depends=('libgnomeui>=2.24.1' 'evolution-data-server>=2.26.3' 'libxslt>=1.1.24' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils>=0.16.1' 'pygtk>=2.14.1')
conflicts=('mrproject' 'libmrproject')
replaces=('mrproject' 'libmrproject' )
options=('!libtool' '!makeflags' '!emptydirs')
install=planner.install
source=(http://ftp.gnome.org/pub/GNOME/sources/planner/${pkgver%.*}/planner-${pkgver}.tar.bz2)
md5sums=('8fcbd1b55dd037662f7710a28a96bb92')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-update-mimedb \
              --enable-python --enable-eds || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain planner ${pkgdir}/etc/gconf/schemas/*.schemas  
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
