# $Id: PKGBUILD 149273 2012-02-06 17:33:27Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure option --enable-shared isn't supported for us
#  - patch to revert stupidity: forced cross compiling, forced cflags
#  - disable neon

pkgname=libvpx
pkgver=1.2.0
pkgrel=2
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=(http://mirrors.kernel.org/archlinux/other/libvpx/libvpx-$pkgver.tar.xz{,.sig}
        no-cross.patch)
sha1sums=('75cd953cf2db694412569272803fbdc73f2b599f'
          'SKIP'
          '9677138685ef36cbb9f2be1a586324b54fab9400')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -p1 -i ../no-cross.patch
    ./configure --prefix=/usr --enable-vp8 \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs --disable-neon
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DIST_DIR="$pkgdir/usr" install
    install -D -m 0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
