# $Id: PKGBUILD 149273 2012-02-06 17:33:27Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building
#  - disable neon

pkgname=libvpx
pkgver=1.4.0
pkgrel=1
pkgdesc='VP8 and VP9 codec'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc')
makedepends=('yasm')
makedepends=('yasm' 'git')
source=($pkgname::git+http://code.google.com/p/webm.libvpx#tag=v$pkgver
        fix-armhf-link.patch)
md5sums=('SKIP'
         '3d54d292cdb5752f199e9551fbc1ac14')

prepare() {
  cd $pkgname
  patch -p1 -i ../fix-armhf-link.patch
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --enable-vp8 \
    --enable-vp9 \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-postproc \
    --enable-pic \
    --disable-install-docs \
    --disable-install-srcs \
    --disable-neon
  make
}

package() {
  cd $pkgname
  make DIST_DIR="$pkgdir/usr" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
