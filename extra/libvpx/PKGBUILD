# Maintainer: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - configure option --enable-shared isn't supported for us

plugrel=1

pkgname=libvpx
pkgver=0.9.6
pkgrel=2
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=(http://webm.googlecode.com/files/${pkgname}-v${pkgver}.tar.bz2)
sha1sums=('a3522bd2b73d52381ba767ded1cbf4760e9cc6f8')

build() {
    cd "${srcdir}/${pkgname}-v${pkgver}"
    ./configure --enable-vp8 \
        --enable-runtime-cpu-detect \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd "${srcdir}/${pkgname}-v${pkgver}"
    make DIST_DIR="$pkgdir/usr" install
    install -D -m 0644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
    chmod 644 "$pkgdir"/usr/include/vpx/*.h
}
