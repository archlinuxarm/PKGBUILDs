# $Id: PKGBUILD 149273 2012-02-06 17:33:27Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building
#  - disable neon

pkgname=libvpx
pkgver=1.2.0
pkgrel=2.1
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=(http://mirrors.kernel.org/archlinux/other/libvpx/libvpx-$pkgver.tar.xz{,.sig}
        fix-armhf-link.patch)
sha1sums=('75cd953cf2db694412569272803fbdc73f2b599f'
          'SKIP'
          'bf81bc89d9d18930c2d8d52d1f7442926381b4d5')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -p1 -i ../fix-armhf-link.patch
    ./configure --prefix=/usr --enable-vp8 \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs --disable-neon
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DIST_DIR="$pkgdir/usr" install
    install -D -m 0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
