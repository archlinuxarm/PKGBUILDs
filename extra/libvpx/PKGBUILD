# $Id: PKGBUILD 149273 2012-02-06 17:33:27Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building
#  - disable neon and neon-asm

pkgname=libvpx
pkgver=1.5.0
pkgrel=2
pkgdesc='VP8 and VP9 codec'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc' 'gcc-libs')
makedepends=('yasm' 'git')
provides=('libvpx.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/webmproject/libvpx/archive/v$pkgver.tar.gz
        fix-armhf-link.patch)
md5sums=('0c662bc7525afe281badb3175140d35c'
         'a2d92d01ecc336fe81682daa8a7fdafa')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-armhf-link.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-vp8 \
    --enable-vp9 \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-postproc \
    --enable-pic \
    --enable-experimental --enable-spatial-svc \
    --disable-install-docs \
    --disable-install-srcs \
    --disable-neon --disable-neon-asm
  make
}

package() {
  cd $pkgname-$pkgver
  make DIST_DIR="$pkgdir/usr" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
