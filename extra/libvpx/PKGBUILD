# $Id: PKGBUILD 149273 2012-02-06 17:33:27Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure option --enable-shared isn't supported for us

plugrel=1

pkgname=libvpx
pkgver=1.1.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=(http://webm.googlecode.com/files/$pkgname-v$pkgver.tar.bz2)
sha1sums=('356af5f770c50cd021c60863203d8f30164f6021')

build() {
    cd "$srcdir/$pkgname-v$pkgver"
    ./configure --prefix=/usr --enable-vp8 \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd "$srcdir/$pkgname-v$pkgver"
    make DIST_DIR="$pkgdir/usr" install
    install -D -m 0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
