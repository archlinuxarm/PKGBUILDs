# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable functional tests

pkgname=tracker
pkgver=2.1.5
pkgrel=1
pkgdesc="Desktop-neutral user information store, search tool and indexer"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(sqlite icu glib2 libffi util-linux libstemmer libseccomp libsoup json-glib upower libnm)
makedepends=(gobject-introspection intltool vala git gtk-doc bash-completion meson)
checkdepends=(python2-gobject python2-dbus)
groups=(gnome)
_commit=e1cdde255348b11cf98a33e4b3c37ae5a77b5147  # tags/2.1.5^0
source=("git+https://gitlab.gnome.org/GNOME/tracker.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -Dfunctional_tests=false
  ninja -C build
}

check() {
  # Many tests are broken:
  # - Assume schemas are installed
  # - Assume services are autolaunched
  dbus-run-session meson test -C build || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
