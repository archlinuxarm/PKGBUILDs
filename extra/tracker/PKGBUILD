# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-functional-tests from configure
#  - comment deleting tracker-tests in packaging

pkgname=tracker
pkgver=2.0.3
pkgrel=2
pkgdesc="Desktop-neutral user information store, search tool and indexer"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(sqlite icu glib2 libffi util-linux libstemmer libseccomp libsoup json-glib upower libnm)
makedepends=(gobject-introspection intltool vala git gtk-doc bash-completion)
checkdepends=(python2-gobject python2-dbus)
groups=(gnome)
_commit=a96f2fabb2a3b8fb02cc344183c0818e8e2ecb13  # tags/2.0.3^0
source=("git+https://git.gnome.org/browse/tracker#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-unit-tests \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  # Many tests are broken:
  # - Assume schemas are installed
  # - Assume services are autolaunched
  dbus-run-session make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  #rm -r "$pkgdir/usr/share/tracker-tests"
}
