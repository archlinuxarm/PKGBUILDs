# $Id: PKGBUILD 36859 2009-04-28 05:47:04Z eric $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-vga
pkgver=4.1.0
pkgrel=8
pkgdesc="X.org VGA 16 color video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'xorg-util-macros')
conflicts=('xorg-server<1.6.0')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	git-1b4bf389.patch LICENSE)
md5sums=('b93e5fe9757db779a75cd3ce97b9613d' '188e94890f8826c26bc16ed59563a78a'\
         'd7c1c0dfc2e183f162162ef619c61f24')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-1b4bf389.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
