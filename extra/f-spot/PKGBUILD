# $Id: PKGBUILD 44538 2009-07-02 17:18:52Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=f-spot
pkgver=0.5.0.3
pkgrel=3
pkgdesc="A personal photo management program for GNOME"
url="http://www.gnome.org/projects/f-spot/"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-desktop-sharp>=2.26.0' 'ndesk-dbus-glib>=0.4.1' 'lcms>=1.18-2' 'libexif' 'libgphoto2>=2.4.6-2' 'sqlite3>=3.6.15' 'libgnomeui>=2.24.1' 'desktop-file-utils')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.16.0' 'gnome-screensaver>=2.26.1')
options=('!libtool' '!makeflags')
install=f-spot.install
source=(http://ftp.gnome.org/pub/gnome/sources/f-spot/0.5/f-spot-${pkgver}.tar.bz2)
md5sums=('52db86f1dc715a3958425aa3d006c900')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain f-spot ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
