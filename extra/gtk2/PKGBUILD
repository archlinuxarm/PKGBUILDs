# $Id: PKGBUILD 58516 2009-11-08 21:24:09Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk2
pkgver=2.18.3
pkgrel=2
pkgdesc="The GTK+ Toolkit (v2)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk2.install
depends=('atk>=1.28.0' 'pango>=1.26.0' 'libxcursor' 'libxinerama' 'libxrandr>=1.3.0' 'libxi>=1.2.1' 'libcups>=1.4.1' 'libxcomposite' 'libxdamage' 'heimdal>=1.2.1' 'gnutls>=2.8.3' 'shared-mime-info')
makedepends=('pkgconfig' 'gtk-doc')
replaces=('gtkprint-cups' 'gail')
conflicts=('gtkprint-cups' 'gail')
provides=('gail=1.22.3')
options=('!libtool' '!docs')
backup=(etc/gtk-2.0/gtkrc)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.18/gtk+-${pkgver}.tar.bz2
        libjpeg-7.patch
        xid-collision-debug.patch)
sha256sums=('1c0665fe560a560851e8b3c9a6501d416c4b54abfb6fa07549d703a04ea768bf'
            '67de89f864a8d10e617a9600025d5bd932ac22adca4cda314d470a2d72e0196e'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558')

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}/libjpeg-7.patch" || return 1
  patch -Np1 -i "${srcdir}/xid-collision-debug.patch" || return 1

  CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-xinput=yes \
      --without-libjasper \
      --with-included-loaders=png || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc" || return 1
}
