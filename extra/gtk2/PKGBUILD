# Maintainer: Philipp Scholl <pscholl@bawue.de>
# From ArchMobile.org

pkgname=gtk2
pkgver=2.18.6
pkgrel=1
pkgdesc="The GTK+ Toolkit (v2)"
arch=(arm)
url="http://www.gtk.org/"
install=gtk2.install
depends=('atk>=1.28.0' 'pango>=1.26.0' 'libxcursor' 'libxinerama' 'libxrandr>=1.3.0' \
         'libxi>=1.2.1' 'libcups>=1.4.2' 'libxcomposite' 'libxdamage' 'heimdal>=1.3.1' \
         'gnutls>=2.8.3' 'shared-mime-info')
makedepends=('pkgconfig' 'gtk-doc')
replaces=('gtkprint-cups' 'gail')
conflicts=('gtkprint-cups' 'gail')
provides=('gail=1.22.3')
options=('!libtool' '!docs')
backup=(etc/gtk-2.0/gtkrc)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.18/gtk+-${pkgver}.tar.bz2
        xid-collision-debug.patch)
sha256sums=('67a83c6b2e774bc20b2a0582fb7fad7b1e8d5e04ede39f299d614e365756d2a7'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558')

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}/xid-collision-debug.patch" || return 1

  CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-xinput=yes \
      --without-libjasper\
      --with-included-loaders=png || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc" || return 1
}
