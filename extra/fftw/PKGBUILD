# $Id: PKGBUILD 51381 2009-09-07 20:54:38Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fftw
pkgver=3.2.2
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.fftw.org/"
depends=('glibc' 'bash')
options=(!libtool)
source=(http://www.fftw.org/${pkgname}-${pkgver}.tar.gz)
install=fftw.install

# notes:
# http://www.fftw.org/fftw2_doc/fftw_6.html#SEC69
# http://www.fftw.org/faq/section2.html#singleprec
# http://www.fftw.org/fftw3_doc/Precision.html#Precision

md5sums=('b616e5c91218cc778b5aa735fefb61ae')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
 # build & install double precision
  ./configure F77=gfortran --prefix=/usr \
              --enable-shared || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  make clean 

  # build & install long double precission
  ./configure F77=gfortran --prefix=/usr \
   --enable-long-double  --enable-shared || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  make clean

  # build + install single precision
  ./configure F77=gfortran --prefix=/usr \
              --enable-float --enable-shared || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
