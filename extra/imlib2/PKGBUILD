# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-amd64 from configure

pkgname=imlib2
pkgver=1.12.0
pkgrel=1
pkgdesc='Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support'
url='https://sourceforge.net/projects/enlightenment/'
arch=('x86_64')
license=('BSD')
makedepends=(# Currently highway does provide a static library only, that libjxl links to.
             # This introduces a build dependency for now...
             'highway'
             'libheif' 'libid3tag' 'libjxl' 'librsvg' 'libspectre' 'libwebp' 'openjpeg2')
depends=('bzip2' 'freetype2' 'giflib' 'libjpeg-turbo' 'libpng' 'libtiff' 'libxext' 'xz')
optdepends=('libheif: HEIF loader (for AVIF)'
            'libid3tag: ID3 loader'
            'libjxl: JXL loader'
            'librsvg: SVG loader'
            'libspectre: PS loader'
            'libwebp: WEBP loader'
            'openjpeg2: J2K loader')
source=("https://downloads.sourceforge.net/project/enlightenment/imlib2-src/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('95ff5d4cc17dd9f934c2e7ad151c360f308880a0a7849a6ca43b7c7b9a4bb216')
sha512sums=('178e1f1a3b9812d6b1c4a389b5c4a7bf229ac68ae725eb1e39979aeab675e55a6e4d62b40e254effafd5279b5fd971795c19b06a822c3771ee8e6f46f1587d66')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/imlib2 \
    --x-libraries=/usr/lib

  make
}


check() {
  cd "${pkgname}-${pkgver}"
  make check
}


package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
