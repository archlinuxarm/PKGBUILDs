# Maintainer: George Rawlinson <grawlinson@archlinux.org>

# remove when bumped upstream

pkgname=python-audioop-lts
pkgver=0.2.2
pkgrel=2
pkgdesc="A LTS port of Python's 'audioop' module"
arch=(x86_64)
url='https://github.com/AbstractUmbra/audioop'
license=(PSF-2.0)
depends=(glibc python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
provides=(python-audioop)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('171d299538e62d68ac2c19449f7a296ddb660e572a07dc3e33a0373ab2c493f220dfaf2004822972a443a10afb64ba305b145e3fc269fbd348f2e41b5c19fdec')
b2sums=('e7143b62b9e8e1fa43964894f6c7dac02947fab4d0d8c33014c87e0b1304aba2e951ef4a840b1d58f56e03521bef460013149c7751c6b1812d945f24862b0081')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  # install to temporary directory
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl

  PYTHONPATH="$PWD/tmp_install$site_packages" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
