# $Id: PKGBUILD 45402 2009-07-11 01:09:58Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor : Markus Meissner <markus@meissna.de>

pkgname=glhack
pkgver=1.2
pkgrel=3
pkgdesc="A port of Nethack, a single player dungeon exploration game in 2D"
arch=('i686' 'x86_64')
url="http://glhack.sourceforge.net/"
license=('custom')
depends=('sdl' 'libpng' 'mesa')
options=('!makeflags')
install=glhack.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('b4c68e3ab689610ca638b7660c1b05bd')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|/usr/lib/games|/usr/share|' include/config.h || return 1
  sed -i 's|/var/lib/games/glhack|/var/games/glhack|' include/unixconf.h || return 1
  sed -i -e 's|PREFIX	 = /usr|PREFIX	 = $(DESTDIR)/usr|' Makefile || return 1
  sed -i -e 's|VARDIR   = /var/lib/games/glhack|VARDIR   = $(DESTDIR)/var/games/glhack|' Makefile || return 1
  sed -i -e 's|/usr/man/man6|$(DESTDIR)/usr/share/man/man6|' doc/Makefile || return 1
  sed -i -e 's|GAMEDIR  = $(PREFIX)/lib/games/$(GAME)|GAMEDIR  = $(PREFIX)/share/$(GAME)|' Makefile || return 1

  make   || return 1
  install -d "${pkgdir}"/usr/share/man/man{5,6}
  make DESTDIR="$pkgdir" install || return 1
  install -D -m644 dat/license "${pkgdir}/usr/share/licenses/${pkgname}/license"

  mv "${pkgdir}/usr/share/glhack/glhack" "${pkgdir}/usr/bin/glhack"
  mv "${pkgdir}/usr/share/glhack/recover_glhack" "${pkgdir}/usr/bin/recover_glhack"

  chown -R root:root "${pkgdir}/usr/share/glhack"

  rm -r "${pkgdir}"/var/games/glhack
  install -d "${pkgdir}/var/games/glhack/save"
  chmod -R 775 "${pkgdir}/var/games"
  chown -R root:games "${pkgdir}/var/games"
}
