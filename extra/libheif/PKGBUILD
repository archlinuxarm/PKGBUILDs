# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Adam Fontenot <fontenot@ucla.edu>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove make/opt depends on svt-av1, -DWITH_SvtEnc=OFF

pkgname=libheif
pkgver=1.21.2
pkgrel=1
pkgdesc='An HEIF and AVIF file format decoder and encoder'
arch=(x86_64)
url='https://github.com/strukturag/libheif'
license=(LGPL-3.0-or-later)
provides=('libheif.so')
makedepends=(cmake
             dav1d
             ffmpeg
             git
             libjpeg-turbo
             libpng
             libtiff
             openjpeg2
             rav1e)
depends=(aom
         gcc-libs
         glibc
         libde265
         libwebp
         openh264
         x264
         x265)
optdepends=('libjpeg-turbo: for heif-dec and heif-enc'
            'libpng: for heif-dec and heif-enc'
            'libtiff: for heif-dec and hei-enc'
            'dav1d: dav1d encoder'
            'ffmpeg: hardware decode'
            'openjpeg2: JPEG2000 decoder'
            'rav1e: rav1e encoder')
source=(git+https://github.com/strukturag/libheif#tag=v$pkgver)
sha256sums=('2eb27d784c24061f996af7d578af25282de08802d56bfaa674a1a3ae9e86acda')

build() {
  CXXFLAGS+=" -Wno-error=stringop-overflow"
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_DAV1D=ON \
    -DWITH_RAV1E=ON \
    -DWITH_FFMPEG_DECODER=ON \
    -DWITH_FFMPEG_DECODER_PLUGIN=ON \
    -DWITH_SvtEnc=OFF \
    -DWITH_OpenJPEG_DECODER=ON \
    -DWITH_GDK_PIXBUF=OFF \
    -DWITH_EXAMPLE_HEIF_THUMB=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
