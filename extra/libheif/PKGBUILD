# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Adam Fontenot <fontenot@ucla.edu>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove make/opt depends on svt-av1

pkgname=libheif
pkgver=1.16.2
pkgrel=3
pkgdesc='An HEIF and AVIF file format decoder and encoder'
arch=(x86_64)
url='https://github.com/strukturag/libheif'
license=(GPL3)
makedepends=(cmake
             gdk-pixbuf2
             libjpeg
             libpng
             rav1e)
depends=(aom
         gcc-libs
         glibc
         dav1d
         libde265
         x265)
optdepends=('libjpeg: for heif-convert and heif-enc'
            'libpng: for heif-convert and heif-enc'
            'rav1e: rav1e encoder')
source=(https://github.com/strukturag/libheif/releases/download/v$pkgver/libheif-$pkgver.tar.gz)
sha256sums=('7f97e4205c0bd9f9b8560536c8bd2e841d1c9a6d610401eb3eb87ed9cdfe78ea')

build() {
  cmake -B build -S $pkgname-$pkgver\
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DAOM_ENCODER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
