# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Adam Fontenot <fontenot@ucla.edu>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove make/opt depends on svt-av1, -DWITH_SvtEnc=OFF

pkgname=libheif
pkgver=1.19.8
pkgrel=1
pkgdesc='An HEIF and AVIF file format decoder and encoder'
arch=(x86_64)
url='https://github.com/strukturag/libheif'
license=(GPL3)
provides=('libheif.so')
makedepends=(cmake
             dav1d
             ffmpeg
             gdk-pixbuf2
             git
             libjpeg-turbo
             libpng
             rav1e)
depends=(aom
         gcc-libs
         glibc
         libde265
         libwebp
         x265)
optdepends=('libjpeg-turbo: for heif-convert and heif-enc'
            'libpng: for heif-convert and heif-enc'
            'dav1d: dav1d encoder'
            'ffmpeg: hardware decode'
            'rav1e: rav1e encoder')
source=(git+https://github.com/strukturag/libheif#tag=v$pkgver)
sha256sums=('b77dfccd82588bcce71615d55d731aca5452d36b70c4127102ac0798cf022970')

prepare() {
  cd "$pkgname"

  # https://gitlab.archlinux.org/archlinux/packaging/packages/libheif/-/issues/2
  git cherry-pick -n \
    '9839c993abdf61e8da28f5ee0f8bbfcf3d24c528'
}

build() {
  CXXFLAGS+=" -Wno-error=stringop-overflow"
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_DAV1D=ON \
    -DWITH_RAV1E=ON \
    -DWITH_FFMPEG_DECODER=ON \
    -DWITH_FFMPEG_DECODER_PLUGIN=ON \
    -DWITH_SvtEnc=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
