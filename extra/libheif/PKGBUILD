# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Adam Fontenot <fontenot@ucla.edu>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove make/opt depends on svt-av1, -DWITH_SvtEnc=OFF

pkgname=libheif
pkgver=1.20.0
pkgrel=1
pkgdesc='An HEIF and AVIF file format decoder and encoder'
arch=(x86_64)
url='https://github.com/strukturag/libheif'
license=(LGPL-3.0-or-later)
provides=('libheif.so')
makedepends=(cmake
             dav1d
             ffmpeg
             gdk-pixbuf2
             git
             libjpeg-turbo
             libpng
             libtiff
             rav1e)
depends=(aom
         gcc-libs
         glibc
         libde265
         libwebp
         x265)
optdepends=('libjpeg-turbo: for heif-convert and heif-enc'
            'libpng: for heif-convert and heif-enc'
            'dav1d: dav1d encoder'
            'ffmpeg: hardware decode'
            'rav1e: rav1e encoder')
source=(git+https://github.com/strukturag/libheif#tag=v$pkgver)
sha256sums=('c12d20fb53a2fdb9ddb6e1184ea2cdd8bb05ecb5cf7d373f768ac49af80f2fbd')

build() {
  CXXFLAGS+=" -Wno-error=stringop-overflow"
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_DAV1D=ON \
    -DWITH_RAV1E=ON \
    -DWITH_FFMPEG_DECODER=ON \
    -DWITH_FFMPEG_DECODER_PLUGIN=ON \
    -DWITH_SvtEnc=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
