# $ Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kdelibs3
pkgver=3.5.10
pkgrel=7
pkgdesc="KDE3 Core Libraries"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL' 'LGPL')
depends=('libxslt' 'pcre' 'libart-lgpl' 'alsa-lib' 'libcups>=1.3.10-3'
         'jasper>=1.900.1-3' 'bzip2' 'openssl' 'libidn' 'libxrender' 'openexr'
	 'aspell' 'heimdal' 'acl' 'libtiff' 'qt3>=3.3.8-12' 'ca-certificates')
makedepends=('pkgconfig' 'cups' 'hspell' 'autoconf<2.64')
options=('libtool')
install='kdelibs3.install'
source=("http://download.kde.org/stable/${pkgver}/src/kdelibs-${pkgver}.tar.bz2"
	'kde3.profile'
	'kde-3.5-1002164.patch'
	'kde4-compatibility.patch'
	'kconf_updaterc')
md5sums=('43cd55ed15f63b5738d620ef9f9fd568'
         'ab14ce2c40698e24e162284d50c6f466'
         '3632ec69d0866e8d6ff7fa2cfce76fb1'
         'f4e0735475b5de59d738d68a9f467b77'
         '11e49bc6aca497f55610146afe217751')

build() {
	cd $srcdir/kdelibs-${pkgver}

	. /etc/profile.d/qt3.sh
	. $srcdir/kde3.profile

	patch -p0 -i $srcdir/kde-3.5-1002164.patch || return 1
	patch -p1 -i $srcdir/kde4-compatibility.patch || return 1

	sed -iautomake -e 's|automake\*1.10\*|automake\*1.1[0-5]\*|' admin/cvs.sh
	sed -iautomake -e 's|automake\*1.10\*|automake\*1.1[0-5]\*|' admin/cvs.shautomake
	make -f admin/Makefile.common dist

	./configure --prefix=/opt/kde \
		--with-distribution='Arch Linux' \
		--with-alsa \
		--disable-debug \
		--disable-dnssd \
		--disable-dnotify \
		--enable-inotify \
		--enable-sendfile \
		--without-lua \
		--with-hspell \
		--enable-gcc-hidden-visibility \
		--enable-final \
		--enable-new-ldflags LDFLAGS="${LDFLAGS} -L/opt/qt/lib" \
		--without-arts \
		--disable-libfam || return 1
	make || return 1
}

package() {
	cd $srcdir/kdelibs-${pkgver}

	make DESTDIR=$pkgdir install || return 1

	# disable broken kconf_update by default
	install -D -m644 $srcdir/kconf_updaterc $pkgdir/opt/kde/share/config/kconf_updaterc

	# install KDE3 profile
	install -D -m755 $srcdir/kde3.profile $pkgdir/etc/profile.d/kde3.sh
	# make KDE3 styles available to Qt3
	install -d -m755 $pkgdir/opt/qt/plugins
	ln -sf /opt/kde/lib/kde3/plugins/styles $pkgdir/opt/qt/plugins/styles

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/opt/kde/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/opt/kde/share/apps/kssl/ca-bundle.crt

	# we don't have khelpcenter anyway
	rm -rf $pkgdir/opt/kde/share/doc
}
