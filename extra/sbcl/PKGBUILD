# $Id$
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Daniel White <daniel@whitehouse.id.au>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Leslie Polzer (skypher)

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable sb-thread, not supported on ARM

buildarch=4

pkgname=sbcl
pkgver=1.2.16
pkgrel=1
pkgdesc="Steel Bank Common Lisp"
url="http://www.sbcl.org/"
arch=('i686' 'x86_64')
license=('custom')
depends=('glibc' 'zlib')
provides=('common-lisp' 'cl-asdf')
makedepends=('sbcl')
install=sbcl.install
source=("http://downloads.sourceforge.net/project/sbcl/sbcl/$pkgver/$pkgname-$pkgver-source.tar.bz2"
  "arch-fixes.lisp")
sha1sums=('29b5c13cb24991d6735f11f3a6a0657207d6f1dd'
          '106f165fb929786730214438b92d65550c18b3cd')


build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS+=" -D_GNU_SOURCE -fno-omit-frame-pointer -DSBCL_HOME=/usr/lib/sbcl"
  export GNUMAKE="make"

  # build system uses LINKFLAGS and OS_LIBS to build LDFLAGS
  export LINKFLAGS="$LDFLAGS"
  unset LDFLAGS
  unset MAKEFLAGS
  
  # Make a multi-threaded SBCL, disable LARGEFILE
  cat >customize-target-features.lisp <<EOF
(lambda (features)
  (flet ((enable (x) (pushnew x features))
         (disable (x) (setf features (remove x features))))
  (disable :sb-thread)
  (enable :sb-core-compression)
  (disable :largefile)))
EOF

  sh make.sh sbcl --prefix=/usr
  make -C doc/manual info
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # cannot have both SBCL_HOME and INSTALL_ROOT
  SBCL_HOME="" INSTALL_ROOT="$pkgdir/usr" sh install.sh

  src/runtime/sbcl --core output/sbcl.core --script "${srcdir}/arch-fixes.lisp"
  mv sbcl-new.core "${pkgdir}/usr/lib/sbcl/sbcl.core"

  # sources
  mkdir -p "$pkgdir/usr/share/sbcl-source"
  cp -R -t "$pkgdir/usr/share/sbcl-source" "$srcdir/$pkgname-$pkgver/"{src,contrib}

  # license
  install -D -m644 "$srcdir/$pkgname-$pkgver/COPYING" \
                   "$pkgdir/usr/share/licenses/$pkgname/license.txt"

  # drop unwanted files
  find "$pkgdir" \( -name Makefile -o -name .cvsignore \) -delete
  find "$pkgdir/usr/share/sbcl-source" -type f \
    \( -name \*.fasl -o -name \*.o -o -name \*.log -o -name \*.so -o -name a.out \) -delete

  rm "$pkgdir/usr/share/sbcl-source/src/runtime/sbcl"{,.nm}

}
