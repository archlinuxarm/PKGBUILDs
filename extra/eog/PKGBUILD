# $Id: PKGBUILD 44535 2009-07-02 17:09:40Z jgc $
# Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=2.26.3
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-desktop>=2.26.3' 'libexif>=0.6.17' 'lcms>=1.18-2' 'desktop-file-utils' 'gnome-icon-theme>=2.26.0' 'exempi>=2.1.0' 'libart-lgpl')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.16.0' 'intltool')
install=eog.install
groups=('gnome-extra')
options=(!emptydirs)
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0660a0ce0fa4a46656fad84d61895917')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain eog ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
