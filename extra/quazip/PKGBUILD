# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly set QT 5 version, doesn't auto-detect correctly

pkgbase=quazip
pkgname=(quazip-qt5 quazip-qt6)
pkgver=1.5
pkgrel=1
pkgdesc='C++ wrapper for the ZIP/UNZIP C package'
url='https://stachenov.github.io/quazip/'
license=(LGPL-2.1-or-later)
arch=(x86_64)
depends=(bzip2
         gcc-libs
         glibc
         zlib)
makedepends=(cmake
             git
             qt5-base
             qt6-5compat)
source=(git+https://github.com/stachenov/quazip#tag=v$pkgver)
sha256sums=('2929e71afe9535b7b6c6c3203534c258d6abe5d43627a5e33e68ca75ad883695')

build() {
  cmake -B build5 -S $pkgbase \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQUAZIP_QT_MAJOR_VERSION=5
  cmake --build build5

  cmake -B build6 -S $pkgbase \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQUAZIP_QT_MAJOR_VERSION=6
  cmake --build build6
}

package_quazip-qt5() {
  depends+=(qt5-base)
  provides=(quazip)
  replaces=(quazip)

  DESTDIR="$pkgdir" cmake --install build5
}

package_quazip-qt6() {
  depends+=(qt6-5compat
            qt6-base)

  DESTDIR="$pkgdir" cmake --install build6
}
