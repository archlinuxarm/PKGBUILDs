# $Id: PKGBUILD 20950 2008-12-08 05:32:07Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributer: Sean Middleditch <elanthis@awesomeplay.com>

pkgname=docbook-xsl
pkgver=1.74.0
pkgrel=1
pkgdesc="XML stylesheets for Docbook-xml transformations."
arch=(i686 x86_64)
license=('custom')
url="http://scrollkeeper.sourceforge.net/docbook.shtml"
depends=('libxml2' 'libxslt' 'docbook-xml')
source=(http://downloads.sourceforge.net/sourceforge/docbook/docbook-xsl-${pkgver}.tar.bz2)
install=docbook-xsl.install
md5sums=('a56b598decd006d5d39e4544c0bfbc6a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  mkdir -p ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/common
  install -m644 common/*.{xml,xsl,dtd,ent} ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/common/

  mkdir -p ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/params
  install -m644 params/*.xml ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/params/

  for fn in fo highlighting html roundtrip; do
    mkdir -p ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}
    install -m644 ${fn}/*.{xml,xsl} ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}/
  done
  for fn in eclipse htmlhelp javahelp lib manpages profiling template website xhtml; do
    mkdir -p ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}
    install -m644 ${fn}/*.xsl ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/${fn}/
  done
  mkdir -p ${pkgdir}/etc/xml
  install -m644 VERSION ${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}/

  install -m644 -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
