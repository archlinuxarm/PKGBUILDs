# $Id: PKGBUILD 44738 2009-07-03 19:41:57Z jgc $
# Maintainer: dale <dale@archlinux.org>

pkgname=epiphany
pkgver=2.26.3
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the mozilla rendering engine."
arch=(i686 x86_64)
license=('GPL')
depends=('xulrunner>=1.9.1' 'gnome-desktop>=2.26.3' 'iso-codes>=3.10.1' 'desktop-file-utils' 'libxslt>=1.1.24' 'enchant>=1.4.2' 'libnotify>=0.4.5' 'libcanberra' 'libgnomeui>=2.24.1' 'python>=2.6')
makedepends=('gnome-doc-utils>=0.16.0' 'gnome-python>=2.26.1' 'pkgconfig' 'networkmanager' 'intltool')
optdepends=('gnome-python: python plugins')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('16f44012bc376077e1443e049d725847')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-python \
      --enable-certificate-manager \
      --enable-dbus \
      --with-gecko=libxul-embedding \
      --disable-scrollkeeper \
      --enable-network-manager \
      --enable-spell-checker || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain epiphany ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
