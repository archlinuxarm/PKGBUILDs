# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop depend on libavif so v5 can build

pkgname=gd
pkgver=2.3.3
pkgrel=1
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('x86_64')
url="https://libgd.github.io/"
license=('custom')
depends=('fontconfig' 'libxpm' 'libwebp' 'libheif')
optdepends=('perl: bdftogd script')
checkdepends=('ttf-liberation')
source=("https://github.com/libgd/libgd/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('24429f9d0dbe0f865aaa4b1a63558242396ba9134e6cfd32ca5e486a84483350')

build() {
  cd libgd-${pkgname}-${pkgver}

  ./bootstrap.sh
  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

check() {
  cd libgd-${pkgname}-${pkgver}

  TMP=$(mktemp -d) make check
}

package() {
  cd libgd-${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
