# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=r
pkgver=2.9.2
pkgrel=1
pkgdesc="R is a language and environment for statistical computing and graphics"
arch=('arm')
license=('GPL')
url=('http://www.r-project.org/')
depends=('blas>=20070405' 'lapack>=3.2' 'bzip2'  'libpng' 'libjpeg>=7' 'libtiff>=3.8.2-5' 'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs>=4.3.3' 'tk' 'libxt' 'libxmu' 'pango')
makedepends=('openjdk6')
source=(http://cran.r-project.org/src/base/R-2/R-${pkgver}.tar.gz
	r.desktop
	r.png)

md5sums=('112e2a1306cf71320e45d14e87e5b913'
         'f6d54d32e510d90c748a0d16d2abcbdc'
         '00659f39e90627fe87f1645df5d4e3a7')

build() {
   cd ${srcdir}/R-${pkgver}
   sed -i 's|#define NeedFunctionPrototypes 0|#define NeedFunctionPrototypes 1|g' src/modules/X11/dataentry.c || return 1
   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/R/docs/ \
                --with-gnu-ld \
		--with-readline \
                --with-x \
                --with-zlib \
                --with-bzlib  \
                --with-pcre \
		--enable-R-shlib \
                --with-lapack \
		--with-blas \
                F77=gfortran \
		LIBnn=lib
   make || return 1
   make -j1 DESTDIR=${pkgdir} install || return 1

   #  Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" ${pkgdir}/usr/bin/R
   rm ${pkgdir}/usr/lib/R/bin/R
   cd ${pkgdir}/usr/lib/R/bin
   ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/r.desktop \
	${pkgdir}/usr/share/applications/r.desktop || return 1
  install -Dm644 ${srcdir}/r.png \
	${pkgdir}/usr/share/pixmaps/r.png || return 1
 }
