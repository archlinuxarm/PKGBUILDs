# $Id: PKGBUILD 36715 2009-04-27 00:23:26Z eric $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-digitaledge
pkgver=1.1.1
pkgrel=3
pkgdesc="X.Org Digital Edge Tablet input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'xorg-util-macros')
conflicts=('xorg-server<1.6.0')
groups=('xorg-input-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	git-5e7b6ec4.patch LICENSE)
md5sums=('4c27e0f3c6447e76df39a837fd46a00f' 'cf6907f42cb02de58213c09b757547fe'\
         '79cd41855c39c23c920e4774e12e0558')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-5e7b6ec4.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
