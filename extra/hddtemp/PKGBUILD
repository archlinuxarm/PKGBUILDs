# $Id: PKGBUILD 29127 2009-03-05 18:08:55Z andyrtr $
# Maintainer: Andrea Scarpino <bash@archlinux.it>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=hddtemp
pkgver=0.3.beta15.45
_origver=0.3-beta15
_patchver=45
pkgrel=2
pkgdesc="Gives you the temperature of your hard drive by reading S.M.A.R.T. information"
arch=('i686' 'x86_64')
url="http://www.guzu.net/linux/hddtemp.php"
license=('GPL')
depends=('glibc')
backup=('etc/conf.d/hddtemp')
source=(http://www.guzu.net/files/hddtemp-${_origver}.tar.bz2
        http://ftp.debian.org/debian/pool/main/h/hddtemp/hddtemp_${_origver}-${_patchver}.diff.gz
        'hddtemp' 'hddtemp.confd')
md5sums=('8b829339e1ae9df701684ec239021bb8'
         'a809a3a28d29ee719b2855af9b4eb1a8'
         'b7ebb86dfeef3bc4577ed1040307e36b'
         'fdca5c43900406f0be76534ef2a5a697')
options=('!libtool')

build() {
  cd "$srcdir/$pkgname-${_origver}"

  patch -Np1 -i "$srcdir/${pkgname}_${_origver}-${_patchver}.diff" || return 1

  ./configure --prefix=/usr --mandir=/usr/share/man --with-db-path=/usr/share/$pkgname/hddtemp.db
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

  install -D -m644 "$srcdir/$pkgname-${_origver}/debian/hddtemp.db" \
    "$pkgdir/usr/share/${pkgname}/hddtemp.db" || return 1
  install -D -m644 "$srcdir/hddtemp.confd" "$pkgdir/etc/conf.d/hddtemp" || return 1
  install -D -m755 "$srcdir/hddtemp" "$pkgdir/etc/rc.d/hddtemp" || return 1
}
