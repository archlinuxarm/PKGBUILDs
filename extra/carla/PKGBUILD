# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

pkgname=carla
pkgver=2.5.10
pkgrel=2
pkgdesc="Audio Plugin Host"
arch=(x86_64)
url="https://kxstudio.linuxaudio.org/Applications:Carla"
_url="https://github.com/falktx/carla"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libx11
  python
  python-pyqt5
  python-pyliblo3
  python-rdflib
  qt5-base
  qt5-svg
  sdl2
  sh
  which
)
makedepends=(
  alsa-lib
  file
  fluidsynth
  freetype2
  git
  libglvnd
  liblo
  libpulse
  libsndfile
  python-requests
  python-tornado
  qt5-tools
)
optdepends=(
  'jack: for using carla with JACK'
  'lv2-host: for the LV2 plugin'
  'vst-host: for the VST plugin'
  'python-requests: for qtweb backend'
  'python-tornado: for MOD UI'
)
provides=(
  dssi-host
  ladspa-host
  lv2-host
  vst-host
  vst3-host
)
source=("git+$_url.git?signed#tag=v$pkgver")
sha512sums=('76b26b957a2a7b4f73253401b7478c10b29d5ca4fcb643798321c43c2883b5a3d2e37439858d008aa1dddaf9a565ccb0f256f03d4ed5dd5208a1edf5b45dded4')
b2sums=('4b07cabda9926f5b4e7d3edf8fc58a4624ed6fb84703430caf67d3a237ae94d4233947315f016e45a2e9e6e91080d534e4c9272d488f2b850a5cd85b26730402')
validpgpkeys=(62B11043D2F6EB6672D93103CDBAA37ABC74FBA0)  # falkTX <falktx@falktx.com>

pkgver() {
  cd $pkgname
  git describe --tag | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n 9370483b0a278eab6462c33b16e53377f7fffc6c  # fix issue with liblo API breakage
}

build() {
  make NOOPT=true features -C $pkgname
  make NOOPT=true -C $pkgname
}

package() {
  depends+=(
    alsa-lib libasound.so
    file libmagic.so
    fluidsynth libfluidsynth.so
    freetype2 libfreetype.so
    libglvnd libGL.so
    liblo liblo.so
    libpulse libpulse.so libpulse-simple.so
    libsndfile libsndfile.so
  )

  make NOOPT=true DEFAULT_QT=5 DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  find "$pkgdir" -type f -iname "*.so" -exec chmod +x {} +
  mv "$pkgdir"/usr/share/{appdata,metainfo}
}
# vim:set ts=2 sw=2 et:
