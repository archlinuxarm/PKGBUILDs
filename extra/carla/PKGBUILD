# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

pkgname=carla
pkgver=2.5.8
pkgrel=2
pkgdesc="Audio Plugin Host"
arch=(x86_64)
url="https://kxstudio.linuxaudio.org/Applications:Carla"
_url="https://github.com/falktx/carla"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libx11
  python
  python-pyqt5
  python-pyliblo
  python-rdflib
  qt5-base
  qt5-svg
  sdl2
  sh
)
makedepends=(
  alsa-lib
  file
  fluidsynth
  freetype2
  git
  libglvnd
  liblo
  libpulse
  libsndfile
  python-requests
  python-tornado
  qt5-tools
)
optdepends=(
  'jack: for using carla with JACK'
  'lv2-host: for the LV2 plugin'
  'vst-host: for the VST plugin'
  'python-requests: for qtweb backend'
  'python-tornado: for MOD UI'
)
provides=(
  dssi-host
  ladspa-host
  lv2-host
  vst-host
  vst3-host
)
source=("git+$_url.git?signed#tag=v$pkgver")
sha512sums=('6d05694f2395258f568caa444230cd652b3cc885d251f54b2cf77f43bfdfcc18bcb673a2dea05091a012349d746c84c9c4e07da323be8cfa93d9583cd459b9d4')
b2sums=('e6444987502854a27598422bbe7f49d7edf298fb3e09470e76ca8fcf30cca8bd997b1a5bcfa1a655df0773b3ece7cffa1bacfd9ef3c93f7bad89f14a376758a9')
validpgpkeys=(62B11043D2F6EB6672D93103CDBAA37ABC74FBA0)  # falkTX <falktx@falktx.com>

pkgver() {
  cd $pkgname
  git describe --tag | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n 9370483b0a278eab6462c33b16e53377f7fffc6c  # fix issue with liblo API breakage
}

build() {
  make NOOPT=true features -C $pkgname
  make NOOPT=true -C $pkgname
}

package() {
  depends+=(
    alsa-lib libasound.so
    file libmagic.so
    fluidsynth libfluidsynth.so
    freetype2 libfreetype.so
    libglvnd libGL.so
    liblo liblo.so
    libpulse libpulse.so libpulse-simple.so
    libsndfile libsndfile.so
  )

  make NOOPT=true DEFAULT_QT=5 DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  find "$pkgdir" -type f -iname "*.so" -exec chmod +x {} +
}
# vim:set ts=2 sw=2 et:
