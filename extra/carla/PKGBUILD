# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

pkgname=carla
pkgver=2.5.9
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=(x86_64)
url="https://kxstudio.linuxaudio.org/Applications:Carla"
_url="https://github.com/falktx/carla"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libx11
  python
  python-pyqt5
  python-pyliblo
  python-rdflib
  qt5-base
  qt5-svg
  sdl2
  sh
  which
)
makedepends=(
  alsa-lib
  file
  fluidsynth
  freetype2
  git
  libglvnd
  liblo
  libpulse
  libsndfile
  python-requests
  python-tornado
  qt5-tools
)
optdepends=(
  'jack: for using carla with JACK'
  'lv2-host: for the LV2 plugin'
  'vst-host: for the VST plugin'
  'python-requests: for qtweb backend'
  'python-tornado: for MOD UI'
)
provides=(
  dssi-host
  ladspa-host
  lv2-host
  vst-host
  vst3-host
)
source=("git+$_url.git?signed#tag=v$pkgver")
sha512sums=('880d28b57a22c825067ad18558218838684132e033f1c44bb18e4adff61cdc0a070c3bf29320a87124ff7b5da5ead6b5540b7b9adcf4e17ab5800709dbcea7aa')
b2sums=('f91e3b3f35e0b9dd43e48dc07a5517f04ce74def6a120c70771b85dc77e8cea68bc61d3ff9d90682a77060f7e48a0a22dee0723557bb329442b12dab1b4e1424')
validpgpkeys=(62B11043D2F6EB6672D93103CDBAA37ABC74FBA0)  # falkTX <falktx@falktx.com>

pkgver() {
  cd $pkgname
  git describe --tag | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n 9370483b0a278eab6462c33b16e53377f7fffc6c  # fix issue with liblo API breakage
}

build() {
  make NOOPT=true features -C $pkgname
  make NOOPT=true -C $pkgname
}

package() {
  depends+=(
    alsa-lib libasound.so
    file libmagic.so
    fluidsynth libfluidsynth.so
    freetype2 libfreetype.so
    libglvnd libGL.so
    liblo liblo.so
    libpulse libpulse.so libpulse-simple.so
    libsndfile libsndfile.so
  )

  make NOOPT=true DEFAULT_QT=5 DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  find "$pkgdir" -type f -iname "*.so" -exec chmod +x {} +
  mv "$pkgdir"/usr/share/{appdata,metainfo}
}
# vim:set ts=2 sw=2 et:
