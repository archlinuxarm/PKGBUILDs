# $Id: PKGBUILD 149986 2012-02-12 02:54:27Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepends on nasm
#  - added --without-simd to configure, no NEON in main repositories yet

plugrel=1

pkgname=libjpeg-turbo
pkgver=1.2.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('glibc')
#makedepends=('nasm')
provides=('libjpeg=8.0.2')
conflicts=('libjpeg')
replaces=('libjpeg')
options=('!libtool')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz)
sha1sums=('a4992e102c6d88146709e8e6ce5896d5d0b5a361')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # put documentation in a reasonable directory...
  sed -i "/docdir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in
  sed -i "/exampledir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in

  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man --without-simd
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h "$pkgdir/usr/include/"

  # do not distributre libturbojpeg as it is unversioned
  rm "$pkgdir"/usr/lib/libturbojpeg.{so,a}
  rm "$pkgdir/usr/include/turbojpeg.h"
  rm "$pkgdir/usr/bin/tjbench"

  install -dm755 "$pkgdir/usr/share/licenses/libjpeg-turbo/"
  ln -s ../../doc/libjpeg-turbo/README \
    "$pkgdir/usr/share/licenses/libjpeg-turbo/README"
  ln -s ../../doc/libjpeg-turbo/README-turbo.txt \
    "$pkgdir/usr/share/licenses/libjpeg-turbo/README-turbo.txt"
}
