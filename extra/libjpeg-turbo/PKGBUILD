# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepends on nasm and java, -DWITH_JAVA=OFF

pkgname=libjpeg-turbo
pkgver=2.1.3
pkgrel=2
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=(x86_64)
url="https://libjpeg-turbo.org/"
license=(BSD)
depends=(glibc)
makedepends=(cmake)
provides=(libjpeg libjpeg.so libturbojpeg.so)
options=(debug)
source=(https://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('975c9835de7d70c6c8c5a1959adfa18f7f956bc9946fde8f1b40c1d853245c38a3118ac06b93841ec3e90be6945c38d687d062b371496c2ee9b5638d7f60418d'
            'SKIP')
b2sums=('87ef47105e66b2ee918e95962ac59963b6adea5b49ab0db692810aee0a13a215a0ef7fe0b549ecd5151274549e12d09eb0b42439bf0a4bc1ad2c4e62dcceb77c'
        'SKIP')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_BUILD_TYPE=None \
        -DENABLE_STATIC=OFF \
        -DWITH_JAVA=OFF \
        -DWITH_JPEG8=ON \
        -W no-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  make test -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" docdir=/usr/share/doc/$pkgname exampledir=/usr/share/doc/$pkgname install -C build

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s ../../doc/$pkgname/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname"
  # header required by some dependants
  # https://bugs.archlinux.org/task/24787
  install -vDm 644 $pkgname-$pkgver/jpegint.h "$pkgdir/usr/include"
}
