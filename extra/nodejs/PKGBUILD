# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

# ALARM: Kevin Mihelich <kevin@archinuxarm.org>
#  - build v7 with vfpv3, neon FTBFS without -flax-vector-conversions and breaks https

pkgname=nodejs
pkgver=25.7.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript ("Current" release)'
arch=('x86_64')
url='https://nodejs.org/'
license=('MIT')
depends=(
#  'ada'
  'brotli'
  'c-ares'
  'icu'
  'libnghttp2'
  'libnghttp3'
  'libngtcp2'
  'libuv'
#  'llhttp'
  'openssl'
  'simdjson'
#  'simdutf'
#  'v8'
  'zlib'
)
makedepends=(
  'git'
  'procps-ng'
  'python'
)
optdepends=('npm: nodejs package manager')
options=('!lto')
source=(
  "git+https://github.com/nodejs/node.git#tag=v$pkgver?signed"
  "update-icu-tests.patch")

sha512sums=('972c32814b34111ed750b06fef9a0033403799ac650a4cb9645bd45242738562d4b4ff377d3a6bc41eb0ddcf2d90a3c6d8c02a0fcedf9418af5f5891e55127da'
            'ab2edd1d99a6bb53f8f1f2cef8a3155dd1a44dda181040543ded1aef8775461676bba24470e8f2f9ffc5c36a6acdc77ecb2ea8492eda6bad382c9e0a06dca446')
validpgpkeys=(
  '8FCCA13FEF1D0C2E91008E09770F7A9A5AE15600' # Michaël Zasso (Targos) <targos@protonmail.com>
  '890C08DB8579162FEE0DF9DB8BEAB4DFCF555EF4' # RafaelGSS <rafael.nunu@hotmail.com>
  'C82FA3AE1CBEDC6BE46B9360C43CEC45C17AB93C' # Richard Lau <rlau@redhat.com>
  'C0D6248439F1D5604AAFFB4021D900FFDB233756' # Antoine du Hamel <duhamelantoine1995@gmail.com>
  '5BE8A3F6C8A5C01D106C0AD820B1A390B168D356' # Antoine du Hamel <antoine.duhamel@rosa.be>
  '108F52B48DB57BB0CC439B2997B01419BD92F80A' # Ruy Adorno <ruyadorno@hotmail.com>
)

_set_flags() {
  [[ $CARCH == "armv7h" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/neon/vfpv3/'` && CXXFLAGS="$CFLAGS"
  # /usr/lib/libnode.so uses malloc_usable_size, which is incompatible with fortification level 3
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
}

prepare() {
  cd node
  # Update ICU tests https://github.com/nodejs/node/pull/60523
  patch -Np1 -i ../update-icu-tests.patch
}

build() {
  _set_flags
  cd node

  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-brotli \
    --shared-cares \
    --shared-libuv \
    --shared-nghttp2 \
    --shared-nghttp3 \
    --shared-ngtcp2 \
    --shared-openssl \
    --shared-simdjson \
    --shared-zlib
    # --shared-ada
    # --shared-http-parser
    # --shared-simdutf
    # --shared-v8

  make
}

check() {
  _set_flags
  cd node
  rm test/parallel/test-http-outgoing-end-cork.js
  rm test/parallel/test-http2-client-set-priority.js
  rm test/parallel/test-http2-client-unescaped-path.js
  rm test/parallel/test-http2-max-invalid-frames.js
  rm test/parallel/test-http2-misbehaving-flow-control.js
  rm test/parallel/test-http2-misbehaving-flow-control-paused.js
  rm test/parallel/test-http2-multi-content-length.js
  rm test/parallel/test-http2-priority-event.js
  rm test/parallel/test-http2-reset-flood.js
  rm test/parallel/test-tls-ocsp-callback.js

  # https://github.com/nodejs/node/pull/60523
  rm test/parallel/test-datetime-change-notify.js

  make test-only
}

package() {
  _set_flags
  cd node
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
