# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.3.2
pkgrel=1
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs-experimental' 'oxygen-icons' 'xorg-xauth'
         'hicolor-icon-theme' 'smbclient' 'rarian')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'openslp' 'xine-lib')
optdepends=('htdig: to build the search index'
            'gdb: drkonq crash handler')
install='kdebase-runtime.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'nepomukserverrc')
md5sums=('b0dfe65bc6d18c259c236889f92e2817'
         '6505676fcc075625733e4251e0ec9d43')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_PulseAudio=OFF
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
	rm -f ${pkgdir}/usr/share/icons/hicolor/index.theme
	ln -sf /usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/
	install -D -m644 ${srcdir}/nepomukserverrc ${pkgdir}/usr/share/config/nepomukserverrc
}
