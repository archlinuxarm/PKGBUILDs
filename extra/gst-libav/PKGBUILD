# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepend on valgrind

pkgname=gst-libav
pkgver=1.12.3+1+ge87c20d
pkgrel=1
pkgdesc="GStreamer Multimedia Framework ffmpeg Plugin"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(GPL)
depends=(gst-plugins-base-libs bzip2 ffmpeg)
makedepends=(python gtk-doc autoconf-archive git yasm)
provides=("gst-ffmpeg=$pkgver-$pkgrel")
_commit=e87c20d325eaf9e6813c46654292d68f8b9bc521  # 1.12
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-libav#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common"
        "git+https://git.videolan.org/git/ffmpeg")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # BRSTM/BFSTM
  git cherry-pick -n a771795005fe5ebc06d788a4520e80890685aadc

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git config --local submodule.gst-libs/ext/libav.url "$srcdir/ffmpeg"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer ffmpeg Plugin (Arch Linux)" \
    --with-package-origin="https://www.archlinux.org/" \
    --without-system-libav --with-libav-extra-configure="--enable-runtime-cpudetect" \
    --enable-experimental --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
