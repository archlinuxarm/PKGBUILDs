# $Id: PKGBUILD 39818 2009-05-21 14:08:51Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=poppler-qt3
pkgver=0.10.7
pkgrel=1
pkgdesc="Poppler Qt3 bindings"
arch=(i686 x86_64)
license=('GPL')
depends=('qt3>=3.3.8' "poppler>=${pkgver}")
makedepends=('pkgconfig')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/poppler-${pkgver}.tar.gz
	poppler-0.10-bindings.patch)
md5sums=('ba120e49a82567d85d00e159011ebe67'
         '3f828a8372687c6c0ca88925ff4f5036')

build() {
  . /etc/profile.d/qt3.sh
  
  cd "${srcdir}/poppler-${pkgver}"

  patch -Np1 -i "${srcdir}/poppler-0.10-bindings.patch" || return 1
  libtoolize --force --copy || return 1
  AT_M4DIR="m4" autoreconf -i || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-zlib \
	      --enable-libjpeg \
	      --enable-cairo-output \
	      --enable-poppler-qt \
	      --disable-poppler-glib \
	      --disable-gtk-test \
	      --disable-poppler-qt4
  sed -i -e 's|^LDFLAGS =|LDFLAGS = -L/opt/qt/lib -lqt-mt|' qt/Makefile
  pushd poppler || return 1
  make libpoppler-cairo.la || return 1
  popd
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
