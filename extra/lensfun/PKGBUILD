# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable sse, sse2

pkgname=lensfun
pkgver=0.3.3
pkgrel=1
epoch=1
pkgdesc='Database of photographic lenses and a library that allows advanced access to the database'
arch=(x86_64)
url='https://lensfun.github.io/'
license=(LGPL3)
depends=(glib2)
makedepends=(python libpng doxygen cmake)
optdepends=('python: for lensfun-update-data and lensfun-add-adapter')
source=(https://github.com/lensfun/lensfun/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('57ba5a0377f24948972339e18be946af12eda22b7c707eb0ddd26586370f6765')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -fno-delete-null-pointer-checks" \
    -DBUILD_FOR_SSE=off \
    -DBUILD_FOR_SSE2=off
  cmake --build build  
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
