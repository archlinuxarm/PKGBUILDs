# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable sse, sse2

pkgname=lensfun
pkgver=0.3.4
pkgrel=2
epoch=1
pkgdesc='Database of photographic lenses and a library that allows advanced access to the database'
arch=(x86_64)
url='https://lensfun.github.io/'
license=(LGPL3)
depends=(glib2)
makedepends=(python-setuptools python-build python-installer python-wheel libpng doxygen cmake)
optdepends=('python: for lensfun-update-data and lensfun-add-adapter')
source=(https://github.com/lensfun/lensfun/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('dafb39c08ef24a0e2abd00d05d7341b1bf1f0c38bfcd5a4c69cf5f0ecb6db112')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -fno-delete-null-pointer-checks" \
    -DBUILD_FOR_SSE=off \
    -DBUILD_FOR_SSE2=off
  cmake --build build
  cd build/apps
  python -m build --wheel --no-isolation
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  cd build/apps
  python -m installer --destdir="$pkgdir" dist/*.whl
}
