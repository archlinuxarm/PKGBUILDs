# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable sse, sse2

pkgname=lensfun
pkgver=0.3.2
pkgrel=3
pkgdesc="Database of photographic lenses and a library that allows advanced access to the database"
arch=(i686 x86_64)
url="http://lensfun.sourceforge.net"
license=('LGPL3')
depends=('glibc' 'glib2')
makedepends=('python' 'libpng' 'doxygen' 'cmake')
source=("http://sourceforge.net/projects/lensfun/files/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        lensfun-fix-python-install-dir.patch)
sha1sums=('1d978b15aa7304d66a4931fa37ca9f8f89396c16'
          '7bf2b17d0fb85800857eaf990541ea9eccca2f77')

prepare() {
    cd $pkgname-$pkgver
  # Fix python module install path
    patch -p1 -i ../lensfun-fix-python-install-dir.patch
}

build() {
    cd ${pkgname}-${pkgver}
    cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_FOR_SSE=off \
      -DBUILD_FOR_SSE2=off
    make all
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
}
