# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable sse, sse2

pkgname=lensfun
pkgver=0.3.95
pkgrel=2
pkgdesc="Database of photographic lenses and a library that allows advanced access to the database"
arch=(x86_64)
url="https://lensfun.github.io/"
license=('LGPL3')
depends=('glib2')
makedepends=('python' 'libpng' 'doxygen' 'cmake')
optdepends=('python: for lensfun-update-data and lensfun-add-adapter')
source=("https://sourceforge.net/projects/lensfun/files/${pkgver}/${pkgname}-${pkgver}.tar.gz"
         lensfun-segfault.patch)
sha256sums=('82c29c833c1604c48ca3ab8a35e86b7189b8effac1b1476095c0529afb702808'
            '090804382c134b813693770383d21289a74f2b7e221127f2634abf3d24b003da')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../lensfun-segfault.patch # Fix null pointer dereference # https://github.com/lensfun/lensfun/issues/1132
}

build() {
    cd $pkgname-$pkgver
    cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fno-delete-null-pointer-checks" \
      -DBUILD_FOR_SSE=off \
      -DBUILD_FOR_SSE2=off
    make all
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
