# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# ALARM:  Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon for !aarch64

pkgname=mpg123
pkgver=1.29.2
pkgrel=1
pkgdesc='Console based real time MPEG Audio Player for Layer 1, 2 and 3'
url="https://sourceforge.net/projects/mpg123"
arch=('x86_64')
license=('LGPL2.1')
depends=('libltdl' 'alsa-lib')
makedepends=('sdl' 'jack' 'libpulse')
optdepends=('sdl: for sdl audio support'
            'jack: for jack audio support'
            'libpulse: for pulse audio support'
            'perl: for conplay')
provides=(libmpg123.so libout123.so)
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('ffb82ffbebedeb12783338b5159bf055afd25cb77e1b705bef29f04fa50bcb2ceaf2a6418d0e111fab1151ea956fe48ba3576d978e6b0c8f4ca72c3883608ec0'
            'SKIP')
validpgpkeys=('D021FF8ECF4BE09719D61A27231C4CBC60D5CAFE')

build() {
  cd ${pkgname}-${pkgver}
  [[ $CARCH != "aarch64" ]] && CONFIG="--with-cpu=arm_nofpu"
  ./configure \
    --prefix=/usr \
    --enable-int-quality \
    --with-audio="alsa oss sdl jack pulse" $CONFIG
  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 scripts/conplay "${pkgdir}/usr/bin/conplay"
}

# vim: ts=2 sw=2 et:
