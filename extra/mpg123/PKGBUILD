# $Id: PKGBUILD 52029 2009-09-14 13:02:46Z hugo $
# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=mpg123
pkgver=1.9.0
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2')
depends=('glibc' 'libtool>=2.2' 'alsa-lib')
makedepends=('esound' 'sdl')
optdepends=('esound: for additional audio support' 'sdl: for additional audio support')
conflicts=('mpg321')
provides=('mpg321')
replaces=('mpg321')
options=('libtool')
source=(http://downloads.sourceforge.net/sourceforge/mpg123/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0ea1a5d45680c847a9594188f582d284')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-audio="alsa esd sdl" || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
