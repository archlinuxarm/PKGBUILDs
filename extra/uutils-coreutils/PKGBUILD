# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Maintainer: Filipe Laíns (ffy00) <lains@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove invalid AArch64 linker specification

_pkgname=coreutils
pkgname=uutils-$_pkgname
pkgver=0.6.0
pkgrel=2
pkgdesc='Cross-platform Rust rewrite of the GNU coreutils'
arch=('x86_64')
url='https://uutils.github.io/'
_url='https://github.com/uutils/coreutils'
license=('MIT')
depends=(
  glibc
  libgcc
  oniguruma
)
makedepends=(
  git
  rust
)
source=(
  $pkgname::git+$_url.git#tag=${pkgver}
)
b2sums=('d031a2e61d172f8c864a3ae4feb8899cd5ff89ddb01d80a7a114305e3c59abbf92be5c25109a51396b9d3a75874da17546cbc4bb92cd43a2d757a56adcebcba7')

# This line inside prepare does not work without chean build
export RUSTONIG_DYNAMIC_LIBONIG=1

check() {
  cd $pkgname
  make test \
    TEST_NO_FAIL_FAST="-- \
    --skip test_chgrp::test_from_with_invalid_group \
    --skip test_chmod::test_chmod_colored_output \
    --skip test_env::test_env_french \
    --skip test_error_messages_french_translation \
    --skip test_french_colored_error_messages \
    --skip test_help_messages_french_translation \
    --skip test_ls::test_localized_possible_values \
    --skip test_sort::test_argument_suggestion \
    --skip test_sort::test_clap_localization_help_message \
    --skip test_sort::test_clap_localization_invalid_value \
    --skip test_sort::test_clap_localization_unknown_argument \
    --skip test_sort::test_error_colors_disabled \
    --skip test_sort::test_error_colors_enabled \
    --skip test_sort::test_french_translations \
    --skip test_sort::test_help_colors_disabled \
    --skip test_sort::test_help_colors_enabled \
    --skip test_df::test_type_option_with_file \
    --skip test_cat::test_cat_broken_pipe_nonzero_and_message \
    --skip test_logname::test_normal \
    --skip test_who::test_boot \
    --skip test_stdbuf"
}

package() {
  cd $pkgname
  # remove invalid AArch64 linker specification
  sed -i '/aarch64/d' .cargo/config.toml
  make install \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    LIBSTDBUF_DIR=/usr/lib/$pkgname \
    PROG_PREFIX=uu- \
    PROFILE=release-fast \
    MULTICALL=y
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
