# $Id: PKGBUILD 64786 2010-01-22 07:44:39Z eric $
# Maintainer: Paul Mattal <paul@archlinux.org>
pkgname=tightvnc
pkgver=1.3.10
pkgrel=5
pkgdesc="VNC Unix server && viewer"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.tightvnc.com"
depends=('libjpeg>=7' 'zlib' 'libxaw' 'libxp' 'xorg-xauth')
makedepends=('imake')
conflicts=('vnc')
options=(!makeflags)
source=(http://downloads.sf.net/sourceforge/vnc-tight/$pkgname-${pkgver}_unixsrc.tar.bz2 
        tightvnc-fontpath-xcolors.patch
	LinuxMachineDefines-arm.patch)

build() {
  cd $srcdir/vnc_unixsrc || return 1
  patch -Np1 -i ../tightvnc-fontpath-xcolors.patch || return 1
  sed -i 's|/usr/local/|/usr/share/|' vncserver || return 1
# The following patch allows the package to compile on ARM. It may not
# be the best way to do it, but it works. A more elegant solution
# is welcomed.
  patch -Np1 -i ../LinuxMachineDefines-arm.patch || return 1

  xmkmf || return 1
  make World || return 1
  cd Xvnc || return 1
  ./configure || return 1
  sed 's|PROTO_DEFINES =|PROTO_DEFINES = -D_XOPEN_SOURCE=500L|' \
  	-i programs/Xserver/os/Makefile lib/font/fc/Makefile || return 1
  make || return 1
  cd .. || return 1
  mkdir -p $pkgdir/usr/bin $pkgdir/usr/share/man/man1 || return 1
  ./vncinstall $pkgdir/usr/bin $pkgdir/usr/share/man || return 1
  
  # install java classes
  mkdir -p $pkgdir/usr/share/vnc/classes || return 1
  install -Dm644 $srcdir/vnc_unixsrc/classes/* \
  	$pkgdir/usr/share/vnc/classes || return 1
}
md5sums=('397b35faad32d5246b6d44b142f8304f'
         '21aa7f3376ec3c608ff22d199a19c601'
         '48a419227940ef3dcd92fab7f8809845')
