# $Id: PKGBUILD 46270 2009-07-15 02:06:13Z allan $
# Maintainer: tobias <tobias funnychar archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.0.1
pkgrel=4
pkgdesc="new modern file manager for Xfce"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2')
url="http://thunar.xfce.org"
groups=('xfce4')
depends=('desktop-file-utils' 'libexif>=0.6.17' 'hicolor-icon-theme' 'fam' \
         'glib2' 'exo' 'libxfce4util' 'hal' 'libpng' 'libjpeg>=7')
makedepends=('intltool' 'pkgconfig' 'xfce4-panel>=4.6.1')
optdepends=('gamin:   a fam replacement to make thunar monitor changes to files on the fly'
            'xfce4-panel: for trash applet')
options=('!libtool')
install=${pkgname}.install
source=(http://www.xfce.org/archive/xfce-4.6.1/src/Thunar-${pkgver}.tar.bz2
	thunar-vfs-fix-umask.patch)
md5sums=('218373aa45d74b6ba8c69c4d5af3bb19'
         '7247041fd2c411e4a129d3f90350893c')

build() {
  cd ${srcdir}/Thunar-${pkgver}
  # http://bugs.archlinux.org/task/15460
  patch -Np0 -i ${srcdir}/thunar-vfs-fix-umask.patch || return 1
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static \
    --disable-gnome-thumbnailers --enable-exif --enable-pcre
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop
}
