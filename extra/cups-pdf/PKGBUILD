# $Id: PKGBUILD 30279 2009-03-18 07:40:11Z andyrtr $
# Maintainer: Andreas Radke <andyrtr at archlinux.org>
# Contributor: Thomas Baechler <thomas.baechler@rwth-aachen.de>

pkgname=cups-pdf
pkgver=2.5.0
pkgrel=1
pkgdesc="PDF printer for cups"
arch=(i686 x86_64)
depends=('cups' 'ghostscript')
install=cups-pdf.install
url="http://cip.physik.uni-wuerzburg.de/~vrbehr/cups-pdf"
license=('GPL2')
source=(http://cip.physik.uni-wuerzburg.de/~vrbehr/cups-pdf/src/cups-pdf_$pkgver.tar.gz)
backup=(etc/cups/cups-pdf.conf)
md5sums=('9194af099a8c0e9aa213505b29ec6818')

build() {
  # Build and install
  cd $srcdir/$pkgname-$pkgver/src
  [ -z "$CC" ] && CC=gcc
  $CC $CFLAGS -Wall -o cups-pdf cups-pdf.c || return 1
  install -D -m700 cups-pdf $pkgdir/usr/lib/cups/backend/cups-pdf || return 1

  # Install Postscript Color printer
  cd ../extra
  install -D -m644 CUPS-PDF.ppd $pkgdir/usr/share/cups/model/CUPS-PDF.ppd || return 1

  # Install config file
  install -D -m644 cups-pdf.conf $startdir/pkg/etc/cups/cups-pdf.conf || return 1
}
