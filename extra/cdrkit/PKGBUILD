# $Id: PKGBUILD 29718 2009-03-12 01:27:00Z hugo $
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=cdrkit
pkgver=1.1.9
pkgrel=2
pkgdesc="Suite of programs for CD/DVD recording, ISO image creation, and audio CD extraction"
arch=('i686' 'x86_64')
url="http://cdrkit.org/"
license=('GPL2')
depends=('libcap>=2.16' 'file' 'bzip2' 'perl')
makedepends=('cmake')
provides=('cdrtools')
conflicts=('cdrtools')
source=(http://cdrkit.org/releases/cdrkit-$pkgver.tar.gz)
md5sums=('cbc0647e5d85f0e8fb3a692ba1d42edd')

build() {
  cd "$srcdir/cdrkit-$pkgver"

  make || return 1
  make PREFIX="$pkgdir/usr" install || return 1

  # Make symlinks for cdrtools compatibility
  cd "$pkgdir/usr/bin"
  ln -s wodim cdrecord || return 1
  ln -s readom readcd || return 1
  ln -s genisoimage mkisofs || return 1
  ln -s genisoimage mkhybrid || return 1
  ln -s icedax cdda2wav || return 1

  cd "$pkgdir/usr/share/man/man1"
  ln -s wodim.1 cdrecord.1 || return 1
  ln -s readom.1 readcd.1 || return 1
  ln -s genisoimage.1 mkisofs.1 || return 1
  ln -s genisoimage.1 mkhybrid.1 || return 1
  ln -s icedax.1 cdda2wav.1 || return 1
}
