# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=2.26.2
pkgrel=2
pkgdesc="Simply a document viewer"
url="http://www.gnome.org"
arch=('arm')
license=('GPL')
depends=('libspectre>=0.2.2' 'gsfonts' 'poppler-glib>=0.10.7' 'libdjvu>=3.5.20' 'gnome-icon-theme>=2.26.0' 't1lib' 'gnome-keyring>=2.26.0' 'desktop-file-utils')
makedepends=('gnome-doc-utils>=0.16.0' 'nautilus>=2.26.0' 'pkgconfig' 'texlive-bin' 'intltool')
optdepends=('texlive-bin: DVI support')
replaces=('gpdf')
groups=('gnome-extra')
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ed8b9fb5c5d6feafd19f0114b736c9ef')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-nautilus \
	      --enable-pdf --enable-tiff \
	      --enable-djvu --enable-dvi \
	      --enable-t1lib --enable-pixbuf \
	      --enable-comics --enable-impress \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain evince ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
