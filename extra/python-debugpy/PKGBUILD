# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: mark.blakeney at bullet-systems dot net

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - comment out extra compile, x86 and doesn't appear to be necessary for ARM

_pipname=debugpy
pkgname=python-$_pipname
pkgver=1.8.1
pkgrel=2
pkgdesc='An implementation of the Debug Adapter Protocol for Python'
arch=(x86_64)
url='https://aka.ms/debugpy'
license=(MIT)
depends=(gcc-libs
         glibc
         python)
makedepends=(python-setuptools)
source=(https://github.com/microsoft/debugpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ef3b939b63cf2898f36e1f43cd3d9ec5dc9b3305c024836f8201a932fe16fd4e')

prepare() {
  cd $_pipname-$pkgver
# remove precompiled libs
  rm src/debugpy/_vendored/pydevd/pydevd_attach_to_process/*.{so,dll,dylib,exe,pdb}
}

build() {
  cd $_pipname-$pkgver
  python setup.py build
# Compile attach libraries
  #cd build/lib*/debugpy/_vendored/pydevd/pydevd_attach_to_process
  #g++ ${CXXFLAGS} -m64 -shared -o attach_linux_amd64.so -fPIC -nostartfiles linux_and_mac/attach.cpp ${LDFLAGS}
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
