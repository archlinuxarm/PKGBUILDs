# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: mark.blakeney at bullet-systems dot net

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - comment out extra compile and install, x86 and doesn't appear to be necessary for ARM

_pipname=debugpy
pkgname=python-$_pipname
pkgver=1.8.2
pkgrel=1
pkgdesc='An implementation of the Debug Adapter Protocol for Python'
arch=(x86_64)
url='https://aka.ms/debugpy'
license=(MIT)
depends=(gcc-libs
         glibc
         python)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/microsoft/debugpy#tag=v$pkgver)
sha256sums=('f8bcf4bb3a2d461e725c53d7372925a3773cfcf17fb276a0a00f6f446e34bed6')

prepare() {
  cd $_pipname
# remove precompiled libs
  rm src/debugpy/_vendored/pydevd/pydevd_attach_to_process/*.{so,dll,dylib,exe,pdb}
}

build() {
  cd $_pipname
# Recythonize sources
  pushd src/debugpy/_vendored/pydevd/_pydevd_bundle
    cythonize pydevd_cython.pyx
  popd

  python -m build --wheel --no-isolation
# Compile attach libraries
  #cd build/lib*/debugpy/_vendored/pydevd/pydevd_attach_to_process
  #g++ ${CXXFLAGS} -m64 -shared -o attach_linux_amd64.so -fPIC -nostartfiles linux_and_mac/attach.cpp ${LDFLAGS}
}

package() {
  cd $_pipname
  python -m installer --destdir="$pkgdir" dist/*.whl
  #install -Dm755 build/lib*/debugpy/_vendored/pydevd/pydevd_attach_to_process/attach_linux_amd64.so \
  #  -t "$pkgdir"`python -c "import site; print(site.getsitepackages()[0])"`/debugpy/_vendored/pydevd/pydevd_attach_to_process
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
