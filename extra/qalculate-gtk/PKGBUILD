# $Id: PKGBUILD 6043 2008-07-23 00:36:54Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Patrick Leslie Polzer <polzer@gnu.org>
# Contributor: J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar>

pkgname=qalculate-gtk
pkgver=0.9.6
pkgrel=3
pkgdesc="GNOME frontend for libqalculate"
arch=('i686' 'x86_64')
url="http://qalculate.sourceforge.net/"
license=('GPL')
depends=('libqalculate' 'libgnome' 'libglade' 'cln>=1.2.0' 'desktop-file-utils')
makedepends=('perlxml' 'gnome-doc-utils>=0.12.0')
optdepends=('yelp: for displaying the help')
replaces=('qalculate')
install=qalculate-gtk.install
source=(http://downloads.sourceforge.net/sourceforge/qalculate/${pkgname}-${pkgver}.tar.gz \
        qalculate-gtk-0.9.6-cln-config.patch)
md5sums=('1f787bc8c2ac7410a878a0e895a127d8' 'c04c35f94adfa4ba206a72b06b195acc')
sha1sums=('1f69097457f6e28b67d1d767670dc519548876b5' '48bd34a19c6272654616ba4bc086a22b33cefea6')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../qalculate-gtk-0.9.6-cln-config.patch || return 1
  autoconf || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
