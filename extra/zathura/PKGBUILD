# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

# ALARM: David Beauchamp <david@archlinuxarm.org>
#  - moved xorg-server-xvfb and weston from checkdepends to makedepends

pkgname=zathura
pkgver=2026.02.22
pkgrel=1
pkgdesc="Minimalistic document viewer"
url="https://pwmt.org/projects/zathura/"
arch=('x86_64')
license=('Zlib')
depends=('girara' 'sqlite' 'desktop-file-utils' 'file' 'libsynctex' 'libseccomp' 'gtk3' 'glib2' 'json-glib')
makedepends=('appstream' 'python-sphinx' 'texlive-bin' 'meson' 'ninja' 'xorg-server-xvfb' 'weston')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support'
            'zathura-cb: Comic book support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('ab5e2df7ec551c13eaa5b86d6a1157b75706d7ccbd268bcdd103af21e2262b5e052cf621f8cf7aeca324c7b6d358bc4fcf278e94827519e06d6891ba007399c6')

build() {
        cd "${pkgname}-${pkgver}"
        arch-meson build
        ninja -C build
}

check() {
        cd "${pkgname}-${pkgver}"
        ninja -C build test
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
