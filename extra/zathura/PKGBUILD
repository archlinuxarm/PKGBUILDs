# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

# ALARM: David Beauchamp <david@archlinuxarm.org>
#  - moved xorg-server-xvfb from checkdepends to makedepends

pkgname=zathura
pkgver=0.5.11
pkgrel=1
pkgdesc="Minimalistic document viewer"
url="https://pwmt.org/projects/zathura/"
arch=('x86_64')
license=('Zlib')
makedepends=('python-sphinx' 'texlive-bin' 'meson' 'ninja' 'appstream-glib' 'check' 'xorg-server-xvfb')
depends=('girara' 'sqlite' 'desktop-file-utils' 'file' 'libsynctex' 'file' 'libseccomp')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support'
            'zathura-cb: Comic book support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('cf7e83c21c7910df4cb1edc29296a4989c4b374cea340d0725f090640c9e58c84732051cab5fe63aa6dda3aae283fa6f92baf46fe733e9003f1b9553eda63613')

build() {
        cd "${pkgname}-${pkgver}"
        arch-meson build
        ninja -C build
}

check() {
        cd "${pkgname}-${pkgver}"
        ninja -C build test
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
