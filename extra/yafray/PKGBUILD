# $Id: PKGBUILD 43947 2009-06-30 07:23:28Z allan $
# Maintainer: dorphell <dorphell@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=yafray
pkgver=0.0.9
pkgrel=4
pkgdesc="Yet another Free Ray Tracer"
arch=(i686 x86_64)
depends=('openexr>=1.6' 'libjpeg>=7')
makedepends=('scons')
source=("http://www.yafray.org/sec/2/downloads/${pkgname}-${pkgver}.tar.gz"
       'gcc4.3_missing_includes.patch')
url="http://www.yafray.org"
license=('GPL')
md5sums=('7365122e0ca6f439988f51e8b4db935c'
         '6ec90b491fb47b39139b2b2eae3b0028')

build() {
  cd ${startdir}/src/${pkgname}
  patch -p1 -i $srcdir/gcc4.3_missing_includes.patch || return 1
  sed -i "s|flags+=.*$|flags+=' ${CFLAGS} -ffast-math -fomit-frame-pointer'|" linux-settings.py
  sed -i 's|return prefix+"/etc"|return "/etc"|' linux-settings.py
  scons prefix=/usr
   # fool the build system
  sed -i 's|return "/etc"|return "'${startdir}/pkg'/etc"|' linux-settings.py
  scons prefix=${startdir}/pkg/usr install
  find ${startdir}/pkg -name '.sconsign' -exec rm -f {} \;
}
