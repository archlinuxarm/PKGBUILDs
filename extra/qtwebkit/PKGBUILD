# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for fix on ARM

pkgname=qtwebkit
pkgver=2.3.3
pkgrel=2
arch=('i686' 'x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt4' 'systemd' 'gst-plugins-base-libs')
makedepends=('gperf' 'python2' 'ruby' 'git' 'mesa')
conflicts=('qt<4.8')
_qtver=4.8.6
source=("ftp://ftp.archlinux.org/other/packages/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://download.qt-project.org/official_releases/qt/4.8/${_qtver}/qt-everywhere-opensource-src-${_qtver}.tar.gz"
        'use-python2.patch'
        'bison3.patch'
        'qwebview.patch'
        'https://gitorious.org/webkit/qtwebkit-23/commit/9fe66e8b769e6fe1c66b8d00b6c8e6403d9d6303.patch'
        'gst-1.patch')
sha1sums=('64f3ab7f8e53b5b971a3a6577fc61db0cc2ea31f'
          'ddf9c20ca8309a116e0466c42984238009525da6'
          '315b6ff603f35e5492a036f7082f6aa075dfb607'
          'd2c6182512e4bcbdf30a9e9d513c52fe4b16d9f2'
          '88ed73e6bd7ea9c6d227ceaed0ff2f1cf0200667'
          '7f075217201d9b50faafdba3bbe63e63ae06d8e7'
          '4b4ce9465ea9b107de7a93ce57afce43a2a1877f')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/use-python2.patch
  patch -p1 -i "${srcdir}"/bison3.patch
  patch -p1 -i "${srcdir}"/9fe66e8b769e6fe1c66b8d00b6c8e6403d9d6303.patch
  patch -p1 -i "${srcdir}"/gst-1.patch
  
  cd ../qt-everywhere-opensource-src-${_qtver}
  patch -p1 -i "${srcdir}"/qwebview.patch
}

build() {
  cd ${pkgname}-${pkgver}

  OPTS="--no-webkit2"
  if [ "${CARCH}" = "i686" ]; then
      # FS#33418
      OPTS="${OPTS} --no-force-sse2"
  fi

  export QTDIR=/usr
  export PATH="/usr/lib/qt4/bin:$PATH"
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" \
    --prefix=/usr \
    ${OPTS}

  # Build the QWebView plugin (FS#27914)
  cd ../qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/qwebview
  qmake-qt4
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
