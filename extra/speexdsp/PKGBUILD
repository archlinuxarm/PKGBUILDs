# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-neon

pkgname=speexdsp
pkgver=1.2rc3
pkgrel=3
pkgdesc="DSP library derived from Speex"
arch=(x86_64)
url="http://www.speex.org/"
license=(BSD)
depends=(glibc)
source=(https://downloads.us.xiph.org/releases/speex/$pkgname-$pkgver.tar.gz
        speexdsp-fixbuilds-774c87d.patch)
sha512sums=('29dfa8345df025eeb076561648a9b5c0485692be699b6da3c2a3734b4329187a1c2eb181252f4df12b21f1309ecdf59797437dfb123d160fd723491ab216e858'
            '0f0c8054591915a4ea06b28b13d50da41be10d594f39a0496e2e80e4514ed53db7cd0e1fdb205ce5e87f602b9c1e15409c33cf946ab25cdef8a3b147dbfcc0f3')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../speexdsp-fixbuilds-774c87d.patch
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static --disable-neon
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
