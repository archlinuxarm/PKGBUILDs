# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-neon

pkgname=speexdsp
pkgver=1.2rc3
pkgrel=2.1
pkgdesc="DSP library derived from Speex"
arch=(i686 x86_64)
url="http://www.speex.org/"
license=(BSD)
depends=(glibc)
source=(http://downloads.us.xiph.org/releases/speex/$pkgname-$pkgver.tar.gz
        speexdsp-fixbuilds-774c87d.patch)
sha256sums=('4ae688600039f5d224bdf2e222d2fbde65608447e4c2f681585e4dca6df692f1'
            '76cd6b1187f415d966c8d62003cd87cfb66e4030b250a316aab8f0fbc2de226a')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../speexdsp-fixbuilds-774c87d.patch
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static --disable-neon
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
