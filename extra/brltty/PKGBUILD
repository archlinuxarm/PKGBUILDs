# $Id: PKGBUILD 46704 2009-07-19 19:42:52Z giovanni $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=4.0
pkgrel=3
pkgdesc="Braille display driver for Linux/Unix"
arch=('i686' 'x86_64')
url="http://mielke.cc/brltty"
license=('GPL' 'LGPL')
depends=('libxaw' 'at-spi>=1.26.0' 'gpm>=1.20.6' 'icu>=4.2' 'python>=2.6.2' 'tcl')
makedepends=('pyrex')
backup=(etc/brltty.conf etc/conf.d/brltty.conf)
options=('!makeflags' '!emptydirs')
source=(http://mielke.cc/${pkgname}/releases/${pkgname}-${pkgver}.tar.gz
        brltty-4.0-api-socket-dir.patch
        brltty
        brltty.conf)
md5sums=('2862727384a1308dbff3031efb85c41e'
         '865062603a4fcdaa34949d0a9de7d743'
         '831ebaf0c56091702929c68805d20c4f'
         'a8ab8b3dd059e96e1734bc9cdcf844fc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Gentoo patch
  patch -Np1 -i ${srcdir}/brltty-4.0-api-socket-dir.patch || return 1
  CFLAGS+="${CFLAGS} -D_GNU_SOURCE" \
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --localstatedir=/var \
              --enable-gpm \
              --disable-java-bindings \
	      --disable-caml-bindings
  make || return 1
  make INSTALL_ROOT="${pkgdir}" install
  install -D -m 755 ${srcdir}/brltty ${pkgdir}/etc/rc.d/brltty
  install -D -m 644 ${srcdir}/brltty.conf ${pkgdir}/etc/conf.d/brltty.conf
  install -D -m 644 Documents/brltty.conf ${pkgdir}/etc/brltty.conf
}

