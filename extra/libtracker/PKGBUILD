# $Id: PKGBUILD 46663 2009-07-19 14:25:11Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libtracker
pkgver=0.6.95
pkgrel=1
pkgdesc="Interface library for the tracker indexer"
arch=(i686 x86_64)
license=('LGPL')
depends=('dbus-glib>=0.80')
makedepends=('pkgconfig' 'intltool' 'gmime' 'sqlite3' 'pango')
source=(http://ftp.gnome.org/pub/GNOME/sources/tracker/0.6/tracker-${pkgver}.tar.bz2)
options=('!libtool')
url="http://www.gnome.org/projects/tracker/"
md5sums=('a4edc8aa830070bd3f1d80a7df07c80c')

build() {
  cd ${startdir}/src/tracker-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-hal --disable-libxml2 || return 1
  pushd src/libtracker || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  popd
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 data/tracker.pc "${pkgdir}/usr/lib/pkgconfig/" || return 1
  rm -rf "${pkgdir}/usr/bin"
}
