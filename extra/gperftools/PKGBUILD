# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Thomas Jost <schnouki@schnouki.net>
# Contributor: JaDa <jada@archlinux.us>
# Contributor: Joaquim Pedro <osmano807@gmail.com>
# Contributor: Jan Rüegg <rggjan@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hack to skip using v7 assembly on v6

pkgname=gperftools
pkgver=2.1
pkgrel=1
pkgdesc="Fast, multi-threaded malloc() and nifty performance analysis tools"
arch=('i686' 'x86_64')
url="http://code.google.com/p/gperftools/"
license=('BSD')
depends=('perl')
optdepends=('graphviz: pprof graph generation'
            'gv: pprof postscript generation')
options=('!libtool')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        "armv6.patch")
sha256sums=('f3ade29924f89409d8279ab39e00af7420593baa4941c318db42e70ead7e494f'
            '879a6e92947a5538885b9e63c2675a93d57870f58c2d61260fec91ce56823df8')

build() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../armv6.patch
  ./configure --prefix=/usr --enable-frame-pointers
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
