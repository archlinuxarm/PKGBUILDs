# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Thomas Jost <schnouki@schnouki.net>
# Contributor: JaDa <jada@archlinux.us>
# Contributor: Joaquim Pedro <osmano807@gmail.com>
# Contributor: Jan Rüegg <rggjan@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hack to skip using v7 assembly on v6
#  - depend on libunwind for AArch64

pkgname=gperftools
pkgver=2.4
pkgrel=1
pkgdesc="Fast, multi-threaded malloc and nifty performance analysis tools"
arch=('i686' 'x86_64')
url="http://code.google.com/p/gperftools/"
license=('BSD')
depends=('perl')
depends_aarch64=('libunwind')
provides=('libtcmalloc.so'
          'libprofiler.so'
          'libtcmalloc_debug.so'
          'libtcmalloc_and_profiler.so'
          'libtcmalloc_minimal.so'
          'libtcmalloc_minimal_debug.so')
optdepends=('graphviz: pprof graph generation'
            'gv: pprof postscript generation')
options=('!libtool')
source=("https://googledrive.com/host/0B6NtGsLhIcf7MWxMMF9JdTN3UVk/$pkgname-$pkgver.tar.gz"
        "armv6.patch")
md5sums=('2171cea3bbe053036fb5d5d25176a160'
         '68606edc5920e10930647b8f276fbe88')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../armv6.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-frame-pointers
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
