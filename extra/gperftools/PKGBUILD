# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Thomas Jost <schnouki@schnouki.net>
# Contributor: JaDa <jada@archlinux.us>
# Contributor: Joaquim Pedro <osmano807@gmail.com>
# Contributor: Jan Rüegg <rggjan@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hack to skip using v7 assembly on v6
#  - depend on libunwind for AArch64

pkgname=gperftools
pkgver=2.7
pkgrel=1
pkgdesc="Fast, multi-threaded malloc and nifty performance analysis tools"
arch=('x86_64')
url="https://github.com/gperftools/gperftools"
license=('BSD')
depends=('perl')
depends_aarch64=('libunwind')
provides=('libtcmalloc.so'
          'libprofiler.so'
          'libtcmalloc_debug.so'
          'libtcmalloc_and_profiler.so'
          'libtcmalloc_minimal.so'
          'libtcmalloc_minimal_debug.so')
makedepends=('git')
optdepends=('graphviz: pprof graph generation'
            'gv: pprof postscript generation')
options=('!libtool')
source=("git+https://github.com/gperftools/gperftools#tag=$pkgname-$pkgver"
        "armv6.patch")
md5sums=('SKIP'
         '68606edc5920e10930647b8f276fbe88')

prepare() {
  cd "$pkgname"

  ./autogen.sh
  patch -p1 -i ../armv6.patch
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr --enable-frame-pointers
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
