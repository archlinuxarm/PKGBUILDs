# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Robin Broda <robin@broda.me>
# Contributor: X0rg
# Contributor: marlock <marlock87@hotmail.com>
# Contributor: Diogo Leal <estranho@diogoleal.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depend on libxnvctrl

pkgname=psensor
pkgver=1.2.1
pkgrel=4
pkgdesc='Graphical hardware temperature monitoring application'
arch=('x86_64')
url='https://wpitchoune.net/psensor'
license=('GPL2')
depends=('curl' 'gtk3' 'json-c' 'libgtop' 'libmicrohttpd' 'libnotify' 'lm_sensors'
         'udisks2')
source=("https://wpitchoune.net/$pkgname/files/$pkgname-$pkgver.tar.gz"
        'psensor.appdata.xml')
sha256sums=('084f785d169f533d3010e50c5933436ed427afc70d41740b762f1db5034833ab'
            'f8079f06959447af0baf3b00ab74cbf6d9ed55a2995114071189cf501bc55009')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/ -Werror//' {src/lib,src/server,tests}/Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
