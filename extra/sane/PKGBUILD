# $Id: PKGBUILD 43887 2009-06-30 06:37:47Z allan $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributed by Sarah Hay <sarahhay@mb.sympatico.ca>, Tobias Powalowski <t.powa@gmx.de>, Simo L. <neotuli@yahoo.com>, eric <eric@archlinux.org>

pkgname=sane
pkgver=1.0.20
pkgrel=3
pkgdesc="Scanner Access Now Easy"
arch=(i686 x86_64)
license=('GPL')
depends=('libtiff>=3.8.2-5' 'libgphoto2>=2.4.6-2' 'libjpeg>=7' 'libieee1284' 'libusb' 'libv4l' 'avahi' 'bash')
source=(ftp://ftp.sane-project.org/pub/sane/$pkgname-backends-$pkgver/$pkgname-backends-$pkgver.tar.gz
        sane.xinetd)
url="http://www.sane-project.org"
install=$pkgname.install
backup=(etc/sane.d/{abaton.conf,agfafocus.conf,apple.conf,artec.conf,artec_eplus48u.conf,avision.conf,bh.conf,canon.conf,canon630u.conf,canon_dr.conf,canon_pp.conf,cardscan.conf,coolscan2.conf,coolscan3.conf,coolscan.conf,dc25.conf,dc210.conf,dc240.conf,dell1600n_net.conf,dll.conf,dmc.conf,epjitsu.conf,epson.conf,epson2.conf,fujitsu.conf,genesys.conf,gphoto2.conf,gt68xx.conf,hp.conf,hp3900.conf,hp4200.conf,hp5400.conf,hpsj5s.conf,hs2p.conf,ibm.conf,leo.conf,lexmark.conf,ma1509.conf,matsushita.conf,microtek.conf,microtek2.conf,mustek.conf,mustek_pp.conf,mustek_usb.conf,nec.conf,net.conf,pie.conf,pixma.conf,plustek.conf,plustek_pp.conf,qcam.conf,ricoh.conf,rts8891.conf,s9036.conf,saned.conf,sceptre.conf,sharp.conf,sm3840.conf,snapscan.conf,sp15c.conf,st400.conf,stv680.conf,tamarack.conf,teco1.conf,teco2.conf,teco3.conf,test.conf,u12.conf,umax.conf,umax1220u.conf,umax_pp.conf,xerox_mfp,v4l.conf} etc/xinetd.d/sane)
options=(!libtool)
md5sums=('a0cfdfdebca2feb4f2ba5d3418b15a42'
         'da946cc36fb83612162cf9505986d4b2')

build() {
  cd "${srcdir}/${pkgname}-backends-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-docdir=/usr/share/doc/sane --enable-avahi || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  # fix hp officejets
  echo "#hpaio" >> "${pkgdir}/etc/sane.d/dll.conf" || return 1
  # install udev files
  install -D -m0644 tools/udev/libsane.rules \
    "${pkgdir}/lib/udev/rules.d/53-sane.rules" || return 1
  # fix group on /var/lock
  mkdir -p "${pkgdir}/var/lock/sane"
  chgrp 0 "${pkgdir}/var"
  chmod 755 "${pkgdir}/var"
  chmod 1777 "${pkgdir}/var/lock"
  chgrp 0 "${pkgdir}/var/lock"
  chmod 775 "${pkgdir}/var/lock/sane"
  chgrp 96 "${pkgdir}/var/lock/sane"
  # install xinetd file
  install -D -m644 "${srcdir}/sane.xinetd" "${pkgdir}/etc/xinetd.d/sane"
}
