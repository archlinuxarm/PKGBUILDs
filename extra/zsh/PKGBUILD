# Maintainer: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=zsh
pkgver=4.3.10
pkgrel=2
pkgdesc="A very advanced and programmable command interpreter (shell) for UNIX"
arch=('arm')
url="http://www.zsh.org/"
license=('custom')
depends=('ncurses' 'pcre')
install=zsh.install
source=(ftp://ftp.zsh.org/pub/$pkgname-$pkgver.tar.bz2)
md5sums=('74c5b275544400082a1cde806c98682a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --bindir=/bin \
              --enable-etcdir=/etc/zsh \
	      	  --enable-zshenv=/etc/zsh/zshenv \
              --enable-zlogin=/etc/zsh/zlogin \
              --enable-zlogout=/etc/zsh/zlogout \
              --enable-zprofile=/etc/profile \
              --enable-zshrc=/etc/zsh/zshrc \
              --enable-maildir-support \
	      	  --with-term-lib="ncursesw" \
	      	  --enable-multibyte \
	      	  --enable-function-subdirs \
	      	  --enable-zsh-mem \
	     	  --with-tcsetpgrp \
              --enable-pcre \
              --enable-zsh-secure-free \
	       	  --disable-gdbm
  make || return 1
  make DESTDIR="$pkgdir/" install

  install -D -m644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
