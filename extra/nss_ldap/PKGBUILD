# $Id: PKGBUILD 158627 2012-05-05 03:23:18Z dreisner $
# Maintainer:
# Contributor: Comete <la_comete@tiscali.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch: nss_ldap-265 uses __libc_lock_lock, removed in glibc-2.16 (http://bugzilla.padl.com/show_bug.cgi?id=445)
#  - patch: Proper pthread support for nss_ldap-265 (http://bugzilla.padl.com/show_bug.cgi?id=446)

pkgname=nss_ldap
pkgver=265
pkgrel=6
pkgdesc="The nss_ldap module provides the means for Linux and Solaris workstations to resolve the entities defined in RFC 2307 from LDAP directories."
arch=('i686' 'x86_64')
url="http://www.padl.com/OSS/nss_ldap.html"
license=('LGPL')
depends=('libldap' 'krb5')
backup=("etc/nss_ldap.conf")
source=("http://www.padl.com/download/${pkgname}-${pkgver}.tar.gz"
         'nss_ldap-265-glibc-2.16.patch'
         'nss_ldap-265-pthread.patch')
md5sums=('c1cb02d1a85538cf16bca6f6a562abe4'
         '49df78dc482f90f5c3d850a664308216'
         '38c881edcb8df6b426f8a9d8f269d567')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p0 -i ../nss_ldap-265-glibc-2.16.patch
  patch -p0 -i ../nss_ldap-265-pthread.patch

  ./configure \
       --prefix=/usr \
       --with-ldap-conf-file=/etc/nss_ldap.conf \
       --with-ldap=openldap \
       --sysconfdir=/etc \
       --mandir=/usr/share/man \
       --enable-schema-mapping \
       --enable-rfc2307bis \
       --enable-configurable-krb5-ccname-gssapi
  env PATH=`pwd`:"$PATH" make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # buildsystem is trying to be clever and hardcodes symlinks in /usr$(libdir)
  rm -rf "${pkgdir}/usr/usr"
}
