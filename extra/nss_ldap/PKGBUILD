# Maintainer:
# Contributor: Comete <la_comete@tiscali.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch: nss_ldap-265 uses __libc_lock_lock, removed in glibc-2.16 (http://bugzilla.padl.com/show_bug.cgi?id=445)
#  - patch: Proper pthread support for nss_ldap-265 (http://bugzilla.padl.com/show_bug.cgi?id=446)
#  - copy autoconf files for v8

pkgname=nss_ldap
pkgver=265
pkgrel=9
pkgdesc="The nss_ldap module provides the means for Linux and Solaris workstations to resolve the entities defined in RFC 2307 from LDAP directories."
arch=('x86_64')
url="http://www.padl.com/OSS/nss_ldap.html"
license=('LGPL')
depends=('libldap' 'krb5')
backup=("etc/nss_ldap.conf")
source=("https://fossies.org/linux/privat/old/${pkgname}-${pkgver}.tar.gz"
        'nss_ldap-265-glibc-2.16.patch'
        'nss_ldap-265-pthread.patch'
        "reproducible-build.patch")
md5sums=('c1cb02d1a85538cf16bca6f6a562abe4'
         '49df78dc482f90f5c3d850a664308216'
         '38c881edcb8df6b426f8a9d8f269d567'
         'fd05b8e76fe16a3ac54bffe58db57ff1')
b2sums=('31b233ef7680204abfb62408141609b9489e133327c787c59afedd9ac41194189bde26d1f3b0a885622f57ec769ddee2bdf7d274b9584d4d5002d1c016c86892'
        'f736842ef193d863c80349cacd2dfc6991e4e92d5e2205f2f40767a9786b86b0fa4b06d671dd17d93e7c7a8da65c2a7d3111914480d7a9cf308a9c2f37753b89'
        'ff732b836d4405041337f653cc3f8d4645704d94023285cd31af58739331459337fb66c1928ff3a4b42b3aed29ffa95e0408734ab5e18b67c4f86ce84b5362af'
        'da2f5b2a35c723acf464a6ec85a1658799e5fcdc898645f57224a50848287a7716b9e02fbaa731838417d1e54f8322f9748d83e324f997bbf737c215cbd42c6e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p0 -i ../nss_ldap-265-glibc-2.16.patch
  patch -p0 -i ../nss_ldap-265-pthread.patch
  cp /usr/share/libtool/build-aux/config.{sub,guess} .

  # reproducible builds
  patch -p1 -i ../reproducible-build.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PERL5LIB="$PWD"
  ./configure \
       --prefix=/usr \
       --with-ldap-conf-file=/etc/nss_ldap.conf \
       --with-ldap=openldap \
       --sysconfdir=/etc \
       --mandir=/usr/share/man \
       --enable-schema-mapping \
       --enable-rfc2307bis \
       --enable-configurable-krb5-ccname-gssapi
  env PATH=`pwd`:"$PATH" make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # buildsystem is trying to be clever and hardcodes symlinks in /usr$(libdir)
  rm -rf "${pkgdir}/usr/usr"
}
