# $Id: PKGBUILD 37475 2009-05-02 05:07:53Z eric $
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-intel-legacy
pkgver=2.3.2
pkgrel=3
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ legacy video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri>=7.4' 'libdrm>=2.4.5-3' 'libxvmc')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'xf86driproto>=2.0.4' 'glproto>=1.4.9' 'mesa>=7.4' 'xineramaproto')
conflicts=('xorg-server<1.6.0' 'xf86-video-i810' 'xf86-video-intel')
replaces=('xf86-video-i810')
options=('!libtool')
source=(${url}/releases/individual/driver/xf86-video-intel-${pkgver}.tar.bz2
	intel-newserver.patch
        LICENSE)
md5sums=('fef32ffc428d9d565ab10b338ce17855' '725afeee507a09a912a4a29922587835'\
         'b924480931774dec14515e270db4924d')

build() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"
  patch -Np1 -i "${srcdir}/intel-newserver.patch" || return 1
  ./configure --prefix=/usr --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
