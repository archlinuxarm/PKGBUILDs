# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=policykit-gnome
pkgver=0.9.2
pkgrel=4
pkgdesc="PolicyKit integration for the GNOME desktop"
arch=('i686' 'x86_64')
url="http://hal.freedesktop.org/docs/PolicyKit-gnome/"
license=('GPL')
depends=('gconf>=2.26.1')
makedepends=('intltool' 'gtk-doc' 'gnome-doc-utils>=0.16.1')
options=(!emptydirs !libtool)
install=${pkgname}.install
source=(http://hal.freedesktop.org/releases/PolicyKit-gnome-${pkgver}.tar.bz2
	bgo572878.patch)
md5sums=('fc478b168d0c926a9766b0b415ff4bbf'
         'aa8aa8c0fa69f69f20af9338429150ad')

build() {
  cd "${srcdir}/PolicyKit-gnome-${pkgver}"
  patch -Np1 -i "${srcdir}/bgo572878.patch" || return 1
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --libexecdir=/usr/lib/PolicyKit \
      --disable-gtk-doc \
      --disable-examples || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m 755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain PolicyKit-gnome ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
