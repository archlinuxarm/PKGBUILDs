# $Id: PKGBUILD 87283 2010-08-11 20:42:35Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=gutenprint
pkgver=5.2.6
pkgrel=1
pkgdesc="Top quality printer drivers for POSIX systems"
arch=('i686' 'x86_64')
license=('GPL')
install=gutenprint.install
depends=('readline' 'gnutls>=2.8.5') # needs to be checked. build log says -Lgnutls but namcap doesn't detect it
makedepends=('gtk2>=2.18.6' 'cups>=1.4.2' 'foomatic-db-engine' 'ghostscript>=8.71')
optdepends=('cups:		to use cups printer spooler(recommended)'
            'foomatic-db-engine:	to use foomatic spooler'
            'ghostscript:	adds postscript support for ijsgutenprint'
            'gimp:		adds gutenprint plugin to gimp')
source=(http://downloads.sourceforge.net/gimp-print/$pkgname-$pkgver.tar.bz2)
url="http://gimp-print.sourceforge.net/"
replaces=('gimp-print')
options=('!libtool' '!emptydirs')
md5sums=('1f0d0cbef6c089f4f095e21869fa70db')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-samples --with-cups \
	--disable-translated-cups-ppds --with-gimp2-as-gutenprint \
	--disable-libgutenprintui  --enable-libgutenprintui2 --disable-gtktest \
	--enable-cups-ppds --enable-cups-ppds-at-top-level \
	--with-foomatic --with-foomatic3 --with-ghostscript \
	--disable-static --disable-static-genppd --with-ijs \
	--disable-globalized-cups-ppds
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

