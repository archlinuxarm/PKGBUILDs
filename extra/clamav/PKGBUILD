# $Id$
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - revert llvm changes (unsupported on ARM):
#    - remove llvm35-libs depend, llvm35 depend, configure options
#    - depend on gcc-libs

pkgname=clamav
pkgver=0.99.2
pkgrel=3
pkgdesc='Anti-virus toolkit for Unix'
url='http://www.clamav.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('libmilter')
depends=('bzip2' 'libltdl' 'gcc-libs' 'libxml2' 'curl')
validpgpkeys=('B964E6D7BC7D7C82CCB8D45840B8EA2364221D53'
              'F79FB2D08751574C5D3FDFFBB3D5342C260429A0')
source=("http://www.clamav.net/downloads/production/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'logrotate'
        'tmpfiles.d'
        'clamd.conf'
        'freshclam.conf'
        'clamd.service'
        'freshclamd.service')
sha1sums=('c1a47411834d8527f7b40727aebee63f01d488af' 'SKIP'
          'bb488a56b0f6a0760446cde89c1e3321e2717b78'
          'a224ea9b4d0f4f196827347d54bed51e11c197ea'
          '887f624eb305f2446f55d8339e2972ad0cfe2b79'
          'b767837d0279ad30b92c314cb762b73e5ad0415e'
          '77899cce83f04cbe134b30da376f879d2841f769'
          'cda9a087e5593992150cb456e34c5f6f589aca82')

backup=('etc/clamav/clamd.conf'
        'etc/clamav/freshclam.conf'
        'etc/logrotate.d/clamav')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# --disable-zlib-vcheck because the configure script thinks that
	# zlib 1.2.11 is older than 1.2.2
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav \
		--disable-zlib-vcheck \
		--enable-milter
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../clamd.conf "${pkgdir}"/etc/clamav/clamd.conf
	install -Dm644 ../freshclam.conf "${pkgdir}"/etc/clamav/freshclam.conf
	install -Dm644 ../freshclamd.service "${pkgdir}"/usr/lib/systemd/system/freshclamd.service
	install -Dm644 ../clamd.service "${pkgdir}"/usr/lib/systemd/system/clamd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/clamav.conf
	install -Dm644 ../logrotate "${pkgdir}"/etc/logrotate.d/clamav

	install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
}
