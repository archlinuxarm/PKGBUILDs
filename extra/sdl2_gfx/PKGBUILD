# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jameson Pugh <imntreal@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable mmx

pkgname=sdl2_gfx
pkgver=7
pkgrel=1
pkgdesc="SDL Graphic Primitives (Version 2)"
arch=('i686' 'x86_64')
url="http://www.ferzkopp.net/joomla/software-mainmenu-14/4-ferzkopps-linux-software/19-sdlgfx"
license=('zlib')
depends=('sdl2')
makedepends=('svn')
options=(!libtool)
source=("sdl2-gfx::svn://svn.code.sf.net/p/sdl2gfx/code/trunk#revision=$pkgver")
md5sums=('SKIP')

build() {
  cd "${srcdir}/sdl2-gfx"

  ./autogen.sh
  ./configure --disable-static --prefix=/usr --disable-mmx
  make
}

package() {
  cd "${srcdir}/sdl2-gfx"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
