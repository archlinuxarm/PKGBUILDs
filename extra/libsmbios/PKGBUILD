# $Id: PKGBUILD 41987 2009-06-08 21:01:23Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: daniel g. siegel <dgsiegel@gmail.com> 

pkgname=libsmbios
pkgver=2.2.16
pkgrel=1
pkgdesc="A library for providing access to as much BIOS information as possible"
arch=(i686 x86_64)
url="http://linux.dell.com/libsmbios/main/index.html"
license=('GPL' 'custom')
depends=('gcc-libs>=4.3.3')
makedepends=('libxml2')
optdepends=('python: tools')
source=(http://linux.dell.com/libsmbios/download/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('6edf6659fbb77e7d0f60d5217a7d88ed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build
  cd build
  ../configure --prefix=/usr --sysconfdir=/etc --disable-static || return 1
  mkdir -p out/libsmbios_c
  mkdir -p out/libsmbios_c++
  LD_PRELOAD="" make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/include"
  cp -a ../src/include/* "${pkgdir}/usr/include/" || return 1
  cp -a out/public-include/* "${pkgdir}/usr/include/" || return 1

  rm -rf "${pkgdir}/etc/yum"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../COPYING-OSL "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
