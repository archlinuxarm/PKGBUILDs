# $Id: PKGBUILD 47952 2009-07-29 20:43:29Z eric $
# Maintainer: Simo Leone <simo@archlinux.org>
pkgname=flite
pkgver=1.3
pkgrel=3
pkgdesc="A lighweight version of festival speech synthesis"
arch=('i686' 'x86_64')
url="http://www.speech.cs.cmu.edu/flite/"
license=('custom')
depends=('glibc')
source=(http://www.speech.cs.cmu.edu/flite/packed/${pkgname}-${pkgver}/${pkgname}-${pkgver}-release.tar.gz)
md5sums=('ae0aca1cb7b4801f4372f3a75a9e52b5')
sha1sums=('233144a772de72741ae1aa2292f672c30224afb3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-release"
  ./configure --prefix=/usr || return 1

  #we want the 16k, not 8k voice
  sed -i 's/cmu_us_kal/cmu_us_kal16/' config/config || return 1
  
  make || return 1
  
  #Ugly hack for bad makefile (lib isn't a dir)
  if [ "$CARCH" = "i686" ]; then
    sed -i 's#@ $(MAKE) -C lib --no-print-directory install#$(INSTALL) -m 644 build/i386-linux-gnu/lib/*.a $(INSTALLLIBDIR)#g' Makefile || return 1
  else
    sed -i 's#@ $(MAKE) -C lib --no-print-directory install#$(INSTALL) -m 644 build/x86_64-linux-gnu/lib/*.a $(INSTALLLIBDIR)#g' Makefile || return 1
  fi

  make prefix="${pkgdir}/usr" install || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
