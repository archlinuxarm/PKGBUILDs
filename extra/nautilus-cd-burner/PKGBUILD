# $Id: PKGBUILD 46878 2009-07-20 16:42:13Z giovanni $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus-cd-burner
pkgver=2.24.0
pkgrel=2
pkgdesc="Adds cd burner functionality to nautilus"
arch=(i686 x86_64)
depends=('cdrkit' 'dvd+rw-tools' 'gnome-mount>=0.8' 'desktop-file-utils' 'libgnomeui>=2.24.0')
makedepends=('intltool' 'pkgconfig' 'nautilus>=2.24.0' 'eel')
optdepends=('nautilus' 'eel')
options=('!libtool' '!emptydirs')
license=('GPL')
url=http://www.gnome.org/
install=nautilus-cd-burner.install
source=(http://ftp.gnome.org/pub/GNOME/sources/nautilus-cd-burner/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('eb211dd03d0518263fa4eadccc3caa88')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/nautilus-cd-burner \
      --localstatedir=/var --enable-hal || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nautilus-cd-burner ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
