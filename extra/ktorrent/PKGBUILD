# $Id: PKGBUILD 53005 2009-09-25 00:41:57Z giovanni $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=3.2.4
pkgrel=1
pkgdesc="A BitTorrent program for KDE"
arch=('i686' 'x86_64')
url="http://ktorrent.org"
license=('GPL2')
depends=('kdelibs>4' 'gmp' 'qca' 'libxft' 'kdebase-runtime' 'taglib' 'hicolor-icon-theme')
# 'kdebase-runtime' is needed for some of the icons in the interface to show
makedepends=('autoconf' 'perl' 'automoc4' 'cmake' 'kdepimlibs' 'kdebase-workspace')
# 'kdebase-workspace' is needed to enable plasmoid building
source=(http://ktorrent.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2 \
	httpserver.patch)
install=ktorrent.install
options=('libtool')
md5sums=('e3c595c9ac667985764058902a1489ea'
         '1f25b94615b0bbfa0457c99fb20de3e6')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# build time patch
	patch -Np0 -i ${srcdir}/httpserver.patch || return 1

	cmake .	-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DENABLE_MEDIAPLAYER_PLUGIN=false
	make || return 1
	make DESTDIR=${pkgdir} install 
}
