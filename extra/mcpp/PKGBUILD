# $Id: PKGBUILD 29373 2009-03-08 13:21:19Z jgc $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=mcpp
pkgver=2.7.2
pkgrel=2
pkgdesc="Matsui's CPP implementation precisely conformed to standards"
arch=(arm)
license=('custom')
url="http://mcpp.sourceforge.net"
depends=(glibc)
options=(!libtool)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz 
	namlen.patch)
md5sums=('512de48c87ab023a69250edc7a0c7b05'
	 '0bfc6177a58e912a9e23e368578dd3fe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/namlen.patch" || return 1
  ./configure --prefix=/usr --enable-mcpplib --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../doc/${pkgname}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
