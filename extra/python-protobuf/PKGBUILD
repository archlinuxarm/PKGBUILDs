# ALARM: BrainDamage <braindamage@archlinux.org>
#  - use pypi tarball as source
#  - use normal python toolchain to build

pkgname=python-protobuf
_basename=protobuf
pkgver=33.1
_ver=6
pkgrel=3
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('aarch64' 'armv7h')
url='https://developers.google.com/protocol-buffers/'
license=('BSD-3-Clause')
depends=('python' "protobuf=${pkgver}")
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_basename::1}/${_basename}/${_basename}-${_ver}.${pkgver}.tar.gz")
sha256sums=('97f65757e8d09870de6fd973aeddb92f85435607235d20b2dfed93405d00c85b')


build() {
	cd "${_basename}-${_ver}.${pkgver}"
	export PYTHONHASHSEED=0
	python -m build --wheel --no-isolation
}

package() {
	cd "${_basename}-${_ver}.${pkgver}"
	python -m installer --compile-bytecode 1 --destdir="${pkgdir}" dist/*.whl
}
