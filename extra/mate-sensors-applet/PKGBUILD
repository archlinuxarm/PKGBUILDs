# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

pkgname=mate-sensors-applet
pkgver=1.28.0
pkgrel=4
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('udisks2' 'libnotify' 'lm_sensors' 'mate-panel' 'gettext')
makedepends=('autoconf-archive' 'docbook-xsl' 'itstool' 'mate-common' 'yelp-tools')
groups=('mate-extra')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-sensors-applet/archive/refs/tags/v${pkgver}.tar.gz"
	"mate-sensors-applet-1_28_0-fix-glib-2_83_2.patch")
sha256sums=('bc4c8a532c385b93cc091210728fe0c35e53c8eda9fd144e80a649ccce368b2e'
	'39afc3f022efec24838354f0c2d14997e3b1cb577bf5fb76e407ba882b6b7786')

prepare() {
	cd "${pkgname}-${pkgver}"

	# Fix issue introduced with glib 2.83.2
	# Commit 9b74dc1 in mate-sensors-applet's repository
	patch -p1 -i "${srcdir}/mate-sensors-applet-1_28_0-fix-glib-2_83_2.patch"

	./autogen.sh
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
	            --prefix=/usr \
	            --libexecdir="/usr/lib/${pkgname}"
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
