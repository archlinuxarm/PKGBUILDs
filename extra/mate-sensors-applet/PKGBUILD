# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

pkgname=mate-sensors-applet
pkgver=1.26.0
pkgrel=1
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libatasmart' 'libnotify' 'lm_sensors' 'mate-panel' 'gettext')
makedepends=('docbook-xsl' 'itstool')
groups=('mate-extra')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4c437c3ff04488543e2cf0c4627fe265c48b4ad8f4946fd9de9845edc0c82968')

build() {
	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname}

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
