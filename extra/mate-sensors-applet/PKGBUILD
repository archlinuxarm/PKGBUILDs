# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop dependency on libxnvctrl (nvidia-settings)

pkgname=mate-sensors-applet
pkgver=1.28.0
pkgrel=3
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('udisks2' 'libnotify' 'lm_sensors' 'mate-panel' 'gettext')
makedepends=('autoconf-archive' 'docbook-xsl' 'itstool' 'mate-common' 'yelp-tools')
groups=('mate-extra')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-sensors-applet/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('bc4c8a532c385b93cc091210728fe0c35e53c8eda9fd144e80a649ccce368b2e')

prepare() {
	cd "${pkgname}-${pkgver}"
	./autogen.sh
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
	            --prefix=/usr \
	            --libexecdir="/usr/lib/${pkgname}"
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
