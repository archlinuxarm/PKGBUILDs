# $Id: PKGBUILD 176352 2013-01-30 20:25:49Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --disable-sse to configure

pkgname=audacity
pkgver=2.0.3
pkgrel=2
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk' 'lame' 'hicolor-icon-theme'
	 'desktop-file-utils' 'jack' 'soundtouch' 'ffmpeg-compat')
makedepends=('cmake')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}.tar.xz)
sha1sums=('72e634711fb96aac9df445e75e16e12e00c047e6')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  PKG_CONFIG_PATH+="/usr/lib/ffmpeg-compat/pkgconfig" \
    ./configure --prefix=/usr --docdir=/usr/share/doc/audacity --disable-sse
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
