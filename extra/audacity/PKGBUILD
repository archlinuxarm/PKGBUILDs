# $Id: PKGBUILD 50915 2009-09-03 02:47:27Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=audacity
pkgver=1.3.9
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' \
         'lame' 'shared-mime-info' 'desktop-file-utils' 'redland' 'jack-audio-connection-kit')
makedepends=('zip' 'python' 'pkgconfig' 'soundtouch')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}.tar.bz2)
md5sums=('0f2bc8971c6e2c4d39457c28aea16b5c')
sha1sums=('77330b3bb70070001262f6e3c7d74a1d94c93c5e')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  WX_CONFIG=/usr/bin/wx-config \
  ./configure --prefix=/usr \
     --with-portaudio=v19 --with-libsamplerate=system \
     --without-libresample --with-libmad=system \
     --with-id3tag=system --with-flac=system \
     --with-vorbis=system --with-libexpat=system \
     --with-libsndfile=system --with-soundtouch \
     --enable-unicode --without-taglib || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -d "${pkgdir}/usr/share/pixmaps"
  ln -s /usr/share/audacity/audacity.xpm "${pkgdir}/usr/share/pixmaps/audacity.xpm" || return 1
}
