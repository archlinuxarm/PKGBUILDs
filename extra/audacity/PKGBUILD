# $Id: PKGBUILD 176352 2013-01-30 20:25:49Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --disable-sse to configure
#  - disable soundtouch on v5

pkgname=audacity
pkgver=2.0.5
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk' 'lame' 'hicolor-icon-theme'
	 'desktop-file-utils' 'jack' 'soundtouch' 'ffmpeg-compat')
makedepends=('cmake')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}.tar.xz)
sha1sums=('f63cafb81f29e7f3813dc9f80fb1dfdbcfe46b33')

build() {
  cd ${pkgname}-src-${pkgver}

  [[ $CARCH == 'arm' ]] && CONFIG="--without-soundtouch"

  PKG_CONFIG_PATH+="/usr/lib/ffmpeg-compat/pkgconfig" \
    ./configure --prefix=/usr --disable-sse $CONFIG
  make
}

package() {
  cd ${pkgname}-src-${pkgver}
  make DESTDIR="${pkgdir}" install
}
