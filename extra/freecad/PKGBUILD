# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: loqs <bugs-archlinux@entropy-collector.net>
# Contributor: Gabriel Souza Franco <gabrielfrancosouza@gmail.com>
# Contributor: Florian Pritz
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi.degroof@gmail.com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi
# Contributor: Adrian Insaurralde
# Contributor: Martin Rodriguez Reboredo
# Contributor: Jamin Collins

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepends on ospray

buildarch=8
highmem=1

pkgname=freecad
pkgver=1.0.1
pkgrel=4
pkgdesc='Feature based parametric 3D CAD modeler'
arch=(x86_64)
url='https://freecadweb.org/'
license=(LGPL-2.0-only)
depends=(
  boost-libs
  coin
  fmt
  glew
  libspnav
  med
  opencascade
  openmpi
  pugixml
  pyside6
  yaml-cpp
  python-markdown
  python-matplotlib
  python-pip
  python-pivy
  python-ply
  python-yaml
  qt6-svg
  qt6-tools
  verdict
  vtk
  xerces-c
)
makedepends=(
  boost
  cgns
  cmake
  eigen
  git
  libharu
  liblas
  ninja
  nlohmann-json
  openvdb
  openvr
  pdal
  postgresql-libs
  python-mpi4py
  shiboken6
  swig
  utf8cpp
  dos2unix
  microsoft-gsl
  fast_float
)
optdepends=(
  'graphviz: dependency graph support'
  'opencamlib: CAM workbench support'
  'openscad: OpenSCAD support'
  'python-ladybug-core: Solar/Wind diagram support'
)
options=(!lto)
source=("git+https://github.com/$pkgname/$pkgname#tag=$pkgver"
        fix-vtk-9.5.patch
        fix-opencascade-7.9.patch)
sha512sums=('3f691eb4d0b6621850381222fe266a62892421dd6e807349928075e3b769f14b16c72d64d2b51b0ca4db0e4bf4b3e1f3a791cfe7e997e874ba1cd99223d8e420'
            '3f84f959cb17b2679e1887a6ae1ec1c367d160059845c0bf810ba9373169f729af8bd5d2cb2e46db7521be644688389c9e81ab9544380e034bd0ddb8e902a6bc'
            '18e4c6f99d9fa3af71eb9e95ebf95dbacc82e1d7506b3b259b18c1f0902806165994bfe4eb45176cca4034ff81a7b02156099b461c4936fad91df19132657e60')
b2sums=('93c737bd08e32023e1c314118b4bee89136f541a255e7dddb26b7e8b58855b15f4a46bc50845c2bc673efb6a6a3a1a48ce4c02bdb8e9d1360a428b241bbcd52a'
        'ceff903d3a839d3ef7e5a0658b2d29710b32f04d2619bd1d7d14f3c53d7ed9a2c58eff421e09d7284e574036ae232b95c15258f4017e5085edbc5363f518044a'
        'ed78606025f2781b67ccf5874d8cb51c5f8740429057e670e7328a566c8307026b626d246a41e0747000f9048b3325c9b0b6625fa1765b612effe2b0efbd7683')

prepare() {
  cd freecad
  git submodule update --init --recursive

  patch -p1 -i ../fix-vtk-9.5.patch
  patch -p1 -i ../fix-opencascade-7.9.patch
}

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -D BUILD_FLAT_MESH=ON
    -D BUILD_ENABLE_CXX_STD=C++17
    -D BUILD_DESIGNER_PLUGIN=ON
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects -fPIC -w"
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects -fPIC -w"
    -D CMAKE_INSTALL_BINDIR=/usr/lib/freecad/bin
    -D CMAKE_INSTALL_DATADIR=/usr/share/freecad
    -D CMAKE_INSTALL_DATAROOTDIR=/usr/share
    -D CMAKE_INSTALL_DOCDIR=/usr/share/freecad/doc
    -D CMAKE_INSTALL_LIBDIR=/usr/lib/freecad/lib
    -D CMAKE_INSTALL_PREFIX=/usr/lib/freecad
    -D FREECAD_USE_EXTERNAL_PIVY=ON
    -D FREECAD_USE_OCC_VARIANT='Official Version'
    -D FREECAD_USE_QT_FILEDIALOG=ON
    -D FREECAD_QT_VERSION=6
    -D INSTALL_TO_SITEPACKAGES=ON
    -D PYTHON_EXECUTABLE=/usr/bin/python
    -G Ninja
    -W no-dev
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # tools
  cd "$pkgname"
  install -Dm755 src/Tools/{$pkgname-thumbnailer,fcinfo} -t "$pkgdir/usr/bin"

  # symlinks
  install -d "$pkgdir/usr/bin"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/freecad"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/FreeCAD"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/freecadcmd"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/FreeCADCmd"
}
