# $Id: PKGBUILD 44489 2009-07-02 07:30:49Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: mgushee

pkgname=djvulibre
pkgver=3.5.22
pkgrel=2
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
arch=("i686" "x86_64")
license=('GPL')
url="http://djvu.sourceforge.net/"
depends=('libtiff>=3.8.2-5' 'hicolor-icon-theme' 'qt3>=3.3.8-12' 'desktop-file-utils' 
         "libdjvu>=${pkgver}-2" 'shared-mime-info')
makedepends=('pkgconfig')
options=(!makeflags)
install=djvulibre.install
source=(http://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz) 

build() {
  [ -z "${QTDIR}" ] && . /etc/profile.d/qt3.sh
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make LIBDJVU="`pkg-config --libs ddjvuapi`" SUBDIRS_FIRST=tools || return 1
  make LIBDJVU="`pkg-config --libs ddjvuapi`" SUBDIRS_FIRST=tools \
       DESTDIR="${pkgdir}" plugindir=/usr/lib/mozilla/plugins \
       install-utils install-gui install-i18n || return 1

  #Install icons, launcher files, mime-types.
  install -m755 -d ${pkgdir}/usr/share/icons/hicolor/{22x22,32x32,48x48}/mimetypes
  install -m755 -d ${pkgdir}/usr/share/icons/hicolor/32x32/apps
  install -m755 -d ${pkgdir}/usr/share/mime/packages/
  install -m755 -d ${pkgdir}/usr/share/applications

  install -m644 desktopfiles/hi22-djvu.png "${pkgdir}/usr/share/icons/hicolor/22x22/mimetypes/image-vnd.djvu.mime.png" || return 1
  install -m644 desktopfiles/hi32-djvu.png "${pkgdir}/usr/share/icons/hicolor/32x32/mimetypes/image-vnd.djvu.mime.png" || return 1
  install -m644 desktopfiles/hi48-djvu.png "${pkgdir}/usr/share/icons/hicolor/48x48/mimetypes/image-vnd.djvu.mime.png" || return 1

  install -m644 desktopfiles/djvulibre-mime.xml "${pkgdir}/usr/share/mime/packages/" || return 1

  install -m644 desktopfiles/hi32-djview3.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/djvulibre-djview3.png" || return 1
  install -m644 desktopfiles/djvulibre-djview3.desktop "${pkgdir}/usr/share/applications/" || return 1
}
