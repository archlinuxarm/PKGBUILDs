# $Id: PKGBUILD 30649 2009-03-21 06:26:40Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=moc
pkgver=2.4.4
pkgrel=3
pkgdesc="An ncurses console audio player with support for the mp3, ogg, and wave formats"
arch=('i686' 'x86_64')
url="http://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack-audio-connection-kit' 'curl>=7.16.2' 'libsamplerate' 'libtool')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec')
optdepends=('speex: for using the speex plugin' 'ffmpeg: for using the ffmpeg plugin' 'taglib and libmpcdec: for using the musepack plugin')
options=('!libtool')
source=(ftp://ftp.daper.net/pub/soft/moc/stable/${pkgname}-${pkgver}.tar.bz2 moc-2.4.4-ffmpeg.diff)
md5sums=('647c770a5542a4ae5437386807a89796' '2e6958ddbb4cd0aa7bdf6c3235782381')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../moc-2.4.4-ffmpeg.diff || return 1
  sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' decoder_plugins/ffmpeg/ffmpeg.c || return 1
  ./configure --prefix=/usr --without-rcc || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
