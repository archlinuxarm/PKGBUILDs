# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches for AArch64 from Fedora

pkgname=js
pkgver=24.2.0
pkgrel=4
pkgdesc="JavaScript interpreter and libraries"
arch=(i686 x86_64)
url="https://developer.mozilla.org/En/SpiderMonkey/17"
license=(MPL)
depends=(nspr gcc-libs readline zlib)
makedepends=(python2 zip libffi)
options=(!staticlibs)
source=(http://ftp.mozilla.org/pub/mozilla.org/js/mozjs-$pkgver.tar.bz2
        mozjs24-0001-Add-AArch64-support.patch
        mozjs24-48-bit-VA-fix.patch)
md5sums=('5db79c10e049a2dc117a6e6a3bc78a8e'
         '06eea5b38cf71b894a079a1c932e421b'
         '82e9016bc35b745d56aec6dbea3ed520')

prepare() {
  cd mozjs-$pkgver/js/src

  rm -r editline
  rm -r ctypes/libffi

  # Fix incompatibility with Perl 5.22
  sed -i 's/(defined\((@TEMPLATE_FILE)\))/\1/' config/milestone.pl

  cd ../..
  patch -p1 -i ../mozjs24-0001-Add-AArch64-support.patch
  patch -p1 -i ../mozjs24-48-bit-VA-fix.patch
}

build() {
  unset CPPFLAGS
  CXXFLAGS+=' -fno-delete-null-pointer-checks'
  cd mozjs-$pkgver/js/src
  ./configure --prefix=/usr --with-system-nspr --enable-system-ffi \
     --enable-readline --enable-threadsafe
  make
}

check() {
  cd mozjs-$pkgver/js/src
  make check
}

package() {
  cd mozjs-$pkgver/js/src
  make DESTDIR="$pkgdir" install
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -x {} +
}

# vim:set ts=2 sw=2 et:
