# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia pl>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to attempt fixing ARM hackery by bandwagoning developers

pkgname=x265
pkgver=1.3
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs')
makedepends=('yasm' 'cmake')
url='https://bitbucket.org/multicoreware/x265'
source=($url/get/$pkgver.tar.bz2
        arm.patch)
md5sums=('ce3a14d588ff7f734d87349b2e58971b'
         'db9fa24ac05001aa88d0b1284ec3b53a')

prepare() {
  cd multicoreware-x265-*
  patch -p1 -i ../arm.patch
}

build() {
  cd multicoreware-x265-*/build/linux
  [[ $CARCH == x86_64 ]] && LDFLAGS+=',-z,noexecstack'
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr ../../source
  make
}

package() {
  cd multicoreware-x265-*/build/linux
  make DESTDIR="$pkgdir" install
}
