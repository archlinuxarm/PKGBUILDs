# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia pl>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to attempt fixing ARM hackery by bandwagoning developers

pkgname=x265
pkgver=0.8
pkgrel=2
pkgdesc='Open Source H265/HEVC video encoder'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs')
makedepends=('yasm' 'cmake')
url='https://bitbucket.org/multicoreware/x265'
source=($url/get/$pkgver.tar.bz2
        arm.patch)
md5sums=('0c690dfad024e87f6b617e6fc2f354eb'
         '9e90fdd64e551be7b76922606d0c8210')

prepare() {
  cd multicoreware-x265-*
  patch -p1 -i ../arm.patch
}

build() {
  cd multicoreware-x265-*/build/linux
  [[ $CARCH == x86_64 ]] && LDFLAGS+=',-z,noexecstack'
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr ../../source
  make VERBOSE=1
}

package() {
  cd multicoreware-x265-*/build/linux
  make DESTDIR="$pkgdir" install
}
