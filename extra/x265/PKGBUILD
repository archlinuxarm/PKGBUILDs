# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz@interia.pl>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to attempt fixing ARM hackery by bandwagoning developers
#  - retain 10/12 bit separation for AArch64
#  - disable all ARM assembly - version 2.8 broke ARM support completely
#  - patch from upstream bug https://bitbucket.org/multicoreware/x265/issues/442

pkgname=x265
pkgver=3.4
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=(x86_64)
url=https://bitbucket.org/multicoreware/x265
license=(GPL)
depends=(gcc-libs)
makedepends=(
  cmake
  mercurial
  nasm
  ninja
)
provides=(libx265.so)
source=(hg+https://bitbucket.org/multicoreware/x265#tag=2a65b720985096bcb1664f7cb05c3d04aeb576f5
        arm.patch)
sha256sums=('SKIP'
            '067b934ae83339deedc58608d272529a004789c3e2819b94636d66a5806fd64b')

pkgver() {
  cd x265

  hg id --tags
}

prepare() {
  cd x265
  patch -p1 -i ../arm.patch
}

build() {
  [[ $CARCH != "aarch64" ]] && CONFIG="-DENABLE_ASSEMBLY=OFF"
  cmake -S x265/source -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_SHARED=TRUE \
    $CONFIG
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
