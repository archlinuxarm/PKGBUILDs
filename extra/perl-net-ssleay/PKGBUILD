# $Id: PKGBUILD 46990 2009-07-22 18:21:21Z francois $
# Maintainer: eric <eric@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.35
pkgrel=1
pkgdesc="Perl extension for using OpenSSL"
arch=(i686 x86_64)
license=('custom:BSD')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('perl>=5.10.0' 'openssl')
options=(!emptydirs)
replaces=('net-ssleay')
provides=('net-ssleay')
source=(http://www.cpan.org/authors/id/F/FL/FLORA/${_cpanname}-${pkgver}.tar.gz)
md5sums=('1e4ec37a4467eb66a62d3c090ac9029b')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor || return 1
  make  || return 1
  make install DESTDIR=${pkgdir} || return 1

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  install -D -m644 README ${pkgdir}/usr/share/licenses/${pkgname}/README
}
# vim: ts=2 sw=2 et ft=sh
