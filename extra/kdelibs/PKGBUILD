# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix unclickable plasmoid buttons on ARM

pkgname=kdelibs
pkgver=4.14.30
_kdeappver=16.12.3
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='https://www.kde.org/'
license=('GPL' 'LGPL' 'FDL')
depends=('attica-qt4' 'libxss' 'qca-qt4' 'libdbusmenu-qt4' 'polkit-qt4'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr'
        'docbook-xsl' 'upower' 'udisks2' 'libxcursor' 'phonon-qt4'
        'media-player-info' 'libutempter' 'icu')
makedepends=('cmake' 'automoc4' 'avahi' 'libgl' 'mesa')
source=("https://download.kde.org/stable/applications/${_kdeappver}/src/$pkgname-$pkgver.tar.xz"
        'kde-applications-menu.patch' 'archlinux-menu.patch' 'qt4.patch' 'kdelibs-no-kdewebkit.patch'
        'arm-coronaoffscreen-input-fixes.diff')
sha256sums=('a39fc3ef320c27ea49e5d238b2a860ef61824a20922268a18392b0521a38196d'
            '0eed754cf3e9f41be5ea22314bf49cf5571b421d81b8f525287830bd1b9b684e'
            '877df7357360b70d9f7d6bc8c211f82fd8b00cc2eb34e9993ff6aae20faf1cad'
            'ced10a9a83ff2a7f82edf51f6f399f8338b34ab5a589ef43637517d204843bea'
            '2df372f1bfcef1902349f9ca8820315030d39dd2bb769776b94fd3fcbf9621f5'
            'f6437abf7ad6dd03a62ef36f1d65ebd42229ae1526bc5687c84878afbbabff46')

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  # avoid file conflict with gnome-menus
  patch -p1 -i ../kde-applications-menu.patch
  # add Archlinux menu entry
  patch -p1 -i ../archlinux-menu.patch
  # qmake refers to Qt5
  patch -p1 -i ../qt4.patch
  # disable kdewebkit (Fedora)
  patch -p1 -i ../kdelibs-no-kdewebkit.patch
  # fix unclickable plasmoid buttons on ARM
  patch -p1 -i ../arm-coronaoffscreen-input-fixes.diff
}

build() {
       cd build
       cmake ../$pkgname-$pkgver \
               -DCMAKE_BUILD_TYPE=Release \
               -DKDE4_BUILD_TESTS=OFF \
               -DCMAKE_SKIP_RPATH=ON \
               -DKDE_DISTRIBUTION_TEXT='Arch Linux' \
               -DCMAKE_INSTALL_PREFIX=/usr \
               -DSYSCONF_INSTALL_DIR=/etc \
               -DLOCALE_INSTALL_DIR=/usr/share/locale/kde4 \
               -DKDE_DEFAULT_HOME='.kde4' \
               -DWITH_FAM=OFF \
               -DWITH_SOLID_UDISKS2=ON
       make
}

package() {
       cd build
       make DESTDIR="$pkgdir" install

       # cert bundle seems to be hardcoded
       # link it to the one from ca-certificates
       rm -f "$pkgdir"/usr/share/apps/kssl/ca-bundle.crt
       ln -sf /etc/ssl/certs/ca-certificates.crt "$pkgdir"/usr/share/apps/kssl/ca-bundle.crt
}
