# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed dependency on numactl
#  - AArch64 patch

pkgname=irqbalance
pkgver=1.1.0
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('i686' 'x86_64')
url="https://github.com/Irqbalance/irqbalance"
license=('GPL')
depends=('glib2' 'libcap-ng')
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
        irqbalance.service
        irqbalance-fix-aarch64.patch)
md5sums=('b15d975336080bcac4be0c1752d43cf3'
         '767061c257b26cc3e3c4a5e462166579'
         'e3c0dce43ae1d68274c674ac3f91f250')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../irqbalance-fix-aarch64.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  install -D -m644 ../irqbalance.service \
    "$pkgdir"/usr/lib/systemd/system/irqbalance.service
}
