# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed dependency on numactl
#  - upstream patch for AArch64

pkgname=irqbalance
pkgver=1.2.0
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('i686' 'x86_64')
url="https://github.com/Irqbalance/irqbalance"
license=('GPL')
depends=('glib2' 'libcap-ng')
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
        irqbalance.service
        0001-fix-aarch64-compile-error-due-to-undefined-variable.patch)
md5sums=('d1b3f2c31389f2ab955dfa3d02f9f296'
         '767061c257b26cc3e3c4a5e462166579'
         'c926a091bc1a50102fd9d4c91eac4fe5')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../0001-fix-aarch64-compile-error-due-to-undefined-variable.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  install -D -m644 ../irqbalance.service \
    "$pkgdir"/usr/lib/systemd/system/irqbalance.service
}
