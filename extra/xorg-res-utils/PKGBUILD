# $Id: PKGBUILD 36734 2009-04-27 03:02:17Z eric $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-res-utils
pkgver=1.0.3
pkgrel=3
pkgdesc="X.Org X11 resource utilities"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw')
makedepends=('pkgconfig')
groups=('xorg')
source=(${url}/releases/individual/app/appres-1.0.1.tar.bz2
        ${url}/releases/individual/app/editres-1.0.3.tar.bz2
        ${url}/releases/individual/app/listres-1.0.1.tar.bz2
        ${url}/releases/individual/app/viewres-1.0.1.tar.bz2
        LICENSE.listres LICENSE.viewres)
md5sums=('420e6cdf38bb771ec8b00228bf09e8ff' 'a25f931cc6c8d03daaed434f5db5df2d'\
         '827a1ac5adf9aadd0c13b54a897e297b' 'cdbed540daf7975b56000420c9aa5bf9'\
         '9c9b17ae5e9be0c570dabe9e54f54f6d' '9c9b17ae5e9be0c570dabe9e54f54f6d')

build() {
  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man --disable-xprint
      make || return 1
      make DESTDIR=${pkgdir} install || return 1
      install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done
#Replacing stub license files
  install -D -m644 "${srcdir}/LICENSE.listres" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.listres"
  install -D -m644 "${srcdir}/LICENSE.viewres" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.viewres"
}
