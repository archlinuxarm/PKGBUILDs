# $Id: PKGBUILD 119805 2011-04-15 11:51:39Z stephane $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - removed valgrind from makedepends, can't build it for ARM
#  - removed --with-valgrind from configure line

plugrel=1

pkgname=python
pkgver=3.2
pkgrel=2
_pybasever=3.2
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org/"
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('tk' 'sqlite3')
optdepends=('tk: for tkinter')
provides=('python3')
replaces=('python3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${_pybasever}/Python-${pkgver}.tar.xz
        CVE-2011-1521.patch)
sha1sums=('55a3a9d39f31563370d0c494373bb6d38e4d1a00'
          '561161ce5ae3a91254352c09a33e3e4434444e14')  
build() {
  cd "${srcdir}/Python-${pkgver}"

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  # urllib Security Vulnerability
  # http://blog.python.org/2011/04/urllib-security-vulnerability-fixed.html
  patch -Np1 -i ../CVE-2011-1521.patch

  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --with-wide-unicode \
              --with-system-expat \
              --with-system-ffi

  make
}

check() {
  cd "${srcdir}/Python-${pkgver}"
  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
     "${srcdir}/Python-${pkgver}/python" -m test.regrtest -x test_distutils test_site
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" install maninstall

  # Why are these not done by default...
  ln -sf python3               "${pkgdir}/usr/bin/python"
  ln -sf python3-config        "${pkgdir}/usr/bin/python-config"
  ln -sf idle3                 "${pkgdir}/usr/bin/idle"
  ln -sf pydoc3                "${pkgdir}/usr/bin/pydoc"
  ln -sf python${_pybasever}.1 "${pkgdir}/usr/share/man/man1/python3.1"

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}mu.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}mu/libpython${_pybasever}mu.so"

  # Clean-up reference to build directory
  sed -i "s#$srcdir/Python-${pkgver}:##" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}mu/Makefile"

  # License
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
