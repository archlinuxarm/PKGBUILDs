# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=2.6.2
pkgrel=3
_pybasever=2.6
pkgdesc="A high-level scripting language"
arch=('arm')
license=('custom')
url="http://www.python.org"
depends=('db' 'bzip2' 'gdbm' 'openssl>=0.9.8d' 'zlib' 'expat' 'sqlite3>=3.6.15-2')
makedepends=('tk>=8.5.0')
optdepends=('tk: for IDLE, pynche and modulator')
provides=('python-elementtree' 'python-ctypes')
replaces=('python-elementtree' 'python-ctypes')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
		python-2.6-internal-expat.patch
		python-2.6-gdbm.patch
		python-2.6-smtplib.patch
		python-2.6-readline.patch
		modulator-launcher
		pynche-launcher)
md5sums=('245db9f1e0f09ab7e0faaa0cf7301011'
         '993800dbcc0c4f199c1ceee998a06e71'
         '13fab4bb2cacbc433e62d7191a17f71e'
         'c38d58521acfb6eec0c3d3c5ad38410a'
         '1f0aed8b96d4531f40fd3e24a80157a5'
         'b4a531570415280dc406c8d5c1fa8d83'
         '9f731b9033db285132a3524fb36409e7')

build() {
  cd ${srcdir}/Python-${pkgver}

  patch -Np0 -i ${srcdir}/python-2.6-internal-expat.patch
  patch -Np1 -i ${srcdir}/python-2.6-gdbm.patch
  patch -Np0 -i ${srcdir}/python-2.6-smtplib.patch
  patch -Np0 -i ${srcdir}/python-2.6-readline.patch

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --enable-shared --with-threads --enable-unicode

  make || return 1
  make DESTDIR=${pkgdir} install

  rm ${pkgdir}/usr/bin/python
  ln -sf python${_pybasever} ${pkgdir}/usr/bin/python
  ln -sf python${_pybasever} ${pkgdir}/usr/bin/python2

  ln -sf ../../libpython${_pybasever}.so \
    ${pkgdir}/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so

  # Install the tools
  #modulator
  sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/modulator-launcher
  install -m755 ${srcdir}/modulator-launcher ${pkgdir}/usr/bin/modulator
  cp -r Tools/modulator ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #pynche
  sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/pynche-launcher
  install -m755 ${srcdir}/pynche-launcher ${pkgdir}/usr/bin/pynche
  rm -f Tools/pynche/*.pyw
  cp -r Tools/pynche ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #gettext
  install -m755 Tools/i18n/pygettext.py ${pkgdir}/usr/bin/
  install -m755 Tools/i18n/msgfmt.py ${pkgdir}/usr/bin/

  mkdir -p ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts
  install Tools/scripts/README ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/
  install Tools/scripts/*py ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/

  # license
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
