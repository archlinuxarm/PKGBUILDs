# $Id: PKGBUILD 144857 2011-12-08 16:10:09Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: macau <tema.prokopenko@gmail.com> //add egl and brightness control for arm systems

pkgname=kdebase-workspace
_pkgname=kde-workspace
pkgver=4.7.4
pkgrel=2
pkgdesc="KDE Base Workspace"
arch=('armv7h')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=('kdepim-runtime' 'lm_sensors' 'libraw1394' 'libqalculate'
         'qimageblitz' 'polkit-kde' 'consolekit' 'xorg-xprop' 'libxdamage'
         'libxklavier' 'xorg-xsetroot' 'libxcomposite' 'libxinerama'
         'xorg-xrdb')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'kdebindings-python'
             'networkmanager')
optdepends=('kde-wallpapers: officials KDE wallapers')
replaces=('kdmtheme' 'kde-common' 'guidance-power-manager' 'policykit-kde'
          'kdebase-kinfocenter')
conflicts=('kde-common' 'guidance-power-manager' 'policykit-kde'
           'kdebase-kinfocenter')
install="${pkgname}.install"
backup=('usr/share/config/kdm/kdmrc'
        'etc/pam.d/kde'
        'etc/pam.d/kde-np'
        'etc/pam.d/kscreensaver')
options=('emptydirs')
source=("http://download.kde.org/stable/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2"
        'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam'
        'fixpath.patch' 'terminate-server.patch' 'kdm-xinitrd.patch' 'pwm-backlight.patch')
sha1sums=('b7810ba13f6f2a1c4783b153ad9349a1dd27b495'
          '5db3a245201bd4a50e65aa2ef583cf5490e4f646'
          'f7b38af38549242a240f1a90ab9964ca8a366129'
          '603cc79c4d2b4eae62bb5f244aeecb3a778b5516'
          '106635aa1aae51d6f0668b1853f6c49a4fe9d3d8'
          'd7b5883f7e65c6839b1f65f94d58026673dd0226'
          'ac7bc292c865bc1ab8c02e6341aa7aeaf1a3eeee'
          'd509dac592bd8b310df27991b208c95b6d907514'
          '338ed25b3820cf85d99720903106bf81be9504b0')

build() {
	cd "${srcdir}"/${_pkgname}-${pkgver}
    patch -p1 -i "${srcdir}"/kdm-xinitrd.patch
	patch -p0 -i "${srcdir}"/fixpath.patch
	patch -p0 -i "${srcdir}"/terminate-server.patch
	patch -p1 -i "${srcdir}"/pwm-backlight.patch

	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${_pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_Xmms=OFF \
        -DWITH_Googlegadgets=OFF \
        -DWITH_libgps=OFF \
        -DWITH_OpenGLES=ON \
        -DKWIN_BUILD_WITH_OPENGLES=ON \
        -DPYTHON_EXECUTABLE=/usr/bin/python2
	make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR="${pkgdir}" install

	install -D -m755 "${srcdir}"/kdm "${pkgdir}"/etc/rc.d/kdm
	install -D -m644 "${srcdir}"/kde.pam "${pkgdir}"/etc/pam.d/kde
	install -D -m644 "${srcdir}"/kde-np.pam "${pkgdir}"/etc/pam.d/kde-np
	install -D -m644 "${srcdir}"/kscreensaver.pam "${pkgdir}"/etc/pam.d/kscreensaver
	install -d -m755 "${pkgdir}"/usr/share/xsessions/
	ln -sf /usr/share/apps/kdm/sessions/kde-plasma{,-safe}.desktop "${pkgdir}"/usr/share/xsessions/
	install -d -m755 "${pkgdir}"/etc/kde/{env,shutdown}

	install -d -g 135 -o 135 "${pkgdir}"/var/lib/kdm
}

