# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
pkgver=4.3.2
pkgrel=1
pkgdesc="KDE Base Workspace"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=('kdelibs-experimental' 'kdepimlibs' 'kdebase-runtime' 'libxxf86misc' 'libxcomposite'
	 'libxss' 'lm_sensors' 'libxklavier' 'qimageblitz' 'polkit-qt' 'libxft' 'libxdamage')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'networkmanager' 'bluez' 'kdebindings-python')
replaces=('kdmtheme' 'kde-common' 'guidance-power-manager' 'policykit-kde')
conflicts=('kde-common' 'guidance-power-manager' 'policykit-kde')
optdepends=('kdebindings-python: plasma scriptengine for Python')
install='kdebase-workspace.install'
backup=('usr/share/config/kdm/kdmrc'
        'etc/pam.d/kde'
        'etc/pam.d/kde-np'
        'etc/pam.d/kscreensaver')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kdm-zsh-profile.patch' 'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam'
        'fixpath.patch')
md5sums=('2966168f421b3093a119a7c7089b87fb'
         '721e97031b62aee8914e8617e86f9235'
         '5d80164932e0d44d8b802d3929a004ab'
         '10a490653b002e6f9e7476ff9d37c011'
         '552337fd9a3982d809ea16c7f0033d42'
         '367a3538f54db71f108b34cfa31088ac'
         '47a1f12673f66e62e2463efd7037a26a')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -p0 -i ${srcdir}/kdm-zsh-profile.patch
	patch -p0 -i ${srcdir}/fixpath.patch

	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_Googlegadgets=OFF \
		-DWITH_QEdje=OFF
	make || return 1
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install

	install -D -m755 ${srcdir}/kdm ${pkgdir}/etc/rc.d/kdm
	install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
	install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
	install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
	install -d -m755 ${pkgdir}/etc/X11/sessions/
	ln -sf /usr/share/apps/kdm/sessions/kde{,-safe}.desktop ${pkgdir}/etc/X11/sessions/
	install -d -m755 ${pkgdir}/etc/kde/{env,shutdown}
}
