# $Id: PKGBUILD 142653 2011-11-12 17:18:30Z ibiru $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - Added patch to fix building on ARM

plugrel=1

pkgbase=koffice
pkgname=(
	'koffice-filters'
	'koffice-interfaces'
	'koffice-libs'
	'koffice-plugins'
	'koffice-pics'
	'koffice-servicetypes'
	'koffice-templates'
	'koffice-tools'
	'koffice-karbon'
	'koffice-karbon-doc'
#	'koffice-kchart'
#	'koffice-kchart-doc'
	'koffice-kexi'
	'koffice-kexi-doc'
	'koffice-kformula'
	'koffice-kformula-doc'
	'koffice-kounavail'
	'koffice-kplato'
#	'koffice-kplato-doc'
	'koffice-kpresenter'
	'koffice-kpresenter-doc'
	'koffice-krita'
#	'koffice-krita-doc' # no krita-doc anymore upstream
	'koffice-kspread'
	'koffice-kspread-doc'
	'koffice-kword'
#	'koffice-kword-doc' # see http://userbase.kde.org/KWord/Manual for documentation (not supplied by upstream anymore in the sources)
	'koffice-handbook'
	'koffice-thesaurus-doc'
#	'koffice-kdgantt' # still an empty package
)
pkgver=2.3.3
pkgrel=11
arch=('i686' 'x86_64')
url='http://koffice.kde.org'
license=('GPL' 'LGPL' 'FDL')
makedepends=('pkg-config' 'cmake' 'automoc4' 'boost' 'eigen' 'gsl' 'lcms'
             'glew' 'qimageblitz' 'kdepimlibs' 'pstoedit' 'poppler-qt' 'libwpd'
             'libwpg' 'opengtl' 'libkdcraw' 'oxygen-icons' 'libgsf')
groups=('koffice')
source=("http://download.kde.org/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.bz2"
        'kde4-koffice-libwpg02.patch' 'gcc46.patch' 'qt48fix.patch' '20_arm_fix.patch')
sha256sums=('31ba0d98c0d29c7b8ab97efdeb6c618b82177b2b0ec85da088178254da43c099'
            '69106deb4081d71b5bd8f2e4f5af67ca689e4ce9f2bb49c11dbce5fb3409d612'
            'e095c0b2bbedf41da6535a68b2275464dafd3f194566028d0135322f596e4739'
            'a20d7412ceeefdf7987b1c5179a740c7ee188e314ffb94cc226967ee194bc2aa'
            '3a2debc7ccf6f821cec1cf0e5c2a38bcf9ac08a8673f445f21276f31cafb92b3')
build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	patch -p1 -i "${srcdir}/kde4-koffice-libwpg02.patch"
	patch -p1 -i "${srcdir}"/gcc46.patch
    patch -p1 -i "${srcdir}"/qt48fix.patch
    patch -p1 -i "${srcdir}/20_arm_fix.patch"

	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

##### 1st package common files, depends, optdepends of the koffice applications #####

package_koffice-filters(){
	pkgdesc="Filters for the KDE office suite"
	depends=('shared-mime-info' 'libgsf')
	optdepends=('libwpd: WordPerfect document importer'
                   'libwpg: Corel WordPerfect Graphics image importer' 'pstoedit: Karbon EPS import filter'
                   'libkdcraw: support for raw images' 'poppler: support for pdf' 
		   'koffice-kspread: Kspread filters' 'koffice-karbon: Karbon filters'
		   'koffice-kplato: Kplato filters')
	install=filters.install
        cd "${srcdir}/build/filters"
        make DESTDIR="${pkgdir}" install
}

package_koffice-interfaces(){
	pkgdesc="Interfaces for the KDE office suite"
	depends=('koffice-libs')
	cd "${srcdir}/build/interfaces"
	make DESTDIR="${pkgdir}" install
}

package_koffice-libs(){
	pkgdesc="Libs for the KDE office suite"
	depends=('kdepimlibs' 'lcms' 'koffice-servicetypes')
        cd "${srcdir}/build/libs"
        make DESTDIR="${pkgdir}" install
}

package_koffice-plugins(){
	pkgdesc="Plugins for the KDE office suite"
	depends=('koffice-libs')
	replaces=('koffice-kchart')
	conflicts=('koffice-kchart')
        cd "${srcdir}/build/plugins"
        make DESTDIR="${pkgdir}" install
}

package_koffice-pics(){
	pkgdesc="Icons for the KDE office suite"
	depends=('desktop-file-utils' 'xdg-utils')
	install=koffice.install
        cd "${srcdir}/build/pics"
        make DESTDIR="${pkgdir}" install
}

package_koffice-servicetypes(){
	pkgdesc="Servicetypes for the KDE office suite"
        cd "${srcdir}/build/servicetypes"
        make DESTDIR="${pkgdir}" install
}

package_koffice-templates(){
	pkgdesc="Templates for the KDE office suite"
        cd "${srcdir}/build/templates"
        make DESTDIR="${pkgdir}" install
}

package_koffice-tools(){
	pkgdesc="Tools for the KDE office suite"
	depends=('koffice-libs' 'oxygen-icons')
        cd "${srcdir}/build/tools"
        make DESTDIR="${pkgdir}" install
}

#### package common files done #####

#### package apps and their docs #####

package_koffice-karbon(){
	pkgdesc="Vector drawing application for KDE"
	depends=('koffice-libs' 'koffice-templates' 'oxygen-icons')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd "${srcdir}/build/karbon"
        make DESTDIR="${pkgdir}" install
}

package_koffice-karbon-doc(){
	pkgdesc="Documentation for karbon"
        cd "${srcdir}/build/doc/karbon"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kexi(){
        pkgdesc="Integrated data management application for the KDE office suite"
        depends=('koffice-kspread' 'oxygen-icons')
	install=koffice.install
        cd "${srcdir}/build/kexi"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kexi-doc(){
        pkgdesc="Documentation for kexi"
        cd "${srcdir}/build/doc/kexi"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kformula(){
        pkgdesc="Formula editor for the KDE office suite"
	depends=('koffice-libs')
	install=koffice.install
        cd "${srcdir}/build/kformula"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kformula-doc() {
	pkgdesc="Documentation for koffice-kformula"
	cd "${srcdir}/build/doc/kformula"
        make DESTDIR="${pkgdir}" install
}


package_koffice-kounavail(){
	pkgdesc="Kounavail for the KDE office suite"
	depends=('koffice-libs')
        cd "${srcdir}/build/kounavail"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kplato(){
	pkgdesc="Project management application for KDE"
	depends=('koffice-kword' 'oxygen-icons')
	install=koffice.install
        cd "${srcdir}/build/kplato"
        make DESTDIR="${pkgdir}" install
}

#package_koffice-kplato-doc(){
#	pkgdesc="Documentation for kplato"
#        cd ${srcdir}/build/doc/kplato
#        make DESTDIR="${pkgdir}" install
#}

package_koffice-kpresenter(){
	pkgdesc="Presentation tool for KDE"
	depends=('koffice-libs' 'koffice-templates' 'oxygen-icons')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd "${srcdir}/build/kpresenter"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kpresenter-doc(){
	pkgdesc="Documentation for kpresenter"
        cd "${srcdir}/build/doc/kpresenter"
        make DESTDIR="${pkgdir}" install
}

package_koffice-krita(){
	pkgdesc="Graphics editor for KDE"
	depends=('glew' 'qimageblitz' 'oxygen-icons' 'koffice-templates' \
		 'koffice-plugins' 'poppler-qt' 'opengtl')
        optdepends=('koffice-filters: import/export filters' 'libkdcraw: support for raw images')
	install=krita.install
        cd "${srcdir}/build/krita"
        make DESTDIR="${pkgdir}" install
}

#package_koffice-krita-doc(){
#	pkgdesc="Documentation for krita"
#        cd ${srcdir}/build/doc/krita
#        make DESTDIR="${pkgdir}" install
#}


package_koffice-kspread(){
	pkgdesc="Spreadsheet application for KDE"
	depends=('gsl' 'koffice-templates' 'koffice-plugins' 'oxygen-icons')
	optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd "${srcdir}/build/kspread"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kspread-doc(){
	pkgdesc="Documentation for kspread"
	cd "${srcdir}/build/doc/kspread"
        make DESTDIR="${pkgdir}" install
}

package_koffice-kword(){
	pkgdesc="Wordprocessor for KDE"
	depends=('koffice-templates' 'koffice-pics' 'oxygen-icons' \
		'koffice-kounavail' 'koffice-plugins')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd "${srcdir}/build/kword"
        make DESTDIR="${pkgdir}" install

        # some sed lines to have python2 in extension scripts
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" "${pkgdir}/usr/share/apps/kword/scripts/extensions/oouno.py"
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" "${pkgdir}/usr/share/apps/kword/scripts/extensions/importdoxyxml.py"
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" "${pkgdir}/usr/share/apps/kword/scripts/extensions/onlinehelp.py"
}

#package_koffice-kword-doc(){
#	pkgdesc="Documentation for kword"
#        cd ${srcdir}/build/doc/kword
#        make DESTDIR="${pkgdir}" install
#}

##### package apps done #####

#package_koffice-kchart(){
#	pkgdesc="Charting tool for KDE"
#	depends=('hicolor-icon-theme' 'koffice-interfaces' 'koffice-templates')
#        optdepends=('koffice-filters: import/export filters')
#	install=koffice.install
#        cd "${srcdir}/build/kchart"
#        make DESTDIR="${pkgdir}" install
#}

#package_koffice-kchart-doc(){
#	pkgdesc="Documentation for kchart"
#        cd "${srcdir}/build/doc/kchart"
#        make DESTDIR="${pkgdir}" install
#}

#package_koffice-kdgantt(){
#	pkgdesc="Gantt for KDE"
#        cd "${srcdir}/build/kdgantt"
#        make DESTDIR="${pkgdir}" install
#}

package_koffice-handbook() {
	pkgdesc="Documentation for koffice"
	cd "${srcdir}/build/doc/koffice"
        make DESTDIR="${pkgdir}" install
}

package_koffice-thesaurus-doc(){
	pkgdesc="Documentation for koffice-thesaurus"
	cd "${srcdir}/build/doc/thesaurus"
        make DESTDIR="${pkgdir}" install
}
