# $Id: PKGBUILD 33905 2009-04-05 22:51:54Z eric $
# Maintainer: juergen <juergen@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gcal
pkgver=3.01
pkgrel=4
pkgdesc="Gcal displays hybrid and proleptic Julian and Gregorian calendar sheets"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gcal/"
license=('GPL')
depends=('perl' 'texinfo')
install=gcal.install
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('65f3394653829fd77f6862045112ad4f')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info --with-included-regexps || return 1
  make || return 1
  make prefix=$pkgdir/usr mandir=$pkgdir/usr/share/man/man1 infodir=$pkgdir/usr/share/info install || return 1
  rm $pkgdir/usr/share/locale/locale.alias
  rm $pkgdir/usr/share/info/dir
  gzip $pkgdir/usr/share/info/*


  # Need to fix up paths for scripts in misc directory
  # that are automatically created by the makefile
  for miscfile in $pkgdir/usr/share/gcal/misc/*/*
  do
	 sed -i "s:${pkgdir}::g" $miscfile || return 1
  done
  
  # Rebuild the symlinks that makefile created into the image /usr/bin
  # directory during make install
  ln -s -f /usr/share/gcal/misc/daily/daily $pkgdir/usr/bin/gcal-daily
  ln -s -f /usr/share/gcal/misc/ddiff/ddiff $pkgdir/usr/bin/gcal-ddiff
  ln -s -f /usr/share/gcal/misc/ddiff/ddiffdrv $pkgdir/usr/bin/gcal-ddiffdrv
  ln -s -f /usr/share/gcal/misc/dst/dst $pkgdir/usr/bin/gcal-dst
  ln -s -f /usr/share/gcal/misc/gcalltx/gcalltx $pkgdir/usr/bin/gcal-gcalltx
  ln -s -f /usr/share/gcal/misc/gcalltx/gcalltx.pl $pkgdir/usr/bin/gcal-gcalltx.pl
  ln -s -f /usr/share/gcal/misc/moon/moon $pkgdir/usr/bin/gcal-moon
  ln -s -f /usr/share/gcal/misc/mrms/mrms $pkgdir/usr/bin/gcal-mrms
  ln -s -f /usr/share/gcal/misc/srss/srss $pkgdir/usr/bin/gcal-srss
  ln -s -f /usr/share/gcal/misc/wloc/wlocdrv $pkgdir/usr/bin/gcal-wlocdrv
}
