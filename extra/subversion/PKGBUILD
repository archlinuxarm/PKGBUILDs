# $Id: PKGBUILD 77223 2010-04-11 14:13:33Z pierre $
# Maintainer: Paul Mattal <paul@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=subversion
pkgver=1.6.9
pkgrel=6
pkgdesc="Replacement for CVS, another versioning system (svn)"
arch=('i686' 'x86_64')
license=('apache' 'bsd')
depends=('neon' 'apr-util' 'libldap')
makedepends=('heimdal' 'apache' 'python' 'perl' 'swig' 'ruby' 
             'autoconf' 'sqlite3' 'db' 'e2fsprogs')
source=(http://subversion.tigris.org/downloads/$pkgname-$pkgver.tar.bz2
        svnserve svn svnserve.conf svnmerge.py
        subversion.rpath.fix.patch
	subversion.suppress.deprecation.warnings.patch)

backup=('etc/xinetd.d/svn' 'etc/conf.d/svnserve')
url="http://subversion.tigris.org/"
provides=('svn')
options=('!makeflags' '!libtool')
optdepends=('bash-completion: for svn bash completion')

build() {
   cd ${srcdir}/${pkgname}-${pkgver} || return 1

   # apply patches
   patch -p0 < $srcdir/subversion.rpath.fix.patch || return 1
   patch -p1 -i $srcdir/subversion.suppress.deprecation.warnings.patch \
               || return 1

   # configure
   autoreconf || return 1
   ./configure --prefix=/usr --with-apr=/usr --with-apr-util=/usr \
               --with-zlib=/usr --with-neon=/usr --with-apxs \
               --with-sqlite=/usr || return 1

   # build
   (make external-all && make LT_LDFLAGS="-L$Fdestdir/usr/lib" local-all ) \
               || return 1

   # install
   export LD_LIBRARY_PATH=${pkgdir}/usr/lib:$LD_LIBRARY_PATH
   make DESTDIR=${pkgdir} install || return 1

   make DESTDIR=${pkgdir} swig-py || return 1
   make install-swig-py DESTDIR=${pkgdir} || return 1

   mkdir -p ${pkgdir}/usr/lib/python2.6 || return 1
   mv ${pkgdir}/usr/lib/svn-python/ ${pkgdir}/usr/lib/python2.6/site-packages \
               || return 1

   mkdir -p ${pkgdir}/usr/share/subversion || return 1
   install -d -m 755 tools/hook-scripts ${pkgdir}/usr/share/subversion/ \
               || return 1
   rm -f ${pkgdir}/usr/share/subversion/hook-scripts/*.in || return 1

   make DESTDIR=${pkgdir} swig-pl || return 1
   make install-swig-pl DESTDIR=${pkgdir} INSTALLDIRS=vendor || return 1
   rm -f ${pkgdir}/usr/lib/perl5/vendor_perl/auto/SVN/_Core/.packlist \
               || return 1
   rm -rf ${pkgdir}/usr/lib/perl5/core_perl || return 1

   make DESTDIR=${pkgdir} swig-rb || return 1
   make install-swig-rb DESTDIR=${pkgdir}  || return 1

   mkdir -p ${pkgdir}/etc/rc.d || return 1
   mkdir -p ${pkgdir}/etc/xinetd.d || return 1
   mkdir -p ${pkgdir}/etc/conf.d || return 1

   install -m 755 ${srcdir}/svnserve ${pkgdir}/etc/rc.d || return 1
   install -m 644 ${srcdir}/svn ${pkgdir}/etc/xinetd.d || return 1
   install -m 644 ${srcdir}/svnserve.conf ${pkgdir}/etc/conf.d/svnserve \
               || return 1
   install -m 755 ${srcdir}/svnmerge.py ${pkgdir}/usr/bin/svnmerge || return 1
   install -D -m 644 ${srcdir}/subversion-$pkgver/COPYING \
               ${pkgdir}/usr/share/licenses/$pkgname/LICENSE || return 1

   # bash completion
   install -Dm 644 ${srcdir}/${pkgname}-${pkgver}/tools/client-side/bash_completion \
               ${pkgdir}/etc/bash_completion.d/subversion || return 1
}
md5sums=('9c30a47b1d48664e7afef68bb4834c53'
         'a2b029e8385007ffb99b437b30521c90'
         'a0db6dd43af33952739b6ec089852630'
         'c459e299192552f61578f3438abf0664'
         '21bf3aa5b797ce98eba8307f30e9c070'
         '6b4340ba9d8845cd8497e013ae01be3f'
         '1166f3b7413d7e7450299b3525680bbe')
