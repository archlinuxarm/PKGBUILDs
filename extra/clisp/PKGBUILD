# $Id: PKGBUILD 43139 2009-06-19 23:22:15Z eric $
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=clisp
pkgver=2.47
pkgrel=3
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=('i686' 'x86_64')
license=('GPL')
url="http://clisp.cons.org/"
depends=('readline' 'libsigsegv')
provides=('common-lisp')
makedepends=('ffcall')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/clisp/clisp-${pkgver}.tar.bz2
        clisp-2.47-cpp.patch)
md5sums=('8fa89bb13e865fc7c7150b82682f35af'
         'c7b706135ca640cc088a4ecc68177d0f')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/clisp-2.47-cpp.patch
  unset CFLAGS CXXFLAGS
  ./configure --prefix=/usr --with-readline --with-ffcall src
  cd src
  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make || return 1
  sed -i 's,http://www.lisp.org/HyperSpec/,http://www.lispworks.com/reference/HyperSpec/,g' config.lisp
  make || return 1
  make DESTDIR=$pkgdir install || return 1
}
