# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix ARM assembly
#  - ulimit change to fix segfaults

pkgname=clisp
pkgver=2.49
pkgrel=4
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=('i686' 'x86_64')
license=('GPL')
url="http://clisp.cons.org/"
depends=('readline' 'libsigsegv')
provides=('common-lisp')
makedepends=('ffcall')
options=('!makeflags' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/clisp/clisp-${pkgver}.tar.bz2"
        'clisp-arm.patch')
md5sums=('1962b99d5e530390ec3829236d168649'
         'f52b37840eaae76410f14f1adc8a00e2')

prepare() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -p0 -i ../clisp-arm.patch
  ulimit -s unlimited
}

build() {
  cd $srcdir/${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-readline --with-ffcall src
  cd src
  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make
  sed -i 's,http://www.lisp.org/HyperSpec/,http://www.lispworks.com/reference/HyperSpec/,g' config.lisp
  make
}

check() {
  cd $srcdir/${pkgname}-${pkgver}/src
  make check
}

package() {
  cd $srcdir/${pkgname}-${pkgver}/src
  make DESTDIR=$pkgdir install
}
