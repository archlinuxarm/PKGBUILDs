# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

# ALARM: dequis <dx@dxzone.com.ar>
#  - remove haskell-xcffib dependency by using the pypi release instead

pkgname=python-xcffib
pkgver=1.4.0
_commit=517169a2fb6dcc127059f581d3890e0861e2e950
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-cffi' 'libxcb')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-xeyes')
_name=${pkgname#python-}
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('7012833f2877edea92cdc15030543883e2a848264c034a81222626ad48a69fcdac8a88cec2e07e61a80102640baa7fbdf29bbf25e08e59fc896aada08533bb77')

prepare() {
  touch xcffib-$pkgver/test/__init__.py
}

build() {
  cd xcffib-$pkgver
  python setup.py build
}

check() {
  cd xcffib-$pkgver
  xvfb-run pytest
}

package() {
  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
