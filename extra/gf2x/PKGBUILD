# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed sse2 configure/cflags options

pkgname=gf2x
pkgver=1.3.0
pkgrel=3
pkgdesc='A library for multiplying polynomials over the binary field'
arch=(x86_64)
url='https://gitlab.inria.fr/gf2x/gf2x'
license=(GPL-3.0-or-later)
depends=(glibc)
makedepends=(git)
source=(git+https://gitlab.inria.fr/gf2x/gf2x#tag=gf2x-$pkgver)
sha256sums=('becc47f5ca6e549393ea70147f7dc7b52d88af0ed5bfb9f2e3a4c47658bf48e9')

prepare() {
  cd gf2x
  git cherry-pick -n a2f0fd388c12ca0b9f4525c6cfbc515418dcbaf8 # Fix build
  autoreconf -vi
}

build() {
  cd gf2x
  ./configure \
    --prefix=/usr \
    --disable-pclmul
  make
}

check() {
  cd gf2x
  make -k check
}

package() {
  cd gf2x
  make prefix="$pkgdir"/usr install
}
