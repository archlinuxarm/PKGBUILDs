# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with -Dnative-arch-optimization=false for !aarch64

pkgbase=efl
pkgname=('efl' 'efl-docs')
pkgver=1.23.3
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries"
arch=('x86_64')
url="https://www.enlightenment.org"
license=('BSD' 'LGPL2.1' 'GPL2' 'custom')
depends=('avahi' 'bullet' 'curl' 'fontconfig' 'fribidi'
         'gst-plugins-base-libs' 'luajit' 'libexif' 'libgl' 'libinput'
         'libpulse' 'libspectre' 'libraw' 'librsvg' 'libwebp' 'libxcomposite'
         'libxcursor' 'libxinerama' 'libxkbcommon' 'libxp' 'libxrandr'
         'libxss' 'libunwind' 'mesa' 'openjpeg2' 'poppler'
         'shared-mime-info' 'mailcap' 'ttf-font' 'scim' 'libibus' 'glib2'
         'wayland' 'wayland-protocols' 'libxkbcommon-x11'
         'pulseaudio')
optdepends=('geoclue: For elocation'
            'gst-plugins-base: Video and thumbnail codecs'
            'gst-plugins-good: Video and thumbnail codecs'
            'gst-plugins-bad: Video and thumbnail codecs'
            'gst-plugins-ugly: Video and thumbnail codecs'
            'gst-libav: Video and thumbnails with ffmpeg/libav'
            'libreoffice: Office document thumbnails'
            'python2: Compare Eina benchmarks')
makedepends=('doxygen' 'python' 'texlive-core' 'ghostscript' 'imagemagick' 'meson' 'ninja')
options=('!emptydirs')
source=(https://download.enlightenment.org/rel/libs/${pkgname}/$pkgname-$pkgver.tar.xz)
sha256sums=('53cea69eaabe443a099fb204b7353e968e7bb62b41fbb0da24451403c7a56901')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -fvisibility=hidden"

  if [ -d build ]; then
    rm -rf build
  fi
  mkdir -p build

  [[ $CARCH != "aarch64" ]] && CONFIG="-Dnative-arch-optimization=false"

  meson --prefix=/usr \
    -Dfb=true \
    -Ddrm=true \
    -Dwl=true \
    -Dnetwork-backend=connman \
    -Devas-loaders-disabler=json \
    -Dbindings= \
    -Dbuild-examples=false \
    -Dbuild-tests=false \
    -Decore-imf-loaders-disabler= \
    $CONFIG . build

  ninja -C build

  cd build/doc
  doxygen
}

package_efl(){
  replaces=('elementary' 'evas_generic_loaders' 'emotion_generic_players')

  cd "${srcdir}/${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -C build install
  
  # install non-standard license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.BSD" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.BSD"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.SMALL" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.SMALL"
}

package_efl-docs() {
  pkgdesc="Documentation for the Enlightenment Foundation Libraries"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}/build"
  install -d "${pkgdir}/usr/share/doc/${pkgbase}"
  cp -a html "${pkgdir}/usr/share/doc/${pkgbase}/html"
  cp -a latex "${pkgdir}/usr/share/doc/${pkgbase}/latex"
  #cp -a man "${pkgdir}/usr/share"
  
}

