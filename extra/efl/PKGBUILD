# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with -Dnative-arch-optimization=false for !aarch64

pkgbase=efl
pkgname=('efl' 'efl-docs')
pkgver=1.27.0
pkgrel=2
pkgdesc="Enlightenment Foundation Libraries"
arch=('x86_64')
url="https://www.enlightenment.org"
license=('BSD' 'LGPL2.1' 'GPL2' 'MIT' 'custom')
depends=('curl' 'dbus' 'fontconfig' 'freetype2' 'fribidi' 'giflib' 'glib2' 'gstreamer'
         'gst-plugins-base-libs' 'harfbuzz' 'hicolor-icon-theme' 'libglvnd' 'libinput'
         'libjpeg-turbo' 'libpng' 'libpulse' 'libsndfile' 'libtiff' 'libunwind' 'libwebp' 'libx11'
         'libxcb' 'libxcomposite' 'libxcursor' 'libxdamage' 'libxext' 'libxfixes' 'libxi'
         'libxinerama' 'libxkbcommon' 'libxkbcommon-x11' 'libxrandr' 'libxrender' 'libxss'
         'libxtst' 'lua52' 'mailcap' 'mesa' 'openjpeg2' 'openssl' 'shared-mime-info' 'systemd-libs'
         'ttf-font' 'util-linux-libs' 'wayland' 'zlib')
optdepends=('gst-plugins-base: Video and thumbnail codecs'
            'gst-plugins-good: Video and thumbnail codecs'
            'gst-plugins-bad: Video and thumbnail codecs'
            'gst-plugins-ugly: Video and thumbnail codecs'
            'gst-libav: Video and thumbnails with ffmpeg/libav'
            'libraw: RAW image loader'
            'libreoffice: Office document loader'
            'librsvg: SVG loader'
            'libspectre: PostScript loader'
            'poppler: PDF loader'
            'scim: IM module for SCIM')
makedepends=('doxygen' 'libraw' 'librsvg' 'libspectre' 'meson' 'ninja' 'poppler' 'python' 'scim'
             'texlive-basic' 'texlive-fontutils' 'wayland-protocols')
options=('!emptydirs')
source=(https://download.enlightenment.org/rel/libs/${pkgname}/$pkgname-$pkgver.tar.xz)
sha256sums=('3dfb99fbcc268c0bc797e2f83e8c503ef9de66284f40b381bb597a08185c00f4')



build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -fvisibility=hidden"

  if [ -d build ]; then
    rm -rf build
  fi
  mkdir -p build

  [[ $CARCH != "aarch64" ]] && CONFIG="-Dnative-arch-optimization=false"

  meson --prefix=/usr \
    -Dfb=true \
    -Ddrm=true \
    -Dwl=true \
    -Dnetwork-backend=connman \
    -Dlua-interpreter=lua \
    -Dbindings= \
    -Dbuild-examples=false \
    -Dbuild-tests=false \
    -Ddocs=true \
    $CONFIG . build

  ninja -C build

  cd build/doc
  doxygen
}

package_efl(){
  replaces=('elementary' 'evas_generic_loaders' 'emotion_generic_players')

  cd "${srcdir}/${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -C build install

  # install non-standard license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.BSD" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.BSD"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.SMALL" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.SMALL"
}

package_efl-docs() {
  pkgdesc="Documentation for the Enlightenment Foundation Libraries"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}/build"
  install -d "${pkgdir}/usr/share/doc/${pkgbase}"
  cp -a html "${pkgdir}/usr/share/doc/${pkgbase}/html"
  cp -a latex "${pkgdir}/usr/share/doc/${pkgbase}/latex"
  #cp -a man "${pkgdir}/usr/share"
  
}
