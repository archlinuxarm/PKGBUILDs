# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-neon for !aarch64
#  - aarch64: depend on lua52, configure with --enable-lua-old

pkgbase=efl
pkgname=('efl' 'efl-docs')
pkgver=1.21.1
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries"
arch=('x86_64')
url="https://www.enlightenment.org"
license=('BSD' 'LGPL2.1' 'GPL2' 'custom')
depends=('bullet' 'libjpeg-turbo' 'gst-plugins-base'
         'luajit' 'curl' 'fribidi' 'libpulse' 'libxcomposite'
         'libxinerama' 'libxrandr' 'libxss' 'libinput'
         'libxcursor' 'libxp' 'libwebp' 'shared-mime-info'
         'libxkbcommon' 'libxkbcommon-x11' 'wayland' 'lz4' 'openjpeg' 'avahi'
         'libspectre' 'libraw' 'librsvg' 'wayland-protocols'
         'mesa')
depends_aarch64=('lua52')
optdepends=('python2: einabench-cmp' 'libreoffice: thumbnailing for DOC/PPT/XLS files')
makedepends=('doxygen' 'python' 'texlive-core' 'ghostscript' 'imagemagick')
options=('!emptydirs')
source=(https://download.enlightenment.org/rel/libs/${pkgname}/$pkgname-$pkgver.tar.xz)
sha256sums=('44774b42b3dbbbe4d124c8fddcd169c6ffab9d602d1a757abcfb9a84e001a928')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/env python$/&2/' src/scripts/eina/eina-bench-cmp
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -fvisibility=hidden"

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"
  [[ $CARCH == "aarch64" ]] && CONFIG="--enable-lua-old"

  ./configure --prefix=/usr \
  --disable-static --disable-tslib --enable-fb \
  --enable-xinput22 --enable-multisense --enable-systemd \
  --enable-image-loader-webp --enable-harfbuzz --enable-wayland \
  --enable-liblz4 --enable-drm --enable-elput --enable-egl --with-opengl=es $CONFIG

  make

  cd doc
  doxygen
  #make -j1 doc || return 0  # don't fail on the docs
}

package_efl(){
  replaces=('elementary' 'evas_generic_loaders' 'emotion_generic_players')

  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR=${pkgdir} install

  # install non-standard license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.BSD" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.BSD"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.SMALL" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.SMALL"
}

package_efl-docs() {
  pkgdesc="Documentation for the Enlightenment Foundation Libraries"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -d "${pkgdir}/usr/share/doc/${pkgbase}"
  cp -a doc/html "${pkgdir}/usr/share/doc/${pkgbase}/html"
  cp -a doc/latex "${pkgdir}/usr/share/doc/${pkgbase}/latex"
}

