# $Id: PKGBUILD 45313 2009-07-09 19:39:42Z giovanni $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=liborigin2
pkgver=20090326
pkgrel=2
pkgdesc="A library for reading OriginLab OPJ project files"
url="http://soft.proindependent.com/liborigin2/";
source=("http://downloads.sourceforge.net/sourceforge/liborigin/${pkgname}-${pkgver}.tar.gz")
license=("GPL3")
arch=("i686" 'x86_64')
depends=('gcc-libs')
makedepends=('qt' 'boost>=1.39.0' 'doxygen')
replaces=('liborigin')
provides=('liborigin')
md5sums=('7cf833d5b7647f6dbd87128459931b4f')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	# fix build issue
	sed -i 's|lboost_thread|lboost_thread-mt|' liborigin.pro
	
	qmake	
	make || return 1

	 # create documentation
        cd doc
        doxygen

	# install manually...nothing to automate it there :(
	install -Dm644 ../liborigin2.so.1.0.0 \
		${pkgdir}/usr/lib/liborigin2.so.1.0.0	
	install -d ${pkgdir}/usr/share/doc/${pkgname}
	cp -rf html/* ${pkgdir}/usr/share/doc/${pkgname}
	cd ${pkgdir}/usr/lib
	ln -s liborigin2.so.1.0.0 liborigin2.so
        ln -s liborigin2.so.1.0.0 liborigin2.so.1
        ln -s liborigin2.so.1.0.0 liborigin2.so.1.0
}
