# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Themaister <maister@archlinux.us>
# Contributor: lifning <definelightning@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable GLES
#  - disable broken pkgver function

pkgname=retroarch
pkgver=1.20.0
pkgrel=2
pkgdesc='Reference frontend for the libretro API'
arch=(x86_64)
url=http://www.libretro.com/
license=(GPL)
groups=(libretro)
depends=(
  alsa-lib
  dbus
  ffmpeg
  flac
  fontconfig
  fribidi
  gcc-libs
  glibc
  libass.so
  libdrm
  libfreetype.so
  libgl
  libpulse
  libudev.so
  libusb
  libx11
  libxcb
  libxext
  libxinerama
  libxkbcommon
  libxrandr
  libxxf86vm
  libxv
  mesa
  openssl
  qt5-base
  sdl2
  v4l-utils
  wayland
  zlib
)
makedepends=(
  gamemode
  git
  libdecor
  vulkan-icd-loader
  wayland-protocols
)
optdepends=(
  'gamemode: GameMode support'
  'libdecor: Wayland decorations'
  'libretro-overlays: Collection of overlays'
  'libretro-shaders: Collection of shaders'
  'python: retroarch-cg2glsl'
  'retroarch-assets-ozone: Ozone menu assets'
  'retroarch-assets-xmb: XMB menu assets'
)
backup=(etc/retroarch.cfg)
_tag=ab3b175848fa6cd8b2340809631e30bc0fe1d136
source=(
  git+https://github.com/libretro/RetroArch.git#tag=${_tag}
  retroarch-config.patch
)
b2sums=('c87e326732a9bf8124068f848ac86eda8d559cd3a36c44bc7625fc21fd1f911d952a1141f21604a27ac5c4da7977f9f12bfd92dab24c3880d4d9655f7d8090e2'
        '5ecbe57126c7f9b66bdf189f0af2f1cec031cb68ea85bb9609847ba9beb0e720c902cd3330d7c286901a73e6c2e272c3f04166cf9a47bb6f80097f19dde8ce3a')

#pkgver() {
#  cd RetroArch
#  git describe --tags | sed 's/^v//'
#}

prepare() {
  cd RetroArch
  patch -Np1 -i ../retroarch-config.patch
}

build() {
  cd RetroArch
  ./configure \
    --prefix=/usr \
    --disable-builtinflac \
    --enable-builtinmbedtls \
    --disable-builtinzlib \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --enable-dbus \
    --enable-opengles
  make
  make -C libretro-common/audio/dsp_filters
  make -C gfx/video_filters
}

package() {
  cd RetroArch
  make DESTDIR="${pkgdir}" install
  install -Dm 644 libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}"/usr/lib/retroarch/filters/audio/
  install -Dm 644 gfx/video_filters/*.{filt,so} -t "${pkgdir}"/usr/lib/retroarch/filters/video/
}

# vim: ts=2 sw=2 et:
