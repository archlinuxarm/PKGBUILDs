# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Themaister <maister@archlinux.us>
# Contributor: lifning <definelightning@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable GLES
#  - disable broken pkgver function

pkgname=retroarch
pkgver=1.22.2
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=(x86_64)
url=http://www.libretro.com/
license=(GPL-3.0-only)
groups=(libretro)
depends=(
  alsa-lib
  dbus
  ffmpeg
  #flac
  fontconfig
  fribidi
  gcc-libs
  glibc
  libass.so
  libdrm
  libfreetype.so
  libgl
  libpulse
  libudev.so
  libusb
  libx11
  libxcb
  libxext
  libxinerama
  libxkbcommon
  libxrandr
  libxxf86vm
  libxv
  mesa
  openssl
  qt6-base
  sdl2
  v4l-utils
  wayland
  zlib
)
makedepends=(
  gamemode
  git
  libdecor
  vulkan-icd-loader
  wayland-protocols
)
optdepends=(
  'gamemode: GameMode support'
  'libdecor: Wayland decorations'
  'libretro-overlays: Collection of overlays'
  'libretro-shaders: Collection of shaders'
  'python: retroarch-cg2glsl'
  'retroarch-assets-ozone: Ozone menu assets'
  'retroarch-assets-xmb: XMB menu assets'
)
backup=(etc/retroarch.cfg)
_tag=69a4f0ea1e8aaf442ae4858f2e7f2b31a1776576
source=(
  git+https://github.com/libretro/RetroArch.git#tag=${_tag}
  retroarch-config.patch
)
b2sums=('001cf261cda10fc42e9261d9eae56452d23904cba544f906fa96db8db149bf3f4cd5ce5a84f90c591b5501cd3e7637be131a2a25342dca745b2ad0161a4755f6'
        '5ecbe57126c7f9b66bdf189f0af2f1cec031cb68ea85bb9609847ba9beb0e720c902cd3330d7c286901a73e6c2e272c3f04166cf9a47bb6f80097f19dde8ce3a')

#pkgver() {
#  cd RetroArch
#  git describe --tags | sed 's/^v//'
#}

prepare() {
  cd RetroArch
  patch -Np1 -i ../retroarch-config.patch
}

build() {
  cd RetroArch
  ./configure \
    --prefix=/usr \
    --enable-builtinmbedtls \
    --disable-builtinzlib \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --enable-dbus \
    #--disable-builtinflac \
    --enable-opengles
  make
  make -C libretro-common/audio/dsp_filters
  make -C gfx/video_filters
}

package() {
  cd RetroArch
  make DESTDIR="${pkgdir}" install
  install -Dm 644 libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}"/usr/lib/retroarch/filters/audio/
  install -Dm 644 gfx/video_filters/*.{filt,so} -t "${pkgdir}"/usr/lib/retroarch/filters/video/
}

# vim: ts=2 sw=2 et:
