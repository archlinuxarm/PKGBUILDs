# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Michael Hansen <zrax0111 gmail com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for chromium GN
#  - skip qtwebengine on v5

pkgname=qt5-doc
_qtver=5.10.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('any')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework (Documentation)'
depends=('qt5-base')
makedepends=('qt5-tools' 'python2' 'pciutils' 'libxtst' 'libxcursor' 'libxrandr' 'libxss' 'libxcomposite' 'gperf' 'nss')
groups=('qt' 'qt5')
_pkgfqn="qt-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/single/${_pkgfqn}.tar.xz"
        qt-no-statx.patch
        0001-ARM-toolchain-fixes.patch)
sha256sums=('05ffba7b811b854ed558abf2be2ddbd3bb6ddd0b60ea4b5da75d277ac15e740a'
            '5cd1d90622cc53609806a3ce0b2811b28f0e6e20eb835861de4492dddeef6e52'
            '8202b09a1caa82538a2eacd79b62b61d8661c65cdfb275560d231aa31a362b12')

prepare() {
  cd ${_pkgfqn}

  ln -s /usr/bin qttools/
  ln -s /usr/bin/{rcc,uic,moc} qtbase/bin/

  # Hack to force using python2
  cd "$srcdir"
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  # Don't use the statx syscall https://bugs.archlinux.org/task/56289 https://bugreports.qt.io/browse/QTBUG-64490
  cd ${_pkgfqn}/qtbase
  patch -p1 -i "$srcdir"/qt-no-statx.patch

  cd ${srcdir}/${_pkgfqn}/qtwebengine/src/3rdparty
  patch -p1 -i ${srcdir}/0001-ARM-toolchain-fixes.patch
}

build() {
  cd ${_pkgfqn}

  [[ $CARCH == "arm" ]] && CONFIG="-skip qtwebengine"
  export PATH="$srcdir/bin:$PATH"
  PYTHON=/usr/bin/python2 ./configure -confirm-license -opensource \
    -prefix /usr \
    -docdir /usr/share/doc/qt \
    -headerdir /usr/include/qt \
    -archdatadir /usr/lib/qt \
    -datadir /usr/share/qt \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt/examples $CONFIG
  make docs
}

package() {
  cd ${_pkgfqn}
  make -j1 INSTALL_ROOT="$pkgdir" install_docs

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}

# Fix conflicts with qt5-examples
  rm -r "$pkgdir"/usr/share/doc/qt/examples
}
