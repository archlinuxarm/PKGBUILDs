# $Id: PKGBUILD 104004 2010-12-25 10:24:12Z allan $
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - define this file:
_curlbuild=curlbuild-32.h

plugrel=2

pkgname=curl
pkgver=7.21.3
pkgrel=1
pkgdesc="An URL retrival utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl' 'bash' 'ca-certificates')
options=('!libtool')
source=(http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2
        curlbuild.h)
md5sums=('5b57fee22090b5c43a6886fdd35af2ce'
         '751bd433ede935c8fae727377625a8ae')

[[ $CARCH == "x86_64" ]] && _curlbuild=curlbuild-64.h
[[ $CARCH == "i686" ]] && _curlbuild=curlbuild-32.h
build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure \
        --with-random=/dev/urandom \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-dependency-tracking \
        --enable-ipv6 \
        --disable-ldaps \
        --disable-ldap \
        --enable-manual \
        --enable-versioned-symbols \
        --with-ca-path=/etc/ssl/certs \
        --without-libidn
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make DESTDIR=${pkgdir} install
    install -Dm644 COPYING \
        ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    install -Dm644 docs/libcurl/libcurl.m4 \
        ${pkgdir}/usr/share/aclocal/libcurl.m4
    mv -f "${pkgdir}/usr/include/curl/curlbuild.h" "${pkgdir}/usr/include/curl/${_curlbuild}"
    install -m 644 ${srcdir}/curlbuild.h "${pkgdir}/usr/include/curl/curlbuild.h"
}
