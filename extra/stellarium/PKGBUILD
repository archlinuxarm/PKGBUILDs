# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop unsupported compiler flag

pkgname=stellarium
pkgver=0.13.0
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=("i686" "x86_64")
url="http://stellarium.free.fr/"
license=('GPL2')
depends=('libpng' 'glu' 'libgl' 'freetype2' 'qt5-quick1' 'openssl' 
	'desktop-file-utils' 'xdg-utils' 'phonon')
install=stellarium.install
makedepends=('cmake' 'boost' 'mesa' 'mesa-libgl' 'qt5-tools')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	stellarium.desktop
        stellarium.png)
sha1sums=('d14078084f7951d7c5ae7ba38499439234f71292'
          'c9cc79212542238227b5bd6af99f60fe056f4ab2'
          'b890d3b6c4dcfcfcc696514202af11b2a50c0fea')
	
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '/mno-ms-bitfields/d' CMakeLists.txt
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 
  make DESTDIR="${pkgdir}" install

  # install desktop file & pixmap
  install -Dm644 "${srcdir}/stellarium.desktop" \
	"${pkgdir}/usr/share/applications/stellarium.desktop"
  install -Dm644 "${srcdir}/stellarium.png" \
	"${pkgdir}/usr/share/pixmaps/stellarium.png"
}
