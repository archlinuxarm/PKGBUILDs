# $Id: PKGBUILD 53336 2009-09-29 07:26:52Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=lm_sensors
pkgver=3.1.1
pkgrel=2
pkgdesc="Collection of user space tools for general SMBus access and hardware monitoring."
arch=('i686' 'x86_64')
url="http://www.lm-sensors.org/"
license=('GPL')
depends=('perl' 'sysfsutils')
makedepends=('rrdtool')
optdepends=('rrdtool: for logging with sensord')
backup=('etc/sensors3.conf')
install=sensors.install
source=(http://dl.lm-sensors.org/lm-sensors/releases/lm_sensors-${pkgver}.tar.bz2 \
	sensors.rc fancontrol.rc sensors-detect.patch healthd healthd.conf healthd.rc \
        sensord.conf sensord.rc)
md5sums=('613d7cfa23b70c0abae3fabb0a72ff5f' 'c370f5e620bfe41113354a1e22c0c18c'\
         'b3695cd70f5cac17205a24f1249819af' '47c40b381d1f25d6634ae84cecf35f33'\
         '6415014dc77365a48525901f30fe99da' 'f649261f52bd4329347bf93f5f83cb0a'\
         '970408d2e509dc4138927020efefe323' '96a8dd468e81d455ec9b165bdf33e0b7'\
         '41a5c20854bbff00ea7174bd2276b736')
sha1sums=('8be15806d229305491f11b77c67496074480faf4' 'b2e664b9b87759991f02d0a1e8cac5e95098c0a5'\
         '1127b042a3a167b656da74d701a6a8aa2b8f7ab4' '47095a32a918d6be50bd8daa8aaa9c24940d60e9'\
         '06128ebb689aa271eef916e14ae1f2c42bee1f1d' 'c6ddfebc20685ba69700f66038c6b00a7c0bdb80'\
         'e662881f5d3f3f35a1bc97ba45d2c471dd28c37f' 'de8d4d65406815c389f8a04e2a8508a1ae6749c8'\
         '72a60251d1d55a67307dab4105d9f3f01a080af4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../sensors-detect.patch || return 1

  make PREFIX=/usr PROG_EXTRA:=sensord || return 1
  make PREFIX=/usr MANDIR=/usr/share/man DESTDIR="${pkgdir}" install || return 1

  install -D -m755 prog/sensord/sensord "${pkgdir}/usr/sbin/sensord" || return 1
  install -D -m755 "${srcdir}/sensors.rc" "${pkgdir}/etc/rc.d/sensors" || return 1
  install -D -m755 "${srcdir}/fancontrol.rc" "${pkgdir}/etc/rc.d/fancontrol" || return 1
  install -D -m755 "${srcdir}/healthd" "${pkgdir}/usr/sbin/healthd" || return 1
  install -D -m755 "${srcdir}/healthd.rc" "${pkgdir}/etc/rc.d/healthd" || return 1
  install -D -m644 "${srcdir}/healthd.conf" "${pkgdir}/etc/conf.d/healthd" || return 1
  install -D -m755 "${srcdir}/sensord.rc" "${pkgdir}/etc/rc.d/sensord" || return 1
  install -D -m644 "${srcdir}/sensord.conf" "${pkgdir}/etc/conf.d/sensord" || return 1

  # remove the static lib
  rm -rf "${pkgdir}/usr/lib/libsensors.a"
  rmdir "${pkgdir}/etc/sensors.d"
}
