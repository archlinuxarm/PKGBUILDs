# $Id: PKGBUILD 36730 2009-04-27 02:00:53Z eric $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-summa
pkgver=1.2.0
pkgrel=3
pkgdesc="X.org Summagraphics tablet input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'xorg-util-macros')
conflicts=('xorg-server<1.6.0')
options=('!libtool')
groups=('xorg-input-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	git-113c24d8.patch LICENSE)
md5sums=('7e17468c0d90dcbbb6202a8242efff4b' 'ee46635e0e53f47ed1be0193f3d6660f'\
         'aa361666e395a1183872a3167807ead4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-113c24d8.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
