# $Id: PKGBUILD 34679 2009-04-11 05:23:36Z thayer $
# Contributor: Tom K <tomk@runbox.com>
# Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=epdfview
pkgver=0.1.7
pkgrel=3
pkgdesc="A free lightweight PDF document viewer."
url="http://www.emma-soft.com/projects/epdfview/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('poppler-glib>=0.10.0' 'heimdal>=1.2.1-1' 'libcups>=1.3.7-2' 'gnutls>=2.4.1' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkgconfig')
install=epdfview.install
source=(http://www.emma-soft.com/projects/epdfview/chrome/site/releases/epdfview-${pkgver}.tar.bz2
        epdfview.desktop.patch
        fix-scrolling.patch)
md5sums=('1919bb19c16ef0a97d48b0a8303d3c7b'
         'fbf22bbabdbb7544db615ac5775d57e2'
         '7a1574e0a8d0bcde316050629fb7bedd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ../epdfview.desktop.patch || return 1
  patch -Np0 -i ../fix-scrolling.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -d ${pkgdir}/usr/share/icons/hicolor/{24x24,32x32,48x48}/apps || return 1
  install -m644 data/icon_epdfview-24.png "${pkgdir}/usr/share/icons/hicolor/24x24/apps/epdfview.png" || return 1
  install -m644 data/icon_epdfview-32.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/epdfview.png" || return 1
  install -m644 data/icon_epdfview-48.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/epdfview.png" || return 1
}
