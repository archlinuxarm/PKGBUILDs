# $Id: PKGBUILD 45460 2009-07-11 23:04:17Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Original Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# New Version - Contributor: Bob Finch <w9ya@arrl.net>
# with help from J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar> aka Angelus

pkgname=wxgtk
pkgver=2.8.10.1
pkgrel=4
pkgdesc="wxGTK - GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2>=2.16.2' 'mesa')
makedepends=('libgnomeprintui')
#source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2 overflow.patch)
md5sums=('65d5ef166f23fe8b4c67f58df164f93e' '325dba65152bc0cfbc1400a2bf54508e')
sha1sums=('6598fbafd979a91f20100171fa23a91779f6dc62' '7c4d1d507aad0b7c25a0de55234be42ea3e3f55f')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  patch -p4 < ../overflow.patch || return 1
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
	  --enable-graphics_ctx --with-gnomeprint --disable-optimize || return 1
  make || return 1
  make -C locale allmo || return 1
  make DESTDIR="${pkgdir}" install || return 1
  cd contrib/src
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
