# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add --disable-strict to configure to remove -Werror

pkgname=mediastreamer
pkgver=2.9.0
pkgrel=4
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL')
depends=('ortp' 'ffmpeg' 'libxv' 'libupnp')
# xxd from Vim is needed to build
makedepends=('intltool' 'vim')
options=('!libtool')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
md5sums=('f2ea0fe731a363749a81b6eaac22a62c'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-strict \
    --disable-glx --enable-xv
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
