# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to disable neon
#  - patch to use system ffmpeg

buildarch=12

pkgname=qt5-webengine
_qtver=5.8.0
pkgver=${_qtver/-/}
pkgrel=5
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 'libvpx' 
         'libevent' 'libsrtp' 'snappy' 'nss' 'protobuf' 'libxslt' 'libxdamage' 'minizip' 'ffmpeg')
         # namcap note: libxdamage is needed for nvidia users
makedepends=('python2' 'git' 'gperf' 'jsoncpp')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-opensource-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz" qt5-webengine-nss.patch
        qtwebengine-opensource-src-5.6.0-beta-no-neon.patch
        qtwebengine-5.7.0-icu58.patch qtbug-58488.patch::"https://github.com/qt/qtwebengine/commit/7e7dd262.patch"
        qtbug-58381.patch::"https://github.com/qt/qtwebengine/commit/8e147ed3.patch"
        qtbug-58515.patch::"https://github.com/qt/qtwebengine/commit/a6c6665d.patch"
        qtbug-58673.patch::"https://github.com/qt/qtwebengine/commit/90501711.patch"
        qtbug-58362.patch::"https://github.com/qt/qtwebengine/commit/31374ba9.patch"
        system-ffmpeg.patch)

md5sums=('6e7fb2be161c6db4d988a4f5b329672f'
         '2a1610b34204102938a24154a52e5571'
         '123d4d0d7db7473649ff2b5650330cc4'
         '9d225d1bf83ea45dbf6556d30d35fcb8'
         '3762cbdbc6a752e4d876e048e5e16de6'
         '951ac7549fff82f9d1d11e9bf50556ba'
         'd5da5608285ad764f37fe196f9afe5a1'
         'c7b5acd58319d23d769c19f42323e8ec'
         '663f8454990de96fad4c8c78bcd6f819'
         '9f79b14f7c5ff603c1fcd87f37a8904f')

prepare() {
  mkdir -p build

  # Hack to force using python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  # Fix opening some websites with recent NSS https://github.com/QupZilla/qupzilla/issues/1870 (KaOSx patch)
  cd ${_pkgfqn}
 # patch -p1 -i ../qt5-webengine-nss.patch

  # Fix build with ICU 58 (gentoo)
  patch -p1 -i "$srcdir"/qtwebengine-5.7.0-icu58.patch

  # Prevent drop-down popups form stealing focus https://bugreports.qt.io/browse/QTBUG-58488
  patch -p1 -i ../qtbug-58488.patch

  # Backport some focus fixes
  patch -p1 -i ../qtbug-58381.patch
  patch -p1 -i ../qtbug-58515.patch
  patch -p1 -i ../qtbug-58673.patch

  # Fix IME support
  patch -p1 -i ../qtbug-58362.patch

  [[ $CARCH == "armv7h" ]] && patch -p0 -i ../qtwebengine-opensource-src-5.6.0-beta-no-neon.patch
  patch -p1 -i ../system-ffmpeg.patch
}

build() {
  cd build

  export PATH="$srcdir/bin:$PATH"
  qmake CONFIG+="proprietary-codecs" WEBENGINE_CONFIG+="use_proprietary_codecs use_system_ffmpeg use_system_icu" ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
