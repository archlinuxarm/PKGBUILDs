# $Id: PKGBUILD 42037 2009-06-09 10:52:26Z douglas $
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=ntop
pkgver=3.3.10
pkgrel=1
pkgdesc="A network traffic probe that shows the network usage"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.ntop.org"
depends=('libevent' 'libpcap>=1.0.0' 'gd' 'glib' 'libxml2' 'openssl' 'gdbm' 'rrdtool>=1.3.5' 'pcre' 'geoip' 'lua')
options=('!libtool' '!makeflags')
install=ntop.install
source=(http://downloads.sourceforge.net/sourceforge/ntop/ntop-$pkgver.tar.gz
        ntop.confd ntop.rcd)

build() {
    # Thanks a lot for the fixes Gerardo

    cd ${srcdir}/${pkgname}-${pkgver}
    ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var/lib \
                 --disable-snmp || return 1
    make || return 1
    make DESTDIR=${pkgdir} install-recursive || return 1

    install -d -m755 --owner=nobody --group=nobody ${pkgdir}/var/lib/ntop || return 1

    install -D -m755 ${srcdir}/ntop.rcd ${pkgdir}/etc/rc.d/ntop  || return 1
    install -D -m644 ${srcdir}/ntop.confd ${pkgdir}/etc/conf.d/ntop || return 1

    (
        cd ${pkgdir}/usr/lib/ntop/plugins
        for _plug in *.so; do
            ln -sf ../../lib${_plug} ${_plug}
        done
    ) || return 1
}
md5sums=('6e2ffa90d5f935c8f03d88a5dd19a866'
         ''
         '')
md5sums=('6e2ffa90d5f935c8f03d88a5dd19a866'
         'b4ea63012f94c2988e767e78ac16a309'
         '76acde24cc5f428ff862d7c0815feb14')
