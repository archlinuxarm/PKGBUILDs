# $Id: PKGBUILD 30321 2009-03-18 21:50:51Z jgc $ 
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgnome
pkgver=2.26.0
pkgrel=2
pkgdesc="Common libraries for GNOME"
arch=(i686 x86_64)
license=('LGPL')
depends=('gnome-vfs>=2.24.1-2' 'esound>=0.2.41' 'libbonobo>=2.24.1')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=libgnome.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('a5634fe9ddc54bbb726cbaaa35abc0b9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgnome-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
