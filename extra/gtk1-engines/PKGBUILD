# $Id: PKGBUILD 19117 2008-11-14 23:05:36Z thayer $
# Contributor: damir <damir@archlinux.org>
# Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=gtk1-engines
pkgver=0.12
pkgrel=2
pkgdesc="Theme-Engines and Themes for GTK+ 1 - including gtk-engines-pixmap"
url="http://ftp.gnome.org/pub/gnome/sources/gtk-engines/0.12/"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('gtk' 'imlib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk-engines/0.12/gtk-engines-$pkgver.tar.gz)
md5sums=('c867d1ebd6dbea355765d689a11330ec')

build() {
  cd $srcdir/gtk-engines-$pkgver

  # arch64 fix from fedora
  if [ "$CARCH" == "x86_64" ]; then
    sed -i -e 's/AM_PROG_LIBTOOL/AC_PROG_LIBTOOL/' configure.in
    rm -f acinclude.m4
    autoreconf -f -i
  fi

  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # these files are already provided by gtk-engines
  rm $pkgdir/usr/share/themes/Metal/README.html
  rm $pkgdir/usr/share/themes/Redmond95/README.html
}
