# $Id: PKGBUILD 356 2008-04-18 22:56:27Z aaron $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patch from https://github.com/openexr/openexr/issues/29

pkgname=ilmbase
pkgver=2.0.1
pkgrel=2
depends=('gcc-libs')
pkgdesc="Base libraries from ILM for OpenEXR"
arch=(i686 x86_64)
url="http://www.openexr.com"
license=('custom')
options=('!libtool')
source=(http://download.savannah.nongnu.org/releases/openexr/$pkgname-$pkgver.tar.gz
        arm.patch) 

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  patch -p1 -i ../arm.patch
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # one of the tests fails randomly for an unknown reason
  make check || true
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
md5sums=('74c0d0d2873960bd0dc1993f8e03f0ae'
         'b652c8106907d867a2f52fe93ac6c5ec')
