# $Id: PKGBUILD 356 2008-04-18 22:56:27Z aaron $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patch from https://github.com/openexr/openexr/issues/29

pkgname=ilmbase
pkgver=2.1.0
pkgrel=1
depends=('gcc-libs')
pkgdesc="Base libraries from ILM for OpenEXR"
arch=(i686 x86_64)
url="http://www.openexr.com"
license=('custom')
source=(http://download.savannah.nongnu.org/releases/openexr/$pkgname-$pkgver.tar.gz
        ilmbase-2.0.1-no_undefined.patch
        ilmbase-1.0.3-pkgconfig.patch
        arm.patch)

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"

  patch -p1 -i ../arm.patch

  # patches from fedora
  patch -p1 -i ../ilmbase-2.0.1-no_undefined.patch
  patch -p1 -i ../ilmbase-1.0.3-pkgconfig.patch
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr
  # manually set PTHREAD_LIBS to include -lpthread until libtool bogosity is fixed,
  # https://bugzilla.redhat.com/show_bug.cgi?id=661333
  make PTHREAD_LIBS="-pthread -lpthread"
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # one of the tests fails randomly for an unknown reason
  make check || true
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
md5sums=('8ba2f608191ad020e50277d8a3ba0850'
         '27ff3915e03964f85a9e9fb896a93479'
         'ed3eb3dfa2393adc6829cc9ca8caa0ff'
         'b652c8106907d867a2f52fe93ac6c5ec')
