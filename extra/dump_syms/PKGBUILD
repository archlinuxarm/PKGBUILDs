# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO
#  - set ARM targets for cargo fetch

pkgname=dump_syms
pkgver=2.0.0
pkgrel=1
pkgdesc="Symbol dumper for Firefox"
url="https://github.com/mozilla/dump_syms"
arch=(x86_64)
license=(APACHE MIT)
depends=(gcc-libs openssl bzip2)
makedepends=(cargo git)
options=(debug !lto)
_commit=8119eb37c1518985373bc55fdb058ab313a69f18  # tags/v2.0.0^0
source=("git+$url#commit=$_commit")
sha512sums=('SKIP')

# Use LTO
#export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

case "$CARCH" in
  armv7h) target=armv7-unknown-linux-gnueabihf ;;
  aarch64) target=aarch64-unknown-linux-gnu ;;
esac

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  cargo fetch --locked --target $target
}

build() {
  cd $pkgname
  cargo build --release --frozen --all-targets
}

check() {
  cd $pkgname
  cargo test --release --frozen
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/dump_syms
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE*
}
