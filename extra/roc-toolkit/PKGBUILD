# Maintainer: David Runge <dvzrv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !distcc, build checks for clang first

pkgname=roc-toolkit
pkgver=0.2.5
pkgrel=1
pkgdesc="Real-time audio streaming over the network"
arch=(x86_64)
url="https://github.com/roc-streaming/roc-toolkit/"
license=(
  MPL2
  custom:CC0
)
depends=(
  glibc
  gcc-libs
  libunwind
  libuv
)
makedepends=(
  alsa-lib
  cpputest
  gengetopt
  libpulse
  openfec
  ragel
  scons
  sox
  speexdsp
)
options=(!distcc)
optdepends=(
  'libpulse: for roc-conv, roc-receive and roc-send'
  'sox: for roc-conv, roc-receive and roc-send'
)
provides=(libroc.so)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('45a524d7c29f2011eaa9f3e6d256ccf714e1abe2921c8ed276d97501cd2f7641bc14288f2f29c892a1c6740cf64049e3d143eb6b15dd2fb14e25a38a4ad23971')
b2sums=('b75a2f101bd9bfecee28abc1dee0102c55a06f6432d66ed0b982b774e330209cdb9c22db442b9a49ec6e5623193de0c55fdb118a5285fb7de199b85eb6daea48')

build() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --disable-openssl  # disable as it is not yet used
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons "${scons_options[@]}"
}

check() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --disable-openssl  # disable as it is not yet used
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons test "${scons_options[@]}"
}

package() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
  )
  depends+=(
    openfec libopenfec.so
    speexdsp libspeexdsp.so
  )

  cd $pkgname-$pkgver
  scons DESTDIR="$pkgdir/" "${scons_options[@]}" install

  install -vDm 644 3rdparty/hedley/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/hedley.COPYING"
}
