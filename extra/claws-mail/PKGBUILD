# $Id: PKGBUILD 102090 2010-12-05 20:57:04Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - removed valgrind from makedepends

plugrel=1

pkgname=claws-mail
pkgver=3.7.8
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
depends=('gtk2>=2.18.5' 'gnutls>=2.8.5' 'startup-notification>=0.10' 'pilot-link>=0.12.4' 'enchant>=1.5.0-2'
         'gpgme>=1.2.0' 'libetpan>=1.0' 'libsm>=1.1.1' 'db' 'dbus-glib>=0.82-2' 'hicolor-icon-theme')
makedepends=('compface' 'spamassassin' 'bogofilter')
optdepends=('python2:           needed for some tools'
            'perl:              needed for some tools'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering'
            'html2ps:		AUR pkg - adds support for printing html mails together with html plugins')
replaces=('sylpheed-claws')
provides=('claws')
options=(!libtool)
install=claws-mail.install
source=(http://downloads.sourceforge.net/sourceforge/sylpheed-claws/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e8c516f67df195ee605ed5e5408b2a42')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  ./configure --prefix=/usr --disable-static \
    --enable-enchant \
    --enable-gnutls \
    --enable-ldap \
    --disable-dillo-viewer-plugin \
    --enable-crash-dialog \
    --enable-pgpmime-plugin \
    --enable-spamassassin-plugin \
    --enable-bogofilter-plugin \
    --enable-jpilot

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 ${i} \
        ${pkgdir}/usr/lib/claws-mail/tools/${i}
  done
}
