# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop valgrind makedepend

pkgname=claws-mail
pkgver=3.11.0
pkgrel=2
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
depends=('gtk2' 'gnutls' 'startup-notification' 'enchant' 'gpgme' 'libetpan' 'compface'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('spamassassin' 'bogofilter' 'intltool'
             # dependencies for plugins
             'libsoup' 'libgdata' 'webkitgtk2' 'libnotify' 'libcanberra' 'poppler-glib' 'pygtk') 
             #'libchamplain' needed for geolocation plugin, disabled for now - most time it's not compatible with current gnome releases
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'libcanberra:       for notification plugin'
            'dbus:              for notification plugin'
            'libxml2:           for gtkhtml2_viewer and rssyl plugins'
            'curl:              for gtkhtml2_viewer, vcalendar, rssyl and spamreport plugins'
            'libarchive:        for archive plugin and various other plugins'
            'libytnef:          for tnef_parse plugin'
            'webkitgtk2:        for the fancy webkit html plugin'
            'libsoup:           for the fancy webkit html plugin'
	        'libgdata:          for gdata plugin'
	        'poppler-glib:      for pdf viewer plugin'
	        'ghostscript:       for pdf viewer plugin')    
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=('claws-mail-extra-plugins')
provides=('claws')
install=claws-mail.install
source=(http://downloads.sourceforge.net/sourceforge/claws-mail/${pkgname}-${pkgver}.tar.xz{,.asc})
sha1sums=('1fc919e7d4357408a04131c4195878ea920de4f0'
          'SKIP')

build() {
  cd ${pkgname}-${pkgver}
 
  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure
  
  ./configure --prefix=/usr --disable-static \
    --enable-enchant \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-pgpmime-plugin \
    --enable-spamassassin-plugin \
    --enable-bogofilter-plugin \
    --enable-appdata
    #--help
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 ${i} \
        ${pkgdir}/usr/lib/claws-mail/tools/${i}
  done
}
