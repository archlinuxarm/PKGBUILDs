# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop valgrind makedepend

pkgname=claws-mail
pkgver=3.16.0
pkgrel=3
pkgdesc="A GTK+ based e-mail client."
arch=('x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
depends=('gtk2' 'gnutls' 'startup-notification' 'enchant' 'gpgme' 'libetpan' 'compface'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('spamassassin' 'bogofilter'
             # dependencies for plugins
             'libgdata' 'libnotify' 'libcanberra' 'poppler-glib' 'pygtk' 'libytnef' 'libical' 'dillo'
             # deps to build the docs
             'docbook-utils')
             #'libchamplain' needed for geolocation plugin, disabled for now - most time it's not compatible with current gnome releases
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'libcanberra:       for notification plugin'
            'dbus:              for notification plugin'
            'libxml2:           for gtkhtml2_viewer and rssyl plugins'
            'curl:              for gtkhtml2_viewer, vcalendar, rssyl and spamreport plugins'
            'libarchive:        for archive plugin and various other plugins'
            'libytnef:          for tnef_parse plugin'
            'libgdata:          for gdata plugin'
            'poppler-glib:      for pdf viewer plugin'
            'ghostscript:       for pdf viewer plugin'
            'libical:           for vcalendar plugin'
            'dillo:             for html viewer plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=('claws-mail-extra-plugins')
provides=('claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
       dillo-plugin.diff)
sha256sums=('c212133d3d962129d8c7995c1c7d973faef2a8914481c0c74578217bf4c897ab'
            'SKIP'
            'ab08ca513145e847d30a29d9e5a9ae63b94222423e88c1b375fd5cfcb427d6c5')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd ${pkgname}-${pkgver}
  # upstream reintroduction of dillo plugin - after fancy plugin dropped
  patch -Np1 -i ../dillo-plugin.diff

  # fix new versioned enchant pkgconfig detection
  sed -i "s:PKG_CHECK_MODULES(ENCHANT, enchant >= 1.4.0:PKG_CHECK_MODULES(ENCHANT, enchant-2 >= 1.4.0:" configure.ac
  sed -i "s:--cflags enchant:--cflags enchant-2:" configure.ac

  autoreconf -vfi
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure
}

build() {
  cd ${pkgname}-${pkgver}

  # fixes for python2
  export PYTHON="/usr/bin/python2"

  ./configure --prefix=/usr --disable-static \
    --enable-enchant \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-pgpmime-plugin \
    --enable-spamassassin-plugin \
    --enable-bogofilter-plugin    #--help
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install extra tools
  cd tools
  install -m755 -d ${pkgdir}/usr/lib/claws-mail/tools
  for files in *.pl *.py *.sh kdeservicemenu multiwebsearch.conf tb2claws-mail update-po uudec uuooffice README; do
    cp -arv $files ${pkgdir}/usr/lib/claws-mail/tools/${i}
  done
}
