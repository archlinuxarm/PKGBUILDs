# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jason Chu <jason@archlinux.org>

# remove when bumped upstream

pkgname=speex
pkgver=1.2.0
pkgrel=3
pkgdesc="A free codec for free speech"
arch=(x86_64)
url="https://speex.org/"
license=(BSD)
depends=(libogg speexdsp)
source=(https://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('eaae8af0ac742dc7d542c9439ac72f1f385ce838392dc849cae4536af9210094')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static \
    --enable-binaries # Must be given or configure won't use pkg-config correctly
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
