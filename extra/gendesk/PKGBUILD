# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !strip option, stripped binary fails under ARM

pkgname=gendesk
pkgver=1.0.9
pkgrel=4
pkgdesc='Utility for generating desktop files by specifying a minimum of information'
arch=(x86_64)
url='https://gendesk.roboticoverlords.org/'
license=(MIT)
makedepends=(go)
options=(!strip !lto)
validpgpkeys=('962855F072C7A01846405864FCF3C8CB5CF9C8D4')
source=("https://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.xz"{,.asc}
        'https://roboticoverlords.org/images/default.png')
b2sums=('07ba72b713bd240e9035ced7555e02bd544ba402a4cf1e8417804ea3d53d7c43cbd47da507b15de0446d07e0b9d84a9bf3c3327b6d508e7f6ced279b2cd6b961'
        'SKIP'
        '4676b13f6154f511497f5264310f861af2fab798d9fc5d9e39ae2379da3fd24317f777ec437d57a58aef344158cfe77bdce9f32bc17eeeb7126e0954af417b35')

build() {
  cd $pkgname-$pkgver
  go build -v -mod=vendor -trimpath -buildmode=pie -ldflags="-s -w -extldflags '$LDFLAGS'"
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.1.gz "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
