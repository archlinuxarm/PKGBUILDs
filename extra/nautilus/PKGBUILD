# $Id: PKGBUILD 39813 2009-05-21 13:57:13Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=2.26.3
pkgrel=1
pkgdesc="The GNOME shell and file manager"
arch=(i686 x86_64)
license=('GPL')
depends=('libbeagle>=0.3.9' 'libtracker>=0.6.6' 'libexif>=0.6.16' 'exempi>=2.1.0' 'gnome-icon-theme>=2.26.0' 'desktop-file-utils' 'gvfs>=1.2.3' 'libunique>=1.0.8' 'gnome-desktop>=2.26.0')
makedepends=('pkgconfig' 'intltool')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2
	nautilus-condrestart.patch)
md5sums=('e05f1b61c27f506451f7d46509631e7b' '44742925ea8d83196ba50e398545c257')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/nautilus-condrestart.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --disable-update-mimedb || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nautilus ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
