# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Yichao Yu <yyc1992@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com> Ben <ben@benmazer.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove qt5-webengine makedepend for v5/v6

pkgbase=pyqt5
pkgname=('pyqt5-common' 'python-pyqt5' 'python2-pyqt5')
pkgver=5.11.2
pkgrel=2.1
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
makedepends=('python-sip-pyqt5' 'python2-sip-pyqt5' 'sip' 'python-opengl' 'python2-opengl' 'python2-enum34'
             'python2-dbus' 'python-dbus' 'qt5-connectivity' 'qt5-multimedia' 'qt5-tools' 'qt5-serialport' 'qt5-svg'
             'qt5-webkit' 'qt5-websockets' 'qt5-x11extras' 'qt5-networkauth')
makedepends_armv7h=('qt5-webengine')
makedepends_aarch64=('qt5-webengine')
source=("http://sourceforge.net/projects/pyqt/files/PyQt5/PyQt-$pkgver/PyQt5_gpl-$pkgver.tar.gz" pyqt-support-new-qt.patch)
sha512sums=('fc60246b5e1ca0d8aeef829fe20de9dd28b77e3c151532f9dbcd92c0deec12d8308cc799901aeec3f84af745e735053d2aaad2866ef64a2426cba343e08bc2c0'
            'b94b707e4265ff8e84d52214cea7b70db33aa436440c74e4918c165a7dfae8949bf3e91491d300e68c1715b26b693ba69eca114cf42ce6114dab1b99d69d417e')

prepare() {
  # Support new versions of Qt - don't remove, needs to be reapplied after every new Qt release
  pushd PyQt5_gpl-$pkgver
#  patch -p1 -i ../pyqt-support-new-qt.patch
  popd

  cp -a PyQt5_gpl-$pkgver{,-py2}
# Remove check for sip from py2, it doesn't work since sip doesn't install __init__.py
  sed -e 's|from PyQt5 import sip|pass|g' -i PyQt5_gpl-$pkgver-py2/configure.py
}

build() {
  cd "$srcdir"/PyQt5_gpl-$pkgver
  python configure.py \
    --confirm-license \
    --no-sip-files \
    --no-dist-info \
    --qsci-api
  make

  cd "$srcdir"/PyQt5_gpl-$pkgver-py2
  python2 configure.py \
    --confirm-license \
    --no-sip-files \
    --no-dist-info \
    --qsci-api
  make
}

package_pyqt5-common(){
  pkgdesc="Common PyQt files shared between python-pyqt5 and python2-pyqt5"
  depends=('qt5-base')

  cd PyQt5_gpl-$pkgver
  install -Dm644 PyQt5.api "$pkgdir"/usr/share/qt/qsci/api/python/PyQt5.api

  install -d "$pkgdir"/usr/share/sip/PyQt5
  cp -a sip/* "$pkgdir"/usr/share/sip/PyQt5
}

package_python-pyqt5(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=('python-sip-pyqt5' 'pyqt5-common')
  optdepends=('python-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras'
              'qt5-networkauth: QtNetworkAuth')

  cd PyQt5_gpl-$pkgver
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Provided by pyqt-common
  rm "$pkgdir"/usr/share/qt/qsci/api/python/PyQt5.api
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=('python2-sip-pyqt5' 'pyqt5-common' 'python2-enum34')
  optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras'
              'qt5-networkauth: QtNetworkAuth')

  cd PyQt5_gpl-$pkgver-py2
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Fix conflicts with python-pyqt5
  mv "$pkgdir"/usr/bin/{,python2-}pyuic5
  mv "$pkgdir"/usr/bin/{,python2-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python2-}pyrcc5

  rm "$pkgdir"/usr/lib/qt/plugins/designer/libpyqt5.so
  rm "$pkgdir"/usr/lib/qt/plugins/PyQt5/libpyqt5qmlplugin.so
  rm "$pkgdir"/usr/share/qt/qsci/api/python/PyQt5.api
}
