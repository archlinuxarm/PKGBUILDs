# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Yichao Yu <yyc1992@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com> Ben <ben@benmazer.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove qt5-webengine makedepend, not built for ARM

pkgbase=pyqt5
pkgname=('pyqt5-common' 'python-pyqt5' 'python2-pyqt5')
pkgver=5.10.1
pkgrel=7
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
makedepends=('python-sip' 'python2-sip' 'python-opengl' 'python2-opengl'
             'python2-dbus' 'python-dbus' 'qt5-connectivity'
             'qt5-multimedia' 'qt5-tools' 'qt5-serialport' 'qt5-svg'
             'qt5-webkit' 'qt5-websockets' 'qt5-x11extras' 'qt5-networkauth')
source=("http://sourceforge.net/projects/pyqt/files/PyQt5/PyQt-$pkgver/PyQt5_gpl-$pkgver.tar.gz" pyqt-support-new-qt.patch
        pyqt5-cura-crash.patch pyqt-qt5.11.patch)
sha512sums=('04cd0292f7a088f9c0d088d7c73ec96cfc40253f771dfdf194eb92be7635e6fcaf04863fa24215e9c34a096658549bb2feeb8d8fdca1e39ed87f0256ef0f790b'
            'b94b707e4265ff8e84d52214cea7b70db33aa436440c74e4918c165a7dfae8949bf3e91491d300e68c1715b26b693ba69eca114cf42ce6114dab1b99d69d417e'
            '3f6e1c0dcd95d3ff39c03f59d7c09cbf2e42431bf9cb6dc7a63da10b7a387e5977992e57ef0ff1dea495f29dfbf4db2568b4588e801cca966a0136fe98d1ed45'
            'bae60dd592701929acf403514d8be87d53330dccff74267efb615005d82f5710ce13636a99b2a75beb04fd93a4ade34486c3c2aa190efd334d01c78b34f1bcc0')

prepare() {
  # Support new versions of Qt - don't remove, needs to be reapplied after every new Qt release
  pushd PyQt5_gpl-$pkgver
  patch -p1 -i ../pyqt-support-new-qt.patch
  patch -p1 -i ../pyqt5-cura-crash.patch # Fix crash in Cura
  patch -p1 -i ../pyqt-qt5.11.patch # Fix build with Qt 5.11 (opensuse)
  popd

  # The additional include path was removed due to this line, I don't really know why they are doing this...
  sed -i '/target_config.dbus_inc_dirs = \[\]/d' PyQt5_gpl-$pkgver/configure.py

  cp -a PyQt5_gpl-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PyQt5_gpl-$pkgver
  python configure.py \
    --confirm-license \
    --no-sip-files \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make

  cd "$srcdir"/PyQt5_gpl-$pkgver-py2
  python2 configure.py \
    --confirm-license \
    --no-sip-files \
    --qsci-api \
    -q /usr/bin/qmake-qt5

  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package_pyqt5-common(){
  pkgdesc="Common PyQt files shared between python-pyqt5 and python2-pyqt5"
  depends=('qt5-base')

  cd PyQt5_gpl-$pkgver
  install -Dm644 PyQt5.api "$pkgdir"/usr/share/qt/qsci/api/python/PyQt5.api

  install -d "$pkgdir"/usr/share/sip/PyQt5
  cp -a sip/* "$pkgdir"/usr/share/sip/PyQt5
}

package_python-pyqt5(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=('python-sip' 'pyqt5-common')
  optdepends=('python-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras'
              'qt5-networkauth: QtNetworkAuth')

  cd PyQt5_gpl-$pkgver
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Provided by pyqt-common
  rm "$pkgdir"/usr/share/qt/qsci/api/python/PyQt5.api
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=('python2-sip' 'pyqt5-common' 'python2-enum34')
  optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-webengine: QtWebEngine, QtWebEngineCore, QtWebEngineWidgets'
              'qt5-x11extras: QtX11Extras'
              'qt5-networkauth: QtNetworkAuth')

  cd PyQt5_gpl-$pkgver-py2
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install

  # Fix conflicts with python-pyqt5
  mv "$pkgdir"/usr/bin/{,python2-}pyuic5
  mv "$pkgdir"/usr/bin/{,python2-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python2-}pyrcc5

  rm "$pkgdir"/usr/lib/qt/plugins/designer/libpyqt5.so
  rm "$pkgdir"/usr/lib/qt/plugins/PyQt5/libpyqt5qmlplugin.so
  rm "$pkgdir"/usr/share/qt/qsci/api/python/PyQt5.api
}
