# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable jit on v5/v6

pkgname=qt5-webkit-ng
_qtver=tp5
pkgver=${_qtver/-/}
pkgrel=2
arch=(i686 x86_64)
url='https://github.com/annulen/webkit/wiki'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API (maintained fork)'
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen)
makedepends=(cmake ruby gperf python2 qt5-doc qt5-tools)
optdepends=('gst-plugins-good: Webm codec support')
conflicts=(qt5-webkit)
provides=(qt5-webkit)
source=("https://github.com/annulen/webkit/archive/qtwebkit-$pkgver.tar.gz"
        qtwebkit-ng-export-cmake-variables.patch::"https://github.com/annulen/webkit/commit/1467a945.patch")
sha256sums=('04bcb819675be6ebe0bba81519319dede6510512a73ef53d20465f91034ee719'
            'e7839c2eb2d4f998a6c7f6316f408bb8a948c769f8d987903368b0fe689558c6')

prepare() {
  mkdir -p build

# Correctly export all needed cmake variables
  cd webkit-qtwebkit-$pkgver
  patch -p1 -i ../qtwebkit-ng-export-cmake-variables.patch
}

build() {
  cd build  

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && CONFIG="-DENABLE_JIT=OFF"

  cmake ../webkit-qtwebkit-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=Qt $CONFIG
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
