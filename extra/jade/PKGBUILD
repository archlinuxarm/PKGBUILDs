# $Id: PKGBUILD 44827 2009-07-04 12:14:02Z jgc $
# Maintainer: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
pkgname=jade
pkgver=1.2.1
pkgrel=5
_debpatch=47
pkgdesc="James Clark's DSSSL Engine"
url='http://www.jclark.com/jade/'
arch=('i686' 'x86_64')
license=('custom')
depends=('gcc-libs')
replaces='openjade'
options=('!libtool' '!makeflags')
source=(ftp://ftp.jclark.com/pub/jade/jade-${pkgver}.tar.gz
        http://ftp.debian.org/debian/pool/main/j/jade/jade_${pkgver}-${_debpatch}.diff.gz)
md5sums=('4239670ca6b578bff68b8c2e7cd1225f'
         'feb3889e2a5fa86ac6bdb742a25d0001')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/jade_${pkgver}-${_debpatch}.diff || return 1
  chmod 755 configure || return 1
  ./configure --prefix=/usr --enable-default-catalog=/usr/share/sgml/catalog || return 1
  make || return 1
  install -d -m755 ${pkgdir}/usr/lib
  make prefix=${pkgdir}/usr install || return 1
  mv ${pkgdir}/usr/bin/sx ${pkgdir}/usr/bin/sgml2xml || return 1
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1
}
