# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Sebastian.Salich@gmx.de
# Contributor: Doc Angelo

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepend on lib32-gcc-libs

# NOTE: needs to be built using multilib for mumble-overlay!
pkgbase=mumble
pkgname=(mumble mumble-server)
pkgver=1.5.857
pkgrel=2
pkgdesc="An Open Source, low-latency, high quality voice chat software"
arch=(x86_64)
url="https://www.mumble.info/"
license=(BSD-3-Clause)
# shared depends
depends=(
  abseil-cpp
  gcc-libs
  glibc
  qt5-base
)
# shared makedepends
makedepends=(
  avahi
  boost
  cmake
  openssl
  protobuf
  python
  qt5-tools
)
# mumble makedepends
makedepends+=(
  alsa-lib
  hicolor-icon-theme
  jack
  libpulse
  libsndfile
  libspeechd
  libx11
  libxi
  mesa
  nlohmann-json
  opus
  poco
  qt5-svg
  rnnoise
  speech-dispatcher
  speexdsp
  xdg-utils
)
# mumble-server makedepends
makedepends+=(
  libcap
  systemd
  zeroc-ice
)
checkdepends=(
  xorg-server-xvfb
)
source=(
  https://github.com/mumble-voip/mumble/releases/download/v$pkgver/$pkgbase-$pkgver.tar.gz{,.sig}
  $pkgbase-1.5.517-config_defaults.patch
)
sha512sums=('a0b6d4f5c86992002bbe687409f22037555a753ed92ff26e420b901c85e96c2c37e1999c5bab9cb2c8f2a3dd9d799dc3ee8641e6b91c85d9514db59c378b55df'
            'SKIP'
            'c12f6269c5745532031f09fba5b9e3118e6beaf387ae0aaba6ff8380a1452b47f9f0d1cae04472a5763b3da695e03467de152a98bf03c01ae59bd6d553ec7100')
b2sums=('764a643c4a5f5831b3e2f929473caf2f3fd6c00015b0c4162e5c8fe52cf50b32705e9677401cc9311a713fe0b98ed09d9b68ba48f462fcae91f882a3976962ad'
        'SKIP'
        'c607246691e9701f54b8d133db7d424a46ab94781178d172ae8a35df8ca505da59734ddae00789b93af316de3344d7e177a1a988e1438121e1e921ff59724f24')
# See https://github.com/mumble-voip/mumble-gpg-signatures
validpgpkeys=(
  'CEB6BB9EC81FA7105F22A017CABE1DB1F21021AE'  # Mumble Automatic Build Infrastructure 2025 <mumble-auto-build-2025@mumble.info>
)

prepare() {
  # add default configuration options
  patch -Np1 -d $pkgbase-$pkgver -i ../$pkgbase-1.5.517-config_defaults.patch
  # Ensure the _mumble-server user is fully locked.
  printf 'u! _mumble-server - "Mumble server user" - -\n' > $pkgbase-$pkgver/auxiliary_files/config_files/mumble-server.sysusers
  # ensure the default server directory is created
  printf "d /var/lib/mumble-server 0750 _mumble-server _mumble-server -\n" >> $pkgbase-$pkgver/auxiliary_files/config_files/mumble-server.tmpfiles.in
}

build() {
  local default_options=(
    -D CMAKE_INSTALL_PREFIX=/usr
    # protobuf 23 requires C++17
    -D CMAKE_CXX_STANDARD=17
    -D CMAKE_C_STANDARD=99
    -D CMAKE_BUILD_TYPE=None
    # upstream requires adding arbitrary build number specifically, as otherwise the version string is wrong:
    # https://github.com/mumble-voip/mumble/issues/5538
    -D BUILD_NUMBER="${pkgver/*./}"
    -D tests=ON
    -D warnings-as-errors=OFF
    -S $pkgbase-$pkgver
    -W no-dev
  )
  local cmake_options_client=(
    -D update=OFF
    -D server=OFF
    -B build-client
    -D bundled-json=OFF
    -D bundled-rnnoise=OFF
    -D bundled-speex=OFF
    -D rnnoise=ON
  )
  local cmake_options_server=(
    -D MUMBLE_INSTALL_ABS_SYSCONFDIR=/etc/mumble-server
    -D CMAKE_INSTALL_SYSCONFDIR=/etc
    -D use-pkgconf-install-paths=ON
    -D client=OFF
    -B build-server
  )

  cmake "${default_options[@]}" "${cmake_options_server[@]}"
  cmake --build build-server --verbose

  cmake "${default_options[@]}" "${cmake_options_client[@]}"
  cmake --build build-client --verbose
}

check() {
  xvfb-run ctest --test-dir build-client --output-on-failure
  ctest --test-dir build-server --output-on-failure
}

package_mumble() {
  pkgdesc+=" (client)"
  # NOTE: jack, libpulse, and pipewire are dlopen'ed
  depends+=(
    alsa-lib libasound.so
    avahi libdns_sd.so
    hicolor-icon-theme
    jack
    protobuf libprotobuf.so
    libpulse
    libsndfile libsndfile.so
    libspeechd
    libx11
    libxi
    openssl libcrypto.so libssl.so
    opus libopus.so
    poco
    qt5-svg
    rnnoise
    speexdsp libspeexdsp.so
    xdg-utils
  )
  optdepends=(
    'bash: for mumble-overlay'
    'espeak-ng: Text-to-speech support'
    'speech-dispatcher: Text-to-speech support'
  )

  DESTDIR="$pkgdir" cmake --install build-client
  install -vDm 644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_mumble-server() {
  pkgdesc+=" (server)"
  depends+=(
    avahi libdns_sd.so
    bash
    libcap libcap.so
    openssl libcrypto.so libssl.so
    protobuf libprotobuf.so
    zeroc-ice
  )
  conflicts=('murmur<1.5')
  replaces=('murmur<1.5')
  backup=(etc/$pkgbase/$pkgname.ini)
  install=mumble-server.install

  DESTDIR="$pkgdir" cmake --install build-server
  install -vDm 644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
# vim: sw=2:ts=2 et:
