# $Id$
# Maintainer: Carlier Laurent <lordheavym@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to link with atomic_ops


pkgname=libfbclient
pkgver=2.5.7.27050
pkgrel=2
pkgdesc="Client library for Firebird."
arch=('i686' 'x86_64')
url="http://www.firebirdsql.org/"
license=('custom')
depends=('gcc-libs' 'icu')
options=('!makeflags')
source=(http://downloads.sourceforge.net/firebird/Firebird-$pkgver-0.tar.bz2
        firebird-c++11.patch
        firebird-c++14.patch
        link_atomic_ops.patch
        LICENSE)
md5sums=('fb34241e96f9707604bf6cd78357d5a2'
         '5094347a8298143ca147edd49b552fe9'
         'a02710203cee81b4f48b7bdca3b1b33f'
         'ca8e2c7a1fe0d3398f72d63a6c85422a'
         '0d61798861e159c85dbdf0f30fe2f2ce')

prepare() {
  cd $srcdir/Firebird-$pkgver-0
  patch -Np1 -i ../firebird-c++11.patch
  patch -Np0 -i ../firebird-c++14.patch
  patch -p1 -i ../link_atomic_ops.patch
}

build() {
  cd $srcdir/Firebird-$pkgver-0

  ./configure --prefix=/usr --with-system-icu --without-fbudf \
    --without-fbsbin --without-fbconf --without-fbdoc --without-fbsample \
    --without-fbsample-db --without-fbintl --without-fbmisc --without-fbhelp \
    --without-fbsecure-db --with-fbmsg=/usr/share/firebird --without-fblog \
    --without-fbglock --without-fbplugins

  CXXFLAGS+=' -fno-lifetime-dse' make
}

package() {
  cd $srcdir/Firebird-$pkgver-0

  mkdir -p $pkgdir/usr/{bin,share/{firebird,licenses/$pkgname}}

  cp -R gen/firebird/{lib,include} $pkgdir/usr
  rm -f $pkgdir/usr/lib/libedit.a

  install -m644 gen/firebird/*.msg $pkgdir/usr/share/firebird
  install -m755 gen/firebird/bin/fb_config $pkgdir/usr/bin
  install -m644 ${srcdir}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # Add libgds support FS#30062 FS#30282
  cd $pkgdir/usr/lib
  ln -s libfbclient.so libgds.so.0
  ln -s libfbclient.so libgds.so
}
