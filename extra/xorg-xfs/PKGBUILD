# $Id: PKGBUILD 50031 2009-08-18 15:04:49Z jgc $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xfs
pkgver=1.1.0
pkgrel=1
pkgdesc="X.Org X11 xfs font server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
backup=('etc/X11/fs/config')
depends=('xorg-font-utils' 'libxfont>=1.4.0' 'libfs>=1.0.2' 'libx11>=1.2.2')
makedepends=('pkgconfig' 'xtrans')
source=(${url}/releases/individual/app/xfs-${pkgver}.tar.bz2
        ${url}/releases/individual/app/xfsinfo-1.0.2.tar.bz2
        ${url}/releases/individual/app/fslsfonts-1.0.2.tar.bz2
        ${url}/releases/individual/app/fstobdf-1.0.3.tar.bz2
        ${url}/releases/individual/app/showfont-1.0.2.tar.bz2
        xfs.rc)
md5sums=('5cfc06a320f74f07a0aa73da62307e9a'
         'e6c0071f6d5436113982f4a575d3f94b'
         'a61be25c87ee42169ee4dc8344acc9e2'
         '49a6225380f6c18fff664a043cd569b1'
         '8f83d774ed4cc2a6dca86edd856d820a'
         '251f5447f64151e9bb096ebc9f999c3f')

build() {
  cd "${srcdir}"
  pushd xfs-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make configdir=/etc/X11/fs || return 1
  make configdir=/etc/X11/fs DESTDIR="${pkgdir}" install || return 1
  sed -i -e 's|/lib/X11/|/share/|g' "${pkgdir}/etc/X11/fs/config"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.xfs" || return 1
  popd
  for i in xfsinfo-* fslsfonts-* fstobdf-* showfont-*; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${i%-*}" || return 1
      popd
    fi
  done
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/xfs.rc" "${pkgdir}/etc/rc.d/xfs" || return 1
}
