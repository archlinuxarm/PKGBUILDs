# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=krita
pkgver=4.2.9
pkgrel=2
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion karchive kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw boost-libs giflib hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 opencolorio python-pyqt5 libheif
             qt5-tools sip python-sip)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz"{,.sig}
         krita-pyqt5-sip5.patch
         krita-deadlock-qt5.14.2.patch::"https://invent.kde.org/kde/krita/commit/afed6a9e.patch")
sha256sums=('9f85bb81eaf4976af57352a482af1fdb86f338051a991f6f43976e0b65956da8'
            'SKIP'
            'ab2f33843d8cad31bc13e0eca7dc732cdbfed054924f25ed61102c0d0971c1d8'
            '6477ceec976d376172cc82a5a8bf85938a7dffd924286fa79e64ccdab8c901eb')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../krita-pyqt5-sip5.patch # Fix sip include dir when PyQt5 is compiled with SIP 5
  patch -p1 -i ../krita-deadlock-qt5.14.2.patch # Fix deadlock on plugin load with Qt 5.14.2
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
