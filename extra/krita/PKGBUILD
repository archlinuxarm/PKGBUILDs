# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=krita
_pkgver=4.4.1
pkgver=${_pkgver/-/}
pkgrel=8
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw giflib openjpeg2 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 opencolorio python-pyqt5 libheif
             qt5-tools sip4 python-sip4 kseexpr)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter'
            'kseexpr: SeExpr generator layer' 'kimageformats: PSD support')
source=("https://download.kde.org/stable/krita/$_pkgver/$pkgname-$_pkgver.tar.gz"{,.sig}
         krita-kseexpr.patch::"https://invent.kde.org/graphics/krita/commit/bdebaf91.patch"
         krita-c++14.patch)
sha256sums=('6f2daf068e4e5cef8a738ab47da443dc82ad1463090a4a1544b35201272c3817'
            'SKIP'
            '6aeec0c8d6bc29ae81e7dd1d971663c32cc0137888da4a1b4f50a69fc4491d80'
            'de84c3c1685e48392d4fb07daab56068cacfc2a846a52a9915b5f3e08357abd1')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
              'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F') # Dmitry Kazakov (main key) <dimula73@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../krita-kseexpr.patch # Port to rebranded seexpr
  patch -d $pkgname-$pkgver -p1 -i ../krita-c++14.patch # Fix build with boost 1.75
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
