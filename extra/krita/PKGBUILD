# $Id: PKGBUILD 262594 2016-03-26 20:49:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc
#  - patch to fix building on ARM

pkgname=krita
pkgver=4.0.1
pkgrel=1
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kio kitemmodels gsl libraw exiv2 openexr fftw curl boost-libs giflib qt5-declarative hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python boost eigen poppler-qt5 opencolorio python-pyqt5)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-Fix-ARM-build.patch')
sha256sums=('da44e964cb2c0e409b18f3d50f92734d6e6e15e2ce494659b6dc52fed8d8218c'
            'SKIP'
            '14c43c57daf4fdff8f28abc844c1804a4bc9c898c4782daca7ab5cbd21ae48d2')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build
  patch -p1 -d $pkgname-$pkgver  -i ../0001-Fix-ARM-build.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
