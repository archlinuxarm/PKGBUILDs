# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=krita
_pkgver=4.4.5
pkgver=${_pkgver/-/}
pkgrel=2
pkgdesc='Edit and paint images'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw giflib openjpeg2 opencolorio1 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 python-pyqt5 libheif
             qt5-tools sip4 python-sip4 kseexpr)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter'
            'kseexpr: SeExpr generator layer' 'kimageformats: PSD support')
source=(https://download.kde.org/stable/krita/$_pkgver/$pkgname-$_pkgver.tar.gz{,.sig}
        krita-openexr3.patch
        https://invent.kde.org/graphics/krita/-/commit/86dc361a.patch
        krita-opencolorio1.patch)
sha256sums=('4bfd7280870d1060a00d1123d836fc1a5b5bf8a028676679f0268cd5e7fb7568'
            'SKIP'
            '3825dac0317f4501c8dd8b0844540bbd0841702cd15903ce3a659755eaa226f5'
            '00c4e519bbef9575f8d4ae2183aff1da3ad9f44d324c1c1d45a7803368a1a008'
            '2f892449e20abc370fe3dc8b5dd12f9964d1d402a909e775641e28685b1719b3')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
              'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F') # Dmitry Kazakov (main key) <dimula73@gmail.com>

prepare() {
  patch -d $pkgname-$_pkgver -p1 < krita-openexr3.patch # Fix build with OpenEXR 3
  patch -d $pkgname-$_pkgver -p1 < 86dc361a.patch # Fix OCIO include dir detection
  patch -d $pkgname-$_pkgver -p1 < krita-opencolorio1.patch # Fix build with renamed OCIO library
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
