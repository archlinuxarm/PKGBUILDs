# $Id: PKGBUILD 44479 2009-07-02 07:01:00Z allan $
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

# Note for rebuilders:
# dcraw.c  is released as pure C file, not as a package with a changing version
# number in the name. The name of the file to be downloaded is always the same!
# So if you are a bad boy (Santa knows everything!) and build as root, this file
# will be saved in /var/cache/pacman/src . Therefore, rebuilding a later version
# likely will fail due to an MD5 error. If so, delete the files from the cache
# to force a new download.

pkgname=dcraw
pkgver=1.425
pkgrel=1
pkgdesc="A command line based converter for digicams raw output"
arch=(i686 x86_64)
license=('custom')
depends=('libjpeg>=7' 'lcms')
makedepends=('gzip')
source=(http://www.cybercom.net/~dcoffin/${pkgname}/${pkgname}.{c,1} license.txt)
url="http://www.cybercom.net/~dcoffin/dcraw/"
install=dcraw.install
md5sums=('d71404427935fb70eb7f2b8c535b8d75'
         '6ba2a797c4ad698589f83171d6b36396'
         '110b4604a6dacd5537339c4082e9e12a')

build() {
  cd ${startdir}/src
  gcc -o dcraw ${CFLAGS} dcraw.c -lm -ljpeg -llcms
  install -D -m755  ${pkgname} ${startdir}/pkg/usr/bin/${pkgname}
 # compress and install a manpage, provided separately on authors homepage
  gzip -c -9 ${pkgname}.1 > ${pkgname}.1.gz
  install -D -m644 ${pkgname}.1.gz ${startdir}/pkg/usr/share/man/man1/${pkgname}.1.gz
  install -Dm 644 ${startdir}/src/license.txt \
    ${startdir}/pkg/usr/share/licenses/${pkgname}/license.txt
}
