# $Id: PKGBUILD 48789 2009-08-03 20:57:24Z jgc $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-savage
pkgver=2.3.1
pkgrel=1
pkgdesc="X.org savage video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc' 'savage-dri')
makedepends=('pkgconfig' 'xorg-server>=1.6.3' "mesa>=7.5" 'xf86driproto' 'glproto>=1.4.9')
conflicts=('xorg-server<1.6.3')
options=(!libtool !makeflags)
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        LICENSE)
md5sums=('66c319f610e3699c3de0f3ef630abb32'
         '0f485840d3cee0a8d9952c0cd34f64c3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
