# $Id: PKGBUILD 249779 2015-10-27 00:47:25Z eric $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on !armv7h

pkgname=libglvnd
pkgver=0.2.999+g4ba53457
_commit=4ba5345742d6c98b528ac28fb7127006d2cc3f75
pkgrel=2
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('i686' 'x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
makedepends=('libxext' 'libx11' 'glproto' 'git' 'python')
provides=('libgl' 'libegl' 'libgles')
#source=("git+https://github.com/NVIDIA/libglvnd.git#tag=v$pkgver"
source=("git+https://github.com/NVIDIA/libglvnd.git#commit=$_commit"
        LICENSE)
sha256sums=('SKIP'
            '1a99f7487849c41fc3520e0af3dfcf28a0df05e78956d9ec58c2c52b1f20a77d')

build() {
  cd $pkgname
  ./autogen.sh
  [[ $CARCH != "armv7h" ]] && CONFIG="--disable-asm"
  ./configure --prefix=/usr $CONFIG
  make
}

package() {
  # libglvnd needs mesa for indirect rendering
  depends=('libxext' 'mesa' 'opengl-driver')
  cd $pkgname
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
