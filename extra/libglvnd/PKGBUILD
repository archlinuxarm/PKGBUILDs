# $Id: PKGBUILD 249779 2015-10-27 00:47:25Z eric $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on !armv7h

pkgname=libglvnd
pkgver=0.0.0.20160315
_commit=f1c345f3a33e537b68406d861965b3113c5122ea
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('i686' 'x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
depends=('libxext')
makedepends=('xorg-server-devel' 'git' 'python2')
source=("git+https://github.com/NVIDIA/libglvnd.git#commit=$_commit"
        LICENSE)
sha1sums=('SKIP'
          'c88e07f333fcf8dcdbef370b85372b4d88ad8a66')

build() {
  cd $pkgname
  ./autogen.sh

  [[ $CARCH != "armv7h" ]] && CONFIG="--disable-asm"

  ./configure --prefix=/usr $CONFIG
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install

  # For compatibility with older nvidia drivers for bumblebee
  # Symlinks to /usr/lib will be in nvidia-libgl
  mkdir "$pkgdir/usr/lib/nvidia"
  for _lib in libGL.so libGLESv1_CM.so libGLESv2.so; do
    mv "$pkgdir"/usr/lib/$_lib* "$pkgdir"/usr/lib/nvidia/
  done

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
