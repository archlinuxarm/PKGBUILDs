# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly

pkgname=libglvnd
pkgver=1.3.2
pkgrel=2
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
makedepends=('libxext' 'libx11' 'xorgproto' 'python' 'meson')
provides=('libgl' 'libegl' 'libgles')
source=("https://gitlab.freedesktop.org/glvnd/libglvnd/-/archive/v$pkgver/libglvnd-v$pkgver.tar.gz"
        https://gitlab.freedesktop.org/glvnd/libglvnd/-/commit/a527411da713b2068974c46d7129326520dc5923.patch
        LICENSE)
sha512sums=('01db155f02fa21f4217ee5ca6dd8eeed3d20672c8aaa43475bd112921716ee8d4124e01f80a89587ddc8ebfa4a552c5e02d70726222702cce1601a6e6f4bc22b'
            '10f630d141f65d8d54adf434b3c0b56c937f495336a873737cbea1b0ca07728b430bd44a7f3f712c5ae20a37b71a864301205c590bd251897155e60b1b1e7e6d'
            'bf0f4a7e04220a407400f89226ecc1f798cc43035f2538cc8860e5088e1f84140baf0d4b0b28f66e4b802d4d6925769a1297c24e1ba39c1c093902b2931781a5')

prepare() {
  # https://bugs.archlinux.org/task/70488
  patch -d $pkgname-v$pkgver -p1 < a527411da713b2068974c46d7129326520dc5923.patch
}

build() {
  arch-meson $pkgname-v$pkgver build \
    -D gles1=false -D asm=disabled

  ninja -C build
}

package() {
  # libglvnd needs mesa for indirect rendering
  depends=('libxext' 'mesa' 'opengl-driver')

  DESTDIR="$pkgdir" ninja -C build install
  
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
