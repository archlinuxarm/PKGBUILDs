# $Id: PKGBUILD 43872 2009-06-30 06:30:28Z allan $
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: twm

pkgname=mrxvt
pkgver=0.5.4
pkgrel=2
pkgdesc="Mrxvt (previously known as materm) is a multi-tabbed X terminal emulator based on rxvt code"
arch=(i686 x86_64)
url="http://materm.sourceforge.net/wiki/pmwiki.php"
license=('GPL')
depends=('libpng' 'libjpeg>=7' 'libxft' 'libxpm')
replaces=('materm')
backup=('etc/mrxvt/mrxvtrc' 'etc/mrxvt/submenus.menu' 'etc/mrxvt/default.menu')
source=(http://downloads.sourceforge.net/sourceforge/materm/${pkgname}-${pkgver}.tar.gz \
        mrxvt-0.5.4-001-fix-segfault-when-wd-empty.patch mrxvt.desktop)
md5sums=('0232c8868484751dcb931a28f0756f69' '2b6ecc9d68185e56a50eca0a2548fa8b'\
         '6ba3bcd484c8dad1b0b48465ded54de3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 < ../mrxvt-0.5.4-001-fix-segfault-when-wd-empty.patch || return 1
  # mrxvt is not a default xterm emulator, let's add some fancy options here, ey
  ./configure --prefix=/usr --sysconfdir=/etc --enable-xft --enable-text-shadow \
    --enable-transparency --enable-smart-resize --enable-menubar \
    --disable-ourstrings --enable-linespace --enable-256colors --enable-xim \
    --enable-thai --enable-greek --enable-cjk --enable-backspace-key \
    --with-save-lines=2048 || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -Dm644 ../mrxvt.desktop ${pkgdir}/usr/share/applications/mrxvt.desktop
}
