# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for AArch64

pkgname=uim
pkgver=1.8.6
pkgrel=4
pkgdesc='Multilingual input method library'
url='http://code.google.com/p/uim/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('libxft' 'libedit' 'm17n-lib')
makedepends=('intltool' 'gettext' 'qt4' 'gtk2' 'gtk3' 'anthy')
optdepends=('qt4: immodule and helper applications'
            'gtk2: immodule and helper applications'
            'gtk3: immodules and helper applications')
source=("https://uim.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        'aarch64.diff')
sha1sums=('409c30b31b9e58e3c18cb7862933487b0585cd1d'
          '35d2a2e2920addc97b6a73eb7ea0f56c89ff014f')

install=install

prepare() {
	patch -p1 -d "${pkgname}-${pkgver}/sigscheme" -i ../../aarch64.diff
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/uim \
		--with-anthy-utf8 \
		--with-qt4-immodule \
		--with-qt4 \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -j1 DESTDIR="${pkgdir}" install
	rm "${pkgdir}"/usr/lib/libgcroots.a
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
