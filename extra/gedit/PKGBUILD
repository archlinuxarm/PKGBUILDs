# $Id: PKGBUILD 44688 2009-07-03 17:58:54Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=2.26.3
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf>=2.26.0-3' 'pygtk>=2.14.1' 'pygtksourceview2>=2.6.0' 'enchant>=1.4.2' 'desktop-file-utils' 'iso-codes' 'gtksourceview2>=2.6.2' 'libsm')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.16.0' 'intltool')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0cdaad6b02af250c9c110010d0a708b2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --libexecdir=/usr/lib --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gedit ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
