# $Id: PKGBUILD 53470 2009-10-01 01:43:22Z eric $
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

pkgname=rxvt-unicode
pkgver=9.06
pkgrel=3
pkgdesc="An unicode enabled rxvt-clone terminal emulator (urxvt)"
arch=(i686 x86_64)
depends=('gcc-libs' 'libxft' 'libxpm')
makedepends=('ncurses' 'perl>=5.10.0' 'pkgconfig')
optdepends=('perl: lots of utilities' 'gtk2-perl: to use the urxvt-tabbed')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=("GPL2")
source=(http://dist.schmorp.de/rxvt-unicode/${pkgname}-${pkgver}.tar.bz2 \
        ${pkgname}.desktop ${pkgname}.png rxvt-unicode-9.06-glibc-2.10.patch \
        popup-menu-hang.diff)
md5sums=('4cb9330e99fc0b0b05cebf3581557dd8' '5bfefa1b41c2b81ca18f2ef847330543'\
         '84328cada91751df07324d95f8be4d1b' '9fdeead0d3a05f18daf831da0204288f'\
         'aead33ba3b08eeb251fb0c1427a4a024')
sha1sums=('df623ebdad001de534781721f8298ba1add0df2c' '79b7cacb7d6ce4a34f8a31a2e33475a8f7bf69cb'\
         '9ff58d7b1d60838c51c9045bc0be342fac456790' '0d6a3c382db068e17a25480eaf67e1a1392a90f3'\
         'ec4013f4af3a45da928b75f40fbf6389070e7fdd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../rxvt-unicode-9.06-glibc-2.10.patch || return 1
  patch -p0 < ../popup-menu-hang.diff || return 1
  ./configure --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-font-styles \
    --enable-xim \
    --enable-keepscrolling \
    --enable-selectionscrolling \
    --enable-smart-resize \
    --enable-xpm-background \
    --enable-transparency \
    --enable-utmp \
    --enable-wtmp \
    --enable-lastlog || return 1
  make || return 1
  install -d "${pkgdir}/usr/share/terminfo"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  make DESTDIR="${pkgdir}" install || return 1
 # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed || return 1
  install -Dm 755 doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed" || return 1
 # install freedesktop menu and icon ( icon from cvs checkout )
  install -Dm644 ../${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 ../${pkgname}.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
