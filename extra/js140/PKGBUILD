# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - --disable-rust-simd, --disable-lto
#  - remove PGO build

pkgname=js140
pkgver=140.1.0
pkgrel=1
pkgdesc="JavaScript interpreter and libraries - Version 140"
url="https://spidermonkey.dev/"
arch=(x86_64)
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
  readline
  sh
  zlib
)
makedepends=(
  cbindgen
  clang
  lld
  llvm
  python
  rust
  zip
)
checkdepends=(
  git
  mercurial
)
options=(
  !lto
)
_relver=${pkgver}esr
source=(
  https://archive.mozilla.org/pub/firefox/releases/$_relver/source/firefox-$_relver.source.tar.xz{,.asc}
  0001-Bug-1973993-Install-ProfilingCategoryList.h-into-bot.patch
  0002-Bug-1973994-mozjs-140.pc-does-not-contain-DXP_UNIX-o.patch
)
validpgpkeys=(
  # Mozilla Software Releases <release@mozilla.com>
  # https://blog.mozilla.org/security/2025/04/01/updated-gpg-key-for-signing-firefox-releases-2/
  14F26682D0916CDD81E37B6D61B7B526D98F0353
)
sha256sums=('d15c65d790e0c371b5c95332141b1bdeb29fefc27f852d22a5f542b6d1bc1922'
            'SKIP'
            '3b869d8568a5ee6533f787772006d594cd995a9c640a98941b9178d0075999a6'
            'b5178a7b8a603b645cbae61467f3fa1fef6e9f34c90e4a22faf4ea2152a50fba')
b2sums=('8a6c1f50802cafc171a4c5a197afd3c724053203d5d92fe35a7453fa1a8c05c743c246b1ac257f073f70b485fb0d5a68071254587677c55926aeeed0e0bc87cc'
        'SKIP'
        '97ce636618029289819bbb9828daaacdb7bfe16b9b0a0f2639a61a4b793d1cb8c07186b45213dde7bd0ace73848865b6d14d2d20a93758c109e28f835ee1bf7c'
        '89f39bd3ef2ca4894d98f860c2296b2afa58e4fe8c68ddb816a0684cc0db3a8e0deec71af5d214fb3ae40f6ad963aefef02d6b71071c41b9b55752c7cf791a9c')

# Make sure the duplication between bin and lib is found
COMPRESSZST+=(--long)

prepare() {
  mkdir mozbuild
  cd firefox-$pkgver

  # https://discourse.gnome.org/t/gnome-49-to-depend-on-spidermonkey-140/30470
  patch -Np1 -i ../0001-Bug-1973993-Install-ProfilingCategoryList.h-into-bot.patch
  patch -Np1 -i ../0002-Bug-1973994-mozjs-140.pc-does-not-contain-DXP_UNIX-o.patch

  cat >../mozconfig <<END
ac_add_options --enable-application=js
mk_add_options MOZ_OBJDIR=${PWD@Q}/obj

ac_add_options --prefix=/usr
ac_add_options --enable-release
ac_add_options --enable-hardening
ac_add_options --enable-optimize
ac_add_options --disable-rust-simd
ac_add_options --enable-linker=lld
ac_add_options --disable-bootstrap
ac_add_options --disable-debug
ac_add_options --disable-lto
ac_add_options --disable-jemalloc
ac_add_options --disable-strip

# System libraries
ac_add_options --with-system-zlib
ac_add_options --without-system-icu

# Features
ac_add_options --enable-readline
ac_add_options --enable-shared-js
ac_add_options --enable-tests
ac_add_options --with-intl-api
END
}

build() {
  cd firefox-$pkgver

  export MACH_BUILD_PYTHON_NATIVE_PACKAGE_SOURCE=pip
  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MOZ_BUILD_DATE="$(date -u${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH} +%Y%m%d%H%M%S)"
  export MOZ_NOSPAM=1

  # malloc_usable_size is used in various parts of the codebase
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cat >.mozconfig ../mozconfig
  ./mach build --priority normal
}

check() {
  local jstests_extra_args=(
    --format=none
    --exclude-random
    --wpt=disabled
  ) jittest_extra_args=(
    --format=none
    --timeout 300
  ) jittest_test_args=(
    basic
  )

  cd firefox-$pkgver/obj
  make -C js/src check-jstests check-jit-test \
    JSTESTS_EXTRA_ARGS="${jstests_extra_args[*]}" \
    JITTEST_EXTRA_ARGS="${jittest_extra_args[*]}" \
    JITTEST_TEST_ARGS="${jittest_test_args[*]}"
}

package() {
  cd firefox-$pkgver/obj
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/*.ajs
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -c a-x {} +
}

# vim:set sw=2 sts=-1 et:
