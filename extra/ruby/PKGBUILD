# $Id: PKGBUILD 53111 2009-09-26 06:56:09Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgname=('ruby' 'ruby-docs')
pkgver=1.9.1_p243
_pkgver=1.9.1-p243
pkgrel=2
pkgdesc="An object-oriented language for quick and easy programming"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.ruby-lang.org/en/"
makedepends=('gdbm>=1.8.3' 'db>=4.8' 'openssl>=0.9.8k' 'zlib>=1.2.3.3' \
             'readline>=5.2.013' 'tk>=8.5')
source=(ftp://ftp.ruby-lang.org/pub/ruby/1.9/${pkgname}-${_pkgver}.tar.bz2) 
options=('!emptydirs')
md5sums=('66d4f8403d13623051091347764881a0')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr --enable-shared --enable-pthread || return 1
  make || return 1
}

package_ruby()
{
  depends=('gdbm>=1.8.3' 'db>=4.8' 'openssl>=0.9.8k' 'zlib>=1.2.3.3' 'readline>=5.2.013')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
  optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install || return 1  
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}

package_ruby-docs()
{
  pkgdesc="Documentation files for ruby"
  #arch=('any')
  depends=('ruby')

  cd "${srcdir}/${pkgname}-${_pkgver}"  
  make -j1 DESTDIR="${pkgdir}" install-doc || return 1
}
