# $Id: PKGBUILD 131846 2011-07-16 03:10:20Z eric $
# Maintainer:
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>
# Contributor: macau <tema.prokopenko@gmail.com> //prepare for arm build, temporary remove graphviz from deps

pkgbase=ruby
pkgname=('ruby' 'ruby-docs')
pkgver=1.9.2_p290
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming"
arch=('armv7h')
url="http://www.ruby-lang.org/en/"
license=('custom')
makedepends=('openssl' 'tk' 'libffi' 'doxygen')
options=('!emptydirs' '!makeflags')
source=(ftp://ftp.ruby-lang.org/pub/${pkgbase}/${pkgver%.*}/${pkgbase}-${pkgver//_/-}.tar.bz2)
md5sums=('096758c3e853b839dc980b183227b182')
sha1sums=('8f25a74f50a10fdd9724e3c74ccd7bae596e198b')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver//_/-}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --enable-shared --enable-pthread --disable-rpath
make

}

package_ruby() {
  depends=('openssl' 'libffi')
  provides=('rubygems' 'rake')
  conflicts=('rake')
  optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgbase}-${pkgver//_/-}"
  make DESTDIR="${pkgdir}" install-nodoc
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ruby-docs() {
  pkgdesc="Documentation files for ruby"

  cd "${srcdir}/${pkgbase}-${pkgver//_/-}"
  make DESTDIR="${pkgdir}" install-doc install-capi
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
