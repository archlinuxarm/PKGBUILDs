# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

# Do not re-package default gems (see https://stdgems.org/). Trying to do that will lead to multiple problems

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - generalize arch-dependent removal of gem remains
#  - add -Wno-incompatible-pointer-types to C(XX)FLAGS

pkgname=(
  ruby
  ruby-docs
  ruby-default-gems
  ruby-bundled-gems
  ruby-stdlib
)
pkgver=3.4.8
pkgrel=1
pkgdesc='An object-oriented language for quick and easy programming'
url='https://www.ruby-lang.org/en/'
arch=(x86_64)
license=(BSD-2-Clause)
makedepends=(
  doxygen
  gcc-libs
  gdbm
  glibc
  gmp
  graphviz
  libffi
  libxcrypt
  libyaml
  openssl
  readline
  rust
  tk
  zlib
)
checkdepends=(
  procps-ng
)
options=('!emptydirs')
source=(
  "https://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/ruby-${pkgver}.tar.xz"
)
sha512sums=('b6289654685c2d381d15123cf07a131fd088edf13a5336966af5a2e8fad9adda42d14caa7bb0998331831af71e35c546091db55c82089e89b9508603ff5f92d6')
b2sums=('e40144cea391c32a4ce1e6c576884dd9ca3a31398b0110bb67e96eca90dc1d8cb0376269e691a1b48b06bbbb23b0137e67bd05ae4aeccb5c26d163fbce5cec55')

_bootstrap=0
_rubyver="${pkgver:0:3}.0"
_bundled_gems=(
  abbrev
  base64
  bigdecimal
  csv
  debug
  drb
  getoptlong
  matrix
  minitest
  mutex_m
  net-ftp
  net-imap
  net-pop
  net-smtp
  nkf
  observer
  power_assert
  prime
  racc
  rake
  rbs
  repl_type_completor
  resolv-replace
  rexml
  rinda
  rss
  test-unit
  syslog
  typeprof
)
_bundled_gems_bins=(
  racc
  rake
  rbs
  rdbg
  typeprof
)
_default_gems=(
  benchmark
  cgi
  date
  delegate
  did_you_mean
  digest
  english
  error_highlight
  etc
  fcntl
  fiddle
  fileutils
  find
  forwardable
  io-console
  io-nonblock
  io-wait
  ipaddr
  json
  logger
  net-http
  net-protocol
  open-uri
  open3
  openssl
  optparse
  ostruct
  pathname
  prism
  pp
  prettyprint
  pstore
  psych
  readline
  reline
  resolv
  ruby2_keywords
  securerandom
  set
  shellwords
  singleton
  stringio
  strscan
  syntax_suggest
  tempfile
  time
  timeout
  tmpdir
  tsort
  un
  uri
  weakref
  yaml
  zlib
)
_default_tool_gems=(
  bundler
  erb
  irb
  rdoc
  rubygems
)
_default_tool_gems_bins=(
  bundle
  bundler
  erb
  gem
  irb
  rdoc
  ri
)

prepare() {
  cd "ruby-${pkgver}"

  autoreconf -fiv
}

build() {
  cd "ruby-${pkgver}"

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2} -Wno-incompatible-pointer-types"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2} -Wno-incompatible-pointer-types"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat

  make
  make rdoc capi
}

check() {
  cd "ruby-${pkgver}"

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  make check
}

package_ruby() {
  depends=(
    gcc-libs
    gdbm
    glibc
    gmp
    libffi
    libxcrypt
    libyaml
    openssl
    readline
    zlib
  )
  optdepends=(
    'tk: for Ruby/TK'
    'ruby-docs: Documentation for Ruby'
    'ruby-default-gems: Default gems which are part of Ruby StdLib'
    'ruby-bundled-gems: Bundled gems which are part of Ruby StdLib'
    'ruby-stdlib: Full Ruby StdLib including default gems, bundled gems and tools'
  )
  provides=(
    libruby.so
  )
  replaces=(
    ruby-benchmark
    ruby-cgi
    ruby-date
    ruby-delegate
    ruby-did_you_mean
    ruby-digest
    ruby-english
    ruby-etc
    ruby-fcntl
    ruby-fiddle
    ruby-fileutils
    ruby-find
    ruby-forwardable
    ruby-io-console
    ruby-io-nonblock
    ruby-io-wait
    ruby-ipaddr
    ruby-json
    ruby-logger
    ruby-net-http
    ruby-open-uri
    ruby-prism
    ruby-psych
    ruby-reline
    ruby-ruby2_keywords
    ruby-set
    ruby-stringio
    ruby-time
    ruby-tmpdir
    ruby-uri
  )
  conflicts=("${replaces[@]}")
  provides+=("${_default_gems[@]/#/ruby-}")

  cd "ruby-${pkgver}"

  make DESTDIR="${pkgdir}" install-nodoc

  install --verbose -D --mode=0644 BSDL COPYING --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"

  # remove unrepreducible files
  rm --recursive --verbose \
    "${pkgdir}/usr/lib/ruby/gems/${_rubyver}/cache"

  # bootstrap switch
  if (( _bootstrap )); then
    # provide everything for a bootstrap build
    # use a reference to provides to srcinfo generation isn't confused
    declare -n bootstrap_provides=provides
    bootstrap_provides+=("${_default_tool_gems[@]/#/ruby-}" rubygems)
    bootstrap_provides+=("${_bundled_gems[@]/#/ruby-}")
    declare -n bootstrap_conflicts=conflicts
    bootstrap_conflicts+=("${_default_tool_gems[@]/#/ruby-}" rubygems)
    bootstrap_conflicts+=("${_bundled_gems[@]/#/ruby-}")
  else
    # remove de-vendored parts
    _remove_default_tool_gems
    _remove_bundled_gems
    # add standard dependencies
    depends+=(
      rubygems
    )
  fi
}

# remove bundled gems - they are provided as dedicated packages
_remove_bundled_gems() {
  local gem bin
  for gem in "${_bundled_gems[@]}"; do
    msg2 "removing bundled gem ${gem}"
    rm --recursive --verbose \
      "${pkgdir}/usr/lib/ruby/gems/${_rubyver}/gems/${gem}"-* \
      "${pkgdir}/usr/lib/ruby/gems/${_rubyver}/specifications/${gem}"-*.gemspec
    rm --recursive --verbose --force \
      "${pkgdir}/usr/lib/ruby/gems/${_rubyver}/extensions"/*-linux*/"${_rubyver}/${gem}"-*
  done
  for bin in "${_bundled_gems_bins[@]}"; do
    rm --recursive --verbose "${pkgdir}/usr/bin/${bin}"
    rm --recursive --verbose --force "${pkgdir}/usr/bin/${bin}.lock"
    rm --recursive --verbose --force "${pkgdir}/usr/share/man/man1/${bin}.1"
  done

}

# remove default tool gems - they are provided as dedicated packages
_remove_default_tool_gems() {
  local gem bin
  for gem in "${_default_tool_gems[@]}"; do
    msg2 "removing default gem ${gem}"
    rm --recursive --verbose \
      "${pkgdir}/usr/lib/ruby/${_rubyver}/${gem}"*
    if [[ ${gem} != rubygems ]]; then
      rm --recursive --verbose \
        "${pkgdir}/usr/lib/ruby/gems/${_rubyver}/gems/${gem}"-* \
        "${pkgdir}/usr/lib/ruby/gems/${_rubyver}/specifications/default/${gem}"-*.gemspec
    fi
    rm --recursive --verbose --force \
      "${pkgdir}/usr/lib/ruby/${_rubyver}"/*-linux*/"${gem}"
  done
  for bin in "${_default_tool_gems_bins[@]}"; do
    rm --recursive --verbose "${pkgdir}/usr/bin/${bin}"
    rm --recursive --verbose --force "${pkgdir}/usr/bin/${bin}.lock"
    rm --recursive --verbose --force "${pkgdir}/usr/share/man/man1/${bin}.1"
  done
}

package_ruby-docs() {
  pkgdesc='Documentation files for Ruby'

  cd "ruby-${pkgver}"
  make DESTDIR="${pkgdir}" install-doc install-capi
  install --verbose -D --mode=0644 BSDL COPYING --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_ruby-bundled-gems() {
  pkgdesc='Bundled gems which are part of Ruby StdLib'
  replaces=(ruby-bundledgems)
  conflicts=(ruby-bundledgems)
  depends=("${_bundled_gems[@]/#/ruby-}")

  cd "ruby-${pkgver}"
  install --verbose -D --mode=0644 BSDL COPYING --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_ruby-default-gems() {
  pkgdesc='Default gems which are part of Ruby StdLib'
  depends=("${_default_tool_gems[@]/#/ruby-}")

  cd "ruby-${pkgver}"
  install --verbose -D --mode=0644 BSDL COPYING --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_ruby-stdlib() {
  pkgdesc='Full Ruby StdLib including default gems, bundled gems and tools'
  depends=(ruby-default-gems ruby-bundled-gems)

  cd "ruby-${pkgver}"
  install --verbose -D --mode=0644 BSDL COPYING --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
