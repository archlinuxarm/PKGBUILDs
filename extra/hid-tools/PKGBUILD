# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on python-pypandoc

pkgname=hid-tools
pkgver=0.12
pkgrel=1
pkgdesc='Python scripts to manipulate HID data'
arch=('any')
url='https://gitlab.freedesktop.org/libevdev/hid-tools'
license=('GPL-2.0-or-later')
depends=(
  'python'
  'python-click'
  'python-libevdev'
  'python-parse'
  'python-pyroute2'
  'python-pyudev'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-sphinx'
  'python-sphinx_rtd_theme'
  'python-sphinxcontrib-apidoc'
)
checkdepends=(
  'python-attrs'
  'python-pytest'
)
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('deaf0b94b83de63c890bc70524ec39589d78b89b377f5aff2548d331ba274c3b7c7cf8c1152bb928775f2f2d7afec988e9249959edcfa2c8f764a7486e9d9081')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
  sphinx-build -b html doc/source _build
}

check() {
  cd $pkgname-$pkgver
  pytest \
    --deselect tests/test_cli_decode.py::TestHidRecorderOnUHIDDevice::test_pass_event_node \
    --deselect tests/test_cli_decode.py::TestHidRecorderOnUHIDDevice::test_sysfs_rdesc_match \
    --ignore tests_kernel
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vdm755 "$pkgdir/usr/share/doc/$pkgname/html"
  cp -va _build/* "$pkgdir/usr/share/doc/$pkgname/html"
}
