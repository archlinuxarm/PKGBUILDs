# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on python-pypandoc

pkgname=hid-tools
pkgver=0.4
pkgrel=1
arch=('any')
url='https://gitlab.freedesktop.org/libevdev/hid-tools'
license=('GPL2')
pkgdesc='Python scripts to manipulate HID data'
depends=('python-parse' 'python-pyudev' 'python-libevdev' 'python-click' 'python-yaml')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme' 'python-sphinxcontrib-apidoc')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1c98b6d3fe25e8c42f78524040f46b8770fac2e3b3a65425287d50eb9f6e63db1c25c297e73c8afed650dce35e69c47cee3416c6b00cb53e13ff04319e0ddcbc')

build() {
  cd $pkgname-$pkgver

  python setup.py build

  cd doc/source

  sphinx-build -b html . _build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root "$pkgdir" --skip-build -O1

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname/html
  cp -r -a --no-preserve=ownership doc/source/_build/* "$pkgdir"/usr/share/doc/$pkgname/html
}
