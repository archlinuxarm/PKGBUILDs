# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DZ3_LINK_TIME_OPTIMIZATION=OFF

pkgbase=z3
pkgname=(z3 python-z3-solver z3-java)
pkgver=4.15.4
pkgrel=1
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=(x86_64)
license=(MIT)
makedepends=(
  cmake
  gcc-libs
  git
  glibc
  ninja
  python
  python-build
  python-installer
  python-setuptools
  python-wheel
  jdk-openjdk
)
source=(
  git+https://github.com/Z3Prover/z3.git#tag=z3-$pkgver
  z3-python-do-not-rebuild.patch
)
sha512sums=('440ada0cc7c21e11ad2ffd6c8a72292eda23c8cd3f1dbb131f63dc16c7c5f98473d9489eb586b2edac94cd7fb13a5dfe6200fbe921bbbbf5bfc2fea3dd69946f'
            'da5dec288c60eb38d92756e2c3a3bc7f507f980f4beed0187c2da5b4c719134908168d9fd6ebac4a96076af362904e784b62a21b3baa49c36228bfe9ef617270')
b2sums=('c8c0458a0b2828f9c8a5909d8f5014e33e9b19923518e1f4bcb98d02da02ea9ef57391c95ba7dbd05168cd63a56c8a06c959bc70e6b6dfa2ee23766e7439f8d9'
        'b86f473fdd084f2324de9af52ab8c12e33c9e3c05d984108593b75e2e2af4256d3040082757619c8aca9125afc81e9eda9aa594142ff6f25829ed8ac2c6a35b8')

prepare() {
  cd z3
  patch -Np1 -i ../z3-python-do-not-rebuild.patch
}

build() {
  cd z3

  # /usr/bin/z3 uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cmake \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZ3_INCLUDE_GIT_DESCRIBE=OFF \
		-DZ3_INCLUDE_GIT_HASH=OFF \
    -DZ3_LINK_TIME_OPTIMIZATION=OFF \
    -DZ3_BUILD_PYTHON_BINDINGS=1 \
    -DZ3_BUILD_JAVA_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  cmake --build build
  cmake --build build --target test-z3

  cd src/api/python
  python -m build --wheel --no-isolation
}

check() {
  cd z3
  ./build/test-z3 -a
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv -v "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_z3() {
  depends=(
    gcc-libs
    glibc
  )
  provides=(
    libz3
    libz3.so
  )

  cd z3

  # use PEP517 installation instead of custom cmake dist
  pushd src/api/python
  python -m installer --destdir="$pkgdir/" dist/*.whl
  cd "$pkgdir"
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/z3/{include,lib}
  _pick python usr/lib/python*
  rm -r "${pkgdir}"/*
  popd

  DESTDIR="$pkgdir" cmake --install build
  rm -r "${pkgdir}"/usr/lib/python*/
  install -Dm644 "${srcdir}/z3/LICENSE.txt" -t "$pkgdir"/usr/share/licenses/$pkgname

  cd "$pkgdir"
  _pick java usr/lib/libz3java.so
  _pick java usr/share/java/com.microsoft.z3.jar
}

package_z3-java() {
  depends=(
    gcc-libs
    glibc
    java-runtime
    z3
  )
  mv java/* "$pkgdir"
  install -Dm644 z3/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_python-z3-solver() {
  depends=(
    python
    z3
  )
  replaces=(python-z3)
  conflicts=(python-z3)

  mv python/* "$pkgdir"
  install -Dm644 z3/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim: ts=2 sw=2 et:
