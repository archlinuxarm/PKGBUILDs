# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DZ3_LINK_TIME_OPTIMIZATION=OFF

pkgbase=z3
pkgname=(z3 python-z3-solver)
pkgver=4.15.1
pkgrel=1
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=(x86_64)
license=(MIT)
makedepends=(
  cmake
  gcc-libs
  git
  glibc
  ninja
  python
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(git+https://github.com/Z3Prover/z3.git#tag=z3-$pkgver)
sha512sums=('0fa6e5e7720bcd7aa3206f46e771ea93d13693015f87d56f98cc52530803622eeefbf4b393bcf73f8b2632663d1ed3b79fedad7dd32304ed23a0a42fe95135bc')
b2sums=('3d1643db55c86fdd5c3ac3e9264bb34e537307daf8782ed8c405292ea4a496109dd1279ba0a5e0620708585f1c207b4afef4a17a51a62bdd571c80a236e4bd9e')

build() {
  cd z3

  # /usr/bin/z3 uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZ3_LINK_TIME_OPTIMIZATION=OFF \
    -DZ3_BUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  ninja -C build
}

package_z3() {
  depends=(
    gcc-libs
    glibc
  )
  provides=(
    libz3
    libz3.so
  )

  cd z3
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  rm -r "$pkgdir"/usr/lib/python*
}

package_python-z3-solver() {
  depends=(
    python
    python-setuptools
    z3
  )
  replaces=(python-z3)
  conflicts=(python-z3)

  cd z3
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname

  cd "$pkgdir"
  rm -r usr/include usr/bin usr/lib/{cmake,pkgconfig,lib*}
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim: ts=2 sw=2 et:
