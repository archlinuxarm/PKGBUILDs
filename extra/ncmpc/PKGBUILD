# $Id: PKGBUILD 53963 2009-10-04 10:12:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.15
pkgrel=2
pkgdesc="A ncurses (command line) interface for MPD"
arch=("i686" "x86_64")
url="http://mpd.wikia.com/wiki/Client:Ncmpc"
license=('GPL')
depends=('ncurses' 'glib2')
optdepends=('python: to enable lyrics plugin'
	'ruby: to enable lyrics plugin')
source=("http://downloads.sourceforge.net/musicpd/$pkgname-$pkgver.tar.bz2")
md5sums=('806dcbb8f6f1a6ce1b60f51c2d94199d')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-lyrics-screen \
    --with-lyrics-plugin-dir=/usr/share/ncmpc/lyrics
  make || return 1
  make prefix=${pkgdir}/usr install
  install -d ${pkgdir}/etc/ncmpc
  install -m644 doc/{config.sample,keys.sample} \
    ${pkgdir}/etc/ncmpc || return 1
  install -d ${pkgdir}/usr/share/ncmpc/lyrics
  install -m755 lyrics/{01-hd.sh,02-lyricwiki.rb,03-leoslyrics.py} \
    ${pkgdir}/usr/share/ncmpc/lyrics || return 1
}
