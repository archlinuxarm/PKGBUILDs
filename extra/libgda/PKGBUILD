# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export java library path
#  - remove firebird package, makedepends on libfbclient

pkgname=(libgda libgda-{jdbc,mysql,postgres})
pkgver=5.2.9
pkgrel=4
pkgdesc="Database access library"
url="https://www.gnome-db.org/"
arch=(x86_64)
license=(GPL)
depends=(gtksourceview3 libxslt libsecret graphviz goocanvas iso-codes libgee openssl)
makedepends=(glade mariadb-libs postgresql-libs jdk8-openjdk intltool
             gobject-introspection gtk-doc vala itstool gnome-common git)
_commit=295fa78d29eee6dbbc5d07fd6884cb95f48d92e0  # tags/LIBGDA_5_2_9^0
source=("git+https://gitlab.gnome.org/GNOME/libgda.git#commit=$_commit"
        fix-crash.diff
        0001-libgda-report-remove-trml2pdf-trml2html-functionalit.patch)
sha256sums=('SKIP'
            '7eab1c7f5c11a87ce7a3e47c5c9058595f14c0b601daa6e0a32d797b708d97ba'
            'd74a70bd7b8a7750049b123c8fc8e38c9b9c85b51f63c061998cf06b3cfc65af')

_apiver=5.0

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^LIBGDA_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir providers

  [[ $CARCH == "aarch64" ]] && export LD_LIBRARY_PATH=/usr/lib/jvm/java-8-openjdk/jre/lib/aarch64/server:$LD_LIBRARY_PATH
  [[ $CARCH != "aarch64" ]] && export LD_LIBRARY_PATH=/usr/lib/jvm/java-8-openjdk/jre/lib/arm/server:$LD_LIBRARY_PATH

  cd $pkgname
  patch -Np1 -i ../fix-crash.diff
  # remove converter functionality that relies on eight-year-old python2 scripts/taken from Fedora
  patch -Np1 -i ../0001-libgda-report-remove-trml2pdf-trml2html-functionalit.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --enable-json --enable-system-sqlite --enable-binreloc \
      --enable-vala --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_libgda() {
  optdepends=('libgda-jdbc: provider for JDBC'
              'libgda-mysql: provider for MySQL'
              'libgda-postgres: provider for PostgreSQL')
  options+=(emptydirs)

  cd $pkgname
  make DESTDIR="$pkgdir" install

  mkdir -p providers
  local provider
  for provider in jdbc mysql postgres; do
    mv -f "$pkgdir"/usr/lib/libgda-$_apiver/providers/libgda-$provider[-.]*so \
       "$pkgdir"/usr/lib/pkgconfig/libgda-$provider-$_apiver.pc \
       "$pkgdir"/usr/share/libgda-$_apiver/${provider}_specs_*.xml \
       "$srcdir/providers"
  done

  mv "$pkgdir"/usr/lib/libgda-$_apiver/providers/gdaprovider-${_apiver}.jar \
     "$pkgdir"/usr/bin/gda-list-jdbc-providers-${_apiver} \
     "$srcdir/providers"

  # remove eight-year-old python2 report converter scripts
  rm -r "$pkgdir"/usr/share/libgda-5.0/gda_trml2{html,pdf}
}

_packageprovider() {
  install -d "$pkgdir"/usr/lib/{pkgconfig,libgda-$_apiver/providers} \
             "$pkgdir"/usr/share/libgda-$_apiver
  mv providers/libgda-${1}[-.]*so "$pkgdir/usr/lib/libgda-$_apiver/providers"
  mv providers/libgda-${1}-$_apiver.pc "$pkgdir/usr/lib/pkgconfig"
  mv providers/${1}_specs_*.xml "$pkgdir/usr/share/libgda-$_apiver"
}

package_libgda-jdbc() {
  pkgdesc="libgda provider for JDBC"
  depends=(libgda java-environment)
  _packageprovider jdbc
  install -d "$pkgdir/usr/bin"
  mv providers/gda-list-jdbc-providers-$_apiver "$pkgdir/usr/bin"
  mv providers/gdaprovider-$_apiver.jar "$pkgdir/usr/lib/libgda-$_apiver/providers"
}

package_libgda-mysql() {
  pkgdesc="libgda provider for MySQL"
  depends=(libgda mariadb-libs)
  _packageprovider mysql
}

package_libgda-postgres() {
  pkgdesc="libgda provider for PostgreSQL"
  depends=(libgda postgresql-libs)
  _packageprovider postgres
}
