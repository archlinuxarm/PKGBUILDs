# $Id: PKGBUILD 184890 2013-05-09 09:43:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch and cmake define to disable opengl for ARM

pkgname=kdeedu-kalgebra
pkgver=4.13.0
pkgrel=1
pkgdesc="Graph Calculator"
url="http://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'kdeedu-analitza')
makedepends=('cmake' 'automoc4' 'glu')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kalgebra-${pkgver}.tar.xz"
        "kalgebra-opengl_optional.patch")
sha1sums=('2c3da95dacf3379c8137f420755031b4269bff40'
          'ae1b75e5192f8519517134447b481421112a6dd7')

prepare() {
  cd "${srcdir}/kalgebra-${pkgver}"
  patch -p1 -i ../kalgebra-opengl_optional.patch

  cd "${srcdir}"
  mkdir build
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSHOULD_BUILD_OPENGL=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
