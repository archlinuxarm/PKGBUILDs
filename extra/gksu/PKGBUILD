# $Id: PKGBUILD 36944 2009-04-28 21:17:40Z hugo $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Eduardo Lopes (eduol@gmx.net)

pkgname=gksu	
pkgver=2.0.2
pkgrel=1
pkgdesc="A graphical frontend for su"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/gksu/index.html"
license=('GPL')
depends=('libgksu>=2.0.7')
makedepends=('gtk-doc' 'pkgconfig' 'nautilus>=2.26.0' 'intltool')
options=('!libtool')
source=(http://people.debian.org/~kov/gksu/${pkgname}-${pkgver}.tar.gz)
md5sums=('cacbcac3fc272dce01c6ea38354489e2')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i -e 's|/usr/bin/x-terminal-emulator|-l gnome-terminal|g' gksu.desktop || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
