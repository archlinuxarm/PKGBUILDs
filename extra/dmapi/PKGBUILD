# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from OpenSUSE to fix AArch64 FTBFS

pkgname=dmapi
pkgver=2.2.12
pkgrel=4
pkgdesc="Data migration API"
arch=('x86_64')
url="http://oss.sgi.com/projects/xfs/"
license=('GPL')
depends=('glibc')
makedepends=('xfsprogs')
provides=('xfsdmapi')
conflicts=('xfsdmapi')
replaces=('xfsdmapi')
#source=("ftp://oss.sgi.com/projects/xfs/previous/cmd_tars/$pkgname-$pkgver.tar.gz" dmapi-headers.patch)
source=(https://sources.archlinux.org/other/packages/dmapi/dmapi-$pkgver.tar.gz dmapi-headers.patch no-getdents-aarch64.diff)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../dmapi-headers.patch # Fix build (Gentoo)
  patch -p0 -i ../no-getdents-aarch64.diff
}

build() {
  cd dmapi-$pkgver
  [ "$CARCH" = "x86_64" ] && export CFLAGS="$CLFAGS -fPIC"
  export INSTALL_USER=root
  export INSTALL_GROUP=root
  export DEBUG=-DNDEBUG
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd dmapi-$pkgver
  make DIST_ROOT="$pkgdir" install install-dev
  # the static lib
  install -D -m644 libdm/.libs/libdm.a "$pkgdir"/usr/lib/libdm.a
  # and the header file, please
  install -D -m644 include/dmapi.h "$pkgdir"/usr/include/xfs/dmapi.h
  # kill libexec dir
  rm -rf "$pkgdir"/usr/libexec
}
md5sums=('cd825d4e141c16011367e0a0dd98c9c5'
         '6360c428bb89a1585669c3cf7a632208'
         '310881a0ff5b86deb311801257de1dc0')
