# $Id: PKGBUILD 44304 2009-07-01 14:15:55Z allan $
# Maintainer: dorphell <dorphell@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=gdk-pixbuf
pkgver=0.22.0
pkgrel=6
pkgdesc="Image loading and manipulation library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL' 'LGPL')
depends=('gtk' 'libtiff>=3.8.2-5' 'libpng')
makedepends=('libxt')
options=('!libtool')
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.22/${pkgname}-${pkgver}.tar.bz2
	gdk-pixbuf-0.22.0-bmp_reject_corrupt.patch
	gdk-pixbuf-0.22.0-bmp_secure.patch
	gdk-pixbuf-0.22.0-loaders.patch
        gdk-pixbuf-0.22.0.patch )
md5sums=('05fcb68ceaa338614ab650c775efc2f2' 'd1fb93f1ae994875158a7e0c108c36f8'\
         '5f59d5772b1482d885a180dbc581cf84' '3cf31ae0509747f72ac27a9fd96109c2'\
         'e0f5f301ce958b7cea0be631ed7b8e56')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export SED=/bin/sed
  patch -Np1 -i ${srcdir}/gdk-pixbuf-0.22.0-bmp_reject_corrupt.patch || return 1
  patch -Np0 -i ${srcdir}/gdk-pixbuf-0.22.0-bmp_secure.patch || return 1
  patch -Np1 -i ${srcdir}/gdk-pixbuf-0.22.0-loaders.patch || return 1
  patch -Np0 -i ${srcdir}/gdk-pixbuf-0.22.0.patch || return 1
  libtoolize --force --copy --automake || return 1
  autoreconf --force --install || return 1
  ./configure --prefix=/usr --disable-gtk-doc || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  rm -rf ${pkgdir}/usr/share/gnome
}

