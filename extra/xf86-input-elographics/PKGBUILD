# $Id: PKGBUILD 41698 2009-06-06 22:16:58Z jgc $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-elographics
pkgver=1.2.3
pkgrel=2
pkgdesc="X.org Elographics TouchScreen input driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('pkgconfig' 'xorg-server>=1.6.1' 'xorg-util-macros')
options=('!libtool')
groups=('xorg-input-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	git-c97c9f47.patch)
md5sums=('cc2923460c8eff9652b01889a063058d' '6ee7c814f57fc898f52cb765c3e50c31')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-c97c9f47.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
