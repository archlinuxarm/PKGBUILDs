# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>
# Contributor: Alessandro "jakedust" Andrioni <jakedust@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO via -DIPO_SUPPORTED=FALSE

pkgname=flint
pkgver=3.2.2
pkgrel=2
pkgdesc='A C library for doing number theory'
arch=(x86_64)
url='http://www.flintlib.org'
license=(LGPL-3.0-only)
depends=(cblas
         glibc
         gmp
         mpfr)
makedepends=(cmake
             git
             python)
replaces=(antic
          arb)
source=(git+https://github.com/flintlib/flint#tag=v$pkgver)
sha256sums=('63b499431b6456660714faa63627c71d2f435e881bf194cc215988a65ea9f02d')

prepare() {
  cd $pkgname
  git cherry-pick -n 77540110b4d27b0efe6b1fe849ddf5d9f47a9036 # Fix disabling AVX2
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_ARCH=NO \
    -DENABLE_AVX2=OFF \
    -DIPO_SUPPORTED=FALSE
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
