# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>
# Contributor: Alessandro "jakedust" Andrioni <jakedust@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO via -DIPO_SUPPORTED=FALSE

pkgname=flint
pkgver=3.0.1
pkgrel=2
pkgdesc='A C library for doing number theory'
arch=(x86_64)
url='http://www.flintlib.org'
license=(GPL)
depends=(cblas
         glibc
         gmp
         mpfr)
makedepends=(cmake
             python)
replaces=(antic
          arb)
source=(http://www.flintlib.org/flint-$pkgver.tar.gz
        https://github.com/flintlib/flint/commit/88bcb2ee.patch)
sha256sums=('7b311a00503a863881eb8177dbeb84322f29399f3d7d72f3b1a4c9ba1d5794b4'
            '2d21909660310d1651639f9dc941abc78f1b0f0cc8f13741fe6ef5c295548d88')

prepare() {
  patch -d $pkgname-$pkgver -p1 < 88bcb2ee.patch # Fix full version in config header
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DIPO_SUPPORTED=FALSE
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
