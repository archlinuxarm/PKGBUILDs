# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>
# Contributor: Alessandro "jakedust" Andrioni <jakedust@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO via -DIPO_SUPPORTED=FALSE

pkgname=flint
pkgver=3.3.1
pkgrel=1
pkgdesc='A C library for doing number theory'
arch=(x86_64)
url='http://www.flintlib.org'
license=(LGPL-3.0-only)
depends=(cblas
         glibc
         gmp
         mpfr)
makedepends=(cmake
             git
             python)
replaces=(antic
          arb)
source=(git+https://github.com/flintlib/flint#tag=v$pkgver)
sha256sums=('ec783fc0e2b6b2b1dc87590b5f5be2ce02bdc3bb20815b90a35a945561b4db82')

prepare() {
  cd $pkgname
  sed -e 's|NOT WIN32|FALSE|' -i CMakeLists.txt
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_ARCH=NO \
    -DENABLE_AVX2=OFF \
    -DBUILD_TESTING=ON \
    -DIPO_SUPPORTED=FALSE
  cmake --build build
}

check() {
  ctest --test-dir build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
