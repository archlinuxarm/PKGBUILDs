# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto via -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=OFF

pkgbase=opencc
pkgname=(opencc opencc-doc)
pkgver=1.1.9
pkgrel=3
pkgdesc="Library for Open Chinese Convert"
url="https://github.com/BYVoid/OpenCC"
arch=('x86_64')
license=('Apache-2.0')
makedepends=('git' 'chrpath' 'cmake' 'darts' 'doxygen' 'marisa' 'pybind11' 'python-setuptools'
             'python-wheel' 'rapidjson' 'tclap' 'gtest')
source=("git+https://github.com/BYVoid/OpenCC.git#tag=ver.$pkgver"
        8f3a5b4b201f091713cb4e2b1b5883a4b12d10b2.patch)
sha512sums=('0b84d51c530ea79d327c3e0cd7c0a5ed0dbb53a45826d4dfc96b6beaac1e3a023872670bc7a363b18f0481f08f6c12d51fa42991fae7f9471dd7342e47e00bd3'
            'febade61cc1734db891234484893128519dffc198f80883fc4838534684f91db2a21641252615820ae9bedffce52451535ea2717a40958b80ea82590850b90ac')

prepare() {
  cd OpenCC
  rm -r deps

  # as of opencc 1.1.8 there is no clean way to disable duplicated building of the clib again. plus, the installation is broken as well.
  # let's revert the offending commit for now.
  patch -Rp1 -i ../8f3a5b4b201f091713cb4e2b1b5883a4b12d10b2.patch

  git cherry-pick -n 72cae18cfe4272f2b11c9ec1c44d6af7907abcab # Fix build with GCC 15
  sed -e '/-std=c++14/d' -i CMakeLists.txt # Don't enforce old C++ standard
}

build() {
  cd OpenCC
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_DOCUMENTATION:BOOL=ON -DBUILD_PYTHON:BOOL=ON \
        -DUSE_SYSTEM_MARISA:BOOL=ON -DUSE_SYSTEM_PYBIND11:BOOL=ON -DUSE_SYSTEM_RAPIDJSON:BOOL=ON \
        -DUSE_SYSTEM_TCLAP:BOOL=ON -DUSE_SYSTEM_DARTS:BOOL=ON -DENABLE_GTEST:BOOL=ON \
        -DUSE_SYSTEM_GTEST:BOOL=ON -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=OFF
  make

  cp opencc_clib.*.so python/opencc/clib/
  python setup.py build
}

check() {
  cd OpenCC
  make test
}

package_opencc() {
  pkgdesc="Library for Open Chinese Convert"
  depends=('marisa')

  cd OpenCC
  make DESTDIR="$pkgdir" install
  python setup.py install --root="$pkgdir" --optimize=1

  # Hack to make opencc's python binding to use system opencc's configs
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  mkdir "$pkgdir"/usr/lib/"python$python_version"/site-packages/opencc/clib/share
  ln -s ../../../../../../share/opencc "$pkgdir"/usr/lib/"python$python_version"/site-packages/opencc/clib/share/

  # Remove insecure RPath
  chrpath --delete "$pkgdir"/usr/lib/"python$python_version"/site-packages/opencc/clib/*.so

  # Remove docs - install in split package
  rm -r "$pkgdir/usr/share/opencc/doc"
}

package_opencc-doc() {
  pkgdesc="Documentation for Library for Open Chinese Convert"

  cd OpenCC/doc
  make DESTDIR="$pkgdir" install
}
