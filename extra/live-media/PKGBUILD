# $Id: PKGBUILD 52919 2009-09-23 09:26:34Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2009.09.21
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=('gcc-libs')
source=(ftp://ftp.archlinux.org/other/live-media/live.${pkgver}.tar.gz)
md5sums=('1edc70c86bbbe7c6349f710295fe732c')

build()
{
  cd ${srcdir}/live
  sed -i 's/$(INCLUDES) -I. -O2 -DSOCKLEN_T/$(INCLUDES) -I. -O2 -I. -fPIC -DSOCKLEN_T/g' config.linux
  ./genMakefiles linux
  make || return 1

  for dir in BasicUsageEnvironment UsageEnvironment groupsock liveMedia; do
    mkdir -p ${pkgdir}/usr/lib/${pkgname}/${dir}
    cp -r ${dir}/*.a ${dir}/include ${pkgdir}/usr/lib/${pkgname}/${dir}
  done

  mkdir -p ${pkgdir}/usr/bin
  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} ${pkgdir}/usr/bin
  done
}
