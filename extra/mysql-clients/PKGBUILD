# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=mysql-clients
pkgver=5.1.38
pkgrel=1
pkgdesc="MySQL client tools"
arch=('arm')
depends=("libmysqlclient>=${pkgver}")
makedepends=('tcp_wrappers' 'libtool' 'gcc' 'readline>=6.0')
url=('http://www.mysql.com/')
options=('!libtool')
license=('GPL')
source=(ftp://mirror.switch.ch/mirror/mysql/Downloads/MySQL-5.1/mysql-$pkgver.tar.gz
        mysql-fix-libs.patch)

build() {
  cd $startdir/src/mysql-${pkgver}

  # fix build with gcc 4.4
  # see http://bugs.archlinux.org/task/15242
  # and http://gcc.gnu.org/bugzilla/show_bug.cgi?id=38562
  CFLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing"
  CXXFLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing"

  patch -Np1 -i ${srcdir}/mysql-fix-libs.patch || return 1
  
  ./configure --prefix=/usr --libexecdir=/usr/sbin \
  --localstatedir=/var --sysconfdir=/etc \
  --without-debug --without-docs --without-readline \
  --enable-local-infile --with-ssl \
  --with-charset=latin1 --with-collation=latin1_general_ci \
  --with-extra-charsets=complex --enable-thread-safe-client \
  --with-libwrap
 
  pushd include || return
  make || return 1
  popd

  pushd libmysql
  make link_sources get_password.lo || return
  popd 
  
  for dir in strings regex mysys dbug extra; do
    pushd ${dir} || return 1
    make || return 1
    popd
  done

  cd client

  sed -i -e 's|\$(top_builddir)/libmysql/libmysqlclient.la|/usr/lib/mysql/libmysqlclient.so|g' Makefile
  make link_sources
  make || return 1
  make DESTDIR=${startdir}/pkg install

  # Removing libmysql stuff
  rm -rf $pkgdir/usr/lib/
}

md5sums=('8ee9bb80079bbd09499a28bc57d902e7'
         'd1921e85821a66196c24ef6e5346b572')
