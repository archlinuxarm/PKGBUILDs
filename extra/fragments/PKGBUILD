# Maintainer: Balló György <ballogyor+arch at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable distcc, breaks meson finding miniupnpc

pkgname=fragments
pkgver=2.1.1
pkgrel=1
pkgdesc='BitTorrent client for GNOME'
arch=('x86_64')
url='https://gitlab.gnome.org/World/Fragments'
license=('GPL3')
options=(!distcc)
depends=('libadwaita' 'transmission-cli')
makedepends=('git' 'meson' 'rust')
_commit=dc622bfc78735190e5bbfa47dfd9c5a363ca48fe
source=("git+https://gitlab.gnome.org/World/Fragments.git#commit=$_commit")
sha256sums=('SKIP')
options=(!lto)

pkgver() {
  cd Fragments
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson Fragments build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
