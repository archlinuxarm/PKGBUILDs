# Maintainer: Balló György <ballogyor+arch at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable distcc, breaks meson finding miniupnpc

pkgname=fragments
pkgver=3.0.1
pkgrel=3
pkgdesc='BitTorrent client for GNOME'
arch=(x86_64)
url='https://apps.gnome.org/Fragments/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  transmission-cli
  zlib
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto !distcc)
source=("git+https://gitlab.gnome.org/World/Fragments.git#tag=$pkgver")
b2sums=(13afaddc26eab6e68199d0d1d8889c96bba394c5be63d641741d9994ae243e79e31ac243afc624b835cc575028723ecea556bfb419e0e01577429f6035f80c77)

prepare() {
  cd Fragments

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Fragments build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
