# $Id: PKGBUILD 281258 2016-11-18 20:36:27Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop avogadrolibs depend for now: GL-only

pkgname=kalzium
pkgver=17.04.3
pkgrel=2
pkgdesc="Periodic Table of Elements"
url="https://kde.org/applications/education/kalzium/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(khtml kdelibs4support knewstuff kplotting openbabel chemical-mime-data)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin python eigen ocaml facile)
conflicts=(kdeedu-kalzium)
replaces=(kdeedu-kalzium)
groups=(kde-applications kdeedu)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kalzium-ocaml-4.04.patch)
sha256sums=('8dfe48d336d746cfc2387d67c5bcb921f9ea4dea1c0231864f7dcd87c35a9d5f'
            'SKIP'
            '1bb03588d074331fb78d4647aa038353cc202b9eff20f6bfec95c9e8340dc83b')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

# Fix solver build with ocaml 4.04
  cd $pkgname-$pkgver
  patch -p2 -i ../kalzium-ocaml-4.04.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
