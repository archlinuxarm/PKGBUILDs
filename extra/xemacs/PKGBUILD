# $Id: PKGBUILD 53243 2009-09-27 13:36:15Z pierre $
# Maintainer: juergen <juergen@archlinux.org>

pkgname=xemacs
pkgver=21.5.28
pkgrel=10
pkgdesc="highly customizable open source text editor and application development system forked from GNU Emacs"
arch=(i686 x86_64)
url="http://www.xemacs.org/"
license=('GPL')
depends=('db>=4.8' 'libpng' 'libtiff' 'gpm>=1.20.4' 'bash' 'ncurses>=5.6-7' 'desktop-file-utils' 'libxaw' 'libjpeg>=7')
optdepends=('xorg-fonts-75dpi : X bitmap fonts needed for the interface'
            'xorg-fonts-100dpi: X bitmap fonts needed for the interface') 
makedepends=('giflib' 'xbitmaps')
install=xemacs.install
source=(http://ftp.xemacs.org/pub/xemacs/beta/$pkgname-$pkgver.tar.gz xemacs.desktop)
md5sums=('12e35715c5239c63651a8189973527ab' 'a4d3d5c0aa2c7ce7bec491f809ca3694')
       
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  infodir=/usr/share/info
  mandir=/usr/share/man/man1
  ./configure --build="${CHOST}" --prefix=/usr --with-dynamic \
      --with-x11 --without-postgresql --with-athena=xaw \
      --enable-database=berkdb --without-ldap \
      --enable-menubars=lucid --enable-scrollbars=lucid \
      --enable-widgets=athena --enable-dialogs=athena \
      --enable-external-widget \
      --with-jpeg --with-png --with-tiff \
      --with-ncurses --with-pop --with-xfs --disable-sound \
      --infodir=${infodir} \
      --mandir=${mandir} || return 1

  make || return 1
  make -j1 prefix="${pkgdir}/usr" mandir="${pkgdir}${mandir}" infodir="${pkgdir}${infodir}" install gzip-el || return 1

  rm ${pkgdir}/usr/bin/{b2m,ctags,etags,rcs-checkin}
  rm ${pkgdir}/usr/share/man/man1/{ctags.1,etags.1}

  # fix  FS#7927
  install -d -m755 "${pkgdir}/usr/share/pixmaps"
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/${pkgname}.desktop" ${pkgdir}/usr/share/applications/ || return 1
  ln -sf /usr/lib/xemacs-21.5-b28/etc/xemacs-icon.xpm "${pkgdir}/usr/share/pixmaps/" || return 1

  # correct permissions
  chown -R root:root "${pkgdir}"

  rm ${pkgdir}/usr/share/info/dir
  gzip ${pkgdir}/usr/share/info/*
  rm ${pkgdir}/usr/share/info/{info.info.gz,texinfo.info-2.gz,cl.info.gz,texinfo.info-1.gz,texinfo.info.gz,standards.info.gz,widget.info.gz}
}
# vim: ts=2 sw=2 et ft=sh
