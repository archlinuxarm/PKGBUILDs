# $Id: PKGBUILD 61559 2009-12-19 19:21:33Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

# NOTE!!!
# You'll need to edit your /usr/include/scsi/scsi.h:
# Change "u8" to "__u8" on lines 148 to 150-something

pkgname=hal
pkgver=0.5.14
pkgrel=1
pkgdesc="Hardware Abstraction Layer"
arch=(arm)
license=('GPL' 'custom')
url="http://www.freedesktop.org/wiki/Software/hal"
depends=('dbus-glib>=0.82' 'libusb>=0.1.12' 'udev>=146' 'filesystem>=0.7.1-5' 'hal-info>=0.20090716' 'eject' 'dmidecode' 'pciutils>=3.0.2' 'usbutils>=0.73-5' 'pm-utils>=1.2.5' 'consolekit>=0.4.1' 'util-linux-ng>=2.16')
makedepends=('pkgconfig' 'gperf')
options=('!libtool' '!makeflags')
install=hal.install
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
        hal
        cryptsetup_location.patch
        hal-0.5.9-hide-diagnostic.patch
        hal-remove-dell-killswitch.patch
        hal-KVM-evdev.patch
        hal-HDAPS-blacklist.patch
        hal-xen-unignore-axes.patch
        hal-use-at-console.patch
        fix-libusb-detection.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/cryptsetup_location.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-0.5.9-hide-diagnostic.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-remove-dell-killswitch.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-KVM-evdev.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-HDAPS-blacklist.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-xen-unignore-axes.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-use-at-console.patch" || return 1
  patch -Np1 -i "${srcdir}/fix-libusb-detection.patch" || return 1
  
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/hal --with-udev-prefix=/etc \
      --enable-static=no --disable-acpi-ibm \
      --disable-man-pages --disable-gtk-doc \
      --disable-docbook-docs --disable-console-kit \
      --disable-policy-kit --disable-acl-management \
      --enable-umount-helper --disable-smbios --disable-acpi \
      --with-hal-user=hal --with-hal-group=hal \
      --with-omap --enable-pmu --without-cpufreq \
      --without-usb-csr --without-keymaps \
      --disable-sonypic --without-dell-backlight \
      --with-pid-file=/var/run/hald.pid || return 1
  
  make || return 1
  
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 -d "${pkgdir}/media" || return 1
  install -m755 "${srcdir}/hal" "${pkgdir}/etc/rc.d/" || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
md5sums=('e9163df591a6f38f59fdbfe33e73bf20'
         '277e96ac130d7bfce0b30f0b80db8782'
         'c688a3c6574699365926f4fef7441545'
         '4d4b6801a1cedca22b8bdd9db73b16fb'
         '6d87c3e63184ae3a69caafc846f538a3'
         '6507e5091ee2d11a87ae738a8e2caecb'
         '1171c2d83b76059f2da7a3538e08fa4e'
         '52bd305299aa22ae07f1a862c22d30fa'
         '3f11234fb5e5044fbfc5199ec65b182c'
         'da088f12cfc2d190bbf95b747e19ab9e')
