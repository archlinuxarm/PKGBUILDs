# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

# ALARM: David Beauchamp <david@archlinuxarm.org>
#  - moved xorg-server-xvfb from checkdepends to makedepends

pkgname=girara
pkgver=2026.01.30
pkgrel=1
pkgdesc="User interface library focused on simplicity and minimalism"
url="https://pwmt.org/projects/girara"
arch=('x86_64')
license=('Zlib')
provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')
depends=('gtk3' 'glib2' 'json-glib' 'pango' 'glibc')
makedepends=('meson' 'ninja' 'doxygen' 'check' 'xorg-server-xvfb')
#checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('dc2ad3cdbba8ca71bc4bd4525f9a1d8d9bd3697178b5972515da3c0c6b98324e43e9d70067abb62d8577396f231c7079c308b2c3749208f428a3a1385175a43f')

build() {
        cd "${pkgname}-${pkgver}"
        arch-meson build
        ninja -C build
}

check() {
        cd "${pkgname}-${pkgver}"
        ninja -C build test
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
