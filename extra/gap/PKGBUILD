# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - rm -fr "$pkgdir"/usr/lib/gap/pkg/digraphs-*/bin/lib/ for aarch64
#  - strip -m32 flags

pkgbase=gap
pkgname=(gap
         gap-packages)
pkgver=4.15.0
pkgrel=4
pkgdesc='Groups, Algorithms, Programming: a system for computational discrete algebra'
arch=(x86_64)
url='https://www.gap-system.org/'
license=(GPL-2.0-or-later)
source=(git+https://github.com/gap-system/gap#tag=v$pkgver
        https://github.com/gap-system/gap/releases/download/v$pkgver/$pkgbase-$pkgver.tar.gz
        https://www.math.rwth-aachen.de/homes/Thomas.Breuer/atlasrep/atlasrepdata.tar.gz
        package-infos-$pkgver.json.gz::https://github.com/gap-system/gap/releases/download/v$pkgver/package-infos.json.gz
        atlasrep-no-remote-access.patch)
sha256sums=('56ae1557aa0030a4501b2553277ace47992a8e9329879a8b16701e31279bbfab'
            '93c7df97df1265c0f9ea6a9b5578b12289d5ab3aed2936cfd592710cfa73609e'
            '9f82d01bb682f24406f07a701dc2cae40dcba672c6ca91e77613b9096d6b49eb'
            'd2c7288670eeb6783fa4fe0f0a90071c21fa44e8b82554db980aa6da7124e17c'
            '1ffbed921a6f289340a1e61179f7f88039fa6dcd0247ab6b9b9270192ff5d015')
makedepends=(bliss
             boost
             c-xsc
             cddlib
             chrpath
             eigen
             fmt
             fplll
             git
             givaro
             jq
             libsemigroups
             libxaw
             mpfi
             normaliz
             planarity
             texlive-fontsrecommended
             texlive-latexextra
             wget
             zeromq)

prepare() {
  cd gap
  ln -s ../gap-$pkgver/pkg .
  git cherry-pick -n ad5dffcc97170f46c79acd1bf0a5bd284befc98b # Fix regressions in rational matrix groups
  git cherry-pick -n 9628fc7c1559a9bc2f036e89abb99eebaf2a816f # Fix performance regression
  autoreconf -fiv

# Use system normaliz
  rm pkg/normalizinterface/prerequisites.sh
# Use system nauty
  rm -r pkg/grape/{configure,Makefile*,nauty*}
# Fix https://bugs.archlinux.org/task/55174
  sed -e '/xgap/d' -i pkg/sonata/PackageInfo.g
  sed -e '/XGAP/d' -i pkg/cryst/PackageInfo.g
# Disable remote access in atlasdep, we preinstall all data (Gentoo)
  patch -d pkg/atlasrep -p1 < "$srcdir"/atlasrep-no-remote-access.patch
# Strip -m32 flags
  sed -e '/-m32/d' -i bin/BuildPackages.sh
}

build() {
  cd gap
  ./configure --prefix=/usr --with-gmp=system
  make
  make doc

  cd pkg
  export CFLAGS+=" -Wno-incompatible-pointer-types -std=gnu11" # Fix build with GCC 15
  ../bin/BuildPackages.sh --strict \
    --with-gaproot="$srcdir"/gap \
    --add-package-config-Semigroups --with-external-libsemigroups \
    --add-package-config-Digraphs "--with-external-planarity --with-external-bliss"
}

_standardpkgs=(gapdoc primgrp smallgrp transgrp atlasrep autpgrp alnuth crisp ctbllib factint fga irredsol laguna
               polenta polycyclic radiroot resclasses sophus tomlib utils autodoc)

package_gap() {
  depends=(glibc
           gmp
           perl
           readline
           sh
           zlib)
  optdepends=('gap-packages: extra packages')
  conflicts=(gap-doc)
  provides=(gap-doc)
  replaces=(gap-doc)

  cd gap
  make DESTDIR="$pkgdir" install

  for _pkg in ${_standardpkgs[@]}; do
    cp -r pkg/$_pkg "$pkgdir"/usr/share/gap/pkg
  done

# Add provides for bundled packages
  for _pkg in $(ls "$pkgdir"/usr/share/gap/pkg); do
    _ver=$(jq .[\"${_pkg}\"].Version "$srcdir"/package-infos-$pkgver.json | sed -e 's/"//g')
    _prov="gap-$_pkg=${_ver/-/.}"
    provides+=($_prov)
  done

# Install atlasrep data
  for _dir in datagens dataword; do
    rm -r "$pkgdir"/usr/share/gap/pkg/atlasrep/$_dir
    mv "$srcdir"/atlasrep/$_dir "$pkgdir"/usr/share/gap/pkg/atlasrep
  done
}

package_gap-packages() {
  depends=(gap
           gcc-libs
           glibc
           perl
           sh)
  optdepends=('bliss: digraph package'
              'c-xsc: float package'
              'cddlib: CddLib interface package'
              'curl: curl interface package'
              'fplll: float package'
              'libmpc: float package'
              'libsemigroups: semigroups package'
              'libxaw: xgap package'
              'mpfi: float package'
              'nauty: grape package'
              'normaliz: NormalizInterface package' 
              'pari: alnuth package'
              'planarity: digraph package'
              'polymake: polymaking package'
              'singular: singular package'
              'zeromq: ZeroMQ interface package')
  pkgdesc='Extra packages for GAP'
  cd gap-$pkgver

  mkdir -p "$pkgdir"/usr/{bin,lib/gap}
  cp -r pkg "$pkgdir"/usr/lib/gap
  chmod 755 "$pkgdir"/usr/lib/gap/pkg

# fix xgap launch script
  sed -e "s|/build/gap/src/gap-$pkgver|/usr/lib/gap|g" -e 's|^GAP=.*|GAP=/usr/lib/gap/gap|g' \
    "$pkgdir"/usr/lib/gap/pkg/xgap/xgap.sh > "$pkgdir"/usr/bin/xgap
  chmod 755 "$pkgdir"/usr/bin/xgap
  rm "$pkgdir"/usr/lib/gap/pkg/xgap/xgap.sh

# provided by main gap package
  for _pkg in ${_standardpkgs[@]}; do
    rm -r "$pkgdir"/usr/lib/gap/pkg/$_pkg
  done

# fix RPATH
  find "$pkgdir"/usr/lib/gap/pkg/ -name '*.so' | xargs chrpath -d

# use system nauty in grape package
  install -d "$pkgdir"/usr/lib/gap/pkg/grape/bin/x86_64-pc-linux-gnu-default64-kv8
  ln -s /usr/bin/dreadnaut "$pkgdir"/usr/lib/gap/pkg/grape/bin/x86_64-pc-linux-gnu-default64-kv8

# remove leftover binaries and source files
  find "$pkgdir"/usr/lib/gap/pkg -name .libs -o -name '*.o' | xargs rm -fr
  find "$pkgdir"/usr/lib/gap/pkg -type d -name src | xargs rm -fr
  rm -r "$pkgdir"/usr/lib/gap/pkg/digraphs/extern
  rm -r "$pkgdir"/usr/lib/gap/pkg/semigroups/libsemigroups
  rm -r "$pkgdir"/usr/lib/gap/pkg/caratinterface/carat*
  rm -r "$pkgdir"/usr/lib/gap/pkg/kbmag/standalone
  rm -r "$pkgdir"/usr/lib/gap/pkg/log
  rm -r "$pkgdir"/usr/lib/gap/pkg/*/gen

# Add provides for bundled packages
  for _pkg in $(ls "$pkgdir"/usr/lib/gap/pkg); do
    _ver=$(jq .[\"${_pkg}\"].Version "$srcdir"/package-infos-$pkgver.json | sed -e 's/"//g')
    _prov="gap-$_pkg=${_ver/-/.}"
    provides+=($_prov)
  done
}
