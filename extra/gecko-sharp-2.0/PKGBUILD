# $Id: PKGBUILD 44687 2009-07-03 17:57:59Z jgc $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=gecko-sharp-2.0
pkgver=0.13
pkgrel=3
pkgdesc="C# bindings for gtkmozembed"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://www.mono-project.com/"
depends=('xulrunner>=1.9.1' 'gtk-sharp-2>=2.12.1')
makedepends=('monodoc' 'pkgconfig')
source=(http://go-mono.com/sources/gecko-sharp-2.0/gecko-sharp-2.0-${pkgver}.tar.bz2
	gecko-sharp-2.0-0.12-xulrunner.patch)
md5sums=('f88eaa06e71f8d8fa34cf59a3e034a6b'
	 '4f2c07c3fb0c1a94adaa4e4c8d57608b')

build() {
  # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="${startdir}/src/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  # build
  cd ${srcdir}/gecko-sharp-2.0-${pkgver}
  patch -Np1 -i ${srcdir}/gecko-sharp-2.0-0.12-xulrunner.patch || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make GACUTIL="/usr/bin/gacutil /root ${startdir}/pkg/usr/lib" \
    DESTDIR=${pkgdir} install || return 1
}
