# Maintainer: Sébastien Luttringer
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Mario Vazquez <mario_vazq@hotmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - !makeflags - bind build doesn't like concurrency

pkgname=bind
pkgver=9.20.4
pkgrel=2
pkgdesc='A complete, highly portable implementation of the DNS protocol'
url='https://www.isc.org/software/bind/'
license=('MPL2')
arch=('x86_64')
options=('!emptydirs' '!makeflags')
depends=('bash' 'dnssec-anchors' 'e2fsprogs' 'glibc' 'icu' 'libedit' 'json-c'
  'krb5' 'libcap' 'libidn2' 'libmaxminddb' 'libnsl' 'libuv' 'libxml2' 'lmdb'
  'openssl' 'readline' 'xz' 'zlib' 'jemalloc' 'libnghttp2' 'liburcu')
makedepends=('git' 'python-sphinx')
conflicts=('bind-tools' 'dnsutils')
replaces=('bind-tools' 'dnsutils' 'host')
provides=('bind-tools' 'dnsutils' 'dns-server')
backup=('etc/named.conf'
        'var/named/127.0.0.zone'
        'var/named/localhost.zone'
        'var/named/localhost.ip6.zone')
install=bind.install
source=("git+https://gitlab.isc.org/isc-projects/bind9.git#tag=v${pkgver}"
        'tmpfiles.conf'
        'sysusers.conf'
        'named.conf'
        'named.service'
        'localhost.zone'
        'localhost.ip6.zone'
        '127.0.0.zone')
sha256sums=('9581ea49349a127d3e9668e8a4c34928fbef4cd0de2cbcaeabe98c35564509a0'
            'f0423c4ee8495da487e07e9144bec1d25f46a0cd2dfa7cfd7a761ef15bfefc98'
            'c968019a5f040a4a8f9ed13d82d4ba597652224b42f7cff123d8125af2c5108c'
            'e08a01d41b18bdb771d534daca99642314939aafdb088e5cfcf0ef2d33f8e7eb'
            '3f0f8db0a1deae270dd166b4750be7c1041b4b44891176f35a8df7dd55d24d34'
            '0011708e516128647dd25b59b6ebc465f36e85bbe0a8fbdc3eb04b7f28c2197f'
            'c06fc270e32a843c8b6d86335a2ec607d405dfba6875de8d8a9abde39a9e2c17'
            'b88fd2b99e7d42d414b329b814b9ff3304fa0ef9c67df81bde235bbfa0f3a3b8')
#validpgpkeys=('D99CCEAF879747014F038D63182E23579462EFAA'  # Michal Nowak
#              '706B6C28620E76F91D11F7DF510A642A06C52CEC') # Michał Kępień

prepare() {
  cd bind9
  autoreconf -fiv
}

build() {
  cd bind9
  export CFLAGS
  # support to chase DNSSEC signature chains
  CFLAGS+=' -DDIG_SIGCHASE'
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --disable-static \
    --enable-fixed-rrset \
    --enable-full-report \
    --with-maxminddb \
    --with-openssl \
    --with-libidn2 \
    --with-json-c \
    --with-libxml2 \
    --with-lmdb
  make
}

# No test suite because it requires root.
# See: https://gitlab.isc.org/isc-projects/bind9/-/blob/main/README.md#testing.

package() {
  cd bind9
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 LICENSE COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/"
  make DESTDIR="$pkgdir" install

  cd "$srcdir"
  install -D -m644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -D -m644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  install -D -m644 named.service "$pkgdir/usr/lib/systemd/system/named.service"
  install -D -m640 -o 0 -g 40 named.conf "$pkgdir/etc/named.conf"

  install -d -m770 -o 0 -g 40 "$pkgdir/var/named"
  install -m640 -o 0 -g 40 localhost.zone "$pkgdir/var/named"
  install -m640 -o 0 -g 40 localhost.ip6.zone "$pkgdir/var/named"
  install -m640 -o 0 -g 40 127.0.0.zone "$pkgdir/var/named"
}

# vim:set ts=2 sw=2 et:
