# $Id: PKGBUILD 42087 2009-06-10 20:03:48Z andrea $
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Jason Chu <jason@archlinux.org>

pkgname=dgen-sdl
pkgver=1.23
pkgrel=23
pkgdesc=" DGen/SDL is a free, open source emulator for Sega Genesis/MegaDrive systems"
arch=(i686 x86_64)
url="http://tamentis.com/projects/dgen/"
license=('custom')
depends=('sdl' 'nasm')
options=('!makeflags')
source=(http://tamentis.com/projects/dgen/files/$pkgname-$pkgver.tar.gz gcc4-dgen-sdl-1.23.patch dgen-permission \
        fm-do-not-use-long.patch mz80-pbPC-pointer-subtract.patch mz80-stdint.patch mz80init.patch \
        ras-emulate-pointer-wraparound.patch sdl-use-stdint.patch)
md5sums=('b1896c1b21ddb152626aec2e8a157a3a' '34cc77565fc8926c05e9f89f93ea2688'\
         '92e51193d1ed7e7e200b83f116e85a33' '013b040833696fa8ca48d5fba090e957'\
         '9ae6adbf7223f072d82ce109407aa342' '1d09f336238d596d3667ccea4aa70dfc'\
         '590a85e8b3942f6a8a711971fcc8f696' '9dbf5c24b305f06a1361a1193078d10e'\
         'ddaf65edfa87ff78a4bbdf4498b210f2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../gcc4-dgen-sdl-1.23.patch || return 1
  patch -p1 < ../fm-do-not-use-long.patch || return 1
  patch -p1 < ../mz80-pbPC-pointer-subtract.patch || return 1
  patch -p1 < ../mz80-stdint.patch || return 1
  patch -p1 < ../mz80init.patch || return 1
  patch -p1 < ../ras-emulate-pointer-wraparound.patch || return 1
  patch -p1 < ../sdl-use-stdint.patch || return 1

  ./configure --prefix=/usr --mandir=/usr/share/man --without-extra-opt || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 star/stardoc.txt "${pkgdir}/usr/share/licenses/${pkgname}/stardoc.txt"
  install -D -m644 mz80/mz80.txt "${pkgdir}/usr/share/licenses/${pkgname}/mz80.txt"
  install -D -m644 "$srcdir/dgen-permission" "${pkgdir}/usr/share/licenses/${pkgname}/dgen-permission"
}
