# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - source changes for ARM

pkgname=swt
# http://download.eclipse.org/eclipse/downloads/drops4
pkgver=4.5
pkgrel=1.1
_date=201506032000
pkgdesc='An open source widget toolkit for Java'
arch=('i686' 'x86_64')
url='http://www.eclipse.org/swt/'
license=('EPL')
depends=('java-runtime>=6' 'gtk2>=2.20.1' 'libxtst')
optdepends=('libgnomeui' 'glu' 'webkitgtk2')
makedepends=('java-environment' 'libxtst' 'glu' 'libgnomeui' 'unzip' 'pkgconfig' 'webkitgtk2' 'apache-ant')
source=(build-swt.xml)
sha256sums=('6bb48007a95e3d8c6b577cc9cc4b61a51ce928b04f4fcd393cf72f8f727fe923')

if [[ $CARCH == "aarch64" ]]; then
  source+=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86_64.zip)
  sha256sums+=('ce89cb9dea5bef927873cbc377a81b5d9f1995622e42402bd9600043a9bc00e3')
else
  source+=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip)
  sha256sums+=('180cf993af914726e6da9abe669a636b057e9353058e76be7644b31955feb00e')
fi

# These examples don't even run anymore (Try Tux Guitar instead)!
# http://www.eclipse.org/swt/examples.php#standaloneOutsideEclipse
# http://download.eclipse.org/eclipse/downloads/

# http://mirror.ibcp.fr/pub/eclipse/eclipse/downloads/drops4/R-4.4-201406061215/org.eclipse.sdk.examples.source-4.4.zip

build() {
  cd ${srcdir}
  unzip -oq src.zip -d src

  export JAVA_HOME=/usr/lib/jvm/default
  . /etc/profile.d/apache-ant.sh

  # Shared objects
  cd src
  ./build.sh

  # SWT jar
  ant -f ../build-swt.xml compile
}

package() {
  cd ${srcdir}/src

  # Shared objects
  export OUTPUT_DIR=${pkgdir}/usr/lib
  install -dm755 ${OUTPUT_DIR} 
  make -f make_linux.mak install

  # SWT jar
  ant -f ../build-swt.xml jar
  install -Dm755 ../swt.jar ${pkgdir}/usr/share/java/swt-${pkgver}.jar
  ln -s swt-${pkgver}.jar ${pkgdir}/usr/share/java/swt.jar
}
