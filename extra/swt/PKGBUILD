# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

# ALARM: Thomas Laroche <tho.laroche@gmail.com>
#  - removed download choice based on architecture, this is always recompiled from source

pkgname=swt
pkgver=4.3.2
pkgrel=1
_date=201402211700
pkgdesc='An open source widget toolkit for Java'
arch=('i686' 'x86_64')
url='http://www.eclipse.org/swt/'
license=('EPL')
depends=('java-runtime>=6' 'gtk2>=2.20.1' 'libxtst')
optdepends=('libgnomeui' 'glu' 'webkitgtk2')
makedepends=('java-environment' 'libxtst' 'glu' 'libgnomeui' 'unzip' 'pkgconfig' 'webkitgtk2' 'apache-ant')
sha256sums=('49c8d6252c0027ad430a9dc5e19c99a392653e6f98ea8183d633114efca67013'
              '6bb48007a95e3d8c6b577cc9cc4b61a51ce928b04f4fcd393cf72f8f727fe923')
source=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip
        build-swt.xml)
# To test this pkg:
# http://www.eclipse.org/swt/examples.php#standaloneOutsideEclipse
# http://download.eclipse.org/eclipse/downloads/

build() {
  cd ${srcdir}
  unzip -oq src.zip -d src

  . /etc/profile.d/jdk.sh
  . /etc/profile.d/apache-ant.sh

  # Shared objects
  cd src
  ./build.sh

  # SWT jar
  ant -f ../build-swt.xml compile
}

package() {
  cd ${srcdir}/src

  # Shared objects
  export OUTPUT_DIR=${pkgdir}/usr/lib
  install -dm755 ${OUTPUT_DIR} 
  make -f make_linux.mak install

  # SWT jar
  ant -f ../build-swt.xml jar
  install -Dm755 ../swt.jar ${pkgdir}/usr/share/java/swt-${pkgver}.jar
  ln -s swt-${pkgver}.jar ${pkgdir}/usr/share/java/swt.jar
}
