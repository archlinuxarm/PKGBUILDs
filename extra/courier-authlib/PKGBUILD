# $Id: PKGBUILD 53167 2009-09-26 09:48:47Z allan $
# Maintainer:  tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=courier-authlib
pkgver=0.62.4
pkgrel=4
pkgdesc="Authentification library for the courier mailserver(s)"
arch=(i686 x86_64)
license=('GPL2')
url="http://courier-mta.org/authlib/"
backup=('etc/authlib/authdaemonrc' 'etc/authlib/authldaprc' \
        'etc/authlib/authmysqlrc' 'etc/authlib/authpgsqlrc')
depends=('openssl' 'db>=4.8' 'perl' 'libtool' 'expect')
makedepends=('pam' 'expect' 'libldap' 'libmysqlclient' 'postgresql-libs>=8.3.0')
optdepends=('libmysqlclient' 'libldap' 'postgresql-libs')
conflicts=('courier-imap-mysql' 'courier-imap-pgsql' 'courier-imap-ldap')
provides=('courier-imap-mysql' 'courier-imap-pgsql' 'courier-imap-ldap')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/project/courier/authlib/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        authdaemond.rc.d)
md5sums=('40b01d7f19c29db7c919c0938a7537c7'
         '911ee9f40d70fafc6bb4cc636c5ad531')

build() {
  export MAKEFLAGS="-j1"
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --with-db=db \
    --with-mailuser=courier --with-mailgroup=courier \
    --with-authpam --with-authpwd --with-authshadow \
    --with-authldap --with-authmysql --with-authpgsql \
    --with-authuserdb --with-authcram --with-authdaemon
  make || return 1
  make DESTDIR=${pkgdir} install
 ###############################################################################
 # post_installation ---- rename the config file and change ownerschip
  for distfile in ${pkgdir}/etc/authlib/*.dist; do
    chown 72:72 ${distfile}
    mv ${distfile} ${pkgdir}/etc/authlib/`basename ${distfile} .dist`
  done
   # copy the .schema; mostly refered to as courier.schema -> rename it
  install -Dm 444 authldap.schema \
    ${pkgdir}/etc/openldap/schema/courier.schema
 ###############################################################################
 # Install daemon, that wraps couriers bashscript
  install -Dm 755 ${srcdir}/authdaemond.rc.d ${pkgdir}/etc/rc.d/authdaemond
  chown -R 72:72 ${pkgdir}/var/spool/authdaemon
  mkdir ${pkgdir}/var/spool/courier
  chown -R 72:72 ${pkgdir}/var/spool/courier
  # docs say we can remove .a files after make
  find ${pkgdir} -name '*\.a' -exec rm -f {} \;
}
