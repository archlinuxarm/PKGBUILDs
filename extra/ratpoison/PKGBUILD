# $Id: PKGBUILD 43148 2009-06-20 09:46:06Z allan $
# Maintainer: juergen <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ratpoison
pkgver=1.4.3
pkgrel=3
pkgdesc="A simple keystroke-driven window manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libxinerama' 'readline' 'bash' 'perl' 'libxtst')
url="http://www.nongnu.org/ratpoison/"
source=(http://savannah.nongnu.org/download/ratpoison/$pkgname-$pkgver.tar.gz
        $pkgname.desktop)
md5sums=('c1899e3e2549d29825cdb1b8d042c836'
         '29c3cb9be59758e39d8471391231a74a')

build() {
  cd $srcdir/${pkgname}-${pkgver}

  # fix timestamp issue
  find . -exec touch \{\} \;
  
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=$pkgdir install

  # fix permissions
  chmod a+x $pkgdir/usr/share/ratpoison/{allwindows.sh,clickframe.pl,rpshowall.sh,rpws,split.sh}

  cd contrib
  ./genrpbindings || return 1
  install -dm755 $pkgdir/usr/share/ratpoison/bindings
  install -m644 {Ratpoison.pm,ratpoison-cmd.el,ratpoison.rb,ratpoison.lisp,ratpoison.py} \
			$pkgdir/usr/share/ratpoison/bindings/ || return 1
        
  install -Dm755 $srcdir/$pkgname.desktop $pkgdir/etc/X11/sessions/$pkgname.desktop
  rm ${pkgdir}/usr/share/info/dir
}

# vim: ts=2 sw=2 et ft=sh
