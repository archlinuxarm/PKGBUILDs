# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Philipp Cla√üen <philipp.classen@posteo.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBENCHMARK_ENABLE_LTO=OFF

pkgname=benchmark
pkgver=1.8.4
pkgrel=1
pkgdesc="A microbenchmark support library"
arch=('x86_64')
url="https://github.com/google/benchmark"
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cmake')

source=("https://github.com/google/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "https://patch-diff.githubusercontent.com/raw/google/benchmark/pull/1793.patch")
sha256sums=('3e7059b6b11fb1bbe28e33e02519398ca94c1818874ebed18e504dc6f709be45'
            'a064ca14bf086113c0eaae21a4f59f39e43e6291554b6d4985388e9553675bbb')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../1793.patch
  mkdir -p build
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"

  cmake \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG -Wno-error=restrict" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DBENCHMARK_ENABLE_LTO=OFF \
    -DBENCHMARK_ENABLE_GTEST_TESTS=OFF \
    ..

  make VERBOSE=1
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
}
