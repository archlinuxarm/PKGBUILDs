# $Id: PKGBUILD 50349 2009-08-25 18:25:29Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=('gstreamer0.10-base')
pkgname=('gstreamer0.10-base' 'gstreamer0.10-base-plugins')
pkgver=0.10.24
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer0.10>=0.10.24' 'liboil>=0.3.16' 'libsm' 'libxv' 'alsa-lib>=1.0.20' 'cdparanoia>=10.2' 'libvisual>=0.4.0' 'libvorbis>=1.2.1rc1' 'libtheora>=1.0' 'pango>=1.24.5' 'udev>=145')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/gst-plugins-base/gst-plugins-base-${pkgver}.tar.bz2)
md5sums=('a3ec977c9c6602caacc2d3a7c12a17bf')

build() {
  cd "${srcdir}/gst-plugins-base-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --disable-gnome_vfs \
    --with-package-name="GStreamer Base Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  make || return 1
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile || return 1
}

package_gstreamer0.10-base() {
  pkgdesc="GStreamer Multimedia Framework Base plugin libraries"
  depends=('gstreamer0.10>=0.10.24' 'liboil>=0.3.16' 'libsm' 'libxv' 'udev>=145')

  cd "${srcdir}/gst-plugins-base-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}

package_gstreamer0.10-base-plugins() {
  pkgdesc="GStreamer Multimedia Framework Base Plugins (gst-plugins-base)"
  depends=("gstreamer0.10-base=${pkgver}" 'alsa-lib>=1.0.20' 'cdparanoia>=10.2' 'libvisual>=0.4.0' 'libvorbis>=1.2.1rc1' 'libtheora>=1.0' 'pango>=1.24.5')
  replaces=('gstreamer0.10-alsa' 'gstreamer0.10-theora' 'gstreamer0.10-libvisual' 'gstreamer0.10-pango' 'gstreamer0.10-cdparanoia' 'gstreamer0.10-vorbis' 'gstreamer0.10-ogg')
  conflicts=('gstreamer0.10-alsa' 'gstreamer0.10-theora' 'gstreamer0.10-libvisual' 'gstreamer0.10-pango' 'gstreamer0.10-cdparanoia' 'gstreamer0.10-vorbis' 'gstreamer0.10-ogg')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-base-${pkgver}"
  make -C gst-libs DESTDIR="${pkgdir}" install || return 1
  make -C ext DESTDIR="${pkgdir}" install || return 1
  make -C gst-libs DESTDIR="${pkgdir}" uninstall || return 1
}
