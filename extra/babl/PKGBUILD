# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - revert to configure/make, meson build not ARM friendly

pkgname=babl
pkgver=0.1.44
pkgrel=2
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=(x86_64)
url="http://gegl.org/babl/"
license=(LGPL3)
depends=(glibc)
makedepends=(git meson)
_commit=9e4bfe9f1470a7836f7f885ea95607427ed7997f  # tags/BABL_0_1_44^0
source=("git+https://git.gnome.org/browse/babl#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BABL_//;s/_/./g;s/-/+/g'
}

build() {
  cd "${srcdir}/$pkgname"
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "${srcdir}/$pkgname"
  make check
}

package() {
  cd "${srcdir}/$pkgname"
  make DESTDIR="${pkgdir}" install
}
