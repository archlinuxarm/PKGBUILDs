# $Id: PKGBUILD 135252 2011-08-11 16:55:55Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com> Ben <ben@benmazer.net>
# Contributor: macau <tema.prokopenko@gmail.com> //prepare for arm build

plugrel=1
pkgbase=pyqt
pkgname=('pyqt' 'python2-pyqt')
pkgver=4.8.5
pkgrel=1
arch=('i686' 'x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
makedepends=('qt' 'python-sip' 'dbus-python' 'python2-sip' 'phonon'
             'python-opengl' 'qt-assistant-compat')
source=("http://riverbankcomputing.co.uk/static/Downloads/PyQt4/PyQt-x11-gpl-${pkgver}.tar.gz" PyQt4-4.7.3-qreal_float_support.patch)
md5sums=('0e4264bb912edfbda319bb236ac84407'
'a20d7022e91071f838bd4908851a0f7b')  

build() {
  cd "${srcdir}"
  cp -r PyQt-x11-gpl-${pkgver} Py2Qt-x11-gpl-${pkgver}

  cd "${srcdir}/PyQt-x11-gpl-${pkgver}"

if [ "$CARCH" = "armv7h" ]; then
  patch -Np1 -i ${srcdir}/PyQt4-4.7.3-qreal_float_support.patch 
fi
  python configure.py \
    --confirm-license \
    --qsci-api

  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make

  ### Python2 version ###
  cd "${srcdir}/Py2Qt-x11-gpl-${pkgver}"
  
  if [ "$CARCH" = "armv7h" ]; then
  patch -Np1 -i ${srcdir}/PyQt4-4.7.3-qreal_float_support.patch 
  fi
  
  python2 configure.py \
    --confirm-license \
    -v /usr/share/sip \
    --qsci-api

  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package_pyqt(){
  pkgdesc="A set of Python bindings for the Qt toolkit"
  depends=('qt' 'python-sip')
  optdepends=('phonon: enable audio and video in PyQt applications'
              'qscintilla: QScintilla API'
              'qt-assistant-compat: add PyQt online help in Qt Assistant')
  replaces=('python-qt')
  provides=('python-qt')
  
  cd "${srcdir}/PyQt-x11-gpl-${pkgver}"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

package_python2-pyqt(){
  pkgdesc="PyQt: A set of Python2 bindings for the Qt toolkit"
  depends=('pyqt' 'python2-sip' 'dbus-python')
  optdepends=('phonon: enable audio and video in PyQt applications'
              'python-opengl: enable OpenGL 3D graphics in PyQt applications'
              'qscintilla: QScintilla API'
              'qt-assistant-compat: add PyQt online help in Qt Assistant')
  replaces=('python2-qt')
  provides=('python2-qt')

  cd "${srcdir}/Py2Qt-x11-gpl-${pkgver}"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  # Provided by pyqt
  rm ${pkgdir}/usr/bin/{pylupdate4,pyrcc4,pyuic4}
  rm ${pkgdir}/usr/lib/qt/plugins/designer/libpythonplugin.so
  rm ${pkgdir}/usr/share/qt/qsci/api/python/PyQt4.api
}

