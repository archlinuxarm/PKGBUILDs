# $Id: PKGBUILD 50814 2009-09-01 16:27:10Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=junit
pkgver=4.7
pkgrel=1
pkgdesc="Automated testing framework for Java"
arch=('i686' 'x86_64')
url="http://www.junit.org/"
license=('custom')
depends=('java-runtime')
makedepends=('fastjar' 'apache-ant')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}${pkgver}.zip
	build.xml)
md5sums=('fa74a6bb1ff143cc2baa14f5a22e0e85'
         'd8bd6d5961386ce602241ed7f87dd5d2')

build() {
  cd ${srcdir}/${pkgname}${pkgver}
  jar xf ${pkgname}-${pkgver}-src.jar || return 1
  rm -f ${pkgname}-${pkgver}-src.jar
  cp ${srcdir}/build.xml . || return 1
  find . -name \*.class -delete || return 1
  find . -name \*.jar -delete || return 1

  ant dist || return 1
  install -d ${pkgdir}/usr/share/java ${pkgdir}/usr/lib
  install -m644 ${pkgname}4.4/junit.jar ${pkgdir}/usr/share/java || return 1
  # Do this for ant, though ant needs to have these files in /usr/share/java, but that will get fixed later.
  ln -sf ../share/java/junit.jar ${pkgdir}/usr/lib/junit.jar || return 1
  install -D cpl-v10.html ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
