# $Id: PKGBUILD 42359 2009-06-13 12:27:05Z jgc $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=nspr
pkgver=4.8
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=(i686 x86_64)
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=(!emptydirs)
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz
	nspr.pc.in)
md5sums=('e0916a72bcc6c427551ea262183fdb26'
         'bce1611f3117b53fc904cab549c09967')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "$CARCH" = "x86_64" ] && confflags="--enable-64bit"
  unset CFLAGS
  unset CXXFLAGS
  ./mozilla/nsprpub/configure \
  	--prefix=/usr \
	--libdir=/usr/lib \
	--includedir=/usr/include/nspr \
	--enable-optimize \
	--disable-debug ${confflags} || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  NSPR_LIBS=`./config/nspr-config --libs`
  NSPR_CFLAGS=`./config/nspr-config --cflags`
  NSPR_VERSION=`./config/nspr-config --version`
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig" || return 1
  sed "${srcdir}/nspr.pc.in" -e "s,%libdir%,/usr/lib," \
  	-e "s,%prefix%,/usr," \
	-e "s,%exec_prefix%,/usr/bin," \
	-e "s,%includedir%,/usr/include/nspr," \
	-e "s,%NSPR_VERSION%,${NSPR_VERSION}," \
	-e "s,%FULL_NSPR_LIBS%,${NSPR_LIBS}," \
	-e "s,%FULL_NSPR_CFLAGS%,${NSPR_CFLAGS}," > "${pkgdir}/usr/lib/pkgconfig/nspr.pc" || return 1
  chmod 644 "${pkgdir}/usr/lib/pkgconfig/nspr.pc" || return 1
  ln -sf nspr.pc "${pkgdir}/usr/lib/pkgconfig/mozilla-nspr.pc" || return 1

  chmod 644 ${pkgdir}/usr/lib/*.a || return 1

  rm -rf "${pkgdir}/usr/bin/compile-et.pl" \
	"${pkgdir}/usr/bin/prerr.properties" \
	"${pkgdir}/usr/share/aclocal/nspr.m4" \
	"${pkgdir}/usr/include/nspr/md"
}
