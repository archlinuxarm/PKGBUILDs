# $Id: PKGBUILD 27018 2009-02-15 14:35:56Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tom K <tom@archlinux.org>

pkgname=linuxdcpp
pkgver=1.0.3
pkgrel=2
pkgdesc="A port of the DC++ direct connect client to Unix/Linux using GTK+"
url="https://launchpad.net/linuxdcpp"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libglade' 'bzip2' 'gcc-libs' 'glib2' 'zlib' 'openssl')
makedepends=('scons' 'pkgconfig')
conflicts=('linuxdcpp-cvs')
source=(http://launchpad.net/$pkgname/1.0/$pkgver/+download/$pkgname-$pkgver.tar.bz2
	linuxdcpp.desktop)
md5sums=('a427b87fa576d8674640f0a7f6ad5e66'
         'bb9571a39b1b6df474106a19a82ce279')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  scons PREFIX=/usr LIBDIR=/usr/share || return 1
  scons install FAKE_ROOT=${pkgdir} || return 1
  
  install -D ../linuxdcpp.desktop ${pkgdir}/usr/share/applications/linuxdcpp.desktop
  install -D pixmaps/linuxdcpp.png ${pkgdir}/usr/share/pixmaps/linuxdcpp.png
}
