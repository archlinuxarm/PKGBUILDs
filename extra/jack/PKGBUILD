# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.net>
# Contributor: Robert Emil Berge <robert@rebi.no>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to not use packed structures on ARM
#  - patch to not use CPU cycles to measure time

pkgname=jack
_longname=jack-audio-connection-kit
pkgver=0.124.1
pkgrel=3.1
pkgdesc="A low-latency audio server"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('libsamplerate' 'db' 'zita-alsa-pcmi' 'zita-resampler')
makedepends=('doxygen' 'libffado' 'celt')
optdepends=('libffado: FireWire support'
            'celt: NetJACK driver')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
provides=("$_longname=$pkgver")
conflicts=("$_longname")
replaces=("$_longname")
install=$pkgname.install
source=("http://jackaudio.org/downloads/$_longname-$pkgver.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules'
        'arm.patch'
        'https://github.com/jackaudio/jack1/commit/59352d43f42d39a69c718fc03f622aae324b325e.patch')
md5sums=('d64e90121be8a54860b870a726fb5b5d'
         'ae65b7c9ebe0fff6c918ba9d97ae342d'
         '471aad533ff56c5d3cbbf65ce32cadef'
         'a9a552982273c85661b41963298236e2'
         'e3781c2ad63a0791786abf95a1e240ae')

prepare() {
  cd "$srcdir/$_longname-$pkgver"

  patch -p1 -i ../arm.patch
  patch -p1 -i ../59352d43f42d39a69c718fc03f622aae324b325e.patch
}

build() {
  cd "$srcdir/$_longname-$pkgver"

  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "$srcdir/$_longname-$pkgver"

  make DESTDIR="$pkgdir" install

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -Dm644 "$srcdir/99-audio.conf" \
    "$pkgdir/etc/security/limits.d/99-audio.conf"

  install -Dm644 "$srcdir/40-hpet-permissions.rules" \
    "$pkgdir/usr/lib/udev/rules.d/40-hpet-permissions.rules"

  # install a missing header forgotten by upstream
  # see https://bugs.archlinux.org/task/26865
  #install -Dm644 "$srcdir/$_longname-$pkgver/jack/jslist.h" \
  #  "$pkgdir/usr/include/jack/jslist.h"
}

# vim:set ts=2 sw=2 et:
