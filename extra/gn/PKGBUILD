# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add ARM headers back that were removed upstream

buildarch=28

pkgname=gn
pkgver=0.1700.97cc440d
_commit=97cc440d84f050f99ff0161f9414bfa2ffa38f65
pkgrel=1
pkgdesc="Meta-build system that generates build files for Ninja"
arch=('x86_64')
url="https://gn.googlesource.com/gn/"
license=('BSD')
depends=('gcc-libs')
makedepends=('clang' 'ninja' 'python' 'git')
source=(git+https://gn.googlesource.com/gn#commit=$_commit
        0001-Add-ARM-headers-back.patch)
md5sums=('SKIP'
         '9948af7a864a8e57697c49d15cdcb395')

pkgver() {
  cd $pkgname
  echo 0.$(git rev-list --count initial-commit..).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  patch -p1 -i ../0001-Add-ARM-headers-back.patch
}

build() {
  cd $pkgname
  ./build/gen.py
  ninja -C out
}

check() {
  cd $pkgname
  ./out/gn_unittests
}

package() {
  cd $pkgname
  install -D out/gn "$pkgdir/usr/bin/gn"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/*
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -r misc/vim/{autoload,ftdetect,ftplugin,syntax} \
    "$pkgdir/usr/share/vim/vimfiles/"
}

# vim:set ts=2 sw=2 et:
