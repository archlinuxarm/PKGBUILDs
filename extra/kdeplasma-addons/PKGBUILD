# $Id: PKGBUILD 140021 2011-10-05 19:12:22Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: macau <tema.prokopenko@gmail.com> //fix sha1sum for 4.7.1 version

pkgbase=kdeplasma-addons
pkgname=('kdeplasma-addons-applets-bball'
         'kdeplasma-addons-applets-binary-clock'
         'kdeplasma-addons-applets-blackboard'
         'kdeplasma-addons-applets-bookmarks'
         'kdeplasma-addons-applets-bubblemon'
         'kdeplasma-addons-applets-calculator'
         'kdeplasma-addons-applets-charselect'
         'kdeplasma-addons-applets-comic'
         'kdeplasma-addons-applets-community'
         'kdeplasma-addons-applets-dict'
         'kdeplasma-addons-applets-eyes'
         'kdeplasma-addons-applets-fifteenpuzzle'
         'kdeplasma-addons-applets-filewatcher'
         'kdeplasma-addons-applets-frame'
         'kdeplasma-addons-applets-fuzzy-clock'
         'kdeplasma-addons-applets-incomingmsg'
         'kdeplasma-addons-applets-kdeobservatory'
         'kdeplasma-addons-applets-kimpanel'
         'kdeplasma-addons-applets-knowledgebase'
         'kdeplasma-addons-applets-kolourpicker'
         'kdeplasma-addons-applets-konqprofiles'
         'kdeplasma-addons-applets-konsoleprofiles'
         'kdeplasma-addons-applets-lancelot'
         'kdeplasma-addons-applets-leavenote'
         'kdeplasma-addons-applets-life'
         'kdeplasma-addons-applets-luna'
         'kdeplasma-addons-applets-magnifique'
         'kdeplasma-addons-applets-mediaplayer'
         'kdeplasma-addons-applets-microblog'
         'kdeplasma-addons-applets-news'
         'kdeplasma-addons-applets-notes'
         'kdeplasma-addons-applets-nowplaying'
         'kdeplasma-addons-applets-paste'
         'kdeplasma-addons-applets-pastebin'
         'kdeplasma-addons-applets-plasmaboard'
         'kdeplasma-addons-applets-previewer'
         'kdeplasma-addons-applets-qalculate'
         'kdeplasma-addons-applets-rememberthemilk'
         'kdeplasma-addons-applets-rssnow'
         'kdeplasma-addons-applets-showdashboard'
         'kdeplasma-addons-applets-showdesktop'
         'kdeplasma-addons-applets-social-news'
         'kdeplasma-addons-applets-spellcheck'
         'kdeplasma-addons-applets-systemloadviewer'
         'kdeplasma-addons-applets-timer'
         'kdeplasma-addons-applets-unitconverter'
         'kdeplasma-addons-applets-weather'
         'kdeplasma-addons-applets-weatherstation'
         'kdeplasma-addons-applets-webslice'
         'kdeplasma-addons-containments'
         'kdeplasma-addons-libs'
         'kdeplasma-addons-runners-audioplayercontrol'
         'kdeplasma-addons-runners-browserhistory'
         'kdeplasma-addons-runners-characters'
         'kdeplasma-addons-runners-contacts'
         'kdeplasma-addons-runners-converter'
         'kdeplasma-addons-runners-datetime'
         'kdeplasma-addons-runners-events'
         'kdeplasma-addons-runners-katesessions'
         'kdeplasma-addons-runners-konquerorsessions'
         'kdeplasma-addons-runners-konsolesessions'
         'kdeplasma-addons-runners-kopete'
         'kdeplasma-addons-runners-mediawiki'
         'kdeplasma-addons-runners-spellchecker'
         'kdeplasma-addons-wallpapers-mandelbrot'
         'kdeplasma-addons-wallpapers-marble'
         'kdeplasma-addons-wallpapers-pattern'
         'kdeplasma-addons-wallpapers-virus'
         'kdeplasma-addons-wallpapers-weather')
pkgver=4.7.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
groups=('kde' 'kdeplasma-addons')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-workspace' 'kdeedu-marble'
             'eigen' 'scim' 'qwt' 'boost' 'libkexiv2')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('301714467bef00c99613d334558eb517664e29a5')

build() {
    cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeplasma-addons-applets-bball() {
	pkgdesc='A bouncy ball for plasma'
	depends=('kdebase-workspace')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/bball
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-binary-clock() {
	pkgdesc='Time displayed in binary format'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/binary-clock
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-blackboard() {
	pkgdesc='Black Board'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/blackboard
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-bookmarks() {
	pkgdesc='Quick Access to the Bookmarks'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/bookmarks
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-bubblemon() {
	pkgdesc='A pretty bubble that monitors your system.'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/bubblemon
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-calculator() {
	pkgdesc='Calculate simple sums'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/calculator
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-charselect() {
	pkgdesc='View, select, and copy characters from a font collection'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/charselect
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-comic() {
	pkgdesc='View comic strips from the Internet'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/applets/comic
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-community() {
	pkgdesc='Communicate using the Social Desktop'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/community
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-dict() {
	pkgdesc='Look up the meaning of words and their translation into different languages'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/dict
	make DESTDIR=$pkgdir install
	# FIXME
	# /usr/share/icons/oxygen/scalable/apps/accessories-dictionary.svgz
	rm -rf $pkgdir/usr/share/icons
}

package_kdeplasma-addons-applets-eyes() {
	pkgdesc='XEyes clone'
	depends=('kdebase-workspace')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/eyes
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-fifteenpuzzle() {
	pkgdesc='Put the pieces in order'
	depends=('kdebase-workspace')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/fifteenPuzzle
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-filewatcher() {
	pkgdesc='Watch for changes in specified files'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/fileWatcher
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-frame() {
	pkgdesc='Display your favorite pictures'
	depends=('kdebase-workspace' 'libkexiv2')
	cd $srcdir/build/applets/frame
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-fuzzy-clock() {
	pkgdesc='Time displayed in a less precise format'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/fuzzy-clock
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-incomingmsg() {
	pkgdesc='Notification of new messages'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/incomingmsg
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-kdeobservatory() {
	pkgdesc='Visualize the KDE ecosystem'
	depends=('kdebase-workspace' 'qwt')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/kdeobservatory
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-kimpanel() {
	pkgdesc='A generic input method panel for Oriental languages'
	depends=('kdebase-workspace')
	optdepends=('scim: SCIM backend'
		'fcitx: FCITX backend')
	cd $srcdir/build/applets/kimpanel
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-knowledgebase() {
	pkgdesc='Opendesktop Knowledgebase'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/knowledgebase
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-kolourpicker() {
	pkgdesc='Pick a color from the desktop'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/kolourpicker
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-konqprofiles() {
	pkgdesc='List and launch Konqueror profiles'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/konqprofiles
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-konsoleprofiles() {
	pkgdesc='List and launch Konsole profiles'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/konsoleprofiles
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-lancelot() {
	pkgdesc='Launcher to start applications'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	replaces=('lancelot')
	provides=('lancelot')
	conflicts=('lancelot')
	install='kdeplasma-addons-applets-lancelot.install'
	cd $srcdir/build/applets/lancelot
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-leavenote() {
	pkgdesc='Leave notes for users while they are away'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/leavenote
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-life() {
	pkgdesc='Life'
	depends=('kdebase-workspace')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/life
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-luna() {
	pkgdesc='Display moon phases for your location'
	depends=('kdebase-workspace')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/luna
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-magnifique() {
	pkgdesc='A magnification glass for the Plasma desktop'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/magnifique
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-mediaplayer() {
	pkgdesc='Widget that can play video and sound'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/mediaplayer
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-microblog() {
	pkgdesc='Update and view your microblog status.'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/applets/microblog
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-news() {
	pkgdesc='Show news from various sources'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/news
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-notes() {
	pkgdesc='Desktop sticky notes'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/notes
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-nowplaying() {
	pkgdesc='Displays currently playing audio'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/nowplaying
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-paste() {
	pkgdesc='Paste text snippets'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/paste
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-pastebin() {
	pkgdesc='Paste text/images to a remote server'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/pastebin
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-plasmaboard() {
	pkgdesc='A virtual, on-screen keyboard'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/plasmaboard
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-previewer() {
	pkgdesc='Preview This File'
	depends=('kdebase-workspace')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/previewer
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-qalculate() {
	pkgdesc='A powerful mathematical equation solver'
	depends=('kdebase-workspace')
	install='kdeplasma-addons-applets.install'
	cd $srcdir/build/applets/qalculate
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-rememberthemilk() {
	pkgdesc='Remember The Milk Todo list applet'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/applets/rememberthemilk
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-rssnow() {
	pkgdesc='Show news from various sources'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/rssnow
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-showdashboard() {
	pkgdesc='Show the Plasma widget dashboard above other windows'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/showdashboard
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-showdesktop() {
	pkgdesc='Show the Plasma desktop'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/showdesktop
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-social-news() {
	pkgdesc='Stay informed with the Social Desktop'
	replaces=('kdeplasma-addons-applets-opendesktop' 'kdeplasma-addons-applets-opendesktop-activities')
	conflicts=('kdeplasma-addons-applets-opendesktop' 'kdeplasma-addons-applets-opendesktop-activities')
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/social-news
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-spellcheck() {
	pkgdesc='Fast spell checking'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/spellcheck
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-systemloadviewer() {
	pkgdesc='Tiny CPU/RAM/Swap monitor'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/systemloadviewer
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-timer() {
	pkgdesc='Countdown over a specified time period'
	depends=('kdebase-workspace')
	cd $srcdir/build/applets/timer
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-unitconverter() {
	pkgdesc='Plasmoid for converting units'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/applets/unitconverter
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-weather() {
	pkgdesc='Displays Weather information'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/applets/weather
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-weatherstation() {
	pkgdesc='Weather reports with an LCD display style'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/applets/weatherstation
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-applets-webslice() {
	pkgdesc='Show a part of a webpage'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/applets/webslice
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-containments() {
	pkgdesc='Activities types for Plasma shells'
	depends=('kdebase-workspace')
	cd $srcdir/build/containments
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-libs() {
	pkgdesc='Plasma Addon Library'
	depends=('kdebase-workspace')
	groups=()
	replaces=('kdeplasma-addons-dataengines')
	provides=('kdeplasma-addons-dataengines')
	conflicts=('kdeplasma-addons-dataengines')
	cd $srcdir/build/libs
	make DESTDIR=$pkgdir install
	cd $srcdir/build/dataengines
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-audioplayercontrol() {
	pkgdesc='Allows to control MPRIS audio players (it is able to search through Amarok´s collection, too)'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/audioplayercontrol
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-browserhistory() {
	pkgdesc='Searches in Konqueror´s history'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/browserhistory
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-characters() {
	pkgdesc='special Characters'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/characters
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-contacts() {
	pkgdesc='Finds entries in your address book'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/contacts
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-converter() {
	pkgdesc='Convert values to different units'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/runners/converter
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-datetime() {
	pkgdesc='The current date and time, locally or in any timezone'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/datetime
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-events() {
	pkgdesc='Calendar Events runner'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/events
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-katesessions() {
	pkgdesc='Matches Kate Sessions'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/katesessions
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-konquerorsessions() {
	pkgdesc='Matches Konqueror Sessions'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/konquerorsessions
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-konsolesessions() {
	pkgdesc='Matches Konsole Sessions'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/konsolesessions
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-kopete() {
	pkgdesc='Kopete Contact runner'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/kopete
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-mediawiki() {
	pkgdesc='Search on Wikitravel'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/mediawiki
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners-spellchecker() {
	pkgdesc='Check the spelling of a word'
	depends=('kdebase-workspace')
	cd $srcdir/build/runners/spellchecker
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-wallpapers-mandelbrot() {
	pkgdesc='Mandelbrot'
	depends=('kdebase-workspace')
	cd $srcdir/build/wallpapers/mandelbrot
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-wallpapers-marble() {
	pkgdesc='Globe'
	depends=('kdebase-workspace' 'kdeedu-marble')
	cd $srcdir/build/wallpapers/marble
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-wallpapers-pattern() {
	pkgdesc='Pattern'
	depends=('kdebase-workspace')
	cd $srcdir/build/wallpapers/pattern
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-wallpapers-virus() {
	pkgdesc='Virus'
	depends=('kdebase-workspace')
	cd $srcdir/build/wallpapers/virus
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-wallpapers-weather() {
	pkgdesc='Weather'
	depends=('kdebase-workspace' 'kdeplasma-addons-libs')
	cd $srcdir/build/wallpapers/weather
	make DESTDIR=$pkgdir install
}

