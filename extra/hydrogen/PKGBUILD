# $Id: PKGBUILD 356 2008-04-18 22:56:27Z aaron $
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: K. Piche <kpiche@rogers.com>

# rebuild 0.9.3-4 -> 0.9.3-5 : bug 7745 drumkits do not load - damir

pkgname=hydrogen
pkgver=0.9.3
pkgrel=6
pkgdesc="an advanced drum machine/pattern editor with midi support for GNU/Linux"
arch=("i686" "x86_64")
license=('GPL')
url="http://www.hydrogen-music.org/"
depends=('liblrdf' 'qt3' 'jack-audio-connection-kit>=0.102.20-2')
options=('!makeflags')
install=hydrogen.install
source=(http://heanet.dl.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}.desktop
	hydrogen-gcc-4.1.patch
	hydrogen-0.9.3-flac113.patch)
md5sums=('d5840b5d330d433d00ea1727efb0fc7f'
	  '8d6eacef58aa22cdba97485f9bdbb4b3'
	  'e43555677b71dcf988a5e85a2304c301'
	  '2fc3124d42d210e769428f149047fe32')

build() {
  . /etc/profile.d/qt3.sh
  cd ${startdir}/src/${pkgname}-${pkgver}
 # build
  patch -Np0 -i ${startdir}/src/hydrogen-gcc-4.1.patch || return 1
  patch -Np0 -i ${startdir}/src/hydrogen-0.9.3-flac113.patch || return 1
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=${startdir}/pkg install
 # install some freedesktop.org compatibility
  install -D -m644 ${startdir}/src/${pkgname}.desktop \
    ${startdir}/pkg/usr/share/applications/${pkgname}.desktop
  mkdir ${startdir}/pkg/usr/share/pixmaps
  cd ${startdir}/pkg/usr/share/pixmaps
  ln -s ../hydrogen/data/img/gray/icon.svg ./hydrogen.svg
  ln -s ../hydrogen/data/img/gray/icon64.png ./hydrogen.png
}
