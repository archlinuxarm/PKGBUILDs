# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto in arch-meson

pkgname=meson
pkgver=1.8.1
pkgrel=1
pkgdesc="High productivity build system"
url="https://mesonbuild.com/"
arch=(any)
license=(Apache-2.0)
depends=(
  bash
  ninja
  python
  python-tqdm
)
makedepends=(
  git
  python-aiohttp
  python-build
  python-installer
  python-setuptools
  python-strictyaml
  python-wheel
)
checkdepends=(
  boost
  clang
  cmake
  cuda
  cython
  doxygen
  gcc-fortran
  gcc-objc
  git
  glib2-devel
  glibc-locales
  gmock
  gnustep-base
  gobject-introspection
  graphviz
  gtest
  gtk-doc
  gtk-sharp-2
  gtk3
  gtkmm3
  hdf5
  hotdoc
  itstool
  java-environment=8
  ldc
  libelf
  libpcap
  libwmf
  llvm
  mercurial
  mono
  nasm
  netcdf-fortran
  openmpi
  openssh
  protobuf
  pypy3
  python-gobject
  python-importlib-metadata
  python-lxml
  python-pytest-xdist
  qt5-base
  qt5-tools
  qt6-base
  qt6-declarative
  qt6-tools
  rust
  rust-bindgen
  sdl2
  vala
  valgrind
  vulkan-headers
  vulkan-icd-loader
  vulkan-validation-layers
  wayland
  wayland-protocols
  wxgtk3
)
source=(
  "git+https://github.com/mesonbuild/meson?signed#tag=$pkgver"
  arch-meson
  cross-lib32
  native-clang
  0001-Skip-broken-tests.patch
)
b2sums=('687d80be8844e6ba92677067b48744c80d3a3e2688b7e889b069e2f8cd1b8ea3a0736f53c6880e4604eb427bec2bd87f4b8c41ef292e22e780c7aeb6db55fcc3'
        '70f042a7603d1139f6cef33aec028da087cacabe278fd47375e1b2315befbfde1c0501ad1ecc63d04d31b232a04f08c735d61ce59d7244521f3d270e417fb5af'
        '9b16477aa77a706492e26fb3ad42e90674b8f0dfe657dd3bd9ba044f921be12ceabeb0050a50a15caee4d999e1ec33ed857bd3bed9e4444d73bb4a4f06381081'
        '7d88929d5a3b49d91c5c9969f19d9b47f3151706526b889515acaeda0141257d5115875ac84832e9ea46f83a7700d673adcc5db84b331cd798c70ae6e90eac1e'
        'd3f84b7ab54bd60ed4ec94d9d7247cf50fc2ea1daa5131201455f35a401dd381cffe968584e52d3c6f52c12d20287661a5555da33530adfbb0d60a2613e72e51')
validpgpkeys=(
  19E2D6D9B46D8DAA6288F877C24E631BABB1FE70 # Jussi Pakkanen <jpakkane@gmail.com>
  BD27B07A5EF45C2ADAF70E0484818A6819AF4A9B # Eli Schwartz <eschwartz93@gmail.com>
)

prepare() {
  cd meson

  # Pass tests
  patch -Np1 -i ../0001-Skip-broken-tests.patch
  git cherry-pick -n 23e4e2f20ca698f4070f7da79f5e06249d34b622
}

build() {
  cd meson
  python -m build --wheel --no-isolation
  ./meson.py setup docs docs.build --prefix /usr -D html=false
  ./meson.py compile -C docs.build
}

check() (
  cd meson
  export LC_CTYPE=en_US.UTF-8 CPPFLAGS= CFLAGS= CXXFLAGS= LDFLAGS=
  ./run_tests.py --failfast
)

package() {
  cd meson
  python -m installer --destdir="$pkgdir" dist/*.whl

  ./meson.py install -C docs.build --destdir "$pkgdir"
  install -Dm644 docs.build/reference_manual.json -t "$pkgdir/usr/share/doc/$pkgname"

  install -d "$pkgdir/usr/share/vim/vimfiles"
  cp -rt "$pkgdir/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

  install -Dm644 data/shell-completions/bash/* -t "$pkgdir/usr/share/bash-completion/completions"
  install -Dm644 data/shell-completions/zsh/*  -t "$pkgdir/usr/share/zsh/site-functions"

  install -D ../arch-meson -t "$pkgdir/usr/bin"

  install -Dm644 ../cross-lib32 "$pkgdir/usr/share/meson/cross/lib32"
  install -Dm644 ../native-clang "$pkgdir/usr/share/meson/native/clang"
}

# vim:set sw=2 sts=-1 et:
