# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto in arch-meson

pkgname=meson
pkgver=0.61.0
pkgrel=4
pkgdesc='High productivity build system'
url='https://mesonbuild.com/'
arch=('any')
license=('Apache')
depends=('python-setuptools' 'ninja')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git' 'cython'
              'gtkmm3' 'gtest' 'gmock' 'protobuf' 'wxgtk3' 'python-gobject' 'gobject-introspection'
              'itstool' 'gtk3' 'java-environment=8' 'gtk-doc' 'llvm' 'clang' 'sdl2' 'graphviz'
              'doxygen' 'vulkan-validation-layers' 'openssh' 'mercurial' 'gtk-sharp-2' 'qt5-tools'
              'libwmf' 'valgrind' 'cmake' 'netcdf-fortran' 'openmpi' 'nasm' 'gnustep-base' 'libelf'
              'python-pytest-xdist' 'python2-setuptools' 'ldc' 'rust-bindgen' 'cuda' 'hotdoc')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc}
        https://github.com/mesonbuild/meson/commit/81fbcd1df48f669b6b23b9dd0752d6d366cc9bf2.patch
        https://github.com/mesonbuild/meson/commit/bcfb4d0f46608db46cd8881e2a02ba6c33ff066e.patch
        https://github.com/mesonbuild/meson/commit/704e9802c9f424a57c32d9e63ececfece807529a.patch
        https://github.com/mesonbuild/meson/commit/63593a55288ee5b89fc9c298dbc300f9c6adaba9.patch
        https://github.com/mesonbuild/meson/commit/bda11fe25d1efe688d251148cb7cf7939cd50433.patch
        https://github.com/mesonbuild/meson/commit/9952446a2a28ea2b009f4b8b2a14ad50f0878bdd.patch
        skip-test.diff
        arch-meson)
sha512sums=('ff739f767710c09a1b238f135c81bdb79675d06cec1b091503809cdbd71f0f92fd76bf068650bbec60688b79fbda94e56cb3203c948aa79f16a88f6d9db219d1'
            'SKIP'
            '7e57c59394b73819e3af9722dc26e68edebec35187ceb39116f15a890fb0277cfe198b0ba84218a7f3333cffb16e217717b94d325224167175c0c1479e0f94a5'
            '8e943cecd9b4a43f690b9bae463f8cdb8dd4a53e6a9c4f9dc93ee0119ffae1d9964604fbc9180c7dda39790542b52ef30b6f76c1321ce493eb24d211895b15fa'
            'f7b0d8e77af19cd666c1efcbed8767483efa4d2b58a08d9a01151f77dbcbd8047a6dfd95fa57146d565c0b7f404368efef6fdc096e127c640c66747641ecce3c'
            '2fc23d7b1ac8320ffb3742a1299f69700b0ead212446aac9ca7d2c63874f4f2cd90e6739287814c61baaf00d92f9c99c714abac30b5b54e4362ca6476a32ca02'
            '0d3181cae84c702060c34fb27b632bef46cca89829fa7cc7ec4bfe9650ad745d79caa593210838e91560eda05e5b752474d3cd093765bfe97a4c2fdd3d5767a9'
            'cf59e2e2f6067fa512445c81f7e4b417c4fb20c720fbf84e3248bb40ff04a71ad18e20d86d28fc8b1c7ace4c3c2a0b9e9053c4a7870553eed7f0b2e69bd0f49d'
            '994d88aaa7d9ad619885674352593826de198ef443c887fe6e9ecdcd84302eb04e0c396263404b4f16e447586f2b61f100643ba627803971070fa7b6baa75f78'
            '278f5e4de3aa1170d9b4f9f212985d664f44d90ffec727febeeea1ed570046c6469558a5d123a41bf4c2fdf99dbe7832515b06f1ace423c63e2e95ba6d0ef235')
validpgpkeys=('19E2D6D9B46D8DAA6288F877C24E631BABB1FE70') # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}

  # gtk-doc fixes
  patch -Np1 -i ../81fbcd1df48f669b6b23b9dd0752d6d366cc9bf2.patch
  patch -Np1 -i ../bcfb4d0f46608db46cd8881e2a02ba6c33ff066e.patch

  # gdbus-codegen fixes
  patch -Np1 -i ../704e9802c9f424a57c32d9e63ececfece807529a.patch
  patch -Np1 -i ../63593a55288ee5b89fc9c298dbc300f9c6adaba9.patch
  patch -Np1 -i ../bda11fe25d1efe688d251148cb7cf7939cd50433.patch

  # vapigen fixes
  patch -Np1 -i ../9952446a2a28ea2b009f4b8b2a14ad50f0878bdd.patch

  # Our containers do not allow sanitizers to run
  patch -Np1 -i ../skip-test.diff
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() (
  cd ${pkgname}-${pkgver}
  export LC_CTYPE=en_US.UTF-8 CPPFLAGS= CFLAGS= CXXFLAGS= LDFLAGS=
  ./run_tests.py
)

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -rt "${pkgdir}/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

  install -Dt "${pkgdir}/usr/share/bash-completion/completions" -m644 data/shell-completions/bash/*
  install -Dt "${pkgdir}/usr/share/emacs/site-lisp" -m644 data/syntax-highlighting/emacs/*
  install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*

  # Arch packaging helper
  install -D ../arch-meson -t "${pkgdir}/usr/bin"
}

# vim:set sw=2 et:
