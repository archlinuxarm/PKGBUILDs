# $Id: PKGBUILD 49733 2009-08-15 13:51:39Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname="notification-daemon"
pkgver=0.4.0
pkgrel=3
pkgdesc="Notification daemon for the desktop notifications framework"
arch=(i686 x86_64)
license=('GPL')
url="http://www.galago-project.org/specs/notification/"
depends=('libsexy>=0.1.11' 'libwnck>=2.26.1' 'gconf>=2.26.2' 'gstreamer0.10-base' 'libnotify>=0.4.5' 'libglade>=2.6.4' 'hicolor-icon-theme')
groups=('gnome')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
install=notification-daemon.install
source=(http://www.galago-project.org/files/releases/source/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e61eff9782551d81045bb53e8a801d58')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/notification-daemon-1.0 \
      --localstatedir=/var --disable-static
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain notification-daemon ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
