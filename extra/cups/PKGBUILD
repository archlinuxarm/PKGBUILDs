# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=cups
pkgver=1.3.10
pkgrel=3
pkgdesc="The CUPS Printing System"
arch=('arm')
license=('GPL')
url="http://www.cups.org/"
depends=('acl' 'openslp' 'pam' "libcups>=${pkgver}" 'xdg-utils' 'heimdal>=1.2.1-3' 'gnutls>=2.6.5' 'poppler>=0.10.6')
makedepends=('xinetd' 'gzip')
install=cups.install
backup=(etc/cups/cupsd.conf
        etc/cups/mime.convs
        etc/cups/mime.types
        etc/cups/snmp.conf
        etc/dbus-1/system.d/cups.conf
        etc/logrotate.d/cups
        etc/pam.d/cups
        etc/xinetd.d/cups-lpd)
source=(ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2
        cups cups.logrotate cups.pam)
options=('!emptydirs') #'!makeflags') seems smp safe now; find pkg/ | sort | wc -l gives 1815
md5sums=('84fffe96b8537c81a463faccead80026'
         '5c85b7d8d2ddd02c2c64955cebbf55ea'
         'f861b18f4446c43918c8643dcbbd7f6d'
         '96f82c38f3f540b53f3e5144900acf17')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --with-logdir=/var/log/cups -with-docdir=/usr/share/cups/doc \
     --with-cups-user=daemon --with-cups-group=lp --enable-ssl=yes \
     --enable-pam=yes --disable-ldap --disable-dbus --enable-raw-printing \
     --enable-pdftops --with-pdftops=pdftops --with-optim="$CFLAGS"
  make || return 1

  #Wreck out cups library installation
  sed -i -e 's/^DIRS\s=\scups/DIRS = /' Makefile

  make BUILDROOT=${pkgdir} install

  #Wreck out some stuff included in libcups
  
  if [ "$CARCH" = "x86_64" ]; then 
    if [ -f $pkgdir/usr/lib64 ]; then
	 rm -rf ${pkgdir}/usr/lib64/
    fi
  fi
  rm -f ${startdir}/pkg/usr/lib/libcupsimage.so* 
  rm -f ${startdir}/pkg/usr/bin/cups-config
  rm -rf ${startdir}/pkg/usr/include

  # kill the sysv stuff
  rm -rf ${startdir}/pkg/etc/rc*.d
  rm -rf ${startdir}/pkg/etc/init.d
  install -D -m755 ../cups ${startdir}/pkg/etc/rc.d/cups
  install -D -m644 ../cups.logrotate ${startdir}/pkg/etc/logrotate.d/cups
  install -D -m644 ../cups.pam ${startdir}/pkg/etc/pam.d/cups
  
  # fix perms on /var/spool and /etc
  chmod 755 ${startdir}/pkg/var/spool
  chmod 755 ${startdir}/pkg/etc

  # fix .desktop file
  sed -i 's|^Exec=htmlview http://localhost:631/|Exec=xdg-open http://localhost:631/|g' $startdir/pkg/usr/share/applications/cups.desktop
  
  # compress some driver files, adopted from Fedora
  find ${pkgdir}/usr/share/cups/model -name "*.ppd" | xargs gzip -n9f
}
