# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon on !AArch64

pkgname=libwebp
pkgver=1.2.1
pkgrel=2
pkgdesc="WebP library and conversion tools"
arch=(x86_64)
url="https://developers.google.com/speed/webp/"
license=(BSD)
depends=(libpng libjpeg libtiff giflib)
makedepends=(freeglut mesa glu)
optdepends=('freeglut: vwebp viewer')
provides=(libwebp{,decoder,demux,mux}.so)
source=(https://storage.googleapis.com/downloads.webmproject.org/releases/webp/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('808b98d2f5b84e9b27fdef6c5372dac769c3bda4502febbfa5031bd3c4d7d018'
            'SKIP')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D') # WebP release signing key

build() {
  cd $pkgname-$pkgver
  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"
  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-swap-16bit-csp \
    --enable-libwebp{mux,demux,decoder,extras} $CONFIG
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
