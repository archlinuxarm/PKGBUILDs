# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon on !AArch64

pkgname=libwebp
pkgver=0.5.2
pkgrel=1
pkgdesc="WebP library and conversion tools"
arch=(x86_64 i686)
url="https://developers.google.com/speed/webp/"
license=(BSD)
depends=(libpng libjpeg libtiff giflib)
makedepends=(freeglut mesa glu)
optdepends=('freeglut: vwebp viewer')
source=(https://storage.googleapis.com/downloads.webmproject.org/releases/webp/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('b75310c810b3eda222c77f6d6c26b061240e3d9060095de44b2c1bae291ecdef'
            'SKIP')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"

  ./configure --prefix=/usr \
     --enable-swap-16bit-csp \
     --enable-experimental \
     --enable-libwebp{mux,demux,decoder,extras} \
     --disable-static $CONFIG
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
