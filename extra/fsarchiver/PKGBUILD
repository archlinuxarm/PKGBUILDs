# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix conflicting type definitions (fixes AArch64 build) (thanks Fedora)

pkgname=fsarchiver
pkgver=0.6.20
pkgrel=1
pkgdesc='Safe and flexible file-system backup and deployment tool'
arch=('i686' 'x86_64')
url='http://www.fsarchiver.org/'
license=('GPL2')
depends=('bzip2' 'e2fsprogs' 'lzo' 'xz' 'libgcrypt')
source=(https://github.com/fdupoux/fsarchiver/releases/download/$pkgver/fsarchiver-$pkgver.tar.gz
        fsarchiver-0.6.19-do-not-duplicate-types.patch)
md5sums=('04d73f8f8e8dd0063e06f74a125bdcb6'
         '009bafd9ec764dc2863e18742992bc45')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../fsarchiver-0.6.19-do-not-duplicate-types.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
