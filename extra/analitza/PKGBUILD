# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to link against GLES for Linux

pkgname=analitza
pkgver=17.04.3
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://edu.kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('qt5-svg' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'eigen' 'qt5-tools')
replaces=('kdebase-analitza')
conflicts=('kdebase-analitza')
source=("https://download.kde.org/stable/applications/${pkgver}/src/analitza-${pkgver}.tar.xz"{,.sig}
        '0001-Link-against-GLESv2-for-Linux.patch')
sha256sums=('2b3047e1cbd61a6084e8466f3ea587b16f3b335b9c8c602cba61be2222469f69'
            'SKIP'
            '3bce76a8106ada89727a5f781441ce8af8c1d619230e25bbecb48aee5b7511d0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  patch -p1 -d $pkgname-$pkgver -i ../0001-Link-against-GLESv2-for-Linux.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
