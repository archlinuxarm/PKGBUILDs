# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to link against GLES for Linux

pkgname=analitza
pkgver=17.04.2
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://edu.kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('qt5-svg' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'eigen' 'qt5-tools')
replaces=('kdebase-analitza')
conflicts=('kdebase-analitza')
source=("https://download.kde.org/stable/applications/${pkgver}/src/analitza-${pkgver}.tar.xz"{,.sig}
        '0001-Link-against-GLESv2-for-Linux.patch')
sha256sums=('975e5972e067a08a3faecb896fc667d18dd15ad58e337f29f249316acfad4263'
            'SKIP'
            '3bce76a8106ada89727a5f781441ce8af8c1d619230e25bbecb48aee5b7511d0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
  patch -p1 -d $pkgname-$pkgver -i ../0001-Link-against-GLESv2-for-Linux.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
