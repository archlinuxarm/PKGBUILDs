# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to disable opengl for ARM

pkgname=analitza
pkgver=14.12.3
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://projects.kde.org/projects/kde/kdeedu/analitza"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('qt5-svg' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'eigen')
replaces=('kdebase-analitza')
conflicts=('kdebase-analitza')
source=("http://download.kde.org/stable/applications/${pkgver}/src/analitza-${pkgver}.tar.xz"
        'analitza-disable-opengl.patch')
sha1sums=('3caf7be69e3603c2846d61e42bc18893178a6925'
          'db4b031c52e10272af54542eebef5b68d8bd0c45')

prepare() {
  mkdir build

  cd "${srcdir}/analitza-${pkgver}"
  patch -p1 -i ../analitza-disable-opengl.patch
}

build() {
  cd build
  cmake ../analitza-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
