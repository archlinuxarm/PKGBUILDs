# $Id: PKGBUILD 35512 2009-04-13 19:47:22Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany-extensions
pkgver=2.26.1
pkgrel=1
_majorver=2.26
url="http://www.gnome.org/projects/epiphany/"
pkgdesc="Various extentions for the Epiphany web browser"
arch=(i686 x86_64)
license=('GPL')
depends=('epiphany>=2.26.1' 'python>=2.6' 'opensp')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.16.0' 'intltool')
options=('!libtool' '!emptydirs')
install=epiphany-extensions.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${_majorver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('bc3044148e915312654f8c82a575e58e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
	      --with-gecko=libxul-embedding \
	      --with-extensions=really-all --disable-scrollkeeper || return 1
  make || return 1

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain epiphany-extenions-${_majorver} ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
