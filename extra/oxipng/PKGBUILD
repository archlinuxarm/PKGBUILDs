# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Nicolas F. <aur@fratti.ch>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove AArch64 cross-compiling options

pkgname=oxipng
pkgver=9.1.5
pkgrel=1
pkgdesc="A lossless PNG compression optimiser"
arch=(x86_64)
url="https://github.com/shssoichiro/oxipng"
license=('MIT')
depends=('gcc-libs')
options=('!lto')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/shssoichiro/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('8f99d5c67efa2a7550023bf610b90e65d421375c9ed7f37097f83ae5c05f85bd')

build() {
  cd "$pkgname-$pkgver"

  # remove AArch64 cross-compiling options
  sed -i -e '/aarch64-linux-gnu-gcc/d' -e '/qemu-aarch64/d' .cargo/config.toml

  # Generate man page
  cargo xtask mangen

  cargo build --release --locked
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 target/release/oxipng "$pkgdir/usr/bin/oxipng"
  install -Dm644 target/xtask/mangen/manpages/oxipng.1 "$pkgdir/usr/share/man/man1/oxipng.1"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
