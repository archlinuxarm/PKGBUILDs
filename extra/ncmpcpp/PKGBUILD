# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <therealgraysky AT protonmail DOT com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --without-lto

pkgname=ncmpcpp
pkgver=0.10
pkgrel=1
pkgdesc='Featureful ncurses based MPD client inspired by ncmpc'
url='https://github.com/ncmpcpp/ncmpcpp'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('curl' 'libcurl.so' 'libmpdclient' 'libmpdclient.so' 'fftw' 'libfftw3.so' 'boost-libs'
         'libboost_filesystem.so' 'libboost_locale.so' 'libboost_program_options.so'
         'libboost_thread.so' 'icu' 'libicui18n.so' 'libicuuc.so'
         'glibc' 'gcc-libs' 'ncurses' 'libncursesw.so' 'readline' 'libreadline.so' 'taglib')
makedepends=('boost' 'git')
source=(git+https://github.com/ncmpcpp/ncmpcpp.git#tag=${pkgver})
sha512sums=('11ecfca5ab9e9c55666990dce720b628e13abc84476770be5956b9736e9ee96de028ac96a60430218f6ce05859617b6875c540c2f4d9fe8dcc252017e72f5f48')
b2sums=('6009107e2c58c2a1281313ab8071723b34a6b26f353f8fb13d99d39cc1315357a2b7f05f89910edd2829023fa54f1a7d6ae29f9a78830116849a847f46ac2f56')

prepare() {
  cd ${pkgname}
  autoreconf -fiv
  sed -e 's/CXXFLAGS=/CXXFLAGS+=/' \
      -e 's/CPPFLAGS=/CPPFLAGS+=/' \
      -e 's/LDFLAGS=/LDFLAGS+=/' \
      -e 's/-march=native//' \
      -i extras/Makefile
}

build() {
  cd ${pkgname}
  BOOST_LIB_SUFFIX='' ./configure \
    --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-visualizer \
    --with-fftw \
    --with-taglib \
    --without-lto
  make
  make -C extras
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
