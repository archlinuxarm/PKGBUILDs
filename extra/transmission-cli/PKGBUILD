# $Id: PKGBUILD 53921 2009-10-03 21:05:34Z eric $
# Maintainer: Alexander Fehr <pizzapunk gmail com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

pkgname=transmission-cli
pkgver=1.75
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
depends=('curl')
makedepends=('intltool')
replaces=('transmission')
backup=('etc/conf.d/transmissiond')
install=transmission-cli.install
source=(http://mirrors.m0k.org/transmission/files/transmission-$pkgver.tar.bz2
        transmissiond
        transmissiond.conf)
md5sums=('ec09b76ca941f5c389d8dd4f469f1fa6' '50b7c17300bc8b38f44fb6f681ba05d0'\
         '4170b71ad5d6c499a8a16c6e86b77cce')
sha1sums=('8ce610a9d743cdd6fa6495824d03416bee9a3512' '5f59846ef4219a7a7679d1feb3ff80d381340f36'\
         '89d2f2d907ddf4410504662b1962282d34b84046')

build() {
  cd "$srcdir/transmission-$pkgver"

  ./configure --prefix=/usr --disable-gtk --disable-wx || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  install -D -m755 "$srcdir/transmissiond" "$pkgdir/etc/rc.d/transmissiond" || return 1
  install -D -m644 "$srcdir/transmissiond.conf" "$pkgdir/etc/conf.d/transmissiond" || return 1

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/transmission-cli/COPYING" || return 1
}
