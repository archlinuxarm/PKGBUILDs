# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with -Wno-incompatible-pointer-types for v7

pkgname=libfabric
pkgver=2.1.0
pkgrel=1
pkgdesc="User-space API for OpenFabrics Interfaces (OFI)"
arch=(x86_64)
url="https://ofiwg.github.io/libfabric/"
license=('BSD-2-Clause OR GPL-2.0-or-later')
depends=(glibc numactl)
options=(!lto)
source=(https://github.com/ofiwg/libfabric/releases/download/v${pkgver}/libfabric-${pkgver}.tar.bz2)
sha512sums=('353c46c3026b34104f654345d4c94007cd4a78c5d851df6b52166c10bcb431c5db46d75f290a15ca98a31c2343bb3a92718313c7dadd57298847015e80e2a612')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -Wno-incompatible-pointer-types"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
