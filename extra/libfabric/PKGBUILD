# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with -Wno-incompatible-pointer-types for v7

pkgname=libfabric
pkgver=2.4.0
pkgrel=1
pkgdesc="User-space API for OpenFabrics Interfaces (OFI)"
arch=(x86_64)
url="https://ofiwg.github.io/libfabric/"
license=('BSD-2-Clause OR GPL-2.0-or-later')
depends=(glibc numactl)
options=(!lto)
source=(https://github.com/ofiwg/libfabric/releases/download/v${pkgver}/libfabric-${pkgver}.tar.bz2)
sha512sums=('0d3036a69c7f7d0e65f4ea9dbd96face3758e74f947842d3925e2b0a9a2ea99f8c2be394a1e532bba268ed9cfcab92d658bc536fe82911542159c64f5c667cc3')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -Wno-incompatible-pointer-types"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
