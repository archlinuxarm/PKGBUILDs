# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove dependencies on libxnvctrl, libXNVCtrl.so
#  - remove -D BUILD_NVIDIA=ON
#  - remove makedepends on pandoc, set -D BUILD_DOCS=OFF

pkgname=conky
pkgver=1.21.5
pkgrel=1
pkgdesc='Light-weight system monitor for X, Wayland, and other things, too'
url='https://github.com/brndnmtthws/conky'
license=(
  'BSD-3-Clause'
  'GPL-3.0-or-later'
)
arch=('x86_64')
makedepends=(
  'catch2-v2'
  'cmake'
  'docbook2x'
  'docbook-xsl'
  'git'
  'man-db'
  'python-yaml'
  'python-jinja'
  'wayland-protocols'
  'gperf'
)
depends=(
  'cairo'
  'curl' libcurl.so
  'fontconfig'
  'gcc-libs'
  'glib2'
  'glibc'
  'hicolor-icon-theme'
  'imlib2'
  'libpulse' libpulse.so
  'libx11'
  'libxdamage'
  'libxext'
  'libxfixes'
  'libxft'
  'libxi'
  'libxinerama'
  'libxml2'
  'lua'
  'ncurses' libncursesw.so
  'pango'
  'systemd-libs' libsystemd.so
  'wayland'
  'wireless_tools'
)
source=("git+$url.git#tag=v${pkgver}?signed")
sha512sums=('740ae8fb1e81807f76aecd091a70f52a282ab88f4fde65a59345d2833e4e91c392f840b116124ce4f88436f0e95daa6e45e6d9515ed8eddef55fc6dbbe724397')
b2sums=('0343e3c78d76ae4f9f2dff5dfde0a9dcbcbb2e2204e58a9c9600f5d15a7b42087282b77686ba66db99fd41a25f43cf130a3f3c56187c9bfeaa7050b9f837b64a')
validpgpkeys=(
  'AF310802C8EFA20499CF3556137B7AC2BDFD8DF0' # Brenden Matthews <github@brenden.brndn.io>
)

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
# Unbundle catch2 to fix build with glibc 2.35
  rm -r tests/catch2
  ln -s /usr/include/catch2 tests
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -D MAINTAINER_MODE=OFF \
    -D BUILD_TESTS=ON \
    -D BUILD_DOCS=OFF \
    -D BUILD_EXTRAS=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_XDBE=ON \
    -D BUILD_XSHAPE=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D BUILD_PULSEAUDIO=ON \
    -D BUILD_JOURNAL=ON \
    -D BUILD_WAYLAND=ON \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -S .

  make -C build
}

package() {
  cd ${pkgname}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 build/extras/vim/syntax/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/syntax
  install -Dm 644 extras/vim/ftdetect/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/ftdetect
}

# vim: ts=2 sw=2 et:
