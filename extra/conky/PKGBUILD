# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove dependencies on libxnvctrl, libXNVCtrl.so
#  - remove -D BUILD_NVIDIA=ON
#  - remove makedepends on pandoc, set -D BUILD_DOCS=OFF

pkgname=conky
pkgver=1.22.2
pkgrel=1
pkgdesc='Light-weight system monitor for X, Wayland, and other things, too'
arch=('x86_64')
url='https://github.com/brndnmtthws/conky'
license=(
  'BSD-3-Clause'
  'GPL-3.0-or-later'
)
depends=(
  'cairo'
  'curl' libcurl.so
  'fontconfig'
  'gcc-libs'
  'glib2'
  'glibc'
  'hicolor-icon-theme'
  'imlib2'
  'libpulse' libpulse.so
  'librsvg' librsvg-2.so
  'libx11'
  'libxdamage'
  'libxext'
  'libxfixes'
  'libxft'
  'libxinerama'
  'libxml2'
  'lua'
  'ncurses' libncursesw.so
  'pango'
  'systemd-libs' libsystemd.so
  'wayland'
  'wireless_tools'
)
makedepends=(
  'catch2'
  'cmake'
  'docbook-xsl'
  'docbook2x'
  'git'
  'gperf'
  'man-db'
  'python-jinja'
  'python-yaml'
  'wayland-protocols'
)
source=(
  "git+$url.git#tag=v${pkgver}?signed"
  "$pkgname-use-system-cmake.patch"
)
sha512sums=('a754f5dcf90629fc2d2f602d76af1a224c201d7afe096554c5deeaa86d48a78b70b362770a54b2c90937d7d645e68c0c0bbb95fba2e37054bf21a1a3f2e92545'
            '22a675e38e0b4322e6ff48a0c089c60188118080de332186dd216456d3f8816d0b3dc761b9f55aa5f56f00d054fc65b25b06d21255dfc1e12ae0f6744bd87bd8')
b2sums=('b337d0030de73774d8b98f7cf55e0572b3b864ae10dd826076a4d87eb64c453a9efd2ff349ea38da45f329b348d305884da6b0a0af421425d755d739d3924714'
        '054b158ce1fd5796e0f5819710cc22caeafdb4d5a5a43eb5806cbe773a58afbaf6b0ef3935b613f1fc4cbe5ced67c3487781dbf0bb3e2e60a4a616c8c9c6981c')
validpgpkeys=(
  'AF310802C8EFA20499CF3556137B7AC2BDFD8DF0' # Brenden Matthews <github@brenden.brndn.io>
)

prepare() {
  cd ${pkgname}
  patch -Np1 -i ../$pkgname-use-system-cmake.patch
}

build() {
  cd ${pkgname}
  cmake \
    -S . \
    -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -D CMAKE_INSTALL_DATAROOTDIR=/usr/share \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -D MAINTAINER_MODE=OFF \
    -D BUILD_TESTS=ON \
    -D BUILD_DOCS=OFF \
    -D BUILD_EXTRAS=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_XDBE=ON \
    -D BUILD_XSHAPE=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D BUILD_PULSEAUDIO=ON \
    -D BUILD_JOURNAL=ON \
    -D BUILD_WAYLAND=ON \
    -D BUILD_LUA_CAIRO=ON \
    -D BUILD_LUA_IMLIB2=ON \
    -D BUILD_LUA_RSVG=ON \
    -D DOC_PATH=share/doc/$pkgname
  cmake --build build
}

check() {
  cd ${pkgname}
  ctest --test-dir build --output-on-failure
}

package() {
  cd ${pkgname}
  DESTDIR="${pkgdir}" cmake --install build
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 build/extras/vim/syntax/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/syntax
  install -vDm 644 extras/vim/ftdetect/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/ftdetect
}

# vim: ts=2 sw=2 et:
