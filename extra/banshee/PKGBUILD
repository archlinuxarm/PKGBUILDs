# $Id: PKGBUILD 37535 2009-05-02 20:36:11Z jgc $
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=banshee
pkgver=1.4.3
pkgrel=3
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee-project.org/"
license=('MIT')
depends=('sqlite3>=3.6.13' 'ndesk-dbus-glib>=0.4.1' 'mono-addins>=0.4' 'taglib-sharp>=2.0.3.2' 'gstreamer0.10-good-plugins' 'gstreamer0.10-base-plugins' 'gnome-sharp>=2.24.1' 'libmtp>=0.3.7' 'ipod-sharp>=0.8.1' 'boo' 'mono-zeroconf' 'libxxf86vm' 'gnome-icon-theme>=2.26.0' 'desktop-file-utils')
makedepends=('perlxml' 'pkgconfig' 'intltool>=0.35.0')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
	    'gstreamer0.10-bad-plugins: Extra media codecs'
	    'gstreamer0.10-ffmpeg: Extra media codecs'
	    'brasero: CD burning'
	    'notify-sharp-svn: Display Banshee tray icon')
options=('!libtool' '!makeflags' '!emptydirs')
install=banshee.install
source=(http://download.banshee-project.org/${pkgname}/${pkgname}-1-${pkgver}.tar.bz2)
md5sums=('880dee65d2bfe62be711a8edfe4d737f')
sh1sums=('3b137c1efe3eb8881da2e86210104eb292a7c560')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
  cd "${srcdir}/${pkgname}-1-${pkgver}"
  LD_PRELOAD="" ./configure --prefix=/usr \
        --sysconfdir=/etc --localstatedir=/var \
	--disable-docs --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
