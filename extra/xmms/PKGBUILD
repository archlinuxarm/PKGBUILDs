# $Id: PKGBUILD 134338 2011-08-02 23:29:16Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added --disable-simd, which builds for MMX instructions

pkgname=xmms
pkgver=1.2.11
pkgrel=7
pkgdesc="The X MultiMedia System - a multimedia player"
arch=('i686' 'x86_64')
url="http://legacy.xmms2.org/"
license=('GPL')
depends=('gtk' 'libxxf86vm' 'libsm' 'zlib' 'desktop-file-utils')
makedepends=('mesa' 'libmikmod' 'libvorbis' 'alsa-lib' 'libgl')
optdepends=('libmikmod: for the MikMod input plugin' \
            'libvorbis: for the Ogg Vorbis input plugin' \
            'alsa-lib: for the alsa output plugin' \
            'libgl: for the OpenGL Spectrum analyser visualization plugin')
install=xmms.install
source=(http://legacy.xmms2.org/${pkgname}-${pkgver}.tar.bz2 \
	xmms-menu.patch xmms-1.2.10-crossfade-0.3.9.patch xmms-fix.diff \
        xmms-1.2.11-CVE-2007-0653.0654.patch xmms-1.2.11-fix-http-title-mpg123.patch \
        xmms-1.2.11-3dse.patch xmms-1.2.10-recode-id3.patch xmms-1.2.11-fix-mikmod-driver.patch \
        xmms-1.2.10-fonts.patch xmms.16.png xmms.32.png xmms.48.png)
sha1sums=('9d1eae4baab25fd35c9ddfb49321ca60222f672d'
          '27fb6e21bd6db923a425a8df8bd07a2a93867ddb'
          'ff5c172752c8bee708d9a9ecd53864cc95a4bcc5'
          'c227284955f2cc16f027dade670155c0f0fbc4ff'
          '4ef47c34b27b6c868d4301b7a078b257c8604471'
          '0c060385561ea2a4b1c7ad3bd35d3646ae9554f5'
          '6f6db13c780ba4cbddb8613f0abd2db2823f3021'
          '2a3b1bf17054b6bd8b1e5937de5c0ad49af8dc92'
          '155e11b344af1ba594b4e339640bcca37c8909af'
          '31abd37c9c57bd13b2088eb11a1e54e5aa312bf3'
          '0089d11aa3bce67f3496592d68ee65837406e220'
          'f24071ae484d9b55e61dba076801fa8e7976ff2f'
          '485c67cb8c8d4678bb3b0b280d2e233b14153b22')

prepare() {
  cd ${pkgname}-${pkgver}
# Fix xmms.desktop
  patch -p0 < ../xmms-menu.patch
# Patch for hang with xmms-crossfade 
  patch -p1 < ../xmms-1.2.10-crossfade-0.3.9.patch
# fix xmms not able to be shown double size,at least needed for x86_64
  patch -p1 < ../xmms-fix.diff
# Several patches from Mandriva
  patch -p1 < ../xmms-1.2.11-fix-http-title-mpg123.patch
  patch -p0 < ../xmms-1.2.10-recode-id3.patch
  patch -p1 < ../xmms-1.2.11-3dse.patch
  patch -p1 < ../xmms-1.2.10-fonts.patch
  patch -p1 < ../xmms-1.2.11-CVE-2007-0653.0654.patch
  patch -p1 < ../xmms-1.2.11-fix-mikmod-driver.patch
}

build() {
  cd ${pkgname}-${pkgver}
  if [ "$CARCH" == "x86_64" ]; then
     SIMDOPT="--disable-simd"
  elif [ "$CARCH" == "i686" ]; then
     SIMDOPT="--enable-simd"
  else
     SIMDOPT="--disable-simd"
  fi
  ./configure --prefix=/usr --mandir=/usr/share/man $SIMDOPT
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 xmms/xmms.desktop "${pkgdir}/usr/share/applications/xmms.desktop"
  install -D -m644 xmms/xmms_mini.xpm "${pkgdir}/usr/share/pixmaps/xmms_mini.xpm"
  install -D -m644 ../xmms.16.png "${pkgdir}/usr/share/pixmaps/xmms16.png"
  install -D -m644 ../xmms.32.png "${pkgdir}/usr/share/pixmaps/xmms32.png"
  install -D -m644 ../xmms.48.png "${pkgdir}/usr/share/pixmaps/xmms48.png"
  ln -s /usr/share/pixmaps/xmms48.png "${pkgdir}/usr/share/pixmaps/xmms.png"

  # don't want wmxmms
  rm -rf "${pkgdir}/usr/bin/wmxmms" "${pkgdir}/usr/share/xmms"
  rm -f "${pkgdir}"/usr/share/man/man1/{gnomexmms.1,wmxmms.1}
}
