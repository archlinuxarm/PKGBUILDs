# $Id: PKGBUILD 24698 2009-01-20 04:18:32Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=xmms
pkgver=1.2.11
pkgrel=2
pkgdesc="The X MultiMedia System"
arch=('i686' 'x86_64')
url="http://www.xmms.org"
license=('GPL')
depends=('libsm' 'libxxf86vm' 'zlib' 'gtk' 'libvorbis' 'esound' 'alsa-lib' 'libgl')
makedepends=('mesa')
options=('!libtool')
source=(http://www.xmms.org/files/1.2.x/${pkgname}-${pkgver}.tar.bz2 \
	xmms-menu.patch xmms-1.2.10-crossfade-0.3.9.patch xmms-fix.diff \
        xmms-1.2.11-CVE-2007-0653.0654.patch xmms-1.2.11-fix-http-title-mpg123.patch \
        xmms-1.2.11-3dse.patch xmms-1.2.10-recode-id3.patch \
        xmms-1.2.10-fonts.patch xmms.16.png xmms.32.png xmms.48.png)
md5sums=('f3e6dbaf0b3f571a532ab575656be506' 'b7b6f1811bad8d14e4c4534240e45980'\
         '4eb222111a82a2e1ebe63b5f6109133d' 'd7f9dc3712c486658149c4504f7478a7'\
         'e59e8c66f4e4077a8096ad98c77f0eb6' '25609a6b5b7632e4c8e6de927f8d70db'\
         '2b534832b0dbb0d06dbcc829e2b702e6' 'd1715c291d35ee8854e5d926912e8b0d'\
         '408ea5e28c4c4c221cf3c8ded76fd1f0' '3c328565c99c85d92ac0b9918805da46'\
         '128406a02c07cd477dc901f2a658add5' 'd022280df4379383732a8a26abea1d08')
sha1sums=('9d1eae4baab25fd35c9ddfb49321ca60222f672d'
          '27fb6e21bd6db923a425a8df8bd07a2a93867ddb'
          'ff5c172752c8bee708d9a9ecd53864cc95a4bcc5'
          'c227284955f2cc16f027dade670155c0f0fbc4ff'
          '4ef47c34b27b6c868d4301b7a078b257c8604471'
          '0c060385561ea2a4b1c7ad3bd35d3646ae9554f5'
          '6f6db13c780ba4cbddb8613f0abd2db2823f3021'
          '2a3b1bf17054b6bd8b1e5937de5c0ad49af8dc92'
          '31abd37c9c57bd13b2088eb11a1e54e5aa312bf3'
          '0089d11aa3bce67f3496592d68ee65837406e220'
          'f24071ae484d9b55e61dba076801fa8e7976ff2f'
          '485c67cb8c8d4678bb3b0b280d2e233b14153b22')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

# Fix xmms.desktop
  patch -p0 < ../xmms-menu.patch || return 1
# Patch for hang with xmms-crossfade 
  patch -p1 < ../xmms-1.2.10-crossfade-0.3.9.patch || return 1
# fix xmms not able to be shown double size,at least needed for x86_64
  patch -p1 < ../xmms-fix.diff || return 1
# Several patches from Mandriva
  patch -p1 < ../xmms-1.2.11-fix-http-title-mpg123.patch || return 1
  patch -p0 < ../xmms-1.2.10-recode-id3.patch || return 1
  patch -p1 < ../xmms-1.2.11-3dse.patch || return 1
  patch -p1 < ../xmms-1.2.10-fonts.patch || return 1
  patch -p1 < ../xmms-1.2.11-CVE-2007-0653.0654.patch || return 1

  if [ "$CARCH" == "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --disable-mikmod --disable-simd || return 1 # for x86_64
  else 
    ./configure --prefix=/usr --mandir=/usr/share/man --disable-mikmod --enable-simd || return 1 # for i686
  fi
  make || return 1
  make DESTDIR=${pkgdir} install  || return 1

  install -D -m644 xmms/xmms.desktop ${pkgdir}/usr/share/applications/xmms.desktop
  install -D -m644 xmms/xmms_mini.xpm ${pkgdir}/usr/share/pixmaps/xmms_mini.xpm
  install -D -m644 ../xmms.16.png ${pkgdir}/usr/share/pixmaps/xmms16.png
  install -D -m644 ../xmms.32.png ${pkgdir}/usr/share/pixmaps/xmms32.png
  install -D -m644 ../xmms.48.png ${pkgdir}/usr/share/pixmaps/xmms48.png
  ln -s /usr/share/pixmaps/xmms48.png ${pkgdir}/usr/share/pixmaps/xmms.png

  # don't want wmxmms
  rm -rf ${pkgdir}/usr/bin/wmxmms ${pkgdir}/usr/share/xmms
  rm -f ${pkgdir}/usr/share/man/man1/{gnomexmms.1,wmxmms.1}
}
