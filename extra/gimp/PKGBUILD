# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix GEGL_MAJOR_MINOR_VERSION substitution

pkgname=gimp
pkgver=2.10.0
pkgrel=5.1
pkgdesc='GNU Image Manipulation Program'
url='https://www.gimp.org/'
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('pygtk' 'lcms' 'libxpm' 'libwmf' 'libxmu' 'librsvg' 'libmng' 'dbus-glib' 'libwebp'
         'libexif' 'gegl>=0.4' 'jasper' 'desktop-file-utils' 'hicolor-icon-theme' 'babl'
         'openexr' 'libgudev' 'poppler-data' 'libmypaint' 'mypaint-brushes' 'glib-networking')
makedepends=('intltool' 'poppler-glib' 'alsa-lib' 'iso-codes' 'curl' 'ghostscript' 'gtk-doc')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support')
source=(https://download.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        0001-do-not-hardcode-gegl-version.patch
        https://github.com/GNOME/gimp/commit/245143532325783fb3414a74cbf58adc8a3199e8.patch
        linux.gpl
        uri-backend-libcurl.patch)
sha256sums=('7fcc96fb88cb0a0595d2610f63a15dec245bb37bf9db527d37a24fb75e547de2'
            '1a52374e5db39d798297159afe2a9ea87460521c5f18a9c99729a9f5e25c4deb'
            '91a9a63f42791804be1bc60156ef348a93d5f6d35c86b4c50a76380886228bc8'
            '1003bbf5fc292d0d63be44562f46506f7b2ca5729770da9d38d3bb2e8a2f36b3'
            '0708a971455fba6406bfa190652797c6620c789a9d21012c57249d2d4878bd55')

prepare() {
  cd ${pkgname}-${pkgver}
  _mypaintver=$(echo /usr/lib/libmypaint-*.so | grep -o -E '\-[0-9]+(\.[0-9]+)*' | head -1)
  sed -i "s|\\(libmypaint\\)\\( >= libmypaint_required_version\\)|\\1${_mypaintver}\\2|g" configure.ac
  patch -Np1 < ../0001-do-not-hardcode-gegl-version.patch
  patch -Np1 < ../245143532325783fb3414a74cbf58adc8a3199e8.patch
  autoreconf -vi
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-mp \
    --enable-gimp-console \
    --enable-gimp-remote \
    --enable-python \
    --enable-gtk-doc \
    --with-gif-compression=lzw \
    --with-libcurl \
    --without-aa \
    --without-hal \
    --without-gvfs \
    --without-gnomevfs
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}

# vim: ts=2 sw=2 et:
