# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch for AArch64

pkgname=jemalloc
epoch=1
pkgver=5.0.1
pkgrel=4
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('x86_64')
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('glibc')
provides=('libjemalloc.so')
optdepends=('perl: for jeprof')
source=("https://github.com/jemalloc/jemalloc/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        '0001-Add-the-mtls-dialect-gnu2-to-compile-flags.patch'
        'https://github.com/jemalloc/jemalloc/commit/8cfc9dec37b312a2686f602bbcdd102ca07cca99.patch')
sha256sums=('4814781d395b0ef093b21a08e8e6e0bd3dab8762f9935bbfb71679b0dea7c3e9'
            'ac1570ab81c6545b8e994eab02239af67acdce682656c77802d090b0809176bf'
            '2a339806cb6366e31d26f4fbaed0d0c4e3b19a99479b15e74fd3580c70946774')

prepare() {
  cd $pkgname-$pkgver
  [[ $CARCH != "aarch64" ]] && patch -p1 -i "$srcdir/0001-Add-the-mtls-dialect-gnu2-to-compile-flags.patch"
  patch -p1 -i ../8cfc9dec37b312a2686f602bbcdd102ca07cca99.patch
  autoconf
}

build() {
  cd $pkgname-$pkgver

  ./configure --enable-autogen --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  chmod 644 "$pkgdir/usr/lib/libjemalloc_pic.a"
}
