# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build tests, unsupported syscall on 32-bit ARM

pkgname=liburing
pkgver=2.1
pkgrel=1
pkgdesc="Linux-native io_uring I/O access library"
arch=('x86_64')
url="https://git.kernel.dk/cgit/liburing/"
license=('LGPL2.1' 'MIT')
depends=('glibc')
makedepends=('git')
provides=('liburing.so')
source=("git+https://git.kernel.dk/liburing#tag=liburing-$pkgver?signed")
sha512sums=('SKIP')
validpgpkeys=('C0FC392DCADE26D975FA5E4AF7D358FB2971E0A6') # Jens Axboe <axboe@kernel.dk>

build() {
  cd "$pkgname"
  sed -i '/-C test$/d' Makefile
  ./configure --prefix='/usr' \
              --mandir='/usr/share/man'
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
