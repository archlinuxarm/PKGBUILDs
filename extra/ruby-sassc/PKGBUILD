# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Anatol Pomozov

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - generalize arch-dependent link line for libsass.so

_name=sassc
pkgname=ruby-$_name
pkgver=2.4.0
pkgrel=7
pkgdesc="Use libsass with Ruby!"
arch=(x86_64)
url='https://github.com/sass/sassc-ruby'
license=(MIT)
depends=(ruby ruby-erb ruby-ffi)
makedepends=(git)
options=(!emptydirs)
source=(
  $pkgname::git+$url#tag=v$pkgver
  libsass::git+https://github.com/sass/libsass.git
)
sha256sums=('f2eb7ab00afba92187b899d130ddfabeaafafd839ecdf7b7129c443fc36d4247'
            'SKIP')


prepare() {
  cd $pkgname

  git submodule init

  git config submodule."ext/libsass".url "${srcdir}/libsass"

  git -c protocol.file.allow=always submodule update
}

build() {
  cd $pkgname

  local _gemdir="$(gem env gemdir)"

  gem build "${_name}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --build-root "tmp_install" \
    "${_name}-${pkgver}.gem"

  # this gem looks in the wrong place for its library, move it to the right place
  ln tmp_install/${_gemdir}/extensions/*-linux*/3.4.0/sassc-2.4.0/sassc/libsass.so \
    "tmp_install/${_gemdir}/gems/sassc-2.4.0/lib/sassc/libsass.so"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_name}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_name}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd $pkgname

  local _gemdir="$(gem env gemdir)"

  # missing dependencies to run tests, test import instead
  GEM_HOME="tmp_install/${_gemdir}" ruby -e 'require "sassc"; puts SassC::VERSION'
}

package() {
  cd $pkgname

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.txt --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
}
