# Maintainer: Maxime Gauduin <alucrydarchlinux.org>
# Contributor: Padfoot <padfoot@exemail.com.au>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora to fix ARM FTBFS

pkgname=lightdm-gtk-greeter
pkgver=2.0.6
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gobject-introspection' 'intltool')
replaces=('lightdm-gtk2-greeter' 'lightdm-gtk3-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/lightdm-gtk-greeter/${pkgver%.?}/${pkgver}/+download/lightdm-gtk-greeter-${pkgver}.tar.gz"
        'fix_arm_compile.patch')
sha256sums=('255cf4039ab2536020ca8714bdc98300c8f71719183bbb5b18ef96f43c2e3ade'
            'bb9f6f0dbca0828076e556a2c2fa1c62c0237d7b6569c6a73e2d0c4269ffcbab')

prepare() {
  cd lightdm-gtk-greeter-${pkgver}

  patch -p1 -i ../fix_arm_compile.patch
}

build() {
  cd lightdm-gtk-greeter-${pkgver}

  export CFLAGS="$CFLAGS -Wno-declaration-after-statement"

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lightdm' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --with-libxklavier \
    --enable-kill-on-sigterm \
    --disable-libido \
    --disable-libindicator \
    --disable-static
  make
}

package() {
  cd lightdm-gtk-greeter-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
