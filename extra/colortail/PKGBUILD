# $Id: PKGBUILD 49920 2009-08-16 21:31:38Z jgc $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Mauricio Barrera <damage@damage.zero.org>

pkgname=colortail
pkgver=0.3.0
pkgrel=3
pkgdesc="Log colorizer to make log checking easier"
arch=(i686 x86_64)
license=('GPL')
url="http://joakimandersson.se/projects/colortail/"
depends=('gcc-libs>=4.4.1')
source=(http://joakimandersson.se/files/${pkgname}-${pkgver}.tar.gz
        http://joakimandersson.se/files/${pkgname}-${pkgver}-gcc3.patch.gz
        colortail-0.3.0-gcc4.4.patch)
md5sums=('2589d3e372080f4052d1cc0d6550508f'
         '504b3c31dde38bba11e2ee407e3225c4'
         'd5093db5a9016544064f94045e862608')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-gcc3.patch" || return 1
  patch -Np1 -i "${srcdir}/colortail-0.3.0-gcc4.4.patch" || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/etc/colortail"
  install -m644 example-conf/conf.messages "${pkgdir}/etc/colortail/" || return 1
}
