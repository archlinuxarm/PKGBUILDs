# $Id: PKGBUILD 45403 2009-07-11 01:25:33Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Eric Johnson <eric@coding-zone.com>

pkgname=pathological
pkgver=1.1.3
pkgrel=5
pkgdesc="A puzzle game with the same feel as frozen bubble"
arch=('i686' 'x86_64')
url="http://pathological.sourceforge.net/"
license=('GPL')
depends=('python-pygame')
makedepends=('netpbm')
install=pathological.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        pathological.desktop pygame_181.patch encoding.patch)
md5sums=('76a446080c0fed12baf39354d8e0ce4a' '0fbf64860efe3d287eb74681758c4f75'\
         'bcd59890efe29a9f8c83227656c31a3e' '06d9221d0d8adaf232a3766870a8bcc0')
sha1sums=('70b1c9e12704c7fd3b301aa90bceee2e8373b7b9'
          '9b57f854ce9621542d810a21c61325de343950d7'
          '6f0aa0f74a87bd53771f2db6d3117981c4f070d3'
          '7dbaa9ced7da0094280425a68b4c8db3382f230a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../encoding.patch || return 1
  patch -p1 < ../pygame_181.patch || return 1
  sed -i 's|X11R6/include/X11|share|' Makefile || return 1
  sed -i 's|usr/games|usr/bin|' Makefile || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 ../pathological.desktop "${pkgdir}/usr/share/applications/pathological.desktop"

  chown root:games "${pkgdir}/var/games"
  chmod 775 "${pkgdir}/var/games"
  rm "${pkgdir}/var/games/pathological_scores"
}
