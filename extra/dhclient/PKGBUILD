#$Id: PKGBUILD 46906 2009-07-21 15:28:15Z daniel $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Mirko Messer <mirk@chao.ch>
pkgname=dhclient 
pkgver=3.1.2p1
pkgrel=3
pkgdesc="dhclient is standalone client from the dhcp package"
arch=(i686 x86_64)
url="http://www.isc.org/index.pl?/sw/dhcp/"
license=('custom')
depends=('bash')
options=('!makeflags')
source=(ftp://ftp.isc.org/isc/dhcp/dhcp-${pkgver}.tar.gz \
	fix-mac80211-wireless.patch
	site.conf)
md5sums=('787b2924a1965f0d8558521b36dca73d'
         'c7b3e5e4e7a5ece9b4f351f00092e240'
         '2e6a845b18bcd188caeead3e47e91151')

build() {
  cd ${startdir}/src/dhcp-${pkgver}
  patch -Np1 -i ${startdir}/src/fix-mac80211-wireless.patch || return 1

  cp ${srcdir}/site.conf .
  ./configure

  #only building dhclient, no server
  sed -i -e 's/\(server\|relay\|dhcpctl\)/ /g' work.*/Makefile

  make || return 1
  make DESTDIR=${startdir}/pkg install
  mkdir -p ${startdir}/pkg/usr/share/licenses/${pkgname}
  install -m644 ${startdir}/src/dhcp-${pkgver}/LICENSE \
                ${startdir}/pkg/usr/share/licenses/${pkgname}/

  install -m755 -d ${startdir}/pkg/var/lib/dhclient

  #not needed after fresh install
  rm -f ${pkgdir}/var/lib/dhclient/dhcpd.leases
  #deleting file conflicts to dhcpd
  rm -rf ${pkgdir}/usr/{include,lib}
  rm -rf ${pkgdir}/usr/share/man/man3
  rm -rf ${pkgdir}/usr/share/man/man5/dhcp-eval.5
  rm -rf ${pkgdir}/usr/share/man/man5/dhcp-options.5

}
