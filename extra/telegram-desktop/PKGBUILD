# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: hexchain <i@hexchain.org>

# ALARM: John <therealgraysky AT proton DOT me>
# - For compatibility with linux-rpi-16k, add jemalloc as dep
#   see: https://archlinuxarm.org/forum/viewtopic.php?f=15

pkgname=telegram-desktop
pkgver=6.1.3
pkgrel=4
pkgdesc='Official Telegram Desktop client'
arch=('x86_64')
url="https://desktop.telegram.org/"
license=('GPL-3.0-or-later WITH OpenSSL-exception')
depends=(
  'abseil-cpp'
  'ada'
  'ffmpeg'
  'glib2'
  'hicolor-icon-theme'
  'hunspell'
  'jemalloc'
  'kcoreaddons'
  'libavif'
  'libdispatch'
  'libheif'
  'libjxl'
  'libxcomposite'
  'libxdamage'
  'libxrandr'
  'libxtst'
  'lz4'
  'minizip'
  'openal'
  'openh264'
  'openssl'
  'pipewire'
  'protobuf'
  'qt6-imageformats'
  'qt6-svg'
  'qt6-wayland'
  'rnnoise'
  'xxhash'
)
makedepends=(
  'boost'
  'cmake'
  'git'
  'glib2-devel'
  'gobject-introspection'
  'gperf'
  'libtg_owt'
  'microsoft-gsl'
  'ninja'
  'python'
  'range-v3'
  'tl-expected'
)
optdepends=(
  'geoclue: geoinformation support'
  'geocode-glib-2: geocoding support'
  'webkit2gtk-4.1: embedded browser features provided by webkit2gtk-4.1'
  'webkitgtk-6.0: embedded browser features provided by webkitgtk-6.0 (Wayland only)'
  'xdg-desktop-portal: desktop integration'
)
install=telegram-desktop.install
_td_commit=6d74326c5ce53aeb52496f157f0080d9b8515970
source=(
  "https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tdesktop-${pkgver}-full.tar.gz"
  "git+https://github.com/tdlib/td.git#tag=${_td_commit}"
  glib2.86.patch
  0001-Fix-compatibility-with-ffmpeg-8.patch
  https://github.com/desktop-app/cmake_helpers/commit/682f1b57.patch
  https://github.com/telegramdesktop/tdesktop/commit/28d19a99.patch
)
sha512sums=('951fbf4a9e0a5929130614aefdf2ac18d8d8e65b2d8d4d55da857136f17114e6a17677db9bb0585cfed121c3fe73e13b4db47b6da30fca7c276f33127eb3dba7'
            '6dc6e684a0bf35bb83f6fa6579a0da82d604190b222f2cd2de9b8ef5b93f5f18ac9a8733e2c5cf2a64ed9933b346ea31e26a4bcc0039956280ec2deef9649457'
            'd1651683e0fecb43e37d5d01d75ae4b42fc1cfbf1d96eee42d37eb901b7d0cc22c4bc21b828dad2f4b40abe26923ed11f8c44dc759f969ca7efdc48280da9e23'
            'e7cdad88fb82f1bd519b46d45a77a487219dc46434cc02fa422e00302d0156de201021f83bdea24e6ec47d4393c7e74bd4cdb16adfd8f3774468c3ba11544e2c'
            '9a0632b5c4742a36774ea937fc36ab139c6c3aadf49ff4b6ab6a088da9ca1555852c50b3a687acce107e62e7ee1ea1c81f70301d0922539b5da5bfb5f642c468'
            '1c4f637c92cfe95c7cd892a745790bc5e939f303c7a7a6624956093e8aa020f2ff3d4993a6d5e8ea4f783f7fe38be4b6ada59256bb6a12dac0f2a250f99b4e34')

prepare() {
  # Fix build with glib2 2.86
  patch -d "tdesktop-$pkgver-full" -Np1 -i ../glib2.86.patch
  # Fix compatibility with ffmpeg 8
  patch -d "tdesktop-$pkgver-full" -Np1 -i ../0001-Fix-compatibility-with-ffmpeg-8.patch
  # Fix build with Qt 6.10
  patch -d "tdesktop-$pkgver-full/cmake" -p1 < 682f1b57.patch
  head -n -7 28d19a99.patch | patch -d "tdesktop-$pkgver-full" -p1
}

build() {
  CXXFLAGS+=' -ffat-lto-objects'

  cmake -S td -B td/build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX="$PWD/td/install" \
    -Wno-dev \
    -DTD_E2E_ONLY=ON
  cmake --build td/build
  cmake --install td/build

  # Turns out we're allowed to use the official API key that telegram uses for
  # their snap builds:
  # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
  # Thanks @primeos!
  cmake -B build -S tdesktop-$pkgver-full -G Ninja \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -Dtde2e_DIR="$PWD/td/install/lib/cmake/tde2e" \
    -DCMAKE_BUILD_TYPE=Release \
    -DTDESKTOP_API_ID=611335 \
    -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
