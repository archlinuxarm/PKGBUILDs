# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
# - remove makedepend on python-pypandoc

pkgname=python-baron
pkgver=0.10.1
pkgrel=1
pkgdesc="Full Syntax Tree for python to make writing refactoring code a realist task"
url="https://github.com/PyCQA/baron"
license=('LGPL')
arch=('any')
depends=('python-rply')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/baron/archive/$pkgver.tar.gz")
sha512sums=('01c3a69227d1de435dc466bc8e848e9d3d88f182cb404c2614bf868615af92a81c6379c872c36b9f28e63e0cec5757a5a8f6e9bb11eeccb9a75a408199448626')

build() {
  cd baron-$pkgver
  python setup.py build
}

check() {
  cd baron-$pkgver
  python setup.py pytest
}

package() {
  cd baron-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
