# Maintainer: David Runge <dvzrv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DWOLFSSL_SHA3=ON, required and not set by default for v7

pkgname=wolfssl
pkgver=5.6.0
pkgrel=1
pkgdesc="Lightweight, portable, C-language-based SSL/TLS library"
arch=(x86_64)
url="https://www.wolfssl.com/"
license=(GPL2)
depends=(glibc)
makedepends=(cmake)
provides=(libwolfssl.so)
source=(
  $pkgname-$pkgver-stable.tar.gz::https://github.com/$pkgname/$pkgname/archive/refs/tags/v$pkgver-stable.tar.gz
  https://github.com/$pkgname/$pkgname/releases/download/v$pkgver-stable/$pkgname-$pkgver-stable.tar.gz.asc
)
sha512sums=('1a7c9dbfe884b6ed22f3f1bf45f36576fcd1bbaec581369043d7200593b28fad16e1ee55df56bce892b02bdafde72d0d75f6df711ffafcbf51219eabe06ec9f8'
            'SKIP')
b2sums=('2fd62c22f336ecc8d6e2c5349ce20ed7d447297b19771726d5a466ccb20a9306622f37344a87e730eb0b3379ec99878ffce540f3e8e9f2d62e47cbabf7f2153c'
        'SKIP')
validpgpkeys=(
  A2A48E7BCB96C5BECB987314EBC80E415CA29677  # wolfSSL <secure@wolfssl.com>
)

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -DWOLFSSL_CURVE25519=ON
    -DWOLFSSL_CURVE448=ON
    -DWOLFSSL_ED25519=ON
    -DWOLFSSL_ED448=ON
    -DWOLFSSL_SHA3=ON
    -DWOLFSSL_REPRODUCIBLE_BUILD=ON
    -DWARNING_C_FLAGS="$CFLAGS"
    -Wno-dev
    -B build
    -S $pkgname-$pkgver-stable
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver-stable/{README,ChangeLog}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
