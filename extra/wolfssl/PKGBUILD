# Maintainer: David Runge <dvzrv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DWOLFSSL_SHA3=ON, required and not set by default for v7

# WARNING: This upstream **does not** guarantee ABI stability. Expect soname changes in patch-level version bumps (https://github.com/wolfSSL/wolfssl/issues/6928)
pkgname=wolfssl
pkgver=5.7.4
pkgrel=1
pkgdesc="Lightweight, portable, C-language-based SSL/TLS library"
arch=(x86_64)
url="https://www.wolfssl.com/"
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(cmake)
provides=(libwolfssl.so)
source=(
  $pkgname-$pkgver-stable.tar.gz::https://github.com/$pkgname/$pkgname/archive/refs/tags/v$pkgver-stable.tar.gz
  https://github.com/$pkgname/$pkgname/releases/download/v$pkgver-stable/$pkgname-$pkgver-stable.tar.gz.asc
)
sha512sums=('ad6a8c8635d7d3355cb4fb70dac31d405976312864f383e83597b3653fb6cde614a4f6427ebe76015fc0eade9741cb9ac4a9bc9d8fa55d97939052c0f7246114'
            'SKIP')
b2sums=('172cbe46881119be255fbc290be176563a69e6ea1610376f81e02074a500a5ce5c18590f9fc2135370d4a90b669b6333d86dee3360668829e93efab2e7d930ec'
        'SKIP')
validpgpkeys=(
  A2A48E7BCB96C5BECB987314EBC80E415CA29677  # wolfSSL <secure@wolfssl.com>
)

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D WOLFSSL_CURVE25519=ON
    -D WOLFSSL_CURVE448=ON
    -D WOLFSSL_ED25519=ON
    -D WOLFSSL_ED448=ON
    -D WOLFSSL_SHA3=ON
    -D WOLFSSL_REPRODUCIBLE_BUILD=ON
    -D WOLFSSL_SNI=ON
    -D WARNING_C_FLAGS="$CFLAGS"
    -W no-dev
    -S $pkgname-$pkgver-stable
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cd build
  ./wolfcrypt/test/testwolfcrypt
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver-stable/{README,ChangeLog}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
