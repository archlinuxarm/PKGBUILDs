# Maintainer: David Runge <dvzrv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DWOLFSSL_SHA3=ON, required and not set by default for v7

pkgname=wolfssl
pkgver=5.6.3
pkgrel=3
pkgdesc="Lightweight, portable, C-language-based SSL/TLS library"
arch=(x86_64)
url="https://www.wolfssl.com/"
license=(GPL2)
depends=(glibc)
makedepends=(cmake)
provides=(libwolfssl.so)
source=(
  $pkgname-$pkgver-stable.tar.gz::https://github.com/$pkgname/$pkgname/archive/refs/tags/v$pkgver-stable.tar.gz
  https://github.com/$pkgname/$pkgname/releases/download/v$pkgver-stable/$pkgname-$pkgver-stable.tar.gz.asc
)
sha512sums=('7c3b5c06ade7826031db089d50ec45c36970555499b6be94d1939b3002369fc98e503418128c6b2e253df7e92f86a71647d37f2cd27d57ac201e5aba32b879dc'
            'SKIP')
b2sums=('b53f5d4c5457372564636b4b9edae1116889d5033ee111f2d8eed05469e9372b4ac286ce1fa4c872f14b2a74ab1715e6726b09940068fc3219f8dbac68a5e020'
        'SKIP')
validpgpkeys=(
  A2A48E7BCB96C5BECB987314EBC80E415CA29677  # wolfSSL <secure@wolfssl.com>
)

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D WOLFSSL_CURVE25519=ON
    -D WOLFSSL_CURVE448=ON
    -D WOLFSSL_ED25519=ON
    -D WOLFSSL_ED448=ON
    -D WOLFSSL_SHA3=ON
    -D WOLFSSL_REPRODUCIBLE_BUILD=ON
    -D WOLFSSL_SNI=ON
    -D WARNING_C_FLAGS="$CFLAGS"
    -W no-dev
    -S $pkgname-$pkgver-stable
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cd build
  ./wolfcrypt/test/testwolfcrypt
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver-stable/{README,ChangeLog}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
