# Maintainer: David Runge <dvzrv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DWOLFSSL_SHA3=ON, required and not set by default for v7

# WARNING: This upstream **does not** guarantee ABI stability. Expect soname changes in patch-level version bumps (https://github.com/wolfSSL/wolfssl/issues/6928)
pkgname=wolfssl
pkgver=5.6.6
pkgrel=1
pkgdesc="Lightweight, portable, C-language-based SSL/TLS library"
arch=(x86_64)
url="https://www.wolfssl.com/"
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(cmake)
provides=(libwolfssl.so)
source=(
  $pkgname-$pkgver-stable.tar.gz::https://github.com/$pkgname/$pkgname/archive/refs/tags/v$pkgver-stable.tar.gz
  https://github.com/$pkgname/$pkgname/releases/download/v$pkgver-stable/$pkgname-$pkgver-stable.tar.gz.asc
)
sha512sums=('d664ab1cd84d7c33d1b34eb934843292b7ffc07b922b4c483b45deeeeda5c425b673640e3049f731bfff364bd7e7c0c14e236afce36622a7d03242d3be0c7382'
            'SKIP')
b2sums=('05113b3a9a6c4b664f6c200b8ae5359615e056da74cc8dd06185f281319b4d027faa8613dc641236cd46d8ddd6169773ed23132ff17467eeb5cee7a910e7b382'
        'SKIP')
validpgpkeys=(
  A2A48E7BCB96C5BECB987314EBC80E415CA29677  # wolfSSL <secure@wolfssl.com>
)

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D WOLFSSL_CURVE25519=ON
    -D WOLFSSL_CURVE448=ON
    -D WOLFSSL_ED25519=ON
    -D WOLFSSL_ED448=ON
    -D WOLFSSL_SHA3=ON
    -D WOLFSSL_REPRODUCIBLE_BUILD=ON
    -D WOLFSSL_SNI=ON
    -D WARNING_C_FLAGS="$CFLAGS"
    -W no-dev
    -S $pkgname-$pkgver-stable
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cd build
  ./wolfcrypt/test/testwolfcrypt
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver-stable/{README,ChangeLog}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
