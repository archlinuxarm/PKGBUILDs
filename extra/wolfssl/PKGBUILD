# Maintainer: David Runge <dvzrv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DWOLFSSL_SHA3=ON, required and not set by default for v7

pkgname=wolfssl
pkgver=5.6.2
pkgrel=1
pkgdesc="Lightweight, portable, C-language-based SSL/TLS library"
arch=(x86_64)
url="https://www.wolfssl.com/"
license=(GPL2)
depends=(glibc)
makedepends=(cmake)
provides=(libwolfssl.so)
source=(
  $pkgname-$pkgver-stable.tar.gz::https://github.com/$pkgname/$pkgname/archive/refs/tags/v$pkgver-stable.tar.gz
  https://github.com/$pkgname/$pkgname/releases/download/v$pkgver-stable/$pkgname-$pkgver-stable.tar.gz.asc
)
sha512sums=('67074f551c45b53f681730a263d1c2d57fb66eded66f20eb5e3e9160027434ccc459213753150f77e31a182097dd634c6ce0e55ca5c68dac987958435ed8d989'
            'SKIP')
b2sums=('984aab80247564133482dbb9a0f511442d122ba04be34a1dc6f3ef92240b97015cd6fd2c61da991468034ae6690a134171ff7663b8df1d28dd76abca5532dc4d'
        'SKIP')
validpgpkeys=(
  A2A48E7BCB96C5BECB987314EBC80E415CA29677  # wolfSSL <secure@wolfssl.com>
)

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -DWOLFSSL_CURVE25519=ON
    -DWOLFSSL_CURVE448=ON
    -DWOLFSSL_ED25519=ON
    -DWOLFSSL_ED448=ON
    -DWOLFSSL_SHA3=ON
    -DWOLFSSL_REPRODUCIBLE_BUILD=ON
    -DWARNING_C_FLAGS="$CFLAGS"
    -Wno-dev
    -B build
    -S $pkgname-$pkgver-stable
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver-stable/{README,ChangeLog}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
