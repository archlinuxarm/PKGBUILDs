# $Id: PKGBUILD 35617 2009-04-14 13:14:24Z hugo $
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=proftpd
pkgver=1.3.2
pkgrel=4
pkgdesc="A high-performance, scalable FTP server"
arch=('i686' 'x86_64')
url="http://www.proftpd.org"
license=('GPL')
depends=('glibc' 'pam' 'ncurses' 'libcap>=2.16')
backup=('etc/proftpd.conf' 'etc/conf.d/proftpd')
source=(ftp://ftp.proftpd.org/distrib/source/$pkgname-$pkgver.tar.bz2 proftpd \
        proftpd.logrotate proftpd.conf.d)
md5sums=('89f5e31fc3d3e02b66424dfc6cc5892d'
         '99f6f9a989e70e3fa50809fc2bbbbb0a'
         'ddb09eb13131becdf0e081eef413116b'
         '71d5932b0461c318ed68c2c0c2660736')

build() {
  cd $startdir/src/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-pam \
    --with-modules=mod_quotatab:mod_quotatab_file:mod_tls \
    --sysconfdir=/etc --localstatedir=/var/run --enable-ctrls
  make || return 1
  make DESTDIR=$startdir/pkg install
  install -D -m644 ../proftpd.logrotate $startdir/pkg/etc/logrotate.d/proftpd
  install -D -m644 ../proftpd.conf.d $startdir/pkg/etc/conf.d/proftpd
  install -D -m755 ../proftpd ${startdir}/pkg/etc/rc.d/proftpd
  cd $startdir/pkg/etc
  sed -i 's|nogroup|nobody|g' proftpd.conf
  rmdir $startdir/pkg/usr/{libexec,share/locale}
}
