# $Id: PKGBUILD 30790 2009-03-21 23:29:42Z eric $
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>

pkgname=fortune-mod
pkgver=1.99.1
pkgrel=4
pkgdesc="The Fortune Cookie Program from BSD games"
arch=('i686' 'x86_64')
url="http://www.redellipse.net/code/fortune"
license=('BSD')
depends=('recode')
source=(ftp://ftp.archlinux.org/other/fortune-mod/$pkgname-$pkgver.tar.gz LICENSE 01_all_fortune_all-fix.patch)
md5sums=('f208805b3b712e32997d7667e0ec52d8' '0217481ae93971296cfb7872157cabb7'\
         'e85b073f16dc6f0e8f0ed90044797aa3')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 < ../01_all_fortune_all-fix.patch  || return 1
  make REGEXDEFS='-DHAVE_REGEX_H -DPOSIX_REGEX -DHAVE_STDBOOL'  \
       COOKIEDIR=/usr/share/fortune  \
       LOCALDIR=/usr/local/share/fortune || return 1
  make COOKIEDIR=/usr/share/fortune fortune/fortune.man || return 1
  make FORTDIR=$pkgdir/usr/bin  \
       COOKIEDIR=$pkgdir/usr/share/fortune  \
       BINDIR=$pkgdir/usr/sbin  \
       BINMANDIR=$pkgdir/usr/share/man/man1  \
       FORTMANDIR=$pkgdir/usr/share/man/man6 install || return 1
  rm $pkgdir/usr/share/fortune/*.u8
  rm $pkgdir/usr/share/fortune/off/*.u8
  install -D -m644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
