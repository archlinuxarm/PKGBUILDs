# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - --disable-rust-simd, --disable-lto
#  - remove PGO build

pkgname=js115
pkgver=115.22.0
pkgrel=1
pkgdesc="JavaScript interpreter and libraries - Version 115"
url="https://spidermonkey.dev/"
arch=(x86_64)
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
  readline
  sh
  zlib
)
makedepends=(
  clang
  lld
  llvm
  python
  rust
  zip
)
checkdepends=(
  git
  mercurial
)
options=(
  !lto
)
_relver=${pkgver}esr
source=(
  https://archive.mozilla.org/pub/firefox/releases/$_relver/source/firefox-$_relver.source.tar.xz{,.asc}
  0001-Bug-1860630-Upgrade-zstandard-to-0.22.-r-firefox-bui.patch
  0002-Bug-1892103-Remove-.-mach-esmify-command.-r-Standard.patch
  0003-Bug-1923217-Upgrade-zstandard-to-0.23.0.-r-firefox-b.patch
  0004-Bug-1926140-Replace-pipes-imports-r-jmaher.patch
  0005-Bug-1935621-Fix-virtual-environment-sysconfig-path-c.patch
  0006-Bug-1894423-Remove-unused-ExclusiveData-move-constru.patch
)
validpgpkeys=(
  # Mozilla Software Releases <release@mozilla.com>
  # https://blog.mozilla.org/security/2023/05/11/updated-gpg-key-for-signing-firefox-releases/
  14F26682D0916CDD81E37B6D61B7B526D98F0353
)
sha256sums=('f57b6507ab1db52183df2aadb1fd81d9f0108b185226d1bac6205b7d7d3005b3'
            'SKIP'
            '4f3501d1fdc63fe3a8bc306d17488bbe889bd4f58bd808db3e84713b9a2e0e4f'
            '59e4e0d1fba94a62e58739d2879775aef0fa4bdc3ecb14dd37bcf2f2d8c606d6'
            '0f443dd96662027dfba974bbb82f91a5c29abaf11e8f6a31616ada5b7f861c35'
            '26871831d8fa4850ac4966118cb630b674c6b3a1bbaf3d0945e607e46e844591'
            '8793e5dd497ac03b9044cf8a0bb2b06d4fd8c778a531f305f5a6bb2273245332'
            'db0855ef5a4b17b46343e6a9228d8bd2571ada77e7f4125ffe62508e4c6f7c93')
b2sums=('3b03613a573aefda9d5916b2ab548cbc9eeb7e47c051bf2a9062f5427c04bc46c18d30a70fe4af34089b41988eb952a7d57a081b6191426bbb43420be75b359a'
        'SKIP'
        'db2ee2a3dd67058655b3a4f494eeb7757356f71d7fac8397fb4b6f2a927e69ec189c250f5a81ae187eee235b7d2f9e9a078597a4a26544669de0af6ee71a5939'
        '8ce476a6cf666b64835888d2f6f74ccec513c30cd18d3d23282eebbad782cfce1ab24f036f79f66d368ca1690af135fc18c18b314c6410ccfc5a7116078e4575'
        'bd510cd2711cc091fb6b5f9554335e36b362d0bec8242243866df724909370f57eeb1123a5c930971ee77ee18526dc5611b28cc35cb69cbb57c3106b6ba5adcf'
        '40b4131867e5b9f87ca9d4e366e35d7932a5372803d6c8d8560d7bed7bfe6ad8e9404fd188903628569789db6d472526f33caba3e3d772d85de47d0d5f69ebbc'
        '727de79e63f06d35202a6d20e45ac690604a3844c1d135ca1a80349243e1d499c2fc5e078b71457657fa293e6bb56cfb2f83586de5225bfb22be38b0b317af35'
        '46284fd3ff320b11c6a5f402151ab7f76c4a5b853ce7714795f4bf0ca6c14370e5ab34f1a0d14c972c82fe8df0a7848eb622038b52b12b14bc5bb6a15d803e37')

# Make sure the duplication between bin and lib is found
COMPRESSZST+=(--long)

prepare() {
  # packed_simd no longer builds with 1.78.0
  #rustup toolchain update --profile minimal 1.77.2
  #rustup default 1.77.2

  mkdir mozbuild
  cd firefox-$pkgver

  # Fix build with Python 3.13
  patch -Np1 -i ../0001-Bug-1860630-Upgrade-zstandard-to-0.22.-r-firefox-bui.patch
  patch -Np1 -i ../0002-Bug-1892103-Remove-.-mach-esmify-command.-r-Standard.patch
  patch -Np1 -i ../0003-Bug-1923217-Upgrade-zstandard-to-0.23.0.-r-firefox-b.patch
  patch -Np1 -i ../0004-Bug-1926140-Replace-pipes-imports-r-jmaher.patch
  patch -Np1 -i ../0005-Bug-1935621-Fix-virtual-environment-sysconfig-path-c.patch

  # Fix build with Clang 19
  patch -Np1 -i ../0006-Bug-1894423-Remove-unused-ExclusiveData-move-constru.patch

  cat >../mozconfig <<END
ac_add_options --enable-application=js
mk_add_options MOZ_OBJDIR=${PWD@Q}/obj

ac_add_options --prefix=/usr
ac_add_options --enable-release
ac_add_options --enable-hardening
ac_add_options --enable-optimize
ac_add_options --disable-rust-simd
ac_add_options --enable-linker=lld
ac_add_options --disable-bootstrap
ac_add_options --disable-debug
ac_add_options --disable-lto
ac_add_options --disable-jemalloc
ac_add_options --disable-strip

# System libraries
ac_add_options --with-system-zlib
ac_add_options --without-system-icu

# Features
ac_add_options --enable-readline
ac_add_options --enable-shared-js
ac_add_options --enable-tests
ac_add_options --with-intl-api
END
}

build() {
  cd firefox-$pkgver

  export MACH_BUILD_PYTHON_NATIVE_PACKAGE_SOURCE=pip
  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MOZ_BUILD_DATE="$(date -u${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH} +%Y%m%d%H%M%S)"
  export MOZ_NOSPAM=1

  # malloc_usable_size is used in various parts of the codebase
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cat >.mozconfig ../mozconfig
  ./mach build --priority normal
}

check() {
  local jstests_extra_args=(
    --format=none
    --exclude-random
    --wpt=disabled
  ) jittest_extra_args=(
    --format=none
    --timeout 300
  ) jittest_test_args=(
    basic
  )

  cd firefox-$pkgver/obj
  make -C js/src check-jstests check-jit-test \
    JSTESTS_EXTRA_ARGS="${jstests_extra_args[*]}" \
    JITTEST_EXTRA_ARGS="${jittest_extra_args[*]}" \
    JITTEST_TEST_ARGS="${jittest_test_args[*]}"
}

package() {
  cd firefox-$pkgver/obj
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/*.ajs
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -c a-x {} +
}

# vim:set sw=2 sts=-1 et:
