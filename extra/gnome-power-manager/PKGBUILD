# $Id: PKGBUILD 46686 2009-07-19 16:01:16Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=2.26.3
pkgrel=1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system."
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=('GPL')
depends=('libnotify>=0.4.5' 'libunique>=1.0.8' 'libcanberra>=0.14' 'policykit-gnome>=0.9.2' 'hicolor-icon-theme' 'devicekit-power>=009' 'libglade>=2.6.4' 'gnome-keyring>=2.26.3')
makedepends=('gettext' 'intltool' 'pkgconfig' 'gnome-doc-utils>=0.16.0' 'gnome-panel>=2.26.3')
optdepends=('gnome-panel')
options=(!emptydirs)
install=gnome-power-manager.install
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-power-manager/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('99236a70487ac3f16da7baae0d8519dc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LDFLAGS="${LDFLAGS} -Wl,--as-needed"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --enable-policykit \
      --enable-legacy-buttons \
      --with-dpms-ext \
      --disable-scrollkeeper \
      --with-dbus-services=/usr/share/dbus-1/services \
      --enable-applets || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-power-manager ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
