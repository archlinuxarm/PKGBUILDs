# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to enable linking with libm to fix FTBFS

pkgname=gnome-power-manager
pkgver=43.0
pkgrel=3
pkgdesc="System power information and statistics"
url="https://gitlab.gnome.org/GNOME/gnome-power-manager"
arch=(x86_64)
license=(GPL)
depends=(gtk4 upower dconf)
makedepends=(git meson)
source=("git+https://gitlab.gnome.org/GNOME/gnome-power-manager.git#commit=GNOME_POWER_MANAGER_${pkgver//./_}"
        0001-set-libm-as-required.patch)
b2sums=('f05008bdabef550f911fbd87ead26da61f23403d759dcd746b37652ea4b56094b08ee7f7109d6876089f5f8f93846e39cc40a74fb6910968c7249839f209db77'
        'a03613688a2daffbaae211a2847e14391911f4e47e4d3c6b8088dc3c2d172fa6eb2f3720a5a1848482b3f4cb70eb81b78826696ad27161f0391d9a17315159c6')

prepare() {
  cd $pkgname

  # Port to GTK4
  # https://gitlab.gnome.org/GNOME/gnome-power-manager/-/merge_requests/22
  git cherry-pick -n a965912774a58bae42890d25b46791eb347d944a

  # Fix icon name 
  # https://gitlab.gnome.org/GNOME/gnome-power-manager/-/merge_requests/23
  git cherry-pick -n b92b61dd4f99b734feac3e60ea46ef3f29243cb6

  # Use GApplication to parse command line options 
  # https://gitlab.gnome.org/GNOME/gnome-power-manager/-/merge_requests/27
  git cherry-pick -n 5b47a4b89800c95d814444f46cdc446d5d7d8d01

  # Add D-Bus service file 
  # https://gitlab.gnome.org/GNOME/gnome-power-manager/-/merge_requests/29
  git cherry-pick -n 3e822609835d3f5b7e757d959787f03bc7885dc2
  # https://gitlab.gnome.org/GNOME/gnome-power-manager/-/merge_requests/31
  git cherry-pick -n 4d9c41ce8aa6528d4837da6ef97e24841e59e182

  # Don't hide in other desktop environments
  # https://gitlab.gnome.org/GNOME/gnome-power-manager/-/merge_requests/30
  git cherry-pick -n 91f5a956b3c3a681d809675824409182831f3454

  git apply ../0001-set-libm-as-required.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
