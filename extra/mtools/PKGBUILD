# $Id: PKGBUILD 49693 2009-08-15 07:34:59Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.10
pkgrel=5
pkgdesc="A collection of utilities to access MS-DOS disks"
url="http://www.gnu.org/software/mtools/"
source=(http://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2)
arch=('i686' 'x86_64')
license=('GPL')
depends=('sh')
makedepends=('texinfo' 'libx11' 'libsm')
optdepends=('libx11: required by floppyd'
	    'libsm:  required by floppyd')
backup=(etc/mtools.conf)
md5sums=('750c2beba968a5d46dbaae24551a82b9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i '/^SAMPLE FILE$/s:^:# :' mtools.conf
  sed -i 's:/usr/local/etc:/etc:g' mtools.5 mtools.texi
  ./configure --prefix=/usr \
	      --sysconfdir=/etc
  make || return 1
  make -j 1 prefix=${pkgdir}/usr install
  install -Dm644 mtools.conf ${pkgdir}/etc/mtools.conf
}

