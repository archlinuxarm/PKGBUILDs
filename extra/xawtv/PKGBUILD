# $Id: PKGBUILD 43937 2009-06-30 07:15:07Z allan $
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Maintainer: Jason Chu <jason@archlinux.org>

pkgname=xawtv
pkgver=3.95
pkgrel=6
pkgdesc="Is a simple Xaw-based TV program which uses the bttv driver or video4linux."
arch=('i686' 'x86_64')
url="http://linux.bytesex.org/xawtv/"
license=('GPL')
depends=('libjpeg>=7' 'alsa-lib' 'lesstif' 'libdv' 'zvbi' 'aalib'
         'libxxf86dga' 'libfs' 'libxrandr' 'libxinerama' 'libxv' 'libxaw'
	 'libxxf86vm' 'libgl' 'gpm>=1.20.4' 'lirc-utils' 'libxft'
	 'libv4l')
makedepends=('mesa')
options=('!makeflags')
source=(http://dl.bytesex.org/releases/$pkgname/$pkgname-$pkgver.tar.gz
        xawtv-395.diff
        xawtv-3.95-pagemask-fix.patch
        xawtv-3.95-fixes.patch
        xawtv-3.95-libv4l2.patch
        xawtv-3.95-no-dga.patch)
md5sums=('ad25e03f7e128b318e392cb09f52207d'
         '87a87b6c62b39348053704f052141843'
         '97493dfaac8c80084d8b66891becd76c'
         'dc83378a5c76c029f4e49434214fc824'
         '56932100c5060bf9dfc45b8af927e697'
         'c456147f15c8f836c678c6499f068085')
                  
build() {
  cd $startdir/src/$pkgname-$pkgver
  patch -Np1 -i $startdir/src/xawtv-395.diff || return 1
  patch -Np1 -i $startdir/src/xawtv-3.95-pagemask-fix.patch || return 1
  # Add patches from https://bugzilla.redhat.com/show_bug.cgi?id=457796
  patch -Np1 -i $startdir/src/xawtv-3.95-fixes.patch || return 1
  patch -Np1 -i $startdir/src/xawtv-3.95-libv4l2.patch || return 1
  patch -Np1 -i $startdir/src/xawtv-3.95-no-dga.patch || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man
  make || return 1
  make DESTDIR=$startdir/pkg resdir=$startdir/pkg/usr/lib/X11 install
}
