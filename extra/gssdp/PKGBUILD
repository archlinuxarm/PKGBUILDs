# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepends on pandoc, set -D manpages=false

pkgbase=gssdp
pkgname=(
  gssdp
  gssdp-docs
)
pkgver=1.6.3
pkgrel=2
pkgdesc="GObject-based API for handling resource discovery and announcement over SSDP"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libsoup3
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  gtk4
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/gssdp.git?signed#tag=gssdp-$pkgver")
b2sums=('b92fd3945674b65f158d9e4b6c384580a8c13d97bdb4bab86895a458e54e79f7d0186b2dc3615a52ac8f029709c7877346a59863d4f26d06cc5fd4358025e1dc')
validpgpkeys=(
  AC9CD4E32D7C7F6357BA8ADD10F6E970175D29E1 # Jens Georg <mail@jensge.org>
)

prepare() {
  cd gssdp
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D manpages=false
  )

  arch-meson gssdp build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gssdp() {
  depends+=(
    libg{lib,object,io}-2.0.so
    libsoup-3.0.so
  )
  optdepends=('gtk4: gssdp-device-sniffer')
  provides=(libgssdp-1.6.so)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"
  _pick docs usr/share/doc
}

package_gssdp-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
