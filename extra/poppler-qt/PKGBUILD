# $Id: PKGBUILD 39816 2009-05-21 14:00:25Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=poppler-qt
pkgver=0.10.7
pkgrel=1
pkgdesc="Poppler Qt bindings"
arch=(i686 x86_64)
license=('GPL')
depends=('qt>=4.5.0' "poppler>=${pkgver}")
makedepends=('pkgconfig')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/poppler-${pkgver}.tar.gz
	poppler-0.10-bindings.patch)
md5sums=('ba120e49a82567d85d00e159011ebe67'
         '3f828a8372687c6c0ca88925ff4f5036')

build() {
  cd "${srcdir}/poppler-${pkgver}"

  patch -Np1 -i "${srcdir}/poppler-0.10-bindings.patch" || return 1
  libtoolize --force --copy || return 1
  AT_M4DIR="m4" autoreconf -i || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-zlib \
	      --enable-libjpeg \
	      --enable-cairo-output \
	      --enable-poppler-qt4 \
	      --disable-poppler-glib \
	      --disable-gtk-test \
	      --disable-poppler-qt
  sed -i -e 's|^LDFLAGS =|LDFLAGS = -lqt-mt|' qt/Makefile
  make -C goo libgoo.la || return 1
  make -C splash libsplash.la || return 1
  make -C fofi libfofi.la || return 1
  make -C poppler libpoppler.la libpoppler-cairo.la libpoppler-arthur.la || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -f "${pkgdir}/usr/lib/pkgconfig/poppler-cairo.pc" || return 1
}
