# $Id: PKGBUILD 30910 2009-03-22 17:28:33Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-utils
pkgver=2.26.0
pkgrel=1
pkgdesc="Common utilities for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgtop>=2.26.0' 'gnome-panel>=2.26.0')
makedepends=('gnome-doc-utils>=0.16.0' 'pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-utils.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('667400cfb9bee954028024ac3cf91a09')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/gnome-utils \
              --localstatedir=/var --disable-static \
              --enable-hal --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-utils-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
