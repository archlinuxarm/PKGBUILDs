# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disabled vdpau

pkgname=xine-lib
pkgver=1.2.9
pkgrel=2
pkgdesc="A multimedia playback engine"
arch=('x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libxvmc' 'ffmpeg' 'libxinerama')
makedepends=('wavpack' 'faad2' 'libmng' 'libmagick6' 'mesa' 'libmodplug'
             'vcdimager' 'jack' 'aalib' 'libdca' 'a52dec' 'libmad' 'libdvdnav'
             'libmpcdec' 'libcaca' 'libbluray' 'glu' 'gdk-pixbuf2')
optdepends=('libmagick6: for using the imagemagick plugin' \
            'jack: for using the jack plugin' \
            'vcdimager: for using the vcd plugin' \
            'glu: for using the opengl plugin' \
            'wavpack: for using the wavpack plugin' \
            'faad2: for using the faad plugin' \
            'libmng: for using the mng plugin' \
            'aalib: for using the aalib plugin' \
            'libmodplug: for using the modplug plugin' \
            'libdca: for using the dca plugin' \
            'a52dec: for using the a52 plugin' \
            'libmad: for using the mp3 plugin' \
            'libmpcdec: for using the musepack plugin' \
            'libcaca: for using the caca plugin' \
            'libbluray: for using the bluray plugin' \
            'smbclient: for using the samba plugin' \
            'gdk-pixbuf2: for using the gdk-pixbuf plugin')
source=(https://downloads.sourceforge.net/project/xine/xine-lib/$pkgver/xine-lib-$pkgver.tar.xz)
sha1sums=('ad6e72b7d8ff6172a8a170ab1bc38577ae321371')

build() {
  cd ${pkgname}-${pkgver}
  PKG_CONFIG_PATH="/usr/lib/imagemagick6/pkgconfig" \
  ./configure --prefix=/usr --with-wavpack --disable-vdpau --with-external-dvdnav \
    --disable-optimizations
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
