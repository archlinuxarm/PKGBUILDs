# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disabled vdpau

pkgname=xine-lib
pkgver=1.2.10
pkgrel=3
pkgdesc="Multimedia playback engine"
arch=('x86_64')
url="https://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libxvmc' 'ffmpeg' 'libxinerama' 'libnsl')
makedepends=('wavpack' 'faad2' 'libmng' 'imagemagick' 'mesa' 'libmodplug'
             'vcdimager' 'jack' 'aalib' 'libdca' 'a52dec' 'libmad' 'libdvdnav'
             'libmpcdec' 'libcaca' 'libbluray' 'glu' 'gdk-pixbuf2'
             'dav1d')
optdepends=('imagemagick: for using the imagemagick plugin' \
            'jack: for using the jack plugin' \
            'vcdimager: for using the vcd plugin' \
            'glu: for using the opengl plugin' \
            'wavpack: for using the wavpack plugin' \
            'faad2: for using the faad plugin' \
            'libmng: for using the mng plugin' \
            'aalib: for using the aalib plugin' \
            'libmodplug: for using the modplug plugin' \
            'libdca: for using the dca plugin' \
            'a52dec: for using the a52 plugin' \
            'libmad: for using the mp3 plugin' \
            'libdvdnav: for using the dvd plugin' \
            'libmpcdec: for using the musepack plugin' \
            'libcaca: for using the caca plugin' \
            'libbluray: for using the bluray plugin' \
            'smbclient: for using the samba plugin' \
            'dav1d: for using the dav1d decoder' \
            'gdk-pixbuf2: for using the gdk-pixbuf plugin')
source=(https://downloads.sourceforge.net/project/xine/xine-lib/$pkgver/xine-lib-$pkgver.tar.xz)
sha512sums=('1cbe033da606d67a0a59f19968b2fe1cb46eaeb32c4b4aca7b91125b7230e15bd36d1e3e39e48e6eda56e556018f9f9bf84acb0012d3dd634306e7110fdc4c5f')
b2sums=('e1bc088814c951ff56e55b626fe1f6f34c3da744e716de369a68e6c82a5adc414b452675ad19a5f82500bade6795b54efd503de8ffe4f1e65b534317ca0a26af')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
      --prefix=/usr \
      --with-wavpack \
      --disable-vdpau \
      --with-external-dvdnav
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
