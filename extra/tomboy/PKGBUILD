# $Id: PKGBUILD 39387 2009-05-15 19:52:42Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tomboy
pkgver=0.14.2
pkgrel=1
pkgdesc="Desktop note-taking application for Linux and Unix"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org/projects/tomboy/"
depends=('gtkspell' 'gmime>=2.2.18' 'ndesk-dbus-glib>=0.4.1' 'gnome-sharp>=2.24.1' 'mono-addins>=0.4' 'libsm' 'gnome-desktop-sharp>=2.26.0')
makedepends=('intltool' 'pkgconfig' 'gnome-panel>=2.26.0' 'gnome-doc-utils>=0.16.0')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=tomboy.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.14/${pkgname}-${pkgver}.tar.bz2)
md5sums=('be86d6e1eb83e126cb7ad3fe52561e5c')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
 
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain tomboy ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
