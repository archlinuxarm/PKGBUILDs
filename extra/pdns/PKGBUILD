# $Id: PKGBUILD 52294 2009-09-17 07:06:46Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pdns
pkgver=2.9.22
pkgrel=4
pkgdesc="A modern, advanced and high performance authoritative-only nameserver"
arch=(i686 x86_64)
url="http://www.powerdns.com"
license=('GPL')
depends=('gcc-libs' 'zlib' 'postgresql-libs>=8.4.1' 'libmysqlclient' 'sqlite3' 'libldap>=2.4.18')
makedepends=('boost')
provides=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
replaces=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
conflicts=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
install=pdns.install
options=(!makeflags !libtool)
backup=('etc/powerdns/pdns.conf')
source=(http://downloads.powerdns.com/releases/${pkgname}-${pkgver}.tar.gz
	    2.9.18-default-mysql-options.patch
        pdns-2.9.22-gcc44.patch
        pdns.rc
        pdns.conf)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/2.9.18-default-mysql-options.patch || return 1
  patch -Np1 -i ${srcdir}/pdns-2.9.22-gcc44.patch || return 1
  touch AUTHORS NEWS
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr \
	--sysconfdir=/etc/powerdns \
	--libexecdir=/usr/lib \
	--libdir=/usr/lib/powerdns \
	--mandir=/usr/share/man \
	--with-modules="" \
	--with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo" \
	--disable-recursor || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m755 -d ${pkgdir}/etc/rc.d
  install -m755 -d ${pkgdir}/etc/powerdns
  rm ${startdir}/pkg/etc/powerdns/pdns.conf-dist
  install -m644 ${startdir}/src/pdns.conf ${startdir}/pkg/etc/powerdns/
  install -m755 ${startdir}/src/pdns.rc ${startdir}/pkg/etc/rc.d/pdns
}
md5sums=('8a6ff842733aca885577eb54e983a1ff'
         '90fb32e843326d14359c508cef855929'
         '530642273005bdabae27792098834ea6'
         '16e2d32e9781be7afa8f1700740bcdb0'
         'a390ab49c4fcde205629be33bddc2e6c')
