# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kalgebra
pkgver=14.12.2
pkgrel=1
pkgdesc="Graph Calculator"
url="http://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('analitza' 'kio' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'glu')
replaces=('kdeedu-kalgebra')
conflicts=('kdeedu-kalgebra' 'kdeedu-analitza')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kalgebra-${pkgver}.tar.xz"
        'kalgebra-disable-opengl.patch')
sha1sums=('b33643332848fc1a77f9750cf2a4974a77ee5f28'
          '103de918b6fdfb74a030db01afeb378fa0599717')

prepare() {
  mkdir build
  cd kalgebra-${pkgver}
  patch -p1 -i ../kalgebra-disable-opengl.patch
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
