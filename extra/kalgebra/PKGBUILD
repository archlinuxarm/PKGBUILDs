# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch and cmake define to disable opengl for ARM

pkgname=kalgebra
pkgver=16.12.3
pkgrel=1
pkgdesc="Graph Calculator"
url="https://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('analitza' 'kio' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'glu')
optdepends=('kirigami: for KAlgebra Mobile')
replaces=('kdeedu-kalgebra')
conflicts=('kdeedu-kalgebra' 'kdeedu-analitza')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kalgebra-${pkgver}.tar.xz"{,.sig}
        'kalgebra-disable-opengl.patch')
sha256sums=('0b6781917d2b5cbe7b400b5ece5d163a846cfec3ff93aaa0a72d90fa4ab41162'
          'SKIP'
          '281bd6ace1614d9982d6a3bf37c42b711e49618c64a4632eab6fac47da360dc7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
  cd kalgebra-${pkgver}
  patch -p1 -i ../kalgebra-disable-opengl.patch
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSHOULD_BUILD_OPENGL=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
