# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch and cmake define to disable opengl for ARM

pkgname=kalgebra
pkgver=15.04.0
pkgrel=1
pkgdesc="Graph Calculator"
url="http://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('analitza' 'kio' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'glu')
replaces=('kdeedu-kalgebra')
conflicts=('kdeedu-kalgebra' 'kdeedu-analitza')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kalgebra-${pkgver}.tar.xz"
        'kalgebra-disable-opengl.patch')
sha1sums=('4566f55db3bb81288c9dee22c3ef0925659426fb'
          '103de918b6fdfb74a030db01afeb378fa0599717')

prepare() {
  mkdir build
  cd kalgebra-${pkgver}
  patch -p1 -i ../kalgebra-disable-opengl.patch
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSHOULD_BUILD_OPENGL=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
