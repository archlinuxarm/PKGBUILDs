# $Id: PKGBUILD 160488 2012-06-02 10:26:26Z bluewind $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - rebuild for perl, remove when bumped upstream

pkgname=irssi
pkgver=0.8.15
pkgrel=6.1
pkgdesc="Modular text mode IRC client with Perl scripting"
arch=('i686' 'x86_64')
url="http://irssi.org/"
license=('GPL')
depends=('glib2' 'openssl')
optdepends=('perl-libwww: for the scriptassist script')
backup=('etc/irssi.conf')
source=("http://irssi.org/files/${pkgname}-${pkgver}.tar.bz2")
options=('!libtool')
md5sums=('1dcb3f511b88df94b0c996f36668c7da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-ipv6 \
	      --with-proxy \
	      --sysconfdir=/etc \
	      --with-perl-lib=vendor
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install 
}
