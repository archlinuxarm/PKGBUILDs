# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Peter Reschenhofer <peter.reschenhofer@gmail.com>

# ALARM: Andrea Scarpino <andrea@archlinux.org>
#  - disable documentation build

_pkgauthor=rfjakob
pkgname=gocryptfs
pkgver=2.3.2
pkgrel=2
pkgdesc='Encrypted overlay filesystem written in Go.'
arch=('x86_64')
url="https://github.com/${_pkgauthor}/${pkgname}"
license=('MIT')
depends=('glibc' 'openssl' 'fuse3')
makedepends=('go')
source=(
    "${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz"
    "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz.asc"
)
sha256sums=('2641145d5adfd259959b1fd7b182b61d0ce2ce3e24361b8f3e69fd3f6caa73cc'
            'SKIP')
validpgpkeys=('FFF3E01444FED7C316A3545A895F5BC123A02740')

build() {
    cd "${srcdir}/${pkgname}_v${pkgver}_src-deps"

    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -mod=vendor"
    ./build.bash
}

package() {
    cd "${srcdir}/${pkgname}_v${pkgver}_src-deps"
    install -Dm755 -t "${pkgdir}/usr/bin/" gocryptfs
    install -Dm755 -t "${pkgdir}/usr/bin/" gocryptfs-xray/gocryptfs-xray
    install -Dm644 -t "${pkgdir}/usr/share/licenses/gocryptfs" LICENSE
}
