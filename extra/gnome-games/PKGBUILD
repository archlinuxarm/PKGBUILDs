# $Id: PKGBUILD 44692 2009-07-03 18:01:35Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-games
pkgver=2.26.3
pkgrel=1
pkgdesc="Some Games for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libsm' 'gconf>=2.26.0-2' 'guile>=1.8.6' 'librsvg>=2.26.0' 'filesystem>=0.8-3' 'desktop-file-utils' 'gnome-python-desktop>=2.26.0' 'gstreamer0.10>=0.10.22' 'libgnomeprintui>=2.18.4' 'ggz-client-libs>=0.0.14.1')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.16.0' 'intltool')
provides=('glchess' 'gnome-sudoku')
conflicts=('glchess' 'gnome-sudoku')
options=('!emptydirs')
install=gnome-games.install
url="http://www.gnome.org"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('32c69a25671d8d23b19f234efcc177ca')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper \
              --with-scores-user=root --with-scores-group=games \
	      --with-sound=gstreamer --disable-tests || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  # Remove all scores, we generate them from postinstall
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-games ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  rm -f "${pkgdir}/etc/ggz.modules"
  install -m644 gnect/data/gnect-client.dsc gnibbles/gnibbles-client.dsc iagno/iagno-client.dsc "${pkgdir}/usr/share/ggz/" || return 1
}
