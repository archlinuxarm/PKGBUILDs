# $Id: PKGBUILD 38644 2009-05-09 18:57:55Z jgc $
# Maintainer: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-appfinder
pkgver=4.6.1
pkgrel=2
pkgdesc="Xfce application finder"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('thunar>=1.0.1' "libxfce4menu>=$pkgver" 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
options=('!libtool')
install=${pkgname}.install
source=(http://www.xfce.org/archive/xfce-${pkgver}/src/${pkgname}-${pkgver}.tar.bz2)
md5sums=('a1bc7d88100cfd92714a300be531589b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
