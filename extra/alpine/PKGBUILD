# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Fabio Scotoni <fabio@esse.ch>

pkgname=alpine
pkgver=2.00
pkgrel=6
_patchlevel=52
pkgdesc="The Apache-licensed PINE (a tool for reading, sending, and managing electronic messages)"
arch=('arm')
url="http://www.washington.edu/alpine/"
license=('APACHE')
depends=('libldap>=2.4.18' 'heimdal>=1.2.1' 'gettext')
optdepends=('aspell: for spell-checking support')
provides=('pine')
conflicts=('pine')
replaces=('pine')
options=('!makeflags')
source=(ftp://ftp.cac.washington.edu/${pkgname}/${pkgname}.tar.bz2 \
        http://staff.washington.edu/chappa/alpine/patches/alpine-${pkgver}/all_${_patchlevel}.patch.gz \
        2.00-lpam.patch CVE-2008-5514.patch)
md5sums=('84e44cbf71ed674800a5d57eed9c1c52' '33d9fa000f896fce23cc58b56068428f'\
         'cd3911c16fc6a072e853c0ccfc35857c' '1b52a54a656979116c09fb1d948a4325')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../all_${_patchlevel}.patch || return 1
  patch -p1 < ../2.00-lpam.patch || return 1
  patch -p1 < ../CVE-2008-5514.patch || return 1
  ./configure --prefix=/usr --without-passfile --without-tcl \
    --disable-shared --with-system-pinerc=/etc/alpine.d/pine.conf \
    --with-system-fixed-pinerc=/etc/alpine.d/pine.conf.fixed || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
