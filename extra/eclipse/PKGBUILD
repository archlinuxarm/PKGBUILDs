# $Id: PKGBUILD 53342 2009-09-29 12:30:51Z paul $
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=eclipse
pkgver=3.5.1
_internal_pkgver=3.5.1
pkgrel=1
_date=200909170800
pkgdesc="An IDE for Java and other languages"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment>=6' 'gtk2>=2.16.1' 'unzip' 'xulrunner>=1.9.0.10')
install=eclipse.install
makedepends=('zip')
license=("EPL/1.1")
if [ "$CARCH" = "x86_64" ]; then
source=(ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-${pkgver}-${_date}/eclipse-SDK-$pkgver-linux-gtk-x86_64.tar.gz
            http://www.bearfruit.org/files/eclipse-icon-clean.svg
            eclipse.sh
            eclipse.desktop
	    eclipse.ini.patch)
md5sums=('aacecb035c82d0f43e3852d87028f5e5'
         '77cff7543ccf7e177cb6667f5a20ce19'
         '85109e54a3ea8bc39e312526e7baaa3a'
         'd88490571b49c8ebb221ed349815d040'
         'ee66a334bc443fd23ad9bc080dc6ea69')
else
source=(ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-${pkgver}-${_date}/eclipse-SDK-$pkgver-linux-gtk.tar.gz
            http://www.bearfruit.org/files/eclipse-icon-clean.svg
            eclipse.sh
            eclipse.desktop
	    eclipse.ini.patch)
md5sums=('c0e1c97b00e90ffde496faeb2468293c'
         '77cff7543ccf7e177cb6667f5a20ce19'
         '85109e54a3ea8bc39e312526e7baaa3a'
         'd88490571b49c8ebb221ed349815d040'
         'ee66a334bc443fd23ad9bc080dc6ea69')
fi

build() {
  cd "${srcdir}/eclipse" || return 1

  # patch to increase default memory limits
  patch -Np0 -i "${srcdir}/eclipse.ini.patch" || return 1

  # install eclipse
  install -m755 -d "${pkgdir}/usr/share"
  mv "${srcdir}/eclipse" "${pkgdir}/usr/share/" || return 1

  # install misc
  mkdir -p ${pkgdir}/usr/bin ${pkgdir}/usr/share/applications \
    ${pkgdir}/usr/share/icons/hicolor/{16x16,32x32,48x48}/apps || return 1
  install -m755 "${srcdir}/eclipse.sh" "${pkgdir}/usr/bin/eclipse" || return 1
  install -m644 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/" || return 1
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse.png \
    "${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png" || return 1
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse32.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png" || return 1
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse48.png \
    "${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png" || return 1


  # install icon
  install -D -m644 $startdir/src/eclipse-icon-clean.svg $startdir/pkg/usr/share/icons/hicolor/scalable/apps/eclipse.svg || return 1
}
