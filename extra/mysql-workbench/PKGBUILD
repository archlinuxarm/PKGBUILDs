# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Alexandre Boily <alexandreboily@gmail.com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to CXXFLAGS

highmem=1
buildarch=8

pkgname=mysql-workbench
pkgver=8.0.43
pkgrel=1
_mysql_version=8.4.6
_connector_version=9.4.0
_boost_version=1.87.0
# bump the antlr4 version as required, this is to match the jar file name
_antlr4_version=4.13.2
pkgdesc='A cross-platform, visual database design tool developed by MySQL'
arch=('x86_64')
url='https://www.mysql.com/products/workbench/'
license=('GPL2')
depends=('antlr4-runtime' 'libantlr4-runtime.so'
         'atkmm' 'libatkmm-1.6.so'
         'at-spi2-core' 'libatk-1.0.so'
         'cairo' 'libcairo.so'
         'desktop-file-utils'
         'freetype2'
         'gcc-libs'
         'gdal'
         'gdk-pixbuf2' 'libgdk_pixbuf-2.0.so'
         'glib2' 'libgio-2.0.so' 'libglib-2.0.so' 'libgmodule-2.0.so' 'libgobject-2.0.so'
         'glibc'
         'gtk3' 'libgdk-3.so' 'libgtk-3.so'
         'gtkmm3' 'libgtkmm-3.0.so' #'libgdkmm-3.0.so'
         'hicolor-icon-theme'
         'libaio'
         'libgl' 'libGLX.so' 'libOpenGL.so'
         'libsecret' 'libsecret-1.so'
         'libssh' 'libssh.so'
         'libx11'
         'libxml2' 'libxml2.so'
         'libzip' 'libzip.so'
         'ncurses' 'libncursesw.so'
         'openssl' 'libcrypto.so' 'libssl.so'
         'pango' 'libpango-1.0.so' 'libpangocairo-1.0.so'
         'proj' #'libproj.so'
         'python'
         'unixodbc' 'libodbc.so'
         'vsqlite++' 'libvsqlitepp.so')
makedepends=("antlr4=${_antlr4_version}"
             "antlr4-runtime=${_antlr4_version}"
             'boost'
             'cmake'
             'imagemagick'
             'java-runtime'
             'mesa'
             'rapidjson'
             'swig')
validpgpkeys=('BCA43417C3B485DD128EC6D4B7B3B788A8D3785C') # MySQL Release Engineering <mysql-build@oss.oracle.com>
source=("https://cdn.mysql.com/Downloads/MySQLGUITools/mysql-workbench-community-${pkgver}-src.tar.gz"{,.asc}
	"https://cdn.mysql.com/Downloads/MySQL-${_mysql_version%.*}/mysql-${_mysql_version}.tar.gz"{,.asc}
	"https://cdn.mysql.com/Downloads/Connector-C++/mysql-connector-c++-${_connector_version}-src.tar.gz"{,.asc}
	"https://downloads.sourceforge.net/project/boost/boost/${_boost_version}/boost_${_boost_version//./_}.tar.bz2"
	'0001-mysql-workbench-no-check-for-updates.patch'
	'0002-disable-unsupported-operating-system-warning.patch'
	'0003-swig-4-3.patch'
	'0004-replace-deprecated-python-modules-pipes-with-shlex.patch'
        '0005-gcc-15.patch'
	'arch_linux_profile.xml')
sha256sums=('13d7e7ef6af7e56ac6cc83a80e8b88bc91597697f0f6c803347c047bfd037542'
            'SKIP'
            'a1e523dc8be96d18a5ade106998661285ca01b6f5b46c08b2654110e40df2fb7'
            'SKIP'
            '36a7c93d4a10d1da2a2e66367559d91741aa0f0362bc0ae943171cf1771f6615'
            'SKIP'
            'af57be25cb4c4f4b413ed692fe378affb4352ea50fbe294a11ef548f4d527d89'
            'cdf687f23bc6e8d52dbee9fa02b23d755e80f88476f0fc2e7c4c71cdfed3792f'
            '2d0f6dcf38f22e49ef7ab9de0230484f1ffac41b7ac40feaf5ef4538ae2f7a18'
            'fb0732919a15dc0b16f5ca104cac7be3edd92dc997fed879c23cd933a1f43367'
            '8a8a013ca0dad92e4859e2e9bc3ff73d018e2d8ecd9ca7a9c922a5ce2368e914'
            'f119302517a748b8ea54fdd84e2f057f6d631b7ea55dcf2198686899d2df4a59'
            'd816164098c90c432b4fe590708c14f95ab137abfe16ad1b7d498b2e83c0e265')

prepare() {
	# fix cmake
	sed -i \
		-e '/cmake_policy/Is|[23]\.[.0-9]\+|3.12|' \
		-e '/cmake_minimum_required/Is|[23]\.[.0-9]\+|3.12|' \
		$(find "${srcdir}"/ -name 'CMakeLists.txt')

	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	# Disable 'Help' -> 'Check for Updates'
	# Updates are provided via Arch Linux packages
	patch -Np1 < "${srcdir}"/0001-mysql-workbench-no-check-for-updates.patch

	# disable unsupported operating system warning
	patch -Np1 < "${srcdir}"/0002-disable-unsupported-operating-system-warning.patch

	# fix build with swig 4.3.x - function SWIG_Python_AppendOutput has a 3rd parameter now
	patch -Np1 < "${srcdir}"/0003-swig-4-3.patch

	# replace deprecated python modules pipes with shlex
	patch -Np1 < "${srcdir}"/0004-replace-deprecated-python-modules-pipes-with-shlex.patch

	# remove '-Werror'
	sed -i '/^\s*set/s| -Werror||' CMakeLists.txt

	# work around crashes on query:
	# disable LTO (link time optimization) for libsqlparser.so
	# TODO: drop when fixed!
	sed -i '/target_compile_options/s|PRIVATE|PRIVATE -fno-lto|' library/sql.parser/source/CMakeLists.txt

	# be happy with more recent antlr
	sed -i "/set(ANTLR_JAR_FILENAME/s/4.13.2/${_antlr4_version}/" CMakeLists.txt

	cd ../mysql-connector-c++-${_connector_version}-src
	# fix build with GCC 15
        patch -Np1 < "$srcdir"/0005-gcc-15.patch
}

build() {
	# Build mysql
	mkdir "${srcdir}/mysql-${_mysql_version}-build"
	cd "${srcdir}/mysql-${_mysql_version}-build"
	echo "Configure mysql..."
	cmake "${srcdir}/mysql-${_mysql_version}" \
		-DWITHOUT_SERVER=ON \
		-DBUILD_CONFIG=mysql_release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONFDIR=/etc/mysql \
		-DMYSQL_DATADIR=/var/lib/mysql \
		-DWITH_BOOST="${srcdir}/boost_${_boost_version//./_}"
	echo "Build mysql..."
	make
	echo "Install mysql..."
	make DESTDIR="${srcdir}/install-bundle/" install

	# Build mysql-connector-c++
	mkdir "${srcdir}/mysql-connector-c++-${_connector_version}-src-build"
	cd "${srcdir}/mysql-connector-c++-${_connector_version}-src-build"
	echo "Configure mysql-connector-c++..."
	cmake "${srcdir}/mysql-connector-c++-${_connector_version}-src" \
		-Wno-dev \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DINSTALL_LIB_DIR=lib \
		-DMYSQL_DIR="${srcdir}/install-bundle/" \
		-DMYSQL_CONFIG_EXECUTABLE="${srcdir}/install-bundle/usr/bin/mysql_config" \
		-DWITH_JDBC=ON
	echo "Build mysql-connector-c++..."
	make
	echo "Install mysql-connector-c++..."
	make DESTDIR="${srcdir}/install-bundle/" install

	# Build MySQL Workbench itself with bundled libs
	mkdir "${srcdir}/mysql-workbench-community-${pkgver}-src-build"
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src-build"
	echo "Configure mysql-workbench..."
	cmake "${srcdir}/mysql-workbench-community-${pkgver}-src" \
		-Wno-dev \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DCMAKE_CXX_FLAGS="$CXXFLAGS -fsigned-char" \
		-DCMAKE_BUILD_TYPE=Release \
		-DMySQL_CONFIG_PATH="${srcdir}/install-bundle/usr/bin/mysql_config" \
		-DMySQLCppConn_LIBRARY="${srcdir}/install-bundle/usr/lib/libmysqlcppconn.so" \
		-DMySQLCppConn_INCLUDE_DIR="${srcdir}/install-bundle/usr/include/jdbc" \
		-DUNIXODBC_CONFIG_PATH='/usr/bin/odbc_config' \
		-DUSE_BUNDLED_MYSQLDUMP=1 \
		-DWITH_ANTLR_JAR="/usr/share/java/antlr-${_antlr4_version}-complete.jar"
	echo "Build mysql-workbench..."
	make
}

package() {
	# install bundled libraries
	for LIBRARY in $(find "${srcdir}/install-bundle/usr/lib/" -type f -regex '.*/lib\(mysql\(client\|cppconn\)\)\.so\..*'); do
		BASENAME="$(basename "${LIBRARY}")"
		SONAME="$(readelf -d "${LIBRARY}" | grep -Po '(?<=(Library soname: \[)).*(?=\])')"
		install -D -m0755 "${LIBRARY}" "${pkgdir}"/usr/lib/mysql-workbench/"${BASENAME}"
		ln -s "${BASENAME}" "${pkgdir}"/usr/lib/mysql-workbench/"${SONAME}"
	done

	# install bundled mysql and mysqldump
	install -m0755 "${srcdir}/install-bundle/usr/bin/mysql"{,dump} "${pkgdir}"/usr/lib/mysql-workbench/

	# install MySQL Workbench itself
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src-build"

	make DESTDIR="${pkgdir}" install

	# icons
	for SIZE in 16 24 32 48 64 96 128; do
		# set modify/create for reproducible builds
		magick \
			"${srcdir}/mysql-workbench-community-${pkgver}-src/images/icons/MySQLWorkbench-128.png" \
			-scale ${SIZE} +set date:create +set date:modify \
			"${srcdir}/mysql-workbench.png"
		install -D -m0644 "${srcdir}/mysql-workbench.png" "${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/mysql-workbench.png"
	done

	install -D -m 0644 "${srcdir}"/arch_linux_profile.xml \
		"${pkgdir}"/usr/share/mysql-workbench/mysql.profiles/Arch_Linux_\(MariaDB\).xml
}
