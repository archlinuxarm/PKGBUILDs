# $Id: PKGBUILD 35598 2009-04-14 08:38:34Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bächler <thomas.archlinux.org>

pkgname=ntfs-3g
pkgver=2009.4.4
pkgrel=1
pkgdesc="Stable read and write NTFS driver"
url="http://www.ntfs-3g.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://www.ntfs-3g.org/${pkgname}-${pkgver}.tgz
	20-ntfs-config-write-policy.fdi)
md5sums=('e0b5c170f088a8d82968f0a6b34d31da'
         'ece35a9108837e2008e5a47f3390755e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # use internal fuse at least until fuse 2.8.0 is relased, it contains
  # some bug & security fixes
  ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr \
	--with-fuse=internal || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m755 -d "${pkgdir}/usr/share/hal/fdi/policy/10osvendor"
  install -m644 "${srcdir}/20-ntfs-config-write-policy.fdi" "${pkgdir}/usr/share/hal/fdi/policy/10osvendor/" || return 1
}
