# $Id: PKGBUILD 30955 2009-03-22 19:41:36Z jgc $
# Maintainer: tobias <tobias@archlinux.org>

pkgname=gnome-sharp
pkgver=2.24.1
pkgrel=1
pkgdesc="GNOME bindings for C#"
arch=(i686 x86_64)
license=(LGPL)
url="http://gtk-sharp.sourceforge.net"
depends=('gtk-sharp-2>=2.12.8' 'libgnomeui>=2.24.1')
makedepends=('pkgconfig' 'monodoc')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gnome-sharp/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('408f83f790a5189bcf76ea9299bc6410')

build() {
 # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make GACUTIL="/usr/bin/gacutil /root ${pkgdir}/usr/lib" \
     	 DESTDIR="${pkgdir}" install || return 1
}
