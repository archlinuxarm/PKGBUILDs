# $Id: PKGBUILD 51924 2009-09-13 21:14:27Z eric $
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Maintainer: roberto <roberto@archlinux.org>

pkgname=transcode
_sripver=0.3-4
pkgver=1.1.4
pkgrel=1
pkgdesc="A video/DVD ripper and encoder for the terminal/console."
arch=('i686' 'x86_64')
url="http://tcforge.berlios.de/"
license=('GPL')
depends=('imagemagick>=6.5.1.9' 'lzo2' 'libdv' 'libdvdread>=4.1.3' 'ffmpeg>=0.5' 'mjpegtools' 'xvidcore' 'libmpeg2' 'libtheora' 'libxaw' 'x264>=20090416' 'libxml2' 'libxv' 'a52dec' 'alsa-lib')
makedepends=('nasm' 'pkgconfig')
options=(!libtool !makeflags)
source=(http://download2.berlios.de/tcforge/${pkgname}-${pkgver}.tar.bz2 \
        http://downloads.sourceforge.net/sourceforge/subtitleripper/subtitleripper-${_sripver}.tgz)
md5sums=('6ba277fde95c26d217a88bbe5e6dd994' 'c0bd49a88f667c68c4430ad25bbed510')
sha1sums=('3d08f67c047b1b498a4a5183ad164c0641fcbc00' 'd93ff3578dd5f722c8f4ef16bc0903eec5781a0d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoconf || return 1

 # building from here
  ./configure --prefix=/usr \
    --enable-sse=no --enable-sse2=no --enable-altivec=no --enable-mmx \
    --enable-lame --enable-ogg --enable-vorbis --enable-theora \
    --enable-libdv --enable-libxml2 --enable-v4l \
    --enable-imagemagick --enable-libjpeg --enable-lzo --enable-mjpegtools \
    --enable-sdl --enable-freetype2 --enable-a52 --enable-libpostproc \
    --enable-xvid --enable-x264 --enable-alsa \
    --with-libxml2-prefix=/usr \
    --with-mjpegtools-prefix=/usr \
    --with-imagemagick-prefix=/usr \
    --with-libdv-includes=/usr \
    --with-libpostproc-includes=/usr/include/libpostproc \
    --with-ogg-prefix=/usr \
    --with-lame-libs=/usr \
    --with-lzo-libs=/usr/lib \
    --with-libavcodec-prefix=/usr \
    --with-libavcodec-includes=/usr/include \
    --with-libavcodec-libs=/usr/lib || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
 ##############################################################################
 # this imports the latest subtitleripper sources and replaces the internal
  mkdir contrib
  mv "${srcdir}/subtitleripper" contrib/subrip
 # removes dependency on libppm, make it dependend on netpbm just failed, argh...
  cd contrib/subrip || return 1
  sed -e 's|^\(.*lppm.*\)$|#\1|' \
      -e 's|^\(.*D_HAVE_LIB_PPM.*\)$|#\1|' \
      -e 's/DEFINES :=/DEFINES = -DHAVE_GETLINE/' \
      -i Makefile || return 1
  make || return 1
  install -m 755 pgm2txt "${pkgdir}/usr/bin/" || return 1
  install -m 755 srttool "${pkgdir}/usr/bin/" || return 1
  install -m 755 subtitle2pgm "${pkgdir}/usr/bin/" || return 1
  install -m 755 subtitle2vobsub "${pkgdir}/usr/bin/" || return 1
  install -m 755 vobsub2pgm "${pkgdir}/usr/bin/" || return 1
}
