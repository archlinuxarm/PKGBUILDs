# $Id: PKGBUILD 50382 2009-08-25 20:26:50Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=0.23.1
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/devhelp/"
depends=('gconf>=2.26.2-2' 'libwnck>=2.26.2' 'libwebkit>=1.1.10' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
install="devhelp.install"
source=(http://ftp.gnome.org/pub/GNOME/sources/devhelp/0.23/devhelp-${pkgver}.tar.bz2)
sha256sums=('186d6e658d9da31e0fdb51b42ccdd65cf535b3afc136a0191057cfffc48e6714')

build() {
  cd "${srcdir}/devhelp-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain devhelp ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
