# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

# PlugApps: Kevin Mihelich <kevin@plugapps.com>
#  - Arch replaced this with libjpeg-turbo, which relies on x86 asm

plugrel=1
noautobuild=1

pkgname=libjpeg
# pkgver follows the library soname to avoid issues with version comparisons
pkgver=8.3.0
_pkgver=8c
pkgrel=1
pkgdesc="Library of JPEG support functions"
arch=('i686' 'x86_64')
url="http://www.ijg.org/"
license=('custom')
depends=('glibc')
makedepends=('libtool')
options=('!libtool')
source=(http://www.ijg.org/files/jpegsrc.v${_pkgver}.tar.gz)
md5sums=('a2c10c04f396a9ce72894beb18b4e1f9')

build() {
  cd $srcdir/jpeg-${_pkgver}
  ./configure --prefix=/usr --enable-shared --enable-static
  make
}

package() {
  cd $srcdir/jpeg-${_pkgver}
  make prefix=$pkgdir/usr mandir=$pkgdir/usr/share/man install
  
  # See FS#11116 - re-assess current need sometime...
  install -m644 jpegint.h $pkgdir/usr/include

  install -Dm644 README $pkgdir/usr/share/licenses/libjpeg/README
}
