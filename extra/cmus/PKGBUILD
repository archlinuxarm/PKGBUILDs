# $Id: PKGBUILD 36121 2009-04-20 02:47:12Z eric $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=cmus
pkgver=2.2.0
pkgrel=9
pkgdesc="A very feature-rich ncurses-based music player"
arch=(i686 x86_64)
url="http://cmus.sourceforge.net/"
license=('GPL')
# namcap assured me it was safe to remove ncurses and the like... /shrug
depends=(libao libmpcdec 'ffmpeg>=0.5' flac libmad 'faad2>=2.7')
source=(http://mirror.greaterscope.net/cmus/$pkgname-$pkgver.tar.bz2)
md5sums=('7a9895ecfc10cd16577c73051436962f')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|ffmpeg/avcodec.h|libavcodec/avcodec.h|' ffmpeg.c || return 1
  sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' ffmpeg.c || return 1
  sed -i 's|ffmpeg/avio.h|libavformat/avio.h|' ffmpeg.c || return 1

  export CONFIG_ARTS=n
  ./configure prefix=/usr || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1
}
