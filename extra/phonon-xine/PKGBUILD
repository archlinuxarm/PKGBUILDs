# $ Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=phonon-xine
pkgver=4.3.1
pkgrel=2
pkgdesc='Xine backend for phonon'
arch=('i686' 'x86_64')
url='http://phonon.kde.org'
license=('LGPL')
install=phonon.install
depends=('qt>=4.5.2-3' 'xine-lib')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://download.kde.org/stable/4.2.1/src/phonon-${pkgver}.tar.bz2")
md5sums=('767cb68052c108e95f293f30acdef3fb')

build() {
	cd $srcdir
	mkdir build
	cd build
	sed -e '/add_subdirectory(phonon)/d' \
	    -e '/add_subdirectory(includes)/d' \
	    -i ../phonon-${pkgver}/CMakeLists.txt
	cmake ../phonon-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DWITH_GStreamer=OFF
	cd xine
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
	mv $pkgdir/usr/lib/kde4 $pkgdir/usr/lib/qt
	mv $pkgdir/usr/share/icons/oxygen $pkgdir/usr/share/icons/hicolor
	rm -f $pkgdir/usr/lib/pkgconfig/phonon.pc
}
