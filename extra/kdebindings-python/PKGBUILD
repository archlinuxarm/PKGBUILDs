# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - pykde sip patch for arm

pkgbase=kdebindings-python
pkgname=('kdebindings-python'
         'kdebindings-python2'
         'kdebindings-python-common')
pkgver=4.14.3
pkgrel=7
url='https://projects.kde.org/projects/kde/kdebindings/pykde4'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
makedepends=('cmake' 'automoc4' 'boost' 'python2-pyqt4' 'python-pyqt4' 'python2-sip' 'python-sip' 'mesa' 'kdelibs')
source=("http://download.kde.org/stable/${pkgver}/src/pykde4-${pkgver}.tar.xz"
        pykde4-4.14.3-missing_symbols.patch
        pykde4-4.14.0-arm-sip.patch)
sha1sums=('9cc8e70d3b107a876cf579beab1f582861cd2c48'
          '54ace7648dc542c681d6d3164685e6fb6be47f9f'
          '7a96f224bb9e28adb3ef2566b4fed973687eeea5')

prepare() {
  mkdir build

  cd pykde4-${pkgver}
  patch -p1 -i ../pykde4-4.14.3-missing_symbols.patch
  patch -p1 -i ../pykde4-4.14.0-arm-sip.patch
}

build() {
  export PYTHONDONTWRITEBYTECODE="TRUE"

  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DPYTHON_LIBRARY=/usr/lib/libpython3.5m.so.1.0 \
    -DPYKDEUIC4_ALTINSTALL=TRUE \
    -DWITH_Nepomuk=OFF \
    -DWITH_Soprano=OFF
  make
  cd ..

  mkdir build-python2
  cd build-python2
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DPYKDEUIC4_ALTINSTALL=TRUE \
    -DWITH_Nepomuk=OFF \
    -DWITH_Soprano=OFF
  make
}

package_kdebindings-python() {
  pkgdesc="A set of Python 3.x bindings for KDE"
  depends=('python-pyqt4' 'kdebindings-python-common' 'kdelibs')

  cd build
  make DESTDIR="${pkgdir}" install

  # Provided by kdebindings-python-common
  rm -r "${pkgdir}"/usr/share/sip

  ln -s /usr/bin/pykdeuic4-3.5 "${pkgdir}"/usr/bin/pykdeuic4
}

package_kdebindings-python2() {
  pkgdesc="A set of Python 2.x bindings for KDE"
  depends=('python2-pyqt4' 'kdebindings-python-common' 'kdelibs')
  conflicts=('kdebindings-python<4.8.1-2')

  cd build-python2
  make DESTDIR="${pkgdir}" install

  # Provided by kdebindings-python-common
  rm -r "${pkgdir}"/usr/share/

  # pykdeuic4 should point to the python3 version
  #rm "${pkgdir}"/usr/bin/pykdeuic4

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_kdebindings-python-common() {
  pkgdesc="Common files shared between python2 and python3 kdebindings"
  depends=()
  conflicts=('kdebindings-python<4.8.1-2')

  cd build
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/usr/{bin,lib,share/apps}
}
