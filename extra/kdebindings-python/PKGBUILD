# $Id: PKGBUILD 153599 2012-03-16 14:50:09Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - pykde sip patch for arm

pkgbase=kdebindings-python
pkgname=('kdebindings-python'
         'kdebindings-python2')
pkgver=4.9.2
pkgrel=2
url='https://projects.kde.org/projects/kde/kdebindings/pykde4'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
makedepends=('cmake' 'automoc4' 'boost' 'kdepim-runtime' 'python2-pyqt'
             'pyqt' 'qscintilla' 'python2-sip' 'python-sip')
source=("http://download.kde.org/stable/${pkgver}/src/pykde4-${pkgver}.tar.xz"
        "pykde4-4.4.4-arm-sip.patch"
        'pyqt495.patch')
sha1sums=('44b3f02797c43a59f8c3a91131a2488a733cfa11'
          '20d37a8b127eebffc4556352168786036969ee78'
          '30def4a0ff94c9f3f173347e0e0bcac195cfe780')

build() {
  export PYTHONDONTWRITEBYTECODE="TRUE"

  cd "${srcdir}/pykde4-${pkgver}"
  patch -Np3 -i "${srcdir}/pykde4-4.4.4-arm-sip.patch"
  patch -p1 -i "${srcdir}"/pyqt495.patch
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DPYTHON_LIBRARY=/usr/lib/libpython3.3m.so.1.0
  make
  cd ..

  mkdir build-python2
  cd build-python2
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package_kdebindings-python() {
  pkgdesc="A set of Python 3.x bindings for KDE"
  depends=('kdepim-runtime' 'pyqt' 'qscintilla')

  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}

package_kdebindings-python2() {
  pkgdesc="A set of Python 2.x bindings for KDE"
  depends=('kdepim-runtime' 'python2-pyqt' 'qscintilla')
  conflicts=('kdebindings-python<4.8.1-2')
 
  cd "${srcdir}"/build-python2
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm "${pkgdir}"/usr/bin/pykdeuic4
  ln -sf /usr/lib/python2.7/site-packages/PyQt4/uic/pykdeuic4.py \
    "${pkgdir}"/usr/bin/python2-pykdeuic4

  mv "${pkgdir}"/usr/share/apps/pykde4/examples \
    "${pkgdir}"/usr/share/apps/pykde4/python2-examples

  mv "${pkgdir}"/usr/share/sip/PyKDE4 \
    "${pkgdir}"/usr/share/sip/python2-PyKDE4

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
