# $Id: PKGBUILD 153599 2012-03-16 14:50:09Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - pykde sip patch for arm

pkgbase=kdebindings-python
pkgname=('kdebindings-python'
         'kdebindings-python2'
         'kdebindings-python-common')
pkgver=4.14.3
pkgrel=1
url='https://projects.kde.org/projects/kde/kdebindings/pykde4'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
makedepends=('cmake' 'automoc4' 'boost' 'kdepim-runtime' 'python2-pyqt4'
             'python-pyqt4' 'qscintilla' 'python2-sip' 'python-sip' 'mesa')
source=("http://download.kde.org/stable/${pkgver}/src/pykde4-${pkgver}.tar.xz"
        "pykde4-4.14.0-arm-sip.patch")
sha1sums=('9cc8e70d3b107a876cf579beab1f582861cd2c48'
          'f0798d25767768006d5f674049e51b2e39f9f4eb')

prepare() {
  mkdir build

  cd pykde4-${pkgver}
  patch -Np1 -i "${srcdir}/pykde4-4.14.0-arm-sip.patch"
}

build() {
  export PYTHONDONTWRITEBYTECODE="TRUE"

  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DPYTHON_LIBRARY=/usr/lib/libpython3.4m.so.1.0 \
    -DPYKDEUIC4_ALTINSTALL=TRUE \
    -DWITH_Nepomuk=OFF \
    -DWITH_Soprano=OFF
  make
  cd ..

  mkdir build-python2
  cd build-python2
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DPYKDEUIC4_ALTINSTALL=TRUE \
    -DWITH_Nepomuk=OFF \
    -DWITH_Soprano=OFF
  make
}

package_kdebindings-python() {
  pkgdesc="A set of Python 3.x bindings for KDE"
  depends=('kdepim-runtime' 'python-pyqt4' 'kdebindings-python-common')

  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Provided by kdebindings-python-common
  rm -r "${pkgdir}"/usr/share/apps/pykde4/examples
  rm -r "${pkgdir}"/usr/share/sip/PyKDE4

  ln -s /usr/bin/pykdeuic4-3.4 "${pkgdir}"/usr/bin/pykdeuic4
}

package_kdebindings-python2() {
  pkgdesc="A set of Python 2.x bindings for KDE"
  depends=('kdepim-runtime' 'python2-pyqt4'  'kdebindings-python-common')
  conflicts=('kdebindings-python<4.8.1-2')
 
  cd "${srcdir}"/build-python2
  make DESTDIR="${pkgdir}" install

  # Provided by kdebindings-python-common
  rm -r "${pkgdir}"/usr/share/apps/pykde4/examples
  rm -r "${pkgdir}"/usr/share/sip/PyKDE4

  # pykdeuic4 should point to the python3 version
  #rm "${pkgdir}"/usr/bin/pykdeuic4

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_kdebindings-python-common() {
  pkgdesc="Common files shared between python2 and python3 kdebindings"
  depends=('qscintilla')
  conflicts=('kdebindings-python<4.8.1-2')

  cd build
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/usr/{bin,lib}
}
