# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Matthew Ellison <matt+aur@arroyonetworks.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto via -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=OFF

pkgname=pybind11
pkgver=3.0.0
pkgrel=1
pkgdesc='A lightweight header-only library that exposes C++ types in Python and vice versa'
arch=('any')
url='https://pybind11.readthedocs.org/'
license=('BSD-3-Clause')
depends=(
    'python')
makedepends=(
    'boost'
    'cmake'
    'eigen'
    'python-build'
    'python-installer'
    'python-pytest'
    'python-scikit-build-core'
    'python-wheel')
checkdepends=(
    'python-numpy')
source=("https://github.com/pybind/pybind11/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('453b1a3e2b266c3ae9da872411cadb6d693ac18063bd73226d96cfb7015a200c')

build () {
    cd "${pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
    
    # tests
    cmake \
        -G 'Unix Makefiles' \
        -B "${srcdir}/build-tests" \
        -S "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DDOWNLOAD_CATCH:BOOL='ON' \
        -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=OFF \
        -Wno-dev
    cmake --build "${srcdir}/build-tests"
}

check() {
    make -C build-tests check
}

package() {
    python -m installer --destdir="$pkgdir" "${pkgname}-${pkgver}/dist"/*.whl
    
    local _pyver
    _pyver="$(python -c 'import sys; print("%s.%s" %sys.version_info[:2])')"
    
    install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s "../../../lib/python${_pyver}/site-packages/pybind11-${pkgver}.dist-info/licenses/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    
    install -d -m755 "${pkgdir}/usr"/{include,lib/{cmake,pkgconfig}}
    ln -s "../lib/python${_pyver}/site-packages/pybind11/include/pybind11" "${pkgdir}/usr/include/pybind11"
    ln -s "../../lib/python${_pyver}/site-packages/pybind11/share/cmake/pybind11" "${pkgdir}/usr/lib/cmake/pybind11"
    ln -s "../../lib/python${_pyver}/site-packages/pybind11/share/pkgconfig/pybind11.pc" "${pkgdir}/usr/lib/pkgconfig/pybind11.pc"
}
