# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Robert Emil Berge

# move to github source? see frontpage news updates
# and also https://github.com/swh/ladspa

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build AArch64 with -fPIC

pkgname=swh-plugins
pkgver=0.4.15
pkgrel=6
pkgdesc="Steve Harris' LADSPA plug-ins suite"
arch=('i686' 'x86_64')
url="http://plugin.org.uk/"
license=('GPL')
depends=('fftw' 'ladspa')
makedepends=('libxml2' 'perl-xml-parser')
groups=('ladspa-plugins')
source=("http://plugin.org.uk/releases/$pkgver/$pkgname-$pkgver.tar.gz"
        'mbeq_crash_fix.patch')
md5sums=('2fbdccef2462ea553901acd429fa3573'
         'a60422c8dea95bee6f9adcdc70dff7e0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Arch64 fix;
  # -fPIC seems to be the only option
  if [ "$CARCH" = 'x86_64' -o "$CARCH" = 'aarch64' ]; then
    export CFLAGS="$CFLAGS -fPIC"
    export CXXFLAGS="$CFLAGS"
  fi

  # fix Firefox crash on x64 when
  # plugin is in use by ALSA;
  # see https://bugs.archlinux.org/task/24579
  patch -Np1 -i "$srcdir/mbeq_crash_fix.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
