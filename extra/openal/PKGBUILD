# $Id: PKGBUILD 155305 2012-04-01 14:23:52Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix for unimplemented _FPU_EXTENDED

pkgname=openal
pkgver=1.15.1
pkgrel=2
pkgdesc="A cross-platform 3D audio library"
arch=(i686 x86_64)
url="http://www.openal.org/"
license=(LGPL)
depends=(glibc)
makedepends=(alsa-lib sdl pkgconfig cmake libpulse)
options=('!libtool')
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-$pkgver.tar.bz2
        no-fpuextended.patch)
md5sums=('ea83dec3b9655a27d28e7bc7cae9cd71'
         'de9569536f9f1c2b7b0e90d381c631de')

build() {
  cd "${srcdir}/$pkgname-soft-$pkgver"
  patch -p1 -i "${srcdir}"/no-fpuextended.patch
  cd "${srcdir}/$pkgname-soft-$pkgver/build"
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd $pkgname-soft-$pkgver
  make -C build DESTDIR="$pkgdir/" install
  install -t "$pkgdir/usr/share/openal" env-vars.txt hrtf.txt
}
