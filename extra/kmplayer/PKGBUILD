# $Id: PKGBUILD 42646 2009-06-18 00:36:54Z giovanni $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.11.1b
pkgrel=1
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
url="http://kmplayer.kde.org"
depends=('kdelibs' 'gtk2')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://kmplayer.kde.org/pkgs/$pkgname-$pkgver.tar.bz2")
md5sums=('a6b2c9dd8650e36cd6521d152bb5d9c9')
options=('force')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
  make DESTDIR=${pkgdir} install

  # icons are included in oxygen-icons
  rm -rf ${pkgdir}/usr/share/icons/oxygen/
}
