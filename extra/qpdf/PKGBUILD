# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Miguel Revilla <yo at miguelrevilla.com>
# Contributor: David Sotelo <dvsotelo at gmail.com>
# Contributor: Nuno Araujo <nuno.araujo@russo79.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=qpdf
pkgver=10.0.1
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="https://github.com/qpdf/qpdf"
license=('custom:Artistic-2.0' 'Apache')
depends=('libjpeg-turbo' 'zlib' 'gnutls')
#optdepends=('perl:	required for fix-qdf')
#checkdepends=('perl')
# https://github.com/qpdf/qpdf/releases/download/release-qpdf-10.0.0/qpdf-10.0.0.sha512
source=(#https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
        https://github.com/qpdf/qpdf/releases/download/release-${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('b73c0839b08905052d90ed78645cc9fb4fda4a0fa803db7b46f3d77a2c2f7e8cec52173834b1e1f008f3a3475a7d913362d22e1ebece11187a90c9035b81de94'
            'SKIP')
validpgpkeys=('C2C96B10011FE009E6D1DF828A75D10998012C7E') # "Jay Berkenbilt <ejb@ql.org>"

build() {
  cd ${pkgname}-${pkgver}
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure --prefix=/usr \
    --disable-static \
    --enable-crypto-gnutls \
    --disable-crypto-openssl \
    --disable-implicit-crypto \
    --enable-show-failed-test-output
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check # passes all
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install

  install -Dm644 completions/bash/qpdf "${pkgdir}/usr/share/bash-completion/completions/qpdf"
  install -Dm644 completions/zsh/_qpdf "${pkgdir}/usr/share/zsh/site-functions/_qpdf"

  mkdir -m755 -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 {Artistic-2.0,LICENSE.txt} "${pkgdir}"/usr/share/licenses/${pkgname}/
}
