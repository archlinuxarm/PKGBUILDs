# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon on !AArch64

pkgname=botan
pkgver=2.17.3
pkgrel=1
pkgdesc='Crypto library written in C++'
arch=(x86_64)
url='https://botan.randombit.net/'
license=(BSD)
depends=(xz sqlite)
makedepends=(python boost openssl)
optdepends=('python: for using botan2.py' 'boost-libs: for the botan executable')
source=("https://botan.randombit.net/releases/Botan-${pkgver}.tar.xz"{,.asc})
sha256sums=('79123b654445a4abba486e09a431788545c708237382a3e765664c9f55b03b88'
            'SKIP')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC') # Botan Distribution Key

build() {
  cd "${pkgname^}-$pkgver"

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"

  ./configure.py \
    --prefix=/usr \
    --with-bzip \
    --with-lzma \
    --with-zlib \
    --with-boost \
    --with-openssl \
    --with-sqlite3 \
    --with-os-feature=getrandom \
    $CONFIG

  make
}

check() {
  cd "${pkgname^}-$pkgver"

  LD_LIBRARY_PATH="$PWD" ./botan-test
}

package() {
  DESTDIR="$pkgdir" make -C "${pkgname^}-$pkgver" install
  install -Dm644 "${pkgname^}-$pkgver/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
