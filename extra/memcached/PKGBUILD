# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depend on libseccomp, don't configure with --enable-seccomp; problematic on ARM

pkgname=memcached
pkgver=1.6.17
pkgrel=1
pkgdesc='Distributed memory object caching system'
url='https://memcached.org/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'libevent')
optdepends=('perl: for memcached-tool usage')
source=(https://www.memcached.org/files/${pkgname}-${pkgver}.tar.gz
        memcached.service.patch
        memcached.tmpfiles
        memcached.sysusers)
sha512sums=('90ddd1e0dac3eb27273c1ee0bf6a7f75248f0267bca37b88455ff48727ab4f9db07382484d1e0c9b4a0126198b147f53aea4c993259ac608e8de393e11dad673'
            'dd21f49062054c601469f9abc9ee39a0f06c604e8805e7bc9bf2cfa65429962f93e7ac67764257c578fc15b1e75394956f350162c99b084c76a6e14d9331f31f'
            '960705ff74d25afed477e0b2a5872a3a4fb49ed3105a351f0d0224abc947778f9dbda81e80be94ab636da4a8411a9dd56a8fd4513e5b86a3096a14fa67f1548b'
            'e6ddcab9a6fee024072b6363ef60aa176ed258369bf3a17d475f19b1f410ffd6195b9c5737dc5b1371e8974b44bdbdaa109927acaeb54fb40302a5d67d7c13a8')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../memcached.service.patch
  sed -e 's/^##safer##//g' -i scripts/*.service
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm 755 scripts/memcached-tool -t "${pkgdir}/usr/bin"
  install -Dm 644 scripts/memcached-tool.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 scripts/*.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 ../memcached.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/memcached.conf"
  install -Dm 644 ../memcached.sysusers "${pkgdir}/usr/lib/sysusers.d/memcached.conf"
}

# vim: ts=2 sw=2 et:
