# $Id: PKGBUILD 52281 2009-09-17 06:15:22Z allan $
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gconf
pkgver=2.26.2
pkgrel=3
pkgdesc="A configuration database system"
arch=(i686 x86_64)
license=('LGPL')
depends=('orbit2>=2.14.17' 'gtk2>=2.16.1' 'libxml2>=2.7.3' 'policykit>=0.9' 'libldap>=2.4.18')
makedepends=('pkgconfig' 'intltool>=0.40.6' 'gtk-doc>=1.10')
options=('!libtool')
install=gconf.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/2.26/GConf-${pkgver}.tar.bz2
	gconf-merge-schema
	gconfpkg
	gconf-reload.patch
	01_xml-gettext-domain.patch
	05_from_vuntz_gconf2-pk-default-path.patch)
md5sums=('8b55d58dab13e821e048627aa4f92b8b'
         '240b473fa17b748fc9020caf372d936c'
         'b745f04b4121d8724c002b14a4c110b6'
         'cfcc8e15be7b8a48de4aa34336ff6090'
         '1f9362976d71312bdb57c57fa031681d'
         'd9a02aee25ec04f2340f4e98963aabb0')

build() {
  cd "${srcdir}/GConf-${pkgver}"
  # Patch from fedora - reloads gconf after installing schemas
  patch -Np1 -i "${srcdir}/gconf-reload.patch" || return 1
  # http://bugzilla.gnome.org/show_bug.cgi?id=568845
  patch -Np1 -i "${srcdir}/01_xml-gettext-domain.patch" || return 1
  # Patch from Ubuntu: change default path
  patch -Np1 -i "${srcdir}/05_from_vuntz_gconf2-pk-default-path.patch" || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --libexecdir=/usr/lib/GConf \
	      --disable-static --enable-defaults-service || return 1
  make pkglibdir=/usr/lib/GConf || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/etc/gconf/gconf.xml.system" || return 1
  install -m755 "${srcdir}/gconf-merge-schema" "${pkgdir}/usr/bin/" || return 1
  install -d -m755 "${pkgdir}/usr/sbin"
  install -m755 "${srcdir}/gconfpkg" "${pkgdir}/usr/sbin/" || return 1
}
