# $Id: PKGBUILD 47699 2009-07-25 12:42:08Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=kvm
pkgver=88
pkgrel=1
pkgdesc="Latest KVM QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL2')
url="http://www.linux-kvm.org"
depends=('libsasl' 'curl' 'sdl' 'alsa-lib' 'esound' 'gnutls>=2.4.1' 'bluez')
install=kvm.install
conflicts=('qemu')
provides=('qemu')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        qemu-fix-build-for-esd-audio.patch
        65-kvm.rules)

build()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    # don't compile kvm kernel module!
    rm kvm/kernel/configure
    # fix esound building
    patch -Np1 -i ../qemu-fix-build-for-esd-audio.patch || return 1
    ./configure --prefix=/usr --audio-drv-list=alsa,sdl,oss,esd --audio-card-list=ac97,sb16,es1370,adlib
    make || return 1
    make DESTDIR=${pkgdir} install || return 1
    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/qemu-kvm
    # install udev rules
    install -D -m644 ${srcdir}/65-kvm.rules \
                     ${pkgdir}/lib/udev/rules.d/65-kvm.rules
}
md5sums=('02371948fcee1fa2a77e7a457384d71c'
         '695089ee5a373f6c0d90949dd62cfc81'
         'cead7ba387159d360f892281499975df')
