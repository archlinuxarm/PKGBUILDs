# $Id: PKGBUILD 22479 2008-12-26 22:10:40Z giovanni $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=revelation
pkgver=0.4.11
pkgrel=4
pkgdesc="Password manager for the GNOME 2 desktop"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-python-desktop>=2.18.0' 'pycrypto>=2.0.1-2' 'gnome-panel>=2.18.1' 'cracklib' 'desktop-file-utils')
install=revelation.install
url="http://oss.codepoet.no/revelation/"
source=(ftp://oss.codepoet.no/revelation/revelation-${pkgver}.tar.bz2
	fs12034.patch)
md5sums=('e2db4a2f00f59b18798d4453c778129f' 'fd720d187ce5ae631c095872e9db9155')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fs12034.patch" || return 1

  sed -i -e 's/gnome-python-extras/gnome-python-desktop/g' configure
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/revelation \
              --localstatedir=/var \
	      --disable-mime-update --disable-desktop-update || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
