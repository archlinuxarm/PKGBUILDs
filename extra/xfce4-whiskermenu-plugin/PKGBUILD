# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DENABLE_LINK_TIME_OPTIMIZATION=off

pkgname=xfce4-whiskermenu-plugin
pkgver=2.8.4
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=('x86_64')
url='https://docs.xfce.org/panel-plugins/xfce4-whiskermenu-plugin/start'
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('accountsservice' 'garcon' 'gtk3' 'gtk-layer-shell' 'xfce4-panel')
makedepends=('cmake' 'git' 'ninja')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-whiskermenu-plugin.git#tag=v${pkgver}")
sha256sums=('bede0499d5852a9d2b6e3712a41bed7a73a4bfb90010b6c0dff048cde91e1fcf')

build() {
  cmake -B build -S "${pkgname}" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DENABLE_LINK_TIME_OPTIMIZATION=off \
    -GNinja \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
