# $Id: PKGBUILD 44377 2009-07-01 21:17:38Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=imlib
pkgver=1.9.15
pkgrel=6
pkgdesc="General image handling library for X11 and Gtk"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/imlib/"
license=('GPL')
depends=('gtk' 'giflib' 'libpng' 'libtiff' 'libjpeg>=7' 'libsm')
makedepends=('libxt')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/imlib/1.9/${pkgname}-${pkgver}.tar.bz2
        debian-bug448360.patch CAN-2004-1026.patch aclocal-fixes.patch)
md5sums=('7db987e6c52e4daf70d7d0f471238eae' '5f9da697934b6bd3b497ac9160ce4f5c'\
         'b273d36aa60adbfaacaf6062234e4c1f' '33b832f0dc6c9723cd0dfe9c8d0a6797')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/debian-bug448360.patch" || return 1
  patch -Np1 -i "${srcdir}/CAN-2004-1026.patch" || return 1
  patch -Np0 -i "${srcdir}/aclocal-fixes.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --enable-shm || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
