# $Id: PKGBUILD 47945 2009-07-29 19:42:49Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk> and Dale Blount <dale@archlinux.org>

pkgname=nedit
pkgver=5.5
pkgrel=5
pkgdesc="A Unix text editor for programmers and general users"
arch=('i686' 'x86_64')
url="http://www.nedit.org/"
license=('GPL')
depends=('lesstif' 'libxpm')
source=(http://www.nedit.org/ftp/v5_5/${pkgname}-${pkgver}-src.tar.bz2 nedit_xorg_composite_fix.patch)
md5sums=('48cb3dce52d44988f3a4d7c6f47b6bbe' 'ae1f56ae9b23163882051720ac52412d')
sha1sums=('7d3c87a793a5047cf857af1ac82e39f3b33e8a87' '1f6c75075a6bdc7b0dd8ca89d8117d68af8fc54e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../nedit_xorg_composite_fix.patch || return 1
  sed -i 's/-Wl,-Bstatic//' makefiles/Makefile.linux || return 1
  sed -i 's|fgets|//fgets|' util/check_lin_tif.c || return 1
  sed -i "s/CFLAGS=-O/CFLAGS=${CFLAGS} -DBUILD_UNTESTED_NEDIT/" makefiles/Makefile.linux || return 1
  sed -i 's|"/bin/csh"|"/bin/sh"|' source/preferences.c || return 1

  make linux || return 1
  install -D -m755 source/nedit "${pkgdir}/usr/bin/nedit" || return 1
  install -D -m755 source/nc "${pkgdir}/usr/bin/nedit-client" || return 1
  install -D -m644 doc/nedit.man "${pkgdir}/usr/share/man/man1/nedit.1" || return 1
  install -D -m644 doc/nc.man "${pkgdir}/usr/share/man/man1/nedit-client.1" || return 1
}
