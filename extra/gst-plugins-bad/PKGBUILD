# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop valgrind makedepend
#  - disable GL and GLX on v5/6/7

pkgname=gst-plugins-bad
pkgver=1.6.2
pkgrel=3
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base-libs' 'curl' 'chromaprint' 'libmms' 'faad2' 'mpg123' 'faac' 'celt' 'libdca'
         'soundtouch' 'spandsp' 'libdvdnav' 'libmodplug' 'libgme' 'opus' 'wayland' 'neon' 'libofa' 'fluidsynth'
         'openjpeg' 'libwebp' 'libsrtp' 'gnutls' 'wildmidi' 'glu' 'sbc' 'rtmpdump' 'libgudev' 'graphene')
makedepends=('schroedinger' 'libexif' 'libdvdread' 'libvdpau' 'libmpeg2' 'python' 'wildmidi' 'librsvg'
             'gobject-introspection' 'gtk-doc' 'gtk3' 'clutter' 'opencv' 'libtiger' 'ladspa' 'openal' 'libusb'
             'qt5-declarative' 'qt5-x11extras' 'qt5-wayland')
options=(!emptydirs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('650855e39ff56a8bb6cb0c192109c5926ce12f536d06e19ebf829de71ef396fe')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-gl --disable-glx --enable-gles2"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --enable-gtk-doc \
    --with-package-name="GStreamer Bad Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --with-gtk=3.0 $CONFIG

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  # don't have a camera for the camerabin test
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
