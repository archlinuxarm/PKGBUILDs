# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO: -DENABLE_LTO='FALSE'

pkgbase=vbam
pkgname=(
  vbam-sdl
  vbam-wx
)
_pkgname=visualboyadvance-m
pkgver=2.1.10
pkgrel=1
pkgdesc='Nintendo GameBoy Advance emulator'
arch=(x86_64)
url='https://visualboyadvance-m.org'
license=(GPL2)
depends=(
  glu
  libsfml-network.so
  libsfml-system.so
  libgl
  libpng
  sdl2
  sfml
  zlib
)
makedepends=(
  cmake
  doctest
  ffmpeg
  git
  libglvnd
  ninja
  wxwidgets-gtk3
  zip
)
source=("git+https://github.com/${_pkgname}/${_pkgname}.git?signed#tag=v${pkgver}")
b2sums=('81c21a5e2ddc1a9c3ca100c95d804cad11b376ba88e0846ba2d25a9ca96c8fe0c379a332daf4f919958b8a47bbf4d7ded412ec0bf879a94203305c310d665438')
validpgpkeys=('A0C0E526E36FD2138C149D4D08AB596679D86240' # Rafael Kitover <rkitover@gmail.com>
              '0662A706ABE516087A23D1B9F2E06F4EE438BE81' # Zach Bacon <zachbacon@vba-m.com>
)

prepare() {
  cd ${_pkgname}
  # Unbundle doctest (bundled one is not glibc 2.34 compatible)
  sed -e 's|${CMAKE_SOURCE_DIR}/third_party|/usr|' -i src/wx/test/CMakeLists.txt
}

build() {
  cmake -S ${_pkgname} -B build-sdl -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=TRUE \
    -DENABLE_SDL=TRUE \
    -DENABLE_WX=FALSE \
    -DENABLE_LINK=TRUE \
    -DENABLE_LTO=FALSE \
    -Wno-dev
  cmake --build build-sdl

  cmake -S ${_pkgname} -B build-wx -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=TRUE \
    -DENABLE_SDL=FALSE \
    -DENABLE_WX=TRUE \
    -DENABLE_FFMPEG=TRUE \
    -DENABLE_LINK=TRUE \
    -DENABLE_LTO=FALSE \
    -Wno-dev
  cmake --build build-wx
}

package_vbam-sdl() {
  backup=(etc/vbam.cfg)
  conflicts=(vbam-wx)

  DESTDIR="${pkgdir}" cmake --install build-sdl
}

package_vbam-wx() {
  depends+=(
    glib2
    gtk3
    libavcodec.so
    libavformat.so
    libavutil.so
    libswscale.so
    openal
    wxwidgets-gtk3
    zip
  )
  conflicts=(vbam-sdl)

  DESTDIR="${pkgdir}" cmake --install build-wx
}

# vim: ts=2 sw=2 et:
