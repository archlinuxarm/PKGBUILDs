# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --enable-high-precision for v6/v7 to fix stack corruption
#  - patch to fix enabling high precision

pkgname=sbc
pkgver=1.3
pkgrel=2.2
pkgdesc="Bluetooth Subband Codec (SBC) library"
arch=('x86_64')
url="http://www.bluez.org/"
license=('GPL' 'LGPL')
depends=('glibc')
source=(http://www.kernel.org/pub/linux/bluetooth/$pkgname-$pkgver.tar.xz
        0001-Add-config.h-in-sbc-sbc_primitives.h.patch)
sha512sums=('c11cb1ede8abdb226d404ce34307d217b716b91cbe550ed90cb5387b062b7b97a6b7a81371de6f462d8c1429cb3299869a1bda347fa175e9d81cfa2bc2056c8b'
            'c2810208cc8b7a80361c4decc9e56ac26d1409174bff450a7f7c30e4b494292fcad7e78664a9fc981dd260cc2a699ad991954a29bbab497413941a7db3178d73')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-Add-config.h-in-sbc-sbc_primitives.h.patch
}

build() {
  cd $pkgname-$pkgver
  [[ $CARCH == "armv6h" || $CARCH == "armv7h" ]] && CONFIG="--enable-high-precision"
  ./configure --prefix=/usr --disable-static --disable-tester $CONFIG
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
