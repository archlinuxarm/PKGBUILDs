# Maintainer: Caleb Maclennan <caleb@alerque.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable lto
#  - remove makedepend on rocm-smi-lib

pkgname=btop
pkgver=1.4.6
pkgrel=1
pkgdesc='A monitor of system resources, bpytop ported to C++'
arch=(x86_64)
url="https://github.com/aristocratos/$pkgname"
license=(Apache-2.0)
depends=(gcc-libs
         glibc)
makedepends=(lowdown)
optdepends=('rocm-smi-lib: AMD GPU support')
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('4beb90172c6acaac08c1b4a5112fb616772e214a7ef992bcbd461453295a58be')

build() {
	cd "$_archive"
	sed -i 's/-flto\S*//' Makefile
	make all
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" PREFIX=/usr install
	make DESTDIR="$pkgdir" PREFIX=/usr setcap
}

