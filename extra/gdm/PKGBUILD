# $Id: PKGBUILD 49666 2009-08-14 20:12:36Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdm
pkgver=2.20.10
pkgrel=2
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=(i686 x86_64)
license=('GPL')
depends=('pam>=1.0.2' 'libdmx' 'tcp_wrappers>=7.6' 'libgnomecanvas>=2.26.0' 'librsvg>=2.26.0' 'gksu>=2.0.0' 'dbus-glib>=0.80' 'consolekit' 'zenity>=2.26.0')
makedepends=('intltool' 'gnome-doc-utils>=0.16.0' 'pkgconfig' 'xorg-server')
install=gdm.install
url="http://www.gnome.org"
groups=('gnome-extra')
backup=('etc/gdm/custom.conf' 'etc/pam.d/gdm' 'etc/pam.d/gdm-autologin')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.20/${pkgname}-${pkgver}.tar.bz2
        gdm
        defaults.conf
        gdm.pam
        gdm-autologin.pam)
md5sums=('981c7ee7fbe453061e095ab52a3513f8'
         '2fdb6d6fd7fd8124b1e00621b3d238f9'
         'fa61d885f0368756a7d8e7f7abdb11d9'
         '2e52f326dccc833e9c135f8df8297b12'
         '157f32e089a7aab50732dc122e592b35')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm \
      --localstatedir=/var/lib --disable-static \
      --with-xevie=yes --disable-scrollkeeper \
      --enable-secureremote || return 1
  sed -i -e 's|${prefix}|/usr|' config.h || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  #PAM, we use our own, not Redhat stuff
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin" || return 1
  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm" || return 1
  
  #init script and configuration
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/" || return 1
  install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/gdm/" || return 1 
  rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop
  # fix gdmsetup 
  sed -i -e 's|^Exec=|Exec=gksu |' "${pkgdir}/usr/share/gdm/applications/gdmsetup.desktop" || return 1
}
