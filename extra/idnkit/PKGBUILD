# $Id: PKGBUILD 180328 2013-03-20 15:10:14Z bisson $
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=idnkit
pkgver=1.0
pkgrel=2
pkgdesc='Provides functionalities about Internationalized Domain Name processing'
url='http://www.nic.ad.jp/ja/idn/idnkit/download/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('glibc')
options=('!makeflags' '!libtool')
source=("http://www.nic.ad.jp/ja/idn/idnkit/download/sources/idnkit-${pkgver}-src.tar.gz")
sha1sums=('7d843ffcf9843b7af02079e5a160520f28c75048')

backup=('etc/idnalias.conf' 'etc/idn.conf')

build() {
	export CPPFLAGS=""
	cd "${srcdir}/idnkit-${pkgver}-src"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--mandir=/usr/share/man \
		--enable-static=no
	make
}

check() {
	cd "${srcdir}/idnkit-${pkgver}-src"
	make check
}

package() {
	cd "${srcdir}/idnkit-${pkgver}-src"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	rm "${pkgdir}"/etc/*.sample
}
