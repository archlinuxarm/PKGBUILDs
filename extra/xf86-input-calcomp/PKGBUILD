# $Id: PKGBUILD 36713 2009-04-27 00:15:55Z eric $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-input-calcomp
pkgver=1.1.2
pkgrel=3
pkgdesc="X.Org CalComp Drawing Board input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'xorg-util-macros')
conflicts=('xorg-server<1.6.0')
options=('!libtool')
groups=('xorg-input-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	git-931367e1.patch LICENSE)
md5sums=('ebc9a64dc72ae3015c16011bbd2d3557' '418bf8f0b75173a5e769c9af20972ba7'\
         'dc316976a34c309431481f51e409d1f6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-931367e1.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
