# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepends on pandoc

pkgname=owl-lisp
pkgver=0.2.1
pkgrel=1
pkgdesc='Simple purely functional lisp'
url='https://gitlab.com/owl-lisp/owl.git'
arch=('x86_64')
license=('MIT')
depends=('glibc')
source=(https://gitlab.com/owl-lisp/owl/-/archive/v${pkgver}/owl-v${pkgver}.tar.gz)
sha256sums=('4a8dd3cce411b64ffbea98357f705a91ff900318d035ffb899fc49068b554e75')
b2sums=('082b86b7b233933c78059971b9820a63442ceef74fa6ba82a5afa6068f254f295a187ba928e8e255077d97d9c82710aa94f77717ba328e68e15ade2dd0c9ec6b')

prepare() {
  cd owl-v${pkgver}

  # support distro CFLAGS and CPPFLAGS
  sed -e '/export CFLAGS/d' -e '/CFLAGS =/d' \
      -e 's|$(CFLAGS)|$(CFLAGS) $(CPPFLAGS)|g' -i Makefile

  # make build reproducible, we compress ourselves
  sed -e 's|gzip -9|@echo|g' -e 's|\.gz||g' -i Makefile

  # fix missing Makefile entry
  sed 's|manual.md:|manual.md: bin/ol|' -i Makefile

  # remove failing tests (can't run inside makepkg)
  rm tests/getpid.sh
}

build() {
  cd owl-v${pkgver}
  make owl documentation
}

check() {
  cd owl-v${pkgver}
  make -j1 test fasltest
}

package() {
  cd owl-v${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 fasl/* -t "${pkgdir}/var/lib/${pkgname}/fasl"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md doc/manual.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
