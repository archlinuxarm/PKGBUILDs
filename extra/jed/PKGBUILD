# $Id: PKGBUILD 3013 2008-06-18 02:11:40Z eric $
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=jed
pkgver=0.99.18
pkgrel=4
pkgdesc="JED is a freely available text editor for Unix and other OS'"
arch=(i686 x86_64)
url="http://www.jedsoft.org/jed"
license=('GPL')
depends=('gpm>=1.20.4' 'slang')
makedepends=('libxext' 'libxft' 'libxt')
options=('!makeflags')
source=(ftp://space.mit.edu/pub/davis/$pkgname/v0.99/$pkgname-0.99-18.tar.bz2)
md5sums=('5378c8e7805854018d9ec5c3cfadf637')

build() {
  cd $startdir/src/$pkgname-0.99-18
  CFLAGS=`/usr/bin/freetype-config  --cflags`\ ${CFLAGS}\ -DXJED_HAS_XRENDERFONT=1 ./configure --prefix=/usr/share --exec-prefix=/usr 
  #Enable mouse support
  sed -i -e 's/#MOUSEFLAGS/MOUSEFLAGS/' -e 's/#MOUSELIB/MOUSELIB/' \
  -e 's/#GPMMOUSEO/GPMMOUSEO/' -e 's/#OBJGPMMOUSEO/OBJGPMMOUSEO/' \
  src/Makefile

  #Enable XRender support
  sed -i -e 's/#XRENDERFONTLIBS/XRENDERFONTLIBS/' src/Makefile

  make JED_ROOT=/usr/share/jed || return 1
  make JED_ROOT=/usr/share/jed xjed || return 1
  make JED_ROOT=/usr/share/jed CFLAGS+=-I/usr/include/freetype2 rgrep || return 1
  make DESTDIR=$startdir/pkg install || return 1
  rmdir $startdir/pkg/usr/share/jed/bin
}
