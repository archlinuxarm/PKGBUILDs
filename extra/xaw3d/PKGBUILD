# $Id: PKGBUILD 38898 2009-05-10 18:17:34Z jgc $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=xaw3d
pkgver=1.5E
pkgrel=2
pkgdesc="Three-D Athena widgets"
arch=('i686' 'x86_64')
url="http://directory.fsf.org/project/xaw3d/"
license=('MIT')
depends=('libxmu' 'libxpm')
makedepends=('imake')
options=('!makeflags' 'force')
source=(ftp://ftp.visi.com/users/hawkeyd/X/Xaw3d-${pkgver}.tar.gz
        Xaw3d-1.5E-xorg-imake.patch
	Xaw3d-1.5-i18n.patch
        Xaw3d-1.5-box.c.patch
       	Xaw3d-1.5-debian-fixes.patch
        xaw3d.patch
	Xaw3d-1.5E-warnings.patch
	Xaw3d-ia64.patch
	LICENSE)
md5sums=('29ecfdcd6bcf47f62ecfd672d31269a1'
         '2cca7950cf7d270609bc810b8996f0a9'
         'e7105bc1452f0c5f5d0bcdcf212aac0c'
         'c061ab65467766f5817752ef216e3cc2'
         '33bf0f1b52822b7293a3f45474178dfe'
         '5acb23e5a52c75e62a22aef59882df14'
         'c3aa4cbf95442081e7af810c13306cc9'
         'f766e8eb6371cbb1f68af4f2932dc118'
         '5b6b0242b3f53b7fe308e702abe28561')

build() {
  cd "${srcdir}/xc/lib/Xaw3d"
  patch -Np0 -i "${srcdir}/Xaw3d-1.5E-xorg-imake.patch" || return 1
  patch -Np4 -i "${srcdir}/xaw3d.patch" || return 1
  patch -Np4 -i "${srcdir}/Xaw3d-ia64.patch" || return 1
  patch -Np4 -i "${srcdir}/Xaw3d-1.5-i18n.patch" || return 1
  patch -Np4 -i "${srcdir}/Xaw3d-1.5-box.c.patch" || return 1
  patch -Np4 -i "${srcdir}/Xaw3d-1.5-debian-fixes.patch" || return 1
  patch -Np4 -i "${srcdir}/Xaw3d-1.5E-warnings.patch" || return 1

  ln -s .. X11
  xmkmf || return 1
  make CDEBUGFLAGS="${CFLAGS} -DARROW_SCROLLBAR"
  make DESTDIR="${pkgdir}" SHLIBDIR=/usr/lib INCDIR=/usr/include install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
