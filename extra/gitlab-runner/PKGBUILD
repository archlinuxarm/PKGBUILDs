# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>
# Contributor: Martin Rys <martin@rys.pw>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build for our architectures

pkgname=gitlab-runner
pkgver=18.0.2
pkgrel=1
pkgdesc="The official GitLab CI runner written in Go"
arch=('x86_64')
url='https://gitlab.com/gitlab-org/gitlab-runner'
license=('MIT')
depends=('ca-certificates' 'curl' 'git' 'glibc' 'tar')
optdepends=('inetutils: hostname command')
makedepends=('git' 'go' 'git' 'mercurial' 'gox')
install=gitlab-runner.install
replaces=('gitlab-ci-multi-runner')
backup=('etc/gitlab-runner/config.toml')
noextract=("prebuilt-alpine-arm-${pkgver}.tar.xz"
           "prebuilt-alpine-arm64-${pkgver}.tar.xz"
           "prebuilt-alpine-s390x-${pkgver}.tar.xz"
           "prebuilt-alpine-x86_64-pwsh-${pkgver}.tar.xz"
           "prebuilt-alpine-x86_64-${pkgver}.tar.xz"
           "prebuilt-ubuntu-arm-${pkgver}.tar.xz"
           "prebuilt-ubuntu-arm64-${pkgver}.tar.xz"
           "prebuilt-ubuntu-s390x-${pkgver}.tar.xz"
           "prebuilt-ubuntu-x86_64-pwsh-${pkgver}.tar.xz"
           "prebuilt-ubuntu-x86_64-${pkgver}.tar.xz")
source=("git+$url.git#tag=v${pkgver}"
        "prebuilt-alpine-arm-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-arm.tar.xz"
        "prebuilt-alpine-arm64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-arm64.tar.xz"
        "prebuilt-alpine-s390x-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-s390x.tar.xz"
        "prebuilt-alpine-x86_64-pwsh-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-x86_64-pwsh.tar.xz"
        "prebuilt-alpine-x86_64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-x86_64.tar.xz"
        "prebuilt-ubuntu-arm-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-arm.tar.xz"
        "prebuilt-ubuntu-arm64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-arm64.tar.xz"
        "prebuilt-ubuntu-s390x-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-s390x.tar.xz"
        "prebuilt-ubuntu-x86_64-pwsh-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-x86_64-pwsh.tar.xz"
        "prebuilt-ubuntu-x86_64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-x86_64.tar.xz"
        "gitlab-runner.service"
        "gitlab-runner.sysusers"
        "gitlab-runner.tmpfiles"
        "config.toml")
sha512sums=('91737689d297abd46ebcc22852e224e487e967ea89f4aa658b60c57afbcb67a92261969eb0aa5ca85bcf4b75f15ef83b863800338669bb5beeeee1f13bacf89e'
            '9874d390c6ca115386e016337e2b97ce87c5d8d33c2ffee5f6c226456e2b1144db5a0840726a6490e2fa67acaaa9a70286a66749a04f690d35e3fa63207d0679'
            '0ad9ecf4d0ef025685abcefb19487601d9d5ef92cf71142896497642f4145255d55d1a4923baf1dc0a1151e521232addbc4c449b1f47e66e0088964cbf4f62de'
            '08ecc507f2f4132738c4cae29c07afabbd956243c67b98d9f2ec3d547f6f967f01337b2251ee17d037a0704233871e76d0d8264c2d1422f39a75a2193468c3fd'
            'c175e9fa0e49d3f426ccae40ada833111c0ddf2cf2163d27b9b75654b840906bb0b8775fed4affccc0de86dc6f03e294fbc9158b86bb2027578bb2e167818226'
            '66d283c13be19e39b48b648258eb1290312ce6c2522dc83a659155eb3c4611d92ceaaa9be559f88d131f7086d37b28f1f921714f43576c62cbee978a7b470ac0'
            '1bcf2c33e9cb2d44d0f4656e34b752a3f091535e8b972687ff88a882c05fe27187549286f68dd945967677a10cebffc629a0ad3d05519a7e2abb0e2463046c79'
            '6636e9fbf0036dac2f1eab1876ab075dce6f81903a0e6755e027044db802e2b490eb9f34c03903c4f7a2ee875ddcee2c234ef9c8ab1bf0f3ee96012f026ddcaa'
            '312aead8e5220de8d729fbadc20dd440768c976aab99a5b8d3fe7262aeb63cdba58b3e5007efb422b9f12117fd2eb95e04b0a0fd263f7ad42ede4b1c487bb690'
            '97f827412675ec25eb5ddaec196f86a3effd2becb072e8d0c418a9e453e07adc9c1d4f6ceab49dad9dd89e5d59b211502126852f5c452d3932ff7b318366247d'
            '46339d401803ad06e48ba87d302fe48ab829d85bfa3495dcee1e3440eb43831940ef98bb8daec34f1d690b9c89558dd9c17d93fd67f730d8c913e12e613c5e9f'
            'a61348bc0c817a21588bd4a82daf7f230d41816b576befbc5727e37da534de6378031a49e9f5e2da34be5d78f4927a64ee856afaa80ec7b6e7037244d9dbead0'
            '8aa7f08702e99053c696fcc2aaba83beb9e9cd6f31973d82862db9350ac46df3a095377625d31fe909677525290d2de922d7a97930ed235774cb8f0da8944d40'
            '6751d9fa0b27172d1b419c4138f5ac15cbc7c9147653a7258cf1470216142c637210bb60608c7ed0974e0e4057e5ddeae32225df1bb36e7dd1f20fec71e33cc3'
            '9718b94bd0ddb09095ffb8c1e60ca1e9649dabb1747e7fc95e58e404b2f9effdeb4cfd759f5b904443dc53a4e18c02003c38f85584713deb49f6a6d1007503de')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  [[ $CARCH == armv7h ]] && export CGO_ENABLED=1

  cd gitlab-runner
  make out/binaries/gitlab-runner-linux-$([[ $CARCH == aarch64 ]] && echo arm64 || echo arm)
}

package() {
  cd gitlab-runner

  install -Dm644 "${srcdir}/config.toml" "${pkgdir}/etc/gitlab-runner/config.toml"
  install -Dm644 "${srcdir}/gitlab-runner.service" "${pkgdir}/usr/lib/systemd/system/gitlab-runner.service"
  install -Dm644 "${srcdir}/gitlab-runner.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-runner.conf"
  install -Dm644 "${srcdir}/gitlab-runner.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/gitlab-runner.conf"
  install -Dm755 out/binaries/gitlab-runner-linux-$([[ $CARCH == aarch64 ]] && echo arm64 || echo arm) "${pkgdir}/usr/bin/gitlab-runner"
  install -Dm755 packaging/root/usr/share/gitlab-runner/clear-docker-cache "${pkgdir}/usr/share/gitlab-runner/clear-docker-cache"

  # Move prebuilt Docker images to hard-coded canonical location
  for image in prebuilt-{alpine,ubuntu}-{arm,arm64,s390x,x86_64-pwsh,x86_64}-${pkgver}.tar.xz; do
    install -Dm644 "${srcdir}/${image}" "${pkgdir}/usr/lib/gitlab-runner/helper-images/${image}"
  done

  install -Dm644 -t "${pkgdir}/usr/share/licenses/$pkgname/" LICENSE
}
