# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>
# Contributor: Martin Rys <martin@rys.pw>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build for our architectures

pkgname=gitlab-runner
pkgver=18.2.1
pkgrel=1
pkgdesc="The official GitLab CI runner written in Go"
arch=('x86_64')
url='https://gitlab.com/gitlab-org/gitlab-runner'
license=('MIT')
depends=('ca-certificates' 'curl' 'git' 'glibc' 'tar')
optdepends=('inetutils: hostname command')
makedepends=('git' 'go' 'git' 'mercurial' 'gox')
install=gitlab-runner.install
replaces=('gitlab-ci-multi-runner')
backup=('etc/gitlab-runner/config.toml')
noextract=("prebuilt-alpine-arm-${pkgver}.tar.xz"
           "prebuilt-alpine-arm64-${pkgver}.tar.xz"
           "prebuilt-alpine-s390x-${pkgver}.tar.xz"
           "prebuilt-alpine-x86_64-pwsh-${pkgver}.tar.xz"
           "prebuilt-alpine-x86_64-${pkgver}.tar.xz"
           "prebuilt-ubuntu-arm-${pkgver}.tar.xz"
           "prebuilt-ubuntu-arm64-${pkgver}.tar.xz"
           "prebuilt-ubuntu-s390x-${pkgver}.tar.xz"
           "prebuilt-ubuntu-x86_64-pwsh-${pkgver}.tar.xz"
           "prebuilt-ubuntu-x86_64-${pkgver}.tar.xz")
source=("git+$url.git#tag=v${pkgver}"
        "prebuilt-alpine-arm-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-arm.tar.xz"
        "prebuilt-alpine-arm64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-arm64.tar.xz"
        "prebuilt-alpine-s390x-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-s390x.tar.xz"
        "prebuilt-alpine-x86_64-pwsh-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-x86_64-pwsh.tar.xz"
        "prebuilt-alpine-x86_64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-x86_64.tar.xz"
        "prebuilt-ubuntu-arm-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-arm.tar.xz"
        "prebuilt-ubuntu-arm64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-arm64.tar.xz"
        "prebuilt-ubuntu-s390x-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-s390x.tar.xz"
        "prebuilt-ubuntu-x86_64-pwsh-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-x86_64-pwsh.tar.xz"
        "prebuilt-ubuntu-x86_64-${pkgver}.tar.xz::https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-x86_64.tar.xz"
        "gitlab-runner.service"
        "gitlab-runner.sysusers"
        "gitlab-runner.tmpfiles"
        "config.toml")
sha512sums=('49e4c1e3097dd6cc8b0f9763388756bbb378438327e39227a6afc426db0e0e53944f49b576e06359bacbe8fd8f33f73ed5671dd4f4b2713ec4909aa908b3dff7'
            '235923b74a992a69ab2077fca3cb23e5ca11a5ec92b388c5fbd5b8c7ec339759a9ee3cc829c47491b5be3050e5716f184b26286c02143f93920ebdf5704e50d6'
            '17f727d7f8cf2c30428267a511497e575485b35676cdba7b0719352ca5eea82ec5ce44cf9a1edc8eccfa63222712052fef3f0c872a36616187cf7efc7481530a'
            '0b026035e721d5f24aae4f8428b6b382063246268839a9e4cd79d79fc3833d050d46c6118f5eea26baecc6c8b611c23ffc5d13cbf887436fa7ea111d1474fcfd'
            '7b63a6b8c69e59d892ed98dd6cd8ad3277c85f810091415d87d458b787be1687d1ce48552df29edaaaeca594b04853f07512884aa9392d6f0c6d0d2dac08ebcb'
            'a2ad9fb482d2c83ee9db11765c59076d9d963b047b30ec8393b424664d2dc9a66badfead2c11aca864712c77d000c9b0608c51311c716d3987e9587754488f98'
            '29c2c8da890f1130406ceceeb6a4542f8c0a4edc641f05e1e3615a146eac955c2f5161f49af661278b4dc59488e9b101704645d69d8b20b8fec83c170369fe59'
            '364ca425fd0f0775aba44f84c2be9b7ff84c818ebd460c1efcc94f6d868ec90d5b4450bb6ff5855d65b0860ed5647294f169ab1f42cd777e4d5ac880c4984be3'
            '9039d279d1aa8eeba15dbb00f5f1c7f8eeb3a314bb1138ae2ed38ebf8bd4c36c4c3b2a70e3cdd58360dea89ba750cd9fe2f7b2c83e11cbe2f0c446d2fd6af74e'
            '5fbac40e787e283abc4ad595fbb20ef7ba733039bb32a5362c056d80f2f8e3da6a540931515e4f5c41e65b7454b2db6b08f7653ab7f3e0d5e64547ca81af37de'
            'c7c702a3a68b850b7a1c9667fd90b712da6ca15db2d1d4de67561a35c8b63cdadbdb45a0d1937bbde61d0db7e2f4e41b721ef7bfc5616da7b580df9a1d9c9b80'
            'a61348bc0c817a21588bd4a82daf7f230d41816b576befbc5727e37da534de6378031a49e9f5e2da34be5d78f4927a64ee856afaa80ec7b6e7037244d9dbead0'
            '8aa7f08702e99053c696fcc2aaba83beb9e9cd6f31973d82862db9350ac46df3a095377625d31fe909677525290d2de922d7a97930ed235774cb8f0da8944d40'
            '6751d9fa0b27172d1b419c4138f5ac15cbc7c9147653a7258cf1470216142c637210bb60608c7ed0974e0e4057e5ddeae32225df1bb36e7dd1f20fec71e33cc3'
            '9718b94bd0ddb09095ffb8c1e60ca1e9649dabb1747e7fc95e58e404b2f9effdeb4cfd759f5b904443dc53a4e18c02003c38f85584713deb49f6a6d1007503de')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  [[ $CARCH == armv7h ]] && export CGO_ENABLED=1

  cd gitlab-runner
  make out/binaries/gitlab-runner-linux-$([[ $CARCH == aarch64 ]] && echo arm64 || echo arm)
}

package() {
  cd gitlab-runner

  install -Dm644 "${srcdir}/config.toml" "${pkgdir}/etc/gitlab-runner/config.toml"
  install -Dm644 "${srcdir}/gitlab-runner.service" "${pkgdir}/usr/lib/systemd/system/gitlab-runner.service"
  install -Dm644 "${srcdir}/gitlab-runner.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-runner.conf"
  install -Dm644 "${srcdir}/gitlab-runner.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/gitlab-runner.conf"
  install -Dm755 out/binaries/gitlab-runner-linux-$([[ $CARCH == aarch64 ]] && echo arm64 || echo arm) "${pkgdir}/usr/bin/gitlab-runner"
  install -Dm755 packaging/root/usr/share/gitlab-runner/clear-docker-cache "${pkgdir}/usr/share/gitlab-runner/clear-docker-cache"

  # Move prebuilt Docker images to hard-coded canonical location
  for image in prebuilt-{alpine,ubuntu}-{arm,arm64,s390x,x86_64-pwsh,x86_64}-${pkgver}.tar.xz; do
    install -Dm644 "${srcdir}/${image}" "${pkgdir}/usr/lib/gitlab-runner/helper-images/${image}"
  done

  install -Dm644 -t "${pkgdir}/usr/share/licenses/$pkgname/" LICENSE
}
