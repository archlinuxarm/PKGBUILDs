# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable CDFS for 32-bit ARM
#  - set -Wno-incompatible-pointer-types in CFLAGS

pkgname=netcdf
pkgver=4.9.3
pkgrel=4
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=(x86_64)
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=(hdf5 curl libxml2 libaec blosc bzip2 libzip zstd)
makedepends=(cmake git)
checkdepends=(unzip)
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=(!makeflags)
license=(custom)
source=(git+https://github.com/Unidata/netcdf-c#tag=v$pkgver)
sha256sums=('01e621bb0a556860bb6e580142e4c05ba1ca7ef46f115ec41f592fd1736423ab')

prepare() {
  # https://github.com/Unidata/netcdf-c/issues/2188 https://github.com/Unidata/netcdf-c/issues/2242
  sed -i "/tst_remote3/d" ${pkgname}-c/ncdap_test/CMakeLists.txt

  git -C $pkgname-c cherry-pick -n 1f1f359aeb4a31c47410d36581a141bb04d7b4a1 \
                                   cf095c623963552a571b1c7fc46a2af69be071e2 \
                                   741c4b4a909179d3fce56b60eede7ee5332af349 # Fix build with HDF5 2.0
}

build() {
  [[ $CARCH != "aarch64" ]] && CDFS="OFF" || CDFS="ON"
  CFLAGS+=" -Wno-incompatible-pointer-types"
  cmake -B build -S ${pkgname}-c \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DNETCDF_WITH_PLUGIN_DIR=/usr/lib/netcdf/plugin \
    -DENABLE_BYTERANGE=ON \
    -DENABLE_CDF5=${CDFS} \
    -DENABLE_DAP_LONG_TESTS=ON \
    -DENABLE_DAP_REMOTE_TESTS=ON \
    -DENABLE_EXAMPLE_TESTS=ON \
    -DENABLE_EXTRA_TESTS=ON \
    -DENABLE_FILTER_TESTING=ON \
    -DENABLE_LARGE_FILE_TESTS=ON \
    -DENABLE_UNIT_TESTS=ON \
    -DNETCDF_ENABLE_LOGGING=ON \
    -DENABLE_PLUGIN_INSTALL=ON
  make -C build
}

check() {
  make -C build test
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-c/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
