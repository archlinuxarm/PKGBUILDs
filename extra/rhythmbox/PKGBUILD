# $Id: PKGBUILD 46301 2009-07-15 11:59:33Z giovanni $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=0.12.3
pkgrel=1
pkgdesc="An iTunes-like music player/libary"
arch=(i686 x86_64)
license=('GPL')
url="http://www.rhythmbox.org"
depends=( 'libgpod>=0.7.2' 'libsoup>=2.26.1' 'gnome-media>=2.26.0' 'totem-plparser>=2.26.1' 'musicbrainz>=2.1.5' 'libmtp>=0.3.7' 'libnotify>=0.4.5' 'lirc-utils' 'desktop-file-utils' 'gstreamer0.10-python>=0.10.13-2' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'gstreamer0.10-python>=0.10.14' 'pygtk>=2.14.1' 'gvfs')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.16.1' 'intltool' 'brasero>=2.26.0') 
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: cd burning')
options=('!libtool' '!emptydirs')
install=rhythmbox.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.12/${pkgname}-${pkgver}.tar.bz2)
md5sums=('4a28e79184f1a9737ac2e719a1478105')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/rhythmbox \
              --localstatedir=/var --disable-static \
              --enable-daap --enable-python \
              --with-mdns=avahi --disable-scrollkeeper || return 1
  
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain rhythmbox ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
