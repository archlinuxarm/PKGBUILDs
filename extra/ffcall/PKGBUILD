# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Michael Baehr <usemike@spamblocked.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure v7/v8 with --with-pic

pkgname=ffcall
pkgver=2.2
pkgrel=1
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('x86_64')
url="https://www.gnu.org/software/libffcall/"
license=('GPL2')
options=('!makeflags' 'staticlibs')
source=(https://ftp.gnu.org/gnu/libffcall/libffcall-${pkgver}.tar.gz)
sha256sums=('ebfa37f97b6c94fac24ecf3193f9fc829517cf81aee9ac2d191af993d73cb747')

build() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  [ "$CARCH" = "x86_64" -o "$CARCH" = "aarch64" -o "$CARCH" = "armv7h" ] && CONFIGFLAG="--with-pic"
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  make
}

check() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  make DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
