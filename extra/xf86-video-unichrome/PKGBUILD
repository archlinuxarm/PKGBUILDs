# $Id: PKGBUILD 36857 2009-04-28 05:41:18Z eric $
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-unichrome
pkgver=0.2.7
pkgrel=1
_gitversion=9f0c08b8e54af3ffb3f12c157e310d7628c8bc9b
pkgdesc="Unichrome video drivers for X.Org"
arch=(i686 x86_64)
url="http://unichrome.sf.net/"
license=('custom')
depends=('unichrome-dri>=7.3')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'xorg-util-macros' 'xf86driproto' 'glproto' 'mesa>=7.3' 'autoconf' 'automake' 'libtool')
options=('!libtool')
conflicts=('xf86-video-via' 'openchrome' 'xf86-video-openchrome' 'xorg-server<1.6.0')
source=(http://cgit.freedesktop.org/~libv/${pkgname}/snapshot/${pkgname}-${_gitversion}.tar.bz2 LICENSE)
md5sums=('41f9e4f666a1eb5fe5d5a76d78486e68' '6ea7d64c87c5c32201a1e38e3336e44a')

build() {
  cd "${srcdir}/${pkgname}-${_gitversion}"
  ./autogen.sh --prefix=/usr \
               --enable-dri
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
