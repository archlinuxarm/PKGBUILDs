# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depends on svt-av1
#  - set -DAVIF_CODEC_SVT=OFF
#  - remove makedepends on pandoc-cli, don't set -DAVIF_BUILD_MAN_PAGES=ON

pkgname=libavif
pkgver=1.0.4
pkgrel=4
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64)
url="https://github.com/AOMediaCodec/libavif"
license=(LicenseRef-libavif)
depends=(glibc aom dav1d librav1e.so libpng libjpeg libyuv libwebp)
makedepends=(cmake
             gdk-pixbuf2
             nasm)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('dc56708c83a4b934a8af2b78f67f866ba2fb568605c7cf94312acf51ee57d146')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=OFF \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
