# $Id: PKGBUILD 53131 2009-09-26 07:58:05Z allan $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libetpan
pkgver=0.58
pkgrel=2
pkgdesc="a portable middleware for email access"
arch=('i686' 'x86_64')
url="http://www.etpan.org/"
license=("custom:etpan")
depends=('db>=4.8' 'libsasl>=2.1.23' 'curl>=7.19.5' 'expat>=2.0.1-1')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('bf20b5c0548f06b94bc588afdf5b3436')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Fix detection of DB version
  sed -e 's/db-4.4/db-4.8/g' -i configure || return 1
  ./configure --prefix=/usr --disable-static
  make || return 1
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/$pkgname/license.txt
}
