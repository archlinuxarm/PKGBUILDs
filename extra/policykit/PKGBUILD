# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=policykit
pkgver=0.9
pkgrel=9
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=('custom')
url="http://hal.freedesktop.org/docs/PolicyKit/"
depends=('dbus-glib>=0.80' 'expat>=2.0.1' 'pam')
makedepends=('intltool' 'docbook-xsl' 'gtk-doc')
options=('!libtool')
backup=('etc/PolicyKit/PolicyKit.conf')
install=policykit.install
source=(http://hal.freedesktop.org/releases/PolicyKit-${pkgver}.tar.gz
	polkit.pam
	pk-ck-api-change.patch
	polkit-0.8-dbus-policy.patch
	entry-leak.patch)
md5sums=('802fd13ae41f73d79359e5ecb0a98716'
         '6564f95878297b954f0572bc1610dd15'
         '15b42b41e98410d8d7ec4f91d0cff190'
         'da5129eee9517d00985a354055d778a9'
         '62adbf90657b17dae429fc92937af513')

build() {
  cd "${srcdir}/PolicyKit-${pkgver}"
  patch -Np1 -i "${srcdir}/pk-ck-api-change.patch" || return 1
  patch -Np1 -i "${srcdir}/polkit-0.8-dbus-policy.patch" || return 1
  patch -Np1 -i "${srcdir}/entry-leak.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --libexecdir=/usr/lib/PolicyKit --disable-static \
	      --with-polkit-user=policykit --with-polkit-group=policykit || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1

  rm -rf "${pkgdir}/etc/profile.d"
  install -m644 "${srcdir}/polkit.pam" "${pkgdir}/etc/pam.d/polkit" || return 1
}
