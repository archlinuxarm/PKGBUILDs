# $Id: PKGBUILD 40238 2009-05-29 01:53:53Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: dorphell <dorphell@gmx.net>

pkgname=screen
pkgver=4.0.3
pkgrel=8
_ptygroup=5 #the UID of our PTY/TTY group
pkgdesc="Full-screen window manager that multiplexes a physical terminal"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/screen/"
license=('GPL')
depends=('ncurses' 'pam')
backup=('etc/screenrc' 'etc/pam.d/screen')
options=('!makeflags')
install=screen.install
source=(ftp://ftp.uni-erlangen.de/pub/utilities/screen/screen-${pkgver}.tar.gz \
        screen.pam)
md5sums=('8506fd205028a96c741e4037de6e3c42' '74f7919d7b65436cc2ea5341fa8ca374')

build() {
  export LDFLAGS="${LDFLAGS} -Wl,--as-needed"

  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-pam \
        --with-sys-screenrc=/etc/screenrc --enable-colors256 \
       --enable-rxvt_osc --with-pty-group=$_ptygroup \
       --mandir=/usr/share/man --infodir=/usr/share/info
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -Dm644 ${srcdir}/screen.pam ${pkgdir}/etc/pam.d/screen

  install -Dm644 etc/completer.zsh ${pkgdir}/usr/share/zsh/site-functions/screen
  install -Dm644 etc/etcscreenrc ${pkgdir}/etc/screenrc
  install -Dm644 etc/screenrc ${pkgdir}/etc/skel/.screenrc

  rm ${pkgdir}/usr/share/info/dir
  gzip -9 ${pkgdir}/usr/share/info/*
}
