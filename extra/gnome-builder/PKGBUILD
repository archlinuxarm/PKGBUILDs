# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix 32bit FTBFS

pkgbase=gnome-builder
pkgname=(gnome-builder gnome-builder-libide-docs)
pkgver=41.3
pkgrel=5
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject sysprof
         desktop-file-utils ctags libpeas vte3 vala autoconf-archive
         gspell libdazzle template-glib jsonrpc-glib python-sphinx webkit2gtk
         glade python-lxml meson libportal-gtk3 cmark clang flatpak-builder)
makedepends=(llvm gobject-introspection gi-docgen yelp-tools appstream-glib git
             mm-common python-sphinx_rtd_theme jedi-language-server)
checkdepends=(xorg-server-xvfb)
_commit=21c097851240dc1242e9fbd819e7d9acf6e6af9c  # tags/41.3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit"
        libportal-0.5.diff
        https://gitlab.gnome.org/GNOME/gnome-builder/-/commit/fcf976d5fcfb81d14ea0ec9b9909bc9b8dc52a81.patch)
sha256sums=('SKIP'
            '350dd0e5b8dc4d7ecdf3284e91983fe8d890ab453df6c5b218af8a6b6e7e7280'
            '32e1e3801ef8180d446c401c92965672710a97ce2a854cc22fe1e47de3ba2412')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/[^-]*-g/r&/;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgbase

  # libportal 0.5
  git apply -3 ../libportal-0.5.diff
  git apply ../fcf976d5fcfb81d14ea0ec9b9909bc9b8dc52a81.patch
}

build() {
  arch-meson $pkgbase build \
    -D docs=true \
    -D help=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gnome-builder() {
  depends+=(libgladeui-2.so)
  optdepends=('gnome-code-assistance: Legacy assistance services'
              'jedi-language-server: Python code assistance')
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

  _pick libide-docs "$pkgdir"/usr/share/doc/libide
}

package_gnome-builder-libide-docs() {
  pkgdesc+=" (libide API documentation)"
  depends=()
  mv libide-docs/* "$pkgdir"
}

# vim:set sw=2 et:
