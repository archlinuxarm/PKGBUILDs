# $Id: PKGBUILD 44459 2009-07-02 06:14:13Z allan $
# Maintainer: François Charette <francois ατ archlinux δοτ org>
# Contributor: Jason Chu <jchu@xentac.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=perl-tk
_cpanname=Tk
pkgver=804.028_501
pkgrel=2
pkgdesc="A graphical user interface toolkit for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/~SREZIC/${_cpanname}"
license=('PerlArtistic' 'GPL' 'custom')
depends=('libx11' 'libpng' 'libjpeg>=7' 'tk' 'perl>=5.10.0')
options=('!emptydirs')
replaces=('perltk')
provides=('perltk')
source=(http://www.cpan.org/authors/id/S/SR/SREZIC/${_cpanname}-${pkgver}.tar.gz) 
md5sums=('bf7b1726ea98472aa586eda17ca6f9d0')

build() {
  cd $srcdir/${_cpanname}-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor || return 1 
  /bin/chmod -R u+w *
  make || return 1
  make install DESTDIR=$pkgdir || return 1
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete

  # license
  install -D -m644 pTk/license.terms \
                   ${startdir}/pkg/usr/share/licenses/${pkgname}/tk.license
  install -D -m644 pTk/Tix.license \
                   ${startdir}/pkg/usr/share/licenses/${pkgname}/tix.license
}
