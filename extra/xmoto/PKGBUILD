# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca> 
# Contributor: Denis (dtonator@gmail.com)

pkgname=xmoto
pkgver=0.5.2
pkgrel=1
pkgdesc="A challenging 2D motocross platform game, where physics play an important role."
arch=('arm')
url="http://xmoto.tuxfamily.org"
license=('GPL')
depends=('bzip2' 'libjpeg' 'libpng' 'lua' 'sdl_mixer' 'ode=0.11' 'curl>=7.16.2' 
	 	 'mesa' 'sqlite3' 'sdl_ttf' 'desktop-file-utils' 'sdl_net')
install=xmoto.install
source=(http://download.tuxfamily.org/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tar.gz)
md5sums=('1e3678ebceca21d61844efb53c140227')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # upstream fix compilation issue
  sed -i 's|v_replay << m_Chunks.size();|v_replay << (unsigned int) m_Chunks.size();|' src/Replay.cpp || return 1

  # build and install 
  ./configure --prefix=/usr --disable-sdltest

  make || return 1
  make DESTDIR=${pkgdir} mangdir=/usr/share/man/man6 install

  # install desktop file
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/extra/xmoto.desktop \
	${pkgdir}/usr/share/applications/xmoto.desktop

  # install icon for desktop file
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/extra/xmoto.xpm \
	${pkgdir}/usr/share/pixmaps/xmoto.xpm
}
