# $Id: PKGBUILD 53931 2009-10-03 23:05:16Z eric $
# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

pkgname=boo
pkgver=0.9.2.3383
pkgrel=2
pkgdesc="A wrist friendly language for the CLI"
arch=('i686' 'x86_64')
url="http://boo.codehaus.org/"
license=('GPL')
depends=('sh' 'mono' 'shared-mime-info')
options=('!makeflags')
install=boo.install
source=(http://dist.codehaus.org/boo/distributions/${pkgname}-${pkgver}.tar.gz)
md5sums=('2b172e95fed2ce31cc2084f51ddebe3d')
sha1sums=('ffbcc6bfed8740eb16c212325202306c15a156c5')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p $MONO_SHARED_DIR
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's|/lib|/lib/mono|' extras/boo.pc.in  || return 1
  ./configure --prefix=/usr --libdir=/usr/lib || return 1
  make VERBOSE=1 || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf $MONO_SHARED_DIR  || return 1
  rm -rf "${pkgdir}/share/" || return 1
  rm -rf "${pkgdir}/usr/share/gtksourceview-1.0/language-specs/boo.lang"
}
