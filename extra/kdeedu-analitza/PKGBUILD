# $Id: PKGBUILD 179350 2013-03-05 15:25:49Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch and cmake define to disable opengl for ARM

pkgname=kdeedu-analitza
pkgver=4.12.0
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://projects.kde.org/projects/kde/kdeedu/analitza"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'glu')
source=("http://download.kde.org/stable/${pkgver}/src/analitza-${pkgver}.tar.xz"
        'analitza-4.10.1-opengl_optional.patch')
sha1sums=('a90c58d24c8cba048fc5303ef35e870536603119'
          '1b3a522acf87ed3dee51af4290a841645bf98a32')

prepare() {
  mkdir build

  cd "${srcdir}/analitza-${pkgver}"
  patch -p1 -i ../analitza-4.10.1-opengl_optional.patch
}

build() {
  cd build
  cmake ../analitza-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSHOULD_BUILD_OPENGL=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
