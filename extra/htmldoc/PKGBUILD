# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable distcc - configure checks for clang first

pkgname=htmldoc
pkgver=1.9.23
pkgrel=2
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/'
options=('!distcc')
license=('GPL-2.0-only')
depends=(
    'fltk'
    'glibc'
    'hicolor-icon-theme'
    'libcups'
    'libgcc'
    'libjpeg-turbo'
    'libpng'
    'libstdc++'
    'libxpm'
    'zlib')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('73bfe91dc96038f465e0bb2be66eaf91f381c42c7f9b36918d5af80edbca99be'
            'SKIP')
validpgpkeys=('9086C3CDC66C3F563CF8F405BE67C75EC81F3244') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix='/usr' --with-gui
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
