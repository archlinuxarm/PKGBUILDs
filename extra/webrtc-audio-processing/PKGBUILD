# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix AArch64 build
#  - disable neon

pkgname=webrtc-audio-processing
pkgver=0.3
pkgrel=2
pkgdesc="AudioProcessing library based on Google's implementation of WebRTC"
url="https://freedesktop.org/software/pulseaudio/webrtc-audio-processing/"
arch=(i686 x86_64)
license=(custom)
depends=(gcc-libs)
makedepends=(git)
_commit=fc0e76139404e08cc5b8024daafa1ddf576a4e21  # tags/v0.3^0
source=("git+https://anongit.freedesktop.org/git/pulseaudio/webrtc-audio-processing#commit=$_commit"
        'webrtc-fix-typedefs-on-other-arches.patch')
sha256sums=('SKIP'
            '98ba3bcf8fc11c9c76eb707ee81a3ab668f10ad8cc4915deb842a3032557d15f')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -p1 -i ../webrtc-fix-typedefs-on-other-arches.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"
  ./configure --prefix=/usr --disable-static $CONFIG
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
