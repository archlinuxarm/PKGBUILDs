# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix AArch64 build

pkgname=webrtc-audio-processing
pkgver=0.1
pkgrel=2
_gitrev=9413986
pkgdesc="AudioProcessing library based on Google's implementation of WebRTC"
arch=(i686 x86_64)
url="http://freedesktop.org/software/pulseaudio/webrtc-audio-processing"
license=(custom)
depends=(gcc-libs)
source=("$url/$pkgname-$pkgver.tar.xz"
        'webrtc-fix-typedefs-on-other-arches.patch')
sha256sums=('ed4b52f9c2688b97628035a5565377d74704d7c04de4254a768df3342c7afedc'
            '883d37c252ea5875af2ddfe1dd5a56aee07c4517f5dfac34253d70b508cd1976')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../webrtc-fix-typedefs-on-other-arches.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 PATENTS "$pkgdir/usr/share/licenses/$pkgname/PATENTS"
}
