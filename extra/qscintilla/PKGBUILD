# $Id: PKGBUILD 46926 2009-07-22 04:56:17Z pierre $
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Bug #8577 Modifications made by DaNiMoTh <jjdanimoth.aur@gmail.com>

pkgname=qscintilla
pkgver=2.4.0
_pkgver=2.4
pkgrel=1
license=('GPL')
pkgdesc="A port to Qt4 of Neil Hodgson's Scintilla C++ editor class."
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.co.uk/software/qscintilla/download"
depends=('qt')
provides=('qscintilla2')
conflicts=('qscintilla2')
replaces=('qscintilla2')
source=(http://riverbankcomputing.com/static/Downloads/QScintilla2/QScintilla-gpl-$_pkgver.tar.gz)
md5sums=('dd69520ec91f1acb97a4eb74ddde37c9')

build() {
    cd $srcdir/QScintilla-gpl-$_pkgver/Qt4
    sed -i "s|DESTDIR = .*|DESTDIR = $pkgdir/usr/lib|g" qscintilla.pro
    sed -i "s|header.path = .*|header.path = $pkgdir/usr/include|g" qscintilla.pro
    sed -i "s|trans.path = .*|trans.path = $pkgdir/usr/share/qt/translations|g" qscintilla.pro
    sed -i "s|qsci.path = .*|qsci.path = $pkgdir/usr/share/qt/translations|g" qscintilla.pro

    qmake qscintilla.pro
    sed -i 's|$(MOVE) $(TARGET)|$(COPY) $(TARGET)|g' Makefile
    make || return 1
    make DESTDIR=$pkgdir install
    
    cd ../designer-Qt4

    # Qt4 designer qscintilla2 plugin
    mkdir -p $pkgdir/usr/lib/qt/plugins/designer

    echo "INCPATH += ../Qt4" >> designer.pro
    sed -i "s|LIBS.*|LIBS += -L../Qt4 -lqscintilla2|g" designer.pro
    qmake designer.pro
    make || return 1
    cp libqscintillaplugin.so $pkgdir/usr/lib/qt/plugins/designer/ || return 1

    msg "Installing the libs to the right place"

    cd ../Qt4

    cp libqscintilla2.so.5.1.0 $pkgdir/usr/lib/ || return 1

    pushd $pkgdir/usr/lib/
        ln -s libqscintilla2.so.5.1.0 libqscintilla2.so
        ln -s libqscintilla2.so.5.1.0 libqscintilla2.so.5
        ln -s libqscintilla2.so.5.1.0 libqscintilla2.so.5.1
    popd
}
