# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define aarch64

buildarch=8

[[ "${CARCH}" == "x86_64" ]] && _EFI_ARCH="x86_64"
[[ "${CARCH}" == "i686" ]] && _EFI_ARCH="ia32"
[[ "${CARCH}" == "aarch64" ]] && _EFI_ARCH="aarch64"

_pkgver="3.0.2"
pkgname="gnu-efi-libs"
pkgver="${_pkgver}"
pkgrel="2"
pkgdesc="Library for building UEFI Applications using GNU toolchain"
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
arch=('x86_64' 'i686')
options=('!strip' '!makeflags' '!buildflags')
makedepends=('pciutils')
source=("http://download.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")
md5sums=('a9db2cabc01a2674715bd6aea2911f01')

build() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make lib gnuefi inc apps
}

package() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -d "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
	install -D -m0644 "${srcdir}/gnu-efi-${_pkgver}/${_EFI_ARCH}/apps"/*.efi "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
}
