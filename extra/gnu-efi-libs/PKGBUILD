# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define aarch64
#  - patches to fix build

buildarch=8

[[ "${CARCH}" == "x86_64" ]] && _EFI_ARCH="x86_64"
[[ "${CARCH}" == "i686" ]] && _EFI_ARCH="ia32"
[[ "${CARCH}" == "aarch64" ]] && _EFI_ARCH="aarch64"

_pkgver=3.0.3
pkgname="gnu-efi-libs"
pkgver="${_pkgver}"
pkgrel=2
pkgdesc="Library for building UEFI Applications using GNU toolchain"
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
arch=('x86_64' 'i686')
options=('!strip' '!makeflags' '!buildflags')
makedepends=('pciutils')
source=("http://download.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2"
        '0001-Add-the-missing-URI-device-path-to-the-unions.patch'
        '0002-From-Pete-Batard-pete-akeo.ie.patch'
        '0003-From-Pete-Batard-pete-akeo.ie.patch'
        '0004-From-Julian-Andres-Klode-jak-debian.org.patch'
        '0005-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch'
        '0006-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch'
        '0007-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch'
        '0008-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch'
        '0009-arm-fix-linker-script-for-building-efi-binaries.patch'
        '0010-Explicitly-place-our-build-id-notes-on-all-arches.patch')
md5sums=('15a4bcbc18a9a5e8110ed955970622e6'
         'c7d8e738044c07b8bfd655e120d01d63'
         'd09521c26ae76ca37bba4c64d5cfc4e2'
         '572220bca179c5b258f46339cc1c5727'
         'b53235519711cc61f18a084376737e8a'
         'f55d6bfd181d10a03b2c284c56afdfd3'
         'cb03dfb51d34767152ff77b36f63b822'
         'b042fc2103f84f2e1013acffb4180fa8'
         'a596355d911c50dea97710fd8038de1f'
         'fe873247639fde48e8588838392df047'
         'c47ab0bb5968938d927c9e290d117b83')

prepare() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	patch -p1 -i ../0001-Add-the-missing-URI-device-path-to-the-unions.patch
	patch -p1 -i ../0002-From-Pete-Batard-pete-akeo.ie.patch
	patch -p1 -i ../0003-From-Pete-Batard-pete-akeo.ie.patch
	patch -p1 -i ../0004-From-Julian-Andres-Klode-jak-debian.org.patch
	patch -p1 -i ../0005-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch
	patch -p1 -i ../0006-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch
	patch -p1 -i ../0007-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch
	patch -p1 -i ../0008-From-Ard-Biesheuvel-ard.biesheuvel-linaro.org.patch
	patch -p1 -i ../0009-arm-fix-linker-script-for-building-efi-binaries.patch
	patch -p1 -i ../0010-Explicitly-place-our-build-id-notes-on-all-arches.patch
}

build() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make lib gnuefi inc apps
}

package() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -d "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
	install -D -m0644 "${srcdir}/gnu-efi-${_pkgver}/${_EFI_ARCH}/apps"/*.efi "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
}
