# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define aarch64
#  - patches to fix build

buildarch=8

[[ "${CARCH}" == "x86_64" ]] && _EFI_ARCH="x86_64"
[[ "${CARCH}" == "i686" ]] && _EFI_ARCH="ia32"
[[ "${CARCH}" == "aarch64" ]] && _EFI_ARCH="aarch64"

_pkgver="3.0.3"
pkgname="gnu-efi-libs"
pkgver="${_pkgver}"
pkgrel="1"
pkgdesc="Library for building UEFI Applications using GNU toolchain"
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
arch=('x86_64' 'i686')
options=('!strip' '!makeflags' '!buildflags')
makedepends=('pciutils')
source=("http://download.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2"
        'lib-arm-setjmp.S-Use-function-instead-of-function.patch'
        'explicit-build-id.patch')
md5sums=('15a4bcbc18a9a5e8110ed955970622e6'
         '3c904fdc8208110ecd3d8d1f5da6c8c5'
         'a104721985027e427f3215a542e6c7b3')

prepare() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	patch -p1 -i ../lib-arm-setjmp.S-Use-function-instead-of-function.patch
	patch -p1 -i ../explicit-build-id.patch
}

build() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make lib gnuefi inc apps
}

package() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -d "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
	install -D -m0644 "${srcdir}/gnu-efi-${_pkgver}/${_EFI_ARCH}/apps"/*.efi "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
}
