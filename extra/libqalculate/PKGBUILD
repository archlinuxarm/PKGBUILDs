# $Id: PKGBUILD 42738 2009-06-18 19:31:58Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Patrick Leslie Polzer <polzer@gnu.org>
# Contributor: J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar>

pkgname=libqalculate
pkgver=0.9.6
pkgrel=5
pkgdesc="Multi-purpose desktop calculator"
arch=('i686' 'x86_64')
url="http://qalculate.sourceforge.net/"
license=('GPL')
depends=('libxml2' 'cln>=1.2.0' 'glib2' 'ncurses>=5.6-7' 'readline>=6.0.00')
makedepends=('perlxml')
optdepends=('gnuplot: for plotting support' 'wget or gnome-vfs: for retrieval of exchange rates (not needed if you use one of the GUI)')
options=('!libtool' '!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/qalculate/${pkgname}-${pkgver}.tar.gz \
        libqalculate-0.9.6-cln-config.patch libqalculate-0.9.6-cl_abort.patch \
        libqalculate-0.9.6-gcc4.3.patch)
md5sums=('e14432fc490f9a697e23e31a74e0eb19' '24fcfc7d1ac7ebc63bc0170c77e2db20'\
         '6613b57319ca500ef65515ee7d88bf5c' 'c89e366aad6b49095ab9d966e5890f25')
sha1sums=('0bcd42e13c23db9489ce594767d5017bdcb338ad'
          '9e8f9aef18e7c6f91a16d3bdd031ebf4d192221a'
          '69619a265d0ac75e0f81bcf0b1bd68f240412567'
          '0bcee9493e17702719db06fbac1f2d45888b54b0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../libqalculate-0.9.6-cln-config.patch || return 1
  patch -p1 < ../libqalculate-0.9.6-cl_abort.patch || return 1
  patch -p1 < ../libqalculate-0.9.6-gcc4.3.patch || return 1
  autoconf || return 1
  ./configure --prefix=/usr --disable-clntest || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
