# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to CXXFLAGS

pkgbase=qca
pkgname=(qca-qt4 qca-qt5)
pkgver=2.1.1
pkgrel=7
pkgdesc="Qt Cryptographic Architecture"
arch=(i686 x86_64)
url="http://delta.affinix.com/qca/"
license=(LGPL)
makedepends=(qt4 qt5-base cmake doxygen nss pkcs11-helper botan1.10)
source=("http://download.kde.org/stable/$pkgbase/$pkgver/src/$pkgbase-$pkgver.tar.xz"
        no-ssl3.patch)
md5sums=('657cc701316600199199a6b6cb2c73c9'
         '209f9187209d255fc3468ffeb26302e0')

prepare() {
  mkdir -p build{4,5}
  cd $pkgbase-$pkgver
  patch -Np1 < "${srcdir}/no-ssl3.patch"
  CXXFLAGS+=" -fsigned-char"
}

build() {
  cd build4
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF \
    -DQCA_LIBRARY_INSTALL_DIR=/usr/lib \
    -DQCA_FEATURE_INSTALL_DIR=/usr/share/qt4/mkspecs/features/ \
    -DQT4_BUILD=ON
  make

  cd ../build5
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF \
    -DQCA_INSTALL_IN_QT_PREFIX=ON \
    -DQCA_MAN_INSTALL_DIR=/usr/share/man
  make
}

package_qca-qt4() {
  depends=(qt4 nss)
  optdepends=('pkcs11-helper: PKCS-11 plugin' 'botan1.10: botan plugin')
  conflicts=(qca qca-gnupg qca-ossl)
  provides=(qca qca-gnupg qca-ossl)
  replaces=(qca qca-gnupg qca-ossl)

  cd build4
  make DESTDIR="$pkgdir" install
}

package_qca-qt5() {
  depends=(qt5-base nss ca-certificates)
  optdepends=('pkcs11-helper: PKCS-11 plugin' 'botan1.10: botan plugin')

  cd build5
  make DESTDIR="$pkgdir" install
}


