# $Id: PKGBUILD 3555 2008-06-25 03:42:34Z kevin $
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=stunnel
pkgver=4.25
pkgrel=1
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL."
arch=(i686 x86_64)
license=(GPL)
url="http://www.stunnel.org"
depends=('openssl' 'perl' 'tcp_wrappers' 'zlib')
source=(http://www.stunnel.org/download/stunnel/src/${pkgname}-${pkgver}.tar.gz
        stunnel-nopem.patch stunnel)
options=(!libtool)
install="$pkgname.install"
md5sums=('7f00799f43910c0362df0e69c65965aa'
         'aec994e209405420eed197fd15ca09b0'
         'cb647c71ff4cb1e035b6e515d5f13ebf')

build() {
  cd $startdir/src/$pkgname-$pkgver

  # don't create a certificate...
  /usr/bin/patch -p1 < ../stunnel-nopem.patch 

  ./configure --prefix=/usr --sysconfdir=/etc --with-tcp-wrappers \
      --localstatedir=/var
  /usr/bin/make || return 1
  /usr/bin/make DESTDIR=$startdir/pkg install

  for i in ca.pl importCA.sh; do
    /bin/install -D -m 755 tools/$i $startdir/pkg/usr/share/stunnel/$i
  done
  /bin/install -D -m 755 $startdir/src/stunnel $startdir/pkg/etc/rc.d/stunnel
  /bin/sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
           -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
           -e "s:nobody:stunnel:g" -e "s:nogroup:stunnel:g" \
           -i $startdir/pkg/etc/stunnel/stunnel.conf-sample
  /bin/install -D -m 644 tools/stunnel.cnf \
      $startdir/pkg/etc/stunnel/stunnel.cnf

  # libtoolslay
  #find $startdir/pkg -name '*.la' -exec rm {} \;
}
# vim: ts=2 sw=2 et ft=sh
