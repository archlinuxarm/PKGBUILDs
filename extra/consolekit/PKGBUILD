# $Id: PKGBUILD 38835 2009-05-10 16:12:13Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=consolekit
pkgver=0.3.0
pkgrel=5
pkgdesc="A framework for defining and tracking users, login sessions, and seats"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/ConsoleKit"
license=('GPL')
depends=('dbus-glib>=0.76' 'glib2>=2.18.2' 'policykit>=0.9' 'zlib')
makedepends=('pkgconfig')
options=(!libtool)
source=(http://people.freedesktop.org/~mccann/dist/ConsoleKit-${pkgver}.tar.bz2
	pam-foreground-compat.ck
	consolekit-dbus-permissions.patch)
md5sums=('43b02a52212330b54cfb34c4044d9ce0'
         'a8a4de71d9b0549b8143e5f6c2a36fc7'
         '02cd8d4d0ea279a70a435c7c28b04e39')

build() {
  cd "${srcdir}/ConsoleKit-${pkgver}"
  patch -Np1 -i "${srcdir}/consolekit-dbus-permissions.patch" || return 1
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/ConsoleKit \
      --enable-pam-module || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 "${srcdir}/pam-foreground-compat.ck" "${pkgdir}/usr/lib/ConsoleKit/run-session.d/" || return 1
}
