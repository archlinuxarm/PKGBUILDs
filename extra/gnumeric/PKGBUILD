# $Id: PKGBUILD 53425 2009-09-30 22:00:44Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.8.4
pkgrel=4
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice>=0.6.1' 'gconf>=2.18.0.1-4' 'desktop-file-utils')
makedepends=('perlxml' 'pygtk' 'psiconv' 'pkgconfig' 'intltool' 'gnome-doc-utils>=0.14.1')
optdepends=('python: for python plugin support' 'psiconv: for Psion 5 file support' 'yelp: for viewing the help manual')
install=gnumeric.install
options=('libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 translations.diff gtk2.16.diff set-flag.diff)
md5sums=('338f0084f04a16f78797ad01f85d3251' '6f4e8cff91aca37104099111a2024c97'\
         '046ca67a7b84674c2cd525e6f633fe7b' 'cec60fa6a1238bca40045ec5b3738652')
sha1sums=('2d765b0d06509e0695c4d3ec96b94f8acba88735' 'c4a7c05cadaf57edec4c32a569d95c2a4f97f67b'\
         'e4139f2e36517171f633a7da0ad1ec9b85ce83b7' 'cc25f1f56ac11d43dde09589d0b655e97627ca13')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../gtk2.16.diff || return 1
  patch -p1 < ../set-flag.diff || return 1
  patch -p1 < ../translations.diff || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
              --without-guile --with-bonobo --enable-ssindex || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas || return 1
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
