# $Id: PKGBUILD 54293 2009-10-07 22:41:25Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebase
pkgname=('kdebase-dolphin'
         'kdebase-kappfinder'
         'kdebase-kdepasswd'
         'kdebase-kdialog'
         'kdebase-kfind'
         'kdebase-kinfocenter'
         'kdebase-konqueror'
         'kdebase-konsole'
         'kdebase-kwrite'
         'kdebase-lib'
         'kdebase-plasma')
pkgver=4.3.2
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
makedepends=('kdebase-workspace' 'pkgconfig' 'cmake' 'automoc4' 'libraw1394')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
        'terminalpanel.patch')
md5sums=('7d6b968d8d8dd3730522389dabbb23e2'
         '52a17f42c2ebac4f33a13f843e394175')

build() {
	cd $srcdir/${pkgbase}-${pkgver}
	# Fix a regression with terminalpanel
	# fix upstream in 4.3.3
	# see https://bugs.kde.org/show_bug.cgi?id=202176
	# and https://bugs.kde.org/show_bug.cgi?id=167810
	patch -p0 -i $srcdir/terminalpanel.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdebase-dolphin() {
	pkgdesc='File Manager'
	depends=('kdebase-runtime' 'kdebase-lib')
	cd $srcdir/build/apps/dolphin
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/dolphin
	make DESTDIR=$pkgdir install
}

package_kdebase-kappfinder() {
	pkgdesc='Menu Updating Tool'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/kappfinder
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kappfinder
	make DESTDIR=$pkgdir install
}

package_kdebase-kdepasswd() {
	pkgdesc='Change Password'
	depends=('kdebase-runtime' 'kdebase-lib')
	cd $srcdir/build/apps/kdepasswd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kdepasswd
	make DESTDIR=$pkgdir install
}

package_kdebase-kdialog() {
	pkgdesc='a utility for displaying dialog boxes from shell scripts'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/apps/kdialog
	make DESTDIR=$pkgdir install
}

package_kdebase-kfind() {
	pkgdesc='Find Files/Folders'
	depends=('kdebase-runtime' 'kdebase-lib')
	install='kdebase.install'
	cd $srcdir/build/apps/kfind
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kfind
	make DESTDIR=$pkgdir install
}

package_kdebase-kinfocenter() {
	pkgdesc='Info Center'
	depends=('kdebase-runtime' 'libraw1394' 'kdelibs')
	cd $srcdir/build/apps/kinfocenter
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kinfocenter
	make DESTDIR=$pkgdir install
}

package_kdebase-konqueror() {
	pkgdesc='KDE File Manager & Web Browser'
	depends=('kdebase-runtime' 'kdebase-lib')
	install='kdebase.install'
	conflicts=('kdebase-keditbookmarks' 'kdebase-nsplugins')
	replaces=('kdebase-keditbookmarks' 'kdebase-nsplugins')
	for i in konqueror doc/konqueror keditbookmarks nsplugins; do
		cd $srcdir/build/apps/${i}
		make DESTDIR=$pkgdir install
	done
}

package_kdebase-konsole() {
	pkgdesc='Terminal'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/apps/konsole
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/konsole
	make DESTDIR=$pkgdir install
}

package_kdebase-kwrite() {
	pkgdesc='Text Editor'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/apps/kwrite
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kwrite
	make DESTDIR=$pkgdir install
}

package_kdebase-lib() {
	pkgdesc='KDE libraries for the basic desktop applications'
	groups=()
	depends=('kdelibs')
	cd $srcdir/build/apps/lib
	make DESTDIR=$pkgdir install
}

package_kdebase-plasma() {
	pkgdesc='Display the contents of folders (UserÂ´s home folder as default)'
	depends=('kdebase-workspace' 'kdebase-lib')
	cd $srcdir/build/apps/plasma
	make DESTDIR=$pkgdir install
}
