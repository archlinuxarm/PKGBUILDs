# $Id$
# Contributor: John Proctor <jproctor@prium.net>
# Maintainer: juergen <juergen@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use system libffi (included version is old and breaks on v5)
#  - specify libffi include path in CPPFLAGS

pkgname=ecl
pkgver=13.5.1
pkgrel=2
pkgdesc="Embeddable Common Lisp"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ecls/"
license=('LGPL')
depends=('bash' 'gmp' 'libffi')
makedepends=('texinfo')
provides=('common-lisp' 'cl-asdf')
options=('!makeflags')
source=(http://downloads.sourceforge.net/project/ecls/ecls/13.5/ecl-${pkgver}.tgz
        add_cas_emulation_for_armel.patch)
md5sums=('f6bb59d9383c51fc94e1c7ca5cf1c586'
         '16a7c651f2f06fb6718e8704a42f8049')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  [[ $CARCH == 'arm' ]] && patch -p1 -i ../add_cas_emulation_for_armel.patch
  sed -i 's|-Wl,--rpath,~A|-Wl,--rpath,/usr/lib/ecl|' src/configure 
}

build() {
  cd $srcdir/$pkgname-$pkgver

  CPPFLAGS+=" -I/usr/lib/libffi-3.0.13/include"
  ./configure \
    --build=$CHOST \
    --prefix=/usr \
    --with-tcp \
    --with-clos-streams \
    --enable-shared \
    --enable-boehm=included \
    --with-system-gmp \
    --without-x \
    --enable-threads \
    --without-clx \
    --with-dffi=system

  make
}

package() {
  make -C $srcdir/$pkgname-$pkgver DESTDIR=$pkgdir install
}
