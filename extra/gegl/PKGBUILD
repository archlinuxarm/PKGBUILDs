# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable instrospection, currently broken

pkgname=gegl
pkgver=0.3.14
pkgrel=2
pkgdesc="Graph based image processing framework"
arch=('i686' 'x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'libspiro' 'json-glib')
makedepends=('intltool' 'ruby' 'lua' 'libraw' 'openexr' 'mesa' 'glu' 'ffmpeg' 'librsvg'
             'jasper' 'libtiff' 'exiv2' 'vala' 'python2' 'suitesparse' 'lensfun'
             'gobject-introspection' 'git' 'gtk-doc')
optdepends=('libraw: raw plugin'
            'openexr: openexr plugin'
            'ffmpeg: ffmpeg plugin'
            'suitesparse: matting-levin plugin'
            'librsvg: svg plugin'
            'jasper: jasper plugin'
            'libtiff: tiff plugin'
            'lua: lua plugin'
            'lensfun: lens-correct plugin')
_commit=285e5b781f2ef6359d264192f826564402119b87  # tags/GEGL_0_3_14^0
source=("git+https://git.gnome.org/browse/gegl#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GEGL_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python

  cd ${pkgname}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${pkgname}
  ./configure  --prefix=/usr  --with-sdl --with-librsvg \
    --with-libavformat --with-jasper --disable-docs \
    --enable-workshop --enable-introspection=no

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${pkgname}
  make -k check || :
}

package() {
  export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}
