# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cherry-pick build fix for ARMv7

pkgname=gegl
pkgver=0.4.66
pkgrel=2
pkgdesc='Graph based image processing framework'
arch=('x86_64')
url='https://www.gegl.org/'
license=('GPL-3.0-or-later OR LGPL-3.0-or-later')
depends=('babl' 'cairo' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'jasper' 'json-glib' 'lcms2'
         'lensfun' 'libjpeg-turbo' 'libpng' 'libraw' 'librsvg' 'libspiro' 'libtiff' 'libwebp'
         'luajit' 'openexr' 'pango' 'poppler-glib' 'suitesparse')
makedepends=('ffmpeg' 'git' 'gobject-introspection' 'libgexiv2' 'meson' 'python-gobject' 'sdl2'
             'vala' 'gi-docgen')
optdepends=('ffmpeg: FFmpeg Frame Loader and FFmpeg Frame Saver plugins'
            'graphviz: for gegl-introspect'
            'sdl2: SDL2 Display plugin')
source=("git+https://gitlab.gnome.org/GNOME/gegl.git#tag=GEGL_${pkgver//./_}")
sha256sums=('607e0744a0c8df6fc7d96189520dc9b2f8711bddc757b3ae4bd94a53a7d1238d')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^GEGL_//;s/_$//;s/_/./g;s/-/+/g'
}

prepare() {
  cd "${pkgname}"

  # Build fix for ffmpeg 8
  git cherry-pick -n eda8ba51786f197b72265eafcdab407d91ec9c70

  # Build fix for ARMv7
  git cherry-pick -n 69c263ae5f3d4a2d251e00c7236efd035db2bb5a
}

build() {
  mkdir -p build
  cd build
  arch-meson ../"${pkgname}" \
    -Dworkshop=true \
    -Dmrg=disabled \
    -Dmaxflow=disabled
  ninja
}

check() {
  cd build
  ninja test || :
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
