# $Id: PKGBUILD 44414 2009-07-02 00:11:59Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Charles-Henri d'Adhemar <cdadhemar@gmail.com>

pkgname=gegl
pkgver=0.0.22
pkgrel=2
pkgdesc="Graph based image processing framework"
arch=('i686' 'x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl>=0.0.22' 'libpng' 'libjpeg>=7' 'gtk2')
makedepends=('ruby')
options=('!libtool')
source=(ftp://ftp.gimp.org/pub/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e2196c2016d16a4479e157f8fdee4162')
sha1sums=('de684d4c8d9eaa9b7e283c55c5f779e5bdabee78')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|#include <ffmpeg/avformat.h>|#include <libavformat/avformat.h>|' operations/external/ff-load.c || return 1
  ./configure --prefix=/usr --without-sdl --without-openexr --without-librsvg --without-libavformat || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
