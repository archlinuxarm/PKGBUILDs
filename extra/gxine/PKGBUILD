# $Id: PKGBUILD 34196 2009-04-07 22:02:50Z alexanderf $
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=gxine
pkgver=0.5.904
pkgrel=1
pkgdesc="GTK+ frontend for xine"
arch=('i686' 'x86_64')
url="http://www.xine-project.org/"
license=('GPL')
depends=('xine-lib' 'gtk2' 'spidermonkey' 'hal' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('libxaw')
optdepends=('libxaw: Mozilla browser plugin')
options=('!libtool')
install=gxine.install
source=(http://downloads.sourceforge.net/xine/gxine-$pkgver.tar.bz2)
md5sums=('00b10fac6ddfc211a8ef217174feb09b')

build() {
  cd $srcdir/gxine-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --disable-lirc --disable-integration-wizard \
    --with-spidermonkey=/usr/include/js --with-logo-format=image \
    VENDOR_PKG_VERSION="$pkgver-$pkgrel; Arch Linux" || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -d -m755 $pkgdir/usr/lib/mozilla/plugins || return 1
  ln -s /usr/lib/gxine/gxineplugin.so $pkgdir/usr/lib/mozilla/plugins || return 1
}
