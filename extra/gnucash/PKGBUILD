# $Id: PKGBUILD 30845 2009-03-22 06:55:41Z eric $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=2.2.9
pkgrel=2
pkgdesc="A personal and small-business financial-accounting application"
arch=(i686 x86_64)
url="http://www.gnucash.org"
license=("GPL")
depends=('gtkhtml' 'slib' 'goffice>=0.6.5' 'guile' 'libofx' 'gconf>=2.24' 'texinfo')
makedepends=('intltool')
optdepends=('evince: for print preview'
            'gnucash-docs: (AUR) for user documentation'
            'perl-finance-quote: for stock information lookups'
            'perl-date-manip: for stock information lookups')
options=('!libtool' '!makeflags')
install=gnucash.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('570eee09a1ae83c9953ec60a7fc146ed')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man \
        --sysconfdir=/etc --libexecdir=/usr/lib --enable-ofx
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

  install -d -m755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  rm -f ${pkgdir}/usr/share/info/dir
  gzip -9 ${pkgdir}/usr/share/info/*
}
