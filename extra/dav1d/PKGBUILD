# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on v5/v6

pkgname=dav1d
pkgver=0.2.1
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm' 'doxygen')
provides=('libdav1d.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('38d0a9e639579bf2046d3afd7ba0d4205081d8e26955be0216acc84ee2adcc81c790c858932009965b61a66fc3df66ed6b14da9d0e61e2505418c17bc4b75004')

build() {
  cd ${pkgbase}-${pkgver}
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && meson_options="-Dbuild_asm=false"
  arch-meson build $meson_options
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
