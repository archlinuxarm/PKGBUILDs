# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on v5/v6

pkgname=dav1d
pkgver=0.6.0
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'vulkan-icd-loader')
makedepends=('meson' 'ninja' 'nasm' 'doxygen' 'vulkan-headers')
provides=('libdav1d.so')
source=(https://downloads.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc})
sha512sums=('aeee206806642188839602c7040340052d8cff2168fd142516499da1718c497b3b6b45f96af2d543a0db8ef5d26acece6ac9bb6c6e00316fa222a2ea0554581c'
            'SKIP')
b2sums=('152f948a89584d706b80f1334a3d430b3a97715365228bab24b610a5742ec07b371503cae18d8df83a40c85599aa9d8bf0fc181ea5c355bf0f9e1bf541f066cd'
        'SKIP')
validpgpkeys=('65F7C6B4206BD057A7EB73787180713BE58D1ADC') # VideoLAN Release Signing Key

build() {
  cd ${pkgbase}-${pkgver}
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && meson_options="-Dbuild_asm=false"
  arch-meson build $meson_options
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
