# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on v5/v6

pkgname=dav1d
pkgver=0.1.0
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm' 'doxygen')
provides=('libdav1d.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('5893ad40af7c4f5c3678ecc604ad360b5d1cd3fadaa16f7183d638745097fed091aeb3456540db61a2f4f0fc8bb93f2a20f428c76a7976d3fd186cc8b8baed53')

build() {
  cd ${pkgbase}-${pkgver}
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && meson_options="-Dbuild_asm=false"
  arch-meson build $meson_options
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
