# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on v5/v6

pkgname=dav1d
pkgver=0.8.1
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'vulkan-icd-loader')
makedepends=('meson' 'ninja' 'nasm' 'doxygen' 'vulkan-headers' 'graphviz')
provides=('libdav1d.so')
source=(https://downloads.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc})
sha512sums=('ee087768f3afcca76b3db7fc6c528d471891f65295b7e0981922cdbdfe26e919c94144dc395cc697035c1200f4baed9fc233666b5ecf0a2281e869b9f1e3a1b5'
            'SKIP')
b2sums=('4be5c666c2212b36e532c77a1bba235d44260723bf398be1e856095def1b123cb964599f75f83717601c8428294a3c3a2bf10d81c1e63134a2e995ee9d0d2655'
        'SKIP')
validpgpkeys=('65F7C6B4206BD057A7EB73787180713BE58D1ADC') # VideoLAN Release Signing Key

build() {
  cd ${pkgbase}-${pkgver}
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && meson_options="-Dbuild_asm=false"
  arch-meson build $meson_options
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
