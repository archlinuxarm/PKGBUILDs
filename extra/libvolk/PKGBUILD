# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v7 without neon to fix FTBFS

pkgname=libvolk
pkgver=3.3.0
pkgrel=1
epoch=2
pkgdesc="The Vector-Optimized Library of Kernels from Gnuradio"
arch=(x86_64)
url="https://www.libvolk.org/"
license=(LGPL-3.0-or-later)
depends=(
  fmt
  glibc
  libgcc
  libstdc++
  orc
  python
)
makedepends=(
  cmake
  git
  python-mako
)
provides=(libvolk.so)
source=(
  "$pkgname::git+https://github.com/gnuradio/volk.git#tag=v$pkgver"
  "git+https://github.com/google/cpu_features.git"
)
b2sums=('a5c471f51c0094bc8704ad050fa1f6e4716ae2028ec129928476b2acc928f4c5d0787d927e3885520c53f1f54c474b95f1411280cef8dfddd4a8d535f9ad3a98'
        'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.cpu_features.url "$srcdir/cpu_features"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $pkgname
  [[ $CARCH == "armv7h" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/neon/vfpv3/'` && CXXFLAGS="$CFLAGS"
  cmake -S . -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  cmake --build build
}

check() {
  cd $pkgname
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install build
  rm -vr "$pkgdir/usr/include/volk/asm"
}
