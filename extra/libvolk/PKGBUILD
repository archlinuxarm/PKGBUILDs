# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v7 without neon to fix FTBFS

pkgname=libvolk
_pkgname=volk
pkgver=3.1.2
pkgrel=4
epoch=2
pkgdesc="The Vector-Optimized Library of Kernels from Gnuradio"
arch=(x86_64)
url="https://www.libvolk.org/"
license=(LGPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  orc
  python
)
makedepends=(
  cmake
  git
  python-mako
)
provides=(libvolk.so)
source=(
  "git+https://github.com/gnuradio/volk.git#tag=v$pkgver"
  "git+https://github.com/google/cpu_features.git"
)
sha256sums=('9b3b30f8428ae7813e1d0d22896b717b8dd802027869065c42d552157652e288'
            'SKIP')

prepare() {
  cd $_pkgname
  git submodule init
  git config submodule.cpu_features.url "$srcdir/cpu_features"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $_pkgname
  [[ $CARCH == "armv7h" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/neon/vfpv3/'` && CXXFLAGS="$CFLAGS"
  cmake -S . -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  cmake --build build
}

check() {
  cd $_pkgname
  ctest --test-dir build --output-on-failure
}

package() {
  cd $_pkgname
  DESTDIR="$pkgdir" cmake --install build
  rm -vr "$pkgdir/usr/include/volk/asm"
}
