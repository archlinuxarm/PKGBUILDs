# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch for AArch64

pkgname=fuse2
pkgver=2.9.8
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
provides=(fuse=$pkgver) # TODO: remove it once all packages start to use 'fuse2' name
replaces=('fuse')
conflicts=('fuse')
depends=('glibc' 'fuse-common')
makedepends=('pkg-config')
options=(!emptydirs)
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.gz{,.asc}
        https://github.com/libfuse/libfuse/commit/914871b20a901e3e1e981c92bc42b1c93b7ab81b.patch)
sha1sums=('fdd8783eda9463d3dfc75b2b942d8547dd3f8cc8'
          'SKIP'
          '5534077f337c99456f93bf82890eec90426660c4')
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

prepare() {
  cd fuse-$pkgver

  patch -p1 -i ../914871b20a901e3e1e981c92bc42b1c93b7ab81b.patch
}

build() {
  cd fuse-$pkgver

  export MOUNT_FUSE_PATH=/usr/bin
  ./configure --prefix=/usr --libdir=/usr/lib --enable-lib --enable-util --disable-example
  make
}

package() {
  cd fuse-$pkgver

  make DESTDIR=${pkgdir} install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -r ${pkgdir}/etc/init.d

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev

  # static device nodes are handled by udev
  rm -r ${pkgdir}/dev

  # part of fuse-common package
  rm ${pkgdir}/usr/share/man/man8/mount.fuse.8 ${pkgdir}/usr/bin/mount.fuse
}
