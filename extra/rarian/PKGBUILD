# $Id: PKGBUILD 11986 2008-09-06 21:33:59Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=rarian
pkgver=0.8.1
pkgrel=1
pkgdesc="Documentation meta-data library, designed as a replacement for Scrollkeeper."
arch=('i686' 'x86_64')
url="http://rarian.freedesktop.org/"
license=('GPL')
depends=('gcc-libs')
makedepends=('libxslt')
conflicts=('scrollkeeper')
provides=('scrollkeeper')
replaces=('scrollkeeper')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.8/${pkgname}-${pkgver}.tar.bz2
        user-segfault.patch)
md5sums=('75091185e13da67a0ff4279de1757b94'
         '457e9827197009cfa360911fe49a5ad7')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/user-segfault.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
     --localstatedir=/var --disable-static || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
}
