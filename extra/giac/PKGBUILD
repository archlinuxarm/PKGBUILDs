# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinxuarm.org>
#  - delete shipped x86 binary src/mkjs to force rebuild for ARM

pkgname=giac
_pkgver=2.0.0-18
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='A free computer algebra system'
arch=(x86_64)
url='https://www-fourier.univ-grenoble-alpes.fr/~parisse/giac.html'
license=(GPL-3.0-only)
depends=(blas
         curl
         fltk1.3
         gcc-libs
         glibc
         glpk
         gmp
         gmp-ecm
         gsl
         hicolor-icon-theme
         lapack
         libao
         libglvnd
         libpng
         libsamplerate
         mpfi
         mpfr
         nauty
         ntl
         pari
         readline
         zlib)
makedepends=(hevea
             libjpeg-turbo
             python
             texlive-latexrecommended
             texlive-mathscience)
optdepends=('perl: for pgiac')
source=(https://www-fourier.univ-grenoble-alpes.fr/~parisse/debian/dists/stable/main/source/${pkgname}_$_pkgver.tar.gz
        format-security.patch)
sha256sums=('25e2a3d9686c9a6800d9e6b4a243eeeb0af7602067f060e6aab4138e63e417db'
            '04288944e408807a15aa502e1215087870d8921229c9f9890d862d3c9de337cd')

prepare() {
  cd $pkgname-${pkgver%.*}
  patch -p1 < ../format-security.patch # Fix -Werror=format-security errors
  rm -f src/mkjs # force rebuild for ARM

  autoreconf -fiv
}

build() {
  cd $pkgname-${pkgver%.*}

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2} -I/usr/include/fltk1.3"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2} -I/usr/include/fltk1.3"

  CXXFLAGS+=" -Wp,-U_GLIBCXX_ASSERTIONS" # Fix crashes
  LDFLAGS+=" -L/usr/lib/fltk1.3"
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
