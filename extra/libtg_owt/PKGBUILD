# Maintainer: Jiachen YANG <farseerfc at archlinux dot org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable neon on v7 with -DTG_OWT_ARCH_ARMV7_USE_NEON=OFF

pkgname=libtg_owt
# It seems the currently desired version can be gotten from
# https://github.com/telegramdesktop/tdesktop/blob/dev/Telegram/build/docker/centos_env/Dockerfile
_commit=5c5c71258777d0196dbb3a09cc37d2f56ead28ab
pkgver=0.git31.${_commit:0:7}
pkgrel=1
pkgdesc='WebRTC library'
arch=('x86_64')
url='https://github.com/desktop-app/tg_owt'
license=('BSD-3-Clause')
# libdrm is dynamically loaded via dlopen
depends=('libdrm' 'libxdamage')
makedepends=('git' 'ninja' 'cmake' 'protobuf' 'libxrandr' 'libxcomposite' 'openssl' 'glibc'
             'ffmpeg' 'libva' 'opus' 'yasm' 'libjpeg-turbo' 'pipewire' 'libxtst' 'libepoxy' 'openh264')
options=('staticlibs')
source=("git+${url}.git#commit=${_commit}"
        0001-Replace-absl-template-nullability-annotations.patch
        "git+https://chromium.googlesource.com/libyuv/libyuv/"
        "git+https://github.com/google/crc32c"
        "git+https://github.com/abseil/abseil-cpp"
        "git+https://github.com/cisco/libsrtp")
b2sums=('921942c33425ee7efb6a0d982f89251dfeb9098a459fe71c8d6b8c934b4fbf812a3c6b320f0e5c0c49e9279ce2cd72dcbaf524c62bd44dcaf35ae7b1afe270f9'
        'b7505db537f72da43fdc648d181387a550a683e204a12df593d68ff51ae2f2a924352adf11ad2fa794e5de87ed96d0d698257b442ca0fd2fbb5fb09ac627a7b6'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd tg_owt
  git submodule init
  git submodule set-url src/third_party/libyuv "$srcdir"/libyuv
  git submodule set-url src/third_party/crc32c/src "$srcdir"/crc32c
  git submodule set-url src/third_party/abseil-cpp "$srcdir"/abseil-cpp
  git submodule set-url src/third_party/libsrtp "$srcdir"/libsrtp
  git -c protocol.file.allow=always submodule update

  # Fix for abseil 20250814; see https://github.com/desktop-app/tg_owt/pull/164.
  patch -p1 -i ../0001-Replace-absl-template-nullability-annotations.patch
}

build() {
  cd tg_owt

  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects -I/usr/include/libdrm"
  # Upstream stated that only static builds are really supported so that's what we'll do.
  # https://github.com/desktop-app/tg_owt/issues/75#issuecomment-992061171
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=OFF \
    -DTG_OWT_ARCH_ARMV7_USE_NEON=OFF
  ninja -C build
}

package() {
  cd tg_owt
  DESTDIR="${pkgdir}/" ninja -C build install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
