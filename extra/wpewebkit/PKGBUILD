# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v7 with vfpv3, neon is broken

pkgbase=wpewebkit
pkgname=(
  wpewebkit
  wpewebkit-docs
)
pkgver=2.44.3
pkgrel=2
pkgdesc="Embeddable web content engine"
url="https://wpewebkit.org"
arch=(x86_64)
license=(
  # :sort ui /\v^\s*['"]?/
  'AFL-2.0 OR GPL-2.0-or-later'
  Apache-2.0
  'Apache-2.0 WITH LLVM-exception'
  BSD-2-Clause
  BSD-2-Clause-Views
  BSD-3-Clause
  BSD-Source-Code
  BSL-1.0
  bzip2-1.0.6
  GPL-2.0-only
  'GPL-3.0-only WITH Autoconf-exception-3.0'
  'GPL-3.0-or-later WITH Bison-exception-2.2'
  ICU
  ISC
  LGPL-2.1-only
  LGPL-2.1-or-later
  MIT
  MPL-1.1
  MPL-2.0
  NCSA
  'NCSA OR MIT'
  OFL-1.1
  SunPro
  Unicode-TOU
)
depends=(
  at-spi2-core
  atk
  bubblewrap
  cairo
  fontconfig
  freetype2
  gcc-libs
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  harfbuzz
  harfbuzz-icu
  icu
  lcms2
  libavif
  libdrm
  libepoxy
  libgcrypt
  libjpeg
  libjxl
  libpng
  libseccomp
  libsoup3
  libsystemd
  libtasn1
  libwebp
  libwpe
  libxkbcommon
  libxml2
  libxslt
  mesa
  openjpeg2
  sqlite
  wayland
  woff2
  wpebackend-fdo
  xdg-dbus-proxy
  zlib
)
makedepends=(
  cmake
  gi-docgen
  glib2-devel
  gobject-introspection
  gperf
  gst-plugins-bad
  ninja
  python
  ruby
  ruby-stdlib
  systemd
  unifdef
  wayland-protocols
)
options=(
  # https://gitlab.archlinux.org/archlinux/packaging/packages/webkit2gtk-4.1/-/issues/1
  # https://bugs.webkit.org/show_bug.cgi?id=278090
  !lto
)
source=(
  $url/releases/wpewebkit-$pkgver.tar.xz{,.asc}
)
sha256sums=('55c639879eaa5f070fb1c11fccb968cd85ca21680cc8cd0cd638135ca24433bb'
            'SKIP')
b2sums=('9ec256150f34b2ca17517f5e5608b0b7e0ff34c6950d444dba2bbe7bb6415c5721a8173b9fa8eda0b3f749b3df4a7afd45aec27b7829cff09e0e9d75f62b55e9'
        'SKIP')
validpgpkeys=(
  # https://wpewebkit.org/release/verify/
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B # Adrián Pérez de Castro <aperez@igalia.com>
  013A0127AC9C65B34FFA62526C1009B693975393 # Carlos Garcia Campos <cgarcia@igalia.com>
)

prepare() {
  cd wpewebkit-$pkgver
}

build() {
  local cmake_options=(
    -DPORT=WPE
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DCMAKE_INSTALL_LIBEXECDIR=lib
    -DCMAKE_SKIP_RPATH=ON
    -DUSE_LIBBACKTRACE=OFF
    -DENABLE_DOCUMENTATION=ON
    -DENABLE_MINIBROWSER=ON
  )

  # JITted code crashes when CET is used
  #CFLAGS+=' -fcf-protection=none'
  #CXXFLAGS+=' -fcf-protection=none'

  # Produce minimal debug info: 4.3 GB of debug data makes the
  # build too slow and is too much to package for debuginfod
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  [[ $CARCH == "armv7h" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/neon/vfpv3/'` && CXXFLAGS="$CFLAGS"

  cmake -S wpewebkit-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package_wpewebkit() {
  depends+=(
    libWPEBackend-fdo-1.0.so
    libwpe-1.0.so
  )
  provides+=(
    libWPEWebKit-2.0.so
  )
  optdepends=(
    'geoclue: Geolocation support'
    'gst-libav: nonfree media decoding'
    'gst-plugins-bad: media decoding'
    'gst-plugins-good: media decoding'
  )

  DESTDIR="$pkgdir" cmake --install build

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  cd wpewebkit-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_wpewebkit-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
