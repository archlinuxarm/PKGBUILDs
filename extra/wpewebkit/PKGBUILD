# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v7 with vfpv3, neon is broken

pkgbase=wpewebkit
pkgname=(wpewebkit wpewebkit-docs)
pkgver=2.38.0
pkgrel=3
pkgdesc="Embeddable web content engine"
url="https://wpewebkit.org"
arch=(x86_64)
license=(custom)
depends=(
  at-spi2-atk
  atk
  bubblewrap
  cairo
  fontconfig
  freetype2
  glib2
  gst-plugins-base-libs
  gstreamer
  harfbuzz
  harfbuzz-icu
  icu
  libepoxy
  libgcrypt
  libjpeg
  libpng
  libseccomp
  libsoup3
  libsystemd
  libtasn1
  libwebp
  libwpe
  libxkbcommon
  libxml2
  libxslt
  openjpeg2
  sqlite
  wayland
  woff2
  wpebackend-fdo
  xdg-dbus-proxy
  zlib
)
makedepends=(
  cmake
  gi-docgen
  gobject-introspection
  gperf
  gst-plugins-bad
  ninja
  python
  ruby
  systemd
  wayland-protocols
)
options=(debug)
source=($url/releases/wpewebkit-$pkgver.tar.xz{,.asc})
sha256sums=('e653e12eca63e6371c06d4cd9efcd5492db67f7d9ef324fd5d8668b1151e72b2'
            'SKIP')
validpgpkeys=('D7FCF61CF9A2DEAB31D81BD3F3D322D0EC4582C3'  # Carlos Garcia Campos <cgarcia@igalia.com>
              '5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

prepare() {
  cd wpewebkit-$pkgver
}

build() {
  # Produce minimal debug info: 4.3 GB of debug data makes the
  # build too slow and is too much to package for debuginfod
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  [[ $CARCH == "armv7h" ]] && CFLAGS=`echo $CFLAGS | sed -e 's/neon/vfpv3/'` && CXXFLAGS="$CFLAGS"

  cmake -S wpewebkit-$pkgver -B build -G Ninja \
    -DPORT=WPE \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_SOUP2=OFF \
    -DENABLE_DOCUMENTATION=ON \
    -DENABLE_MINIBROWSER=ON
  cmake --build build
}

package_wpewebkit() {
  depends+=(libwpe-1.0.so libWPEBackend-fdo-1.0.so)
  provides+=(libWPEWebKit-1.1.so)
  optdepends=('geoclue: Geolocation support'
              'gst-plugins-good: media decoding'
              'gst-plugins-bad: media decoding'
              'gst-libav: nonfree media decoding')

  DESTDIR="$pkgdir" cmake --install build

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc

  cd wpewebkit-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_wpewebkit-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
