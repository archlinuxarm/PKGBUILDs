# $Id: PKGBUILD 51867 2009-09-12 23:24:22Z eric $
# Maintainer: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=xboard
pkgver=4.4.0
pkgrel=1
pkgdesc="A graphical user interfaces for chess"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/xboard/"
license=('GPL3')
depends=('libxaw' 'gnuchess' 'texinfo')
install=xboard.install
source=(ftp://ftp.gnu.org/gnu/xboard/${pkgname}-${pkgver}.tar.gz)
md5sums=('0eef62b9f710654cd7de7cda681bd0ea')
sha1sums=('20a582926a4c91efdc21595b9fb7e9dc674e8141')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/fairymax/gnuchess/' xboard.h || return 1
  ./autogen.sh || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
