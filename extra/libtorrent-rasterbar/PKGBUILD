# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=libtorrent-rasterbar
pkgver=1.2.11
pkgrel=1
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="https://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs')
makedepends=('boost' 'python')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        $pkgname-cxx14-fix.patch::https://github.com/arvidn/libtorrent/pull/5026.patch)
sha512sums=('abcfaa9f22f9adc030f0c4ec8dfd2e5070d1177cc2cf0bcb4e08dfca1b2e806a5c90a82d1860b5dc51efe3e8d2bea3c1d3d966c132a39bfdd5d4791e59bb503a'
            '94ddc3bdfc33f7b5300cc01b7c821dbd9a1433cae93bb7d467a1a732e84e7be4a6e75fbd69e88802e2eecca9c8541ecd1856d4ca2b508af224562cb05de80cca')

prepare() {
  cd $pkgname-$pkgver
  # https://bugs.archlinux.org/task/67754
  patch -p1 -i ../$pkgname-cxx14-fix.patch

  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  autoreconf -if
}

build() {
  cd $pkgname-$pkgver
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv \
    --with-boost-python=boost_python3
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
