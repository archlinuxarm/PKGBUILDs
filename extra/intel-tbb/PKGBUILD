# $Id$
# Maintainer:
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#        Romain Reignier <rom.reignier AT gmail.com>
# - add specfic CXXFLAGS for armv7h and aarch64

buildarch=12

pkgname=intel-tbb
pkgver=2017_20170412
_pkgver=2017_U6
_file=${_tag}_src.tgz
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(i686 x86_64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(https://github.com/01org/tbb/archive/$_pkgver.tar.gz)
sha256sums=('1f7df7af6045179a45eba9b5dfde74d185ee7f573328f103c137696f81e4ae5a')

build() {
  cd tbb-$_pkgver
  export CXXFLAGS+=" -fno-lifetime-dse" # FS#49898
  [[ $CARCH == "armv7h" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=0"
  [[ $CARCH == "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=1"
  make
}

package() {
  cd tbb-$_pkgver
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
