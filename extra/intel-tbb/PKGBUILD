# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#        Romain Reignier <rom.reignier AT gmail.com>
# - add specfic CXXFLAGS for ARM

pkgname=intel-tbb
pkgver=2020.0
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(x86_64)
url='https://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake inetutils)
source=($pkgname-$pkgver.tar.gz::https://github.com/intel/tbb/archive/v$pkgver.tar.gz)
sha256sums=('8eed2377ac62e6ac10af5a8303ce861e4525ffe491a061b48e8fe094fc741ce9')

build() {
  cd tbb-$pkgver
  [[ $CARCH != "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=0"
  [[ $CARCH == "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=1"
  make
}

package() {
  cd tbb-$pkgver
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
  cmake \
    -DINSTALL_DIR="$pkgdir"/usr/lib/cmake/TBB \
    -DSYSTEM_NAME=Linux -DTBB_VERSION_FILE="$pkgdir"/usr/include/tbb/tbb_stddef.h \
    -P cmake/tbb_config_installer.cmake
}
