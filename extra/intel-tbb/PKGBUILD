# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#        Romain Reignier <rom.reignier AT gmail.com>
# - add specfic CXXFLAGS for armv7h and aarch64

buildarch=12

pkgname=intel-tbb
pkgver=4.4_20151115
_dir=tbb${pkgver/\./}oss
pkgrel=2
pkgdesc='High level abstract threading library'
arch=('i686' 'x86_64')
url='http://www.threadingbuildingblocks.org/'
license=('GPL')
depends=('gcc-libs')
source=("http://threadingbuildingblocks.org/sites/default/files/software_releases/source/tbb${pkgver/\./}oss_src.tgz")
sha1sums=('5d81fe397e97e74a5645ce30c5b218852c799f07')

build() {
  cd $_dir
  [[ $CARCH == "armv7h" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=0"
  [[ $CARCH == "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=1"
  make
}

package() {
  cd $_dir
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
