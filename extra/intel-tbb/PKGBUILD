# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#        Romain Reignier <rom.reignier AT gmail.com>
# - add specfic CXXFLAGS for ARM

pkgname=intel-tbb
pkgver=2019.9
_tagname=${pkgver/./_U}
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(x86_64)
url='https://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake inetutils)
source=($pkgname-$pkgver.tar.gz::https://github.com/01org/tbb/archive/$_tagname.tar.gz)
sha256sums=('15652f5328cf00c576f065e5cd3eaf3317422fe82afb67a9bcec0dc065bd2abe')

build() {
  cd tbb-$_tagname
  sed -i '/debug/d' Makefile
  [[ $CARCH != "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=0"
  [[ $CARCH == "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=1"
  make
}

package() {
  cd tbb-$_tagname
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
  cmake \
    -DINSTALL_DIR="$pkgdir"/usr/lib/cmake/TBB \
    -DSYSTEM_NAME=Linux -DTBB_VERSION_FILE="$pkgdir"/usr/include/tbb/tbb_stddef.h \
    -P cmake/tbb_config_installer.cmake
}
