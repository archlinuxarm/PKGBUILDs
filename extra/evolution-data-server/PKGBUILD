# $Id: PKGBUILD 52381 2009-09-19 02:00:57Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=2.26.3
pkgrel=2
pkgdesc="Evolution Data Server provides a central location for addressbook and calendar in the GNOME Desktop"
arch=(i686 x86_64)
depends=('libsoup>=2.26.3' 'nss>=3.12.3' 'libgnome>=2.26.0' 'libglade>=2.6.4' 'gnome-keyring>=2.26.3' 'db4.1' 'heimdal>=1.2.1' 'libgweather>=2.26.2.1' 'libical>=0.43')
makedepends=('intltool' 'pkgconfig' 'gtk-doc')
options=('!libtool')
url="http://www.gnome.org"
license=('GPL')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('01205605a598253bc227ff360c7c3dad')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Look for correct DB, we want 4.1
  sed -e 's|DB_CFLAGS="-I$withval/include"|DB_CFLAGS="-I$withval/include/db4.1"|' \
      -e 's|DB_LIBS="-L$withval/lib -ldb"|DB_LIBS="-L$withval/lib -ldb-4.1"|' \
      -i configure || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --with-openldap=yes \
      --libexecdir=/usr/lib/evolution-data-server-1.2 \
      --with-krb5=/usr --enable-gnome-keyring \
      --with-libdb=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
