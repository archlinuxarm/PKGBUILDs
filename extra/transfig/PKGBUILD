# $Id: PKGBUILD 49622 2009-08-14 04:05:21Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar>

pkgname=transfig
pkgver=3.2.5a
pkgrel=1
pkgdesc="Format conversion utility that can be used with xfig"
arch=('i686' 'x86_64')
url="http://www.xfig.org"
license=('custom')
depends=('libpng' 'libxpm')
makedepends=('imake')
options=('force')
source=(http://downloads.sourceforge.net/mcj/${pkgname}.${pkgver}.tar.gz LICENSE)
md5sums=('c3e4259d0b5b263baa2d8d5e811b2ef1' 'a0b2ea903821c46dbc77afefc06d1aa4')
sha1sums=('65f772a51389be2b1cfebdb1354130aa71609739' 'b8b712871615308b8b6add92f86d218437d652f2')

build() {
  cd "${srcdir}/${pkgname}.${pkgver}"
  sed -i 's/XCOMM USELATEX2E = -DLATEX2E/USELATEX2E = -DLATEX2E/' transfig/Imakefile || return 1
  sed -i 's/XCOMM USEINLINE = -DUSE_INLINE/USEINLINE = -DUSE_INLINE/' fig2dev/Imakefile || return 1

  xmkmf || return 1
  make FIG2DEV_LIBDIR=/usr/share/fig2dev Makefiles || return 1
  make FIG2DEV_LIBDIR=/usr/share/fig2dev XFIGLIBDIR=/usr/share/xfig || return 1
  make DESTDIR="${pkgdir}" XFIGLIBDIR=/usr/share/xfig FIG2DEV_LIBDIR=/usr/share/fig2dev MANPATH=/usr/share/man install install.man || return 1
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
