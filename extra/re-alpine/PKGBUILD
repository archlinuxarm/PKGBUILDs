# $Id$
# Maintainer:  Eric BÃ©langer <eric@archlinux.org>
# Contributor: Smith Dhumbumroong <zodmaner@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - specify --with-ssl-dir=/usr to overcome retarded autodetection

pkgname=re-alpine
pkgver=2.03
pkgrel=2
pkgdesc="The continuation of the Alpine email client from University of Washington"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/re-alpine/"
license=('APACHE')
depends=('libldap' 'krb5' 'pam')
optdepends=('aspell: spell-checking support'
	    'hunspell: spell-checking support')
options=('!makeflags')
source=(http://downloads.sourceforge.net/project/re-alpine/${pkgname}-${pkgver}.tar.bz2 
        maildir.patch)
sha1sums=('8e1c4f4a4d38814478e8bd3bbeed1c0f8ee9491b'
          'c09a8e42f9dba3e63a3755a9c418af95da721d8d')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/maildir.patch"
}

build() {
  cd ${pkgname}-${pkgver}
   LIBS+="-lpam -lkrb5 -lcrypto" ./configure --prefix=/usr --without-passfile --without-tcl \
    --disable-shared --with-system-pinerc=/etc/alpine.d/pine.conf \
    --with-system-fixed-pinerc=/etc/alpine.d/pine.conf.fixed --with-ssl-dir=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
