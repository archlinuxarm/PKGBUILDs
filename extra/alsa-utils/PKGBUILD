# Maintainer: Alexander Foremny <alexanderforemny@gmail.com>

pkgname=alsa-utils
pkgver=1.0.20
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=(arm)
url="http://www.alsa-project.org"
depends=("alsa-lib==$pkgver" dialog pciutils ncurses)
license=('GPL')
backup=(etc/conf.d/alsa)
source=(ftp://ftp.alsa-project.org/pub/utils/alsa-utils-$pkgver.tar.bz2
        alsa
        alsaconf.patch
        alsa.conf.d
        alsactl-init-fix-headphone.patch)
md5sums=('6837e673ef19da96d8bd2f9e18cd9574'
         '9dff78913b13b10e67c4195e90395ed2'
         '434102eb210cc452e3dfd2a4a9841881'
         '93089f0d8872078939f76dd2acaebc2b'
         '7ac2e3ddf559c7383806c6f5844ad583')

build() {
  cd $srcdir/alsa-utils-$pkgver

  patch -Np1 -i ../alsaconf.patch || return 1
  patch -Np1 -i ../alsactl-init-fix-headphone.patch || return 1

  ./configure --prefix=/usr --disable-xmlto || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -D -m755 ../alsa $startdir/pkg/etc/rc.d/alsa
  install -D -m644 ../alsa.conf.d $startdir/pkg/etc/conf.d/alsa
}

# vim: set ft=sh ts=2 sw=2 et:
