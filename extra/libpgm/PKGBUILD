# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Vladimir Kirillov <proger@wilab.org.ua>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from debian to fix error in header used by other programs

pkgname=libpgm
pkgver=5.3.128
_pkgver="${pkgver//./-}"
pkgrel=2
pkgdesc="OpenPGM: implementation of the Pragmatic General Multicast (PGM, RFC3208)"
arch=('x86_64')
url='https://www.freshports.org/net/openpgm/'
license=('LGPL2.1')
depends=('glibc')
makedepends=('python')
options=(!strip)
#source=("http://openpgm.googlecode.com/files/$pkgname-$pkgver.tar.gz")
#source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/openpgm/libpgm-$pkgver.tar.gz")
source=("libpgm-$pkgver.tgz::https://github.com/steve-o/openpgm/archive/release-${_pkgver}.tar.gz"
        'inline-pgm_family_string.patch')
sha256sums=('8d707ef8dda45f4a7bc91016d7f2fed6a418637185d76c7ab30b306499c6d393'
            '0373d16324605639442d24020e09dcda3e3ab5b86fda7b01c865b49163650706')

prepare() {
  cd "$srcdir/openpgm-release-$_pkgver/openpgm/pgm"
  2to3 -wn version_generator.py
  # 5.3.128 is a little broken
  cp openpgm-5.2.pc.in openpgm-5.3.pc.in
  patch -p3 -i $srcdir/inline-pgm_family_string.patch
}

build() {
  cd "$srcdir/openpgm-release-$_pkgver/openpgm/pgm"
  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/openpgm-release-$_pkgver/openpgm/pgm"
  make prefix="$pkgdir/usr" install
}
