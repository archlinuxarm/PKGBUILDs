# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.26.2.2
pkgrel=1
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('arm')
license=('GPL')
depends=('autogen>=5.9.5' 'devhelp>=0.23' 'gnome-build>=2.24.1' 'vte>=0.20.5' 'graphviz>=2.22.2' 'subversion>=1.6.1' 'gtksourceview2>=2.6.2' 'glade>=3.6.7' 'libglade>=2.6.4' 'libxslt>=1.1.24' 'libunique>=1.0.8' 'libgda>=4.0.2' 'gdl>=2.26.2' 'libgnomeui>=2.24.1' 'desktop-file-utils' 'ctags')
makedepends=('gnome-doc-utils>=0.16.1' 'intltool>=0.40.6' 'valgrind>=3.4.1')
optdepends=('valgrind: for the valgrind debugger plugin')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
md5sums=('8b05a4ee536b8057e38434efeaf468ff')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-plugin-subversion --enable-plugin-glade \
    --enable-plugin-valgrind \
    --enable-plugin-sourceview --disable-gtk-doc \
    --enable-debug --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
   
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain anjuta ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
  #exists already in gnome-build
  rm -f "${pkgdir}/usr/bin/gbf-am-parse"
  rm -f "${pkgdir}/usr/bin/gbf-mkfile-parse"
}
