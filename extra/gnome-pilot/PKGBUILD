# $Id: PKGBUILD 24756 2009-01-20 16:04:27Z douglas $
# Jan de Groot <jgc@archlinux.org>

pkgname=gnome-pilot
pkgver=2.0.17
pkgrel=1
pkgdesc="Pilot Support for Gnome"
arch=(i686 x86_64)
license=('GPL')
depends=('pilot-link>=0.12.3-3')
makedepends=('pkgconfig' 'intltool' 'gob2' 'gnome-doc-utils>=0.14.2' 'gnome-panel>=2.24.3')
optdepends=('gnome-panel')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gnome-pilot.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.0/${pkgname}-${pkgver}.tar.bz2)
md5sums=('6086573332d607ddba06fe705667b240')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/gnome-pilot \
              --localstatedir=/var --disable-static \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  rm -f "${pkgdir}/usr/share/applications/gpilot-applet.desktop"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
