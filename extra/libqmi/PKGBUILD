# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --enable-qrtr

pkgname=libqmi
pkgver=1.30.4
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/libqmi/"
license=(GPL2)
depends=(libmbim libqrtr-glib libgudev)
makedepends=(gtk-doc python git help2man gobject-introspection)
provides=(libqmi-glib.so)
_commit=f6c2feaf199e0f129a0bde8c5e6cba5f2117b564  # tags/1.30.4
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqmi.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libqmi
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libqmi
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libqmi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --enable-gtk-doc \
    --enable-compile-warnings=yes --enable-qrtr
  make
}

check() {
  cd libqmi
  make check
}

package() {
  cd libqmi
  make DESTDIR="$pkgdir" install
}
