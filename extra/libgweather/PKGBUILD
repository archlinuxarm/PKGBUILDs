# $Id: PKGBUILD 44710 2009-07-03 18:12:35Z jgc $
# Maintainer: Jan de Groot  <jgc@archlinux.org>
pkgname=libgweather
pkgver=2.26.2.1
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=(i686 x86_64)
license=('LGPL')
depends=('gconf>=2.26.0-3' 'libsoup>=2.26.3')
makedepends=('pkgconfig' 'intltool' 'gtk-doc')
options=('!libtool' '!emptydirs')
conflicts=('gnome-applets<2.22.0')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2
        01_gettext_not_xml.patch
        revert-shave.patch)
md5sums=('cef0447b62235c4662aa9691dd9a6c92'
         '71a3897af7f236345869efd87da3e9b3'
         'e33966183e56b0f9d6e932ca4422fdb0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -R -i "${srcdir}/revert-shave.patch" || return 1
  patch -Np1 -i "${srcdir}/01_gettext_not_xml.patch" || return 1
  libtoolize --force || return 1
  gtkdocize || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-locations-compression || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgweather ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
