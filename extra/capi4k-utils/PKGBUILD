# $Id: PKGBUILD 39034 2009-05-11 08:28:48Z tpowa $
#Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=capi4k-utils
pkgver=050718
pkgrel=7
pkgdesc="capi utils for isdn cards"
arch=(i686 x86_64)
license=('GPL2')
url="ftp://ftp.in-berlin.de/pub/capi4linux/"
makedepends=('ppp')
depends=('glibc' 'bash')
backup=(etc/capi.conf)
source=(ftp://ftp.in-berlin.de/pub/capi4linux/capi4k-utils-2005-07-18.tar.gz 
        ppp-2.4.4.patch 
        config 
        capi.conf 
        capiinit.rc
        capi-compile.patch)
options=(!libtool)
md5sums=('c745759b6b3d64e19763727176648cdf'
         'c5416b8b1b76a82c4bcaad76140369e1'
         '146aa9e06588511c447d7e81dafec86a'
         'c88940f0ace5759b7a0e8b267ca77ce2'
         'aa59439a70299290a4d9bcdfaeafed72'
         '4fa27c5b7b1ee60524fce875bb54bca8')

build() {
  cd $srcdir/$pkgname
  patch -Np1 -i ../ppp-2.4.4.patch || return 1
  # fix compiling with gcc 4.2
  patch -Np1 -i ../capi-compile.patch || return 1
  cp ../config .config
  # fix pppd plugin
  sed -i -e 's/2.3.11 2.4.0 2.4.1 2.4.1b1 2.4.1b2 2.4.2b3 2.4.2 2.4.3/2.4.4/g' $startdir/src/$pkgname/pppdcapiplugin/Makefile
  # patch all Makefile.* and Rules.make to use our CFLAGS
  sed -i -e "s:^\(CFLAGS.*\)-O2:\1${CFLAGS}:g" */Makefile.* */Rules.make
  # Arch64 fix from Gentoo
  # patch capi20/Makefile.* to use -fPIC for shared library
  [ "$CARCH" = "x86_64" ] &&  (sed -i -e "s:^\(CFLAGS.*\):\1 -fPIC:g" capi20/Makefile.* || return 1)
  cd capi20
  aclocal
  libtoolize --copy --force
  cd $srcdir/$pkgname
  make subconfig || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1
  install -m755 -D $srcdir/capiinit.rc $pkgdir/etc/rc.d/capiinit
  install -m644 $srcdir/capi.conf $pkgdir/etc
  cd $pkgdir/usr/lib
  ln -s libcapi20 libcapi20.so.2
  # fix wrong manpage permission
  chmod 644 $pkgdir/usr/share/man/man8/capiplugin.8
}
