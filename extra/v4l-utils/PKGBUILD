# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable distcc - configure checks for clang first

pkgname=v4l-utils
pkgver=1.28.1
pkgrel=2
pkgdesc="Userspace tools and conversion library for Video 4 Linux"
arch=('x86_64')
url="https://linuxtv.org/"
provides=("libv4l=$pkgver")
replaces=('libv4l')
conflicts=('libv4l')
backup=(etc/rc_maps.cfg)
license=('LGPL')
options=('!distcc')
depends=('hicolor-icon-theme' 'gcc-libs' 'libbpf' 'libjpeg-turbo'  'systemd-libs' 'json-c')
makedepends=('qt6-base' 'qt6-5compat' 'alsa-lib' 'meson' 'clang' 'doxygen')
optdepends=('qt6-base: for qv4l2 and qvidcap'
            'qt6-5compat: for qv4l2'
            'alsa-lib: for qv4l2')
source=(https://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.xz{,.asc}
        v4l-utils-qt-6.7-compat.patch)
sha256sums=('0fa075ce59b6618847af6ea191b6155565ccaa44de0504581ddfed795a328a82'
            'SKIP'
            '8f00f6af2ea090e472d1a4ac8c0aa824d8847894cdeb379a64b8bbef4c5d7ecc')
validpgpkeys=('05D0169C26E41593418129DF199A64FADFB500FF') # Gregor Jasny <gjasny@googlemail.com>

prepare() {
  # HACK: inform upstream to make this configurable
  cd "${pkgname}-${pkgver}"
  sed -i 's/sbin/bin/' utils/v4l2-dbg/meson.build

  # Fix compatibility with Qt >= 6.7
  # https://github.com/gjasny/v4l-utils/commit/cb8d855fad89b89044bb5ae3812f86a6578d4955
  patch -Np1 -i ../v4l-utils-qt-6.7-compat.patch
}

build() {
  arch-meson -Dgconv=disabled "${pkgname}-${pkgver}" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
