# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - manually disable qt5 OpenGL support via config.h

pkgname=v4l-utils
pkgver=1.12.3
pkgrel=1
pkgdesc="Userspace tools and conversion library for Video 4 Linux"
arch=('i686' 'x86_64')
url="http://linuxtv.org/"
provides=("libv4l=$pkgver")
replaces=('libv4l')
conflicts=('libv4l')
backup=(etc/rc_maps.cfg)
license=('LGPL')
makedepends=('qt5-base' 'alsa-lib')
optdepends=('qt5-base' 'alsa-lib')
depends=('glibc' 'gcc-libs' 'sysfsutils' 'libjpeg-turbo')
source=(http://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha256sums=('5a47dd6f0e7dfe902d94605c01d385a4a4e87583ff5856d6f181900ea81cf46e'
            'SKIP')
validpgpkeys=('05D0169C26E41593418129DF199A64FADFB500FF') # Gregor Jasny <gjasny@googlemail.com>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  sed -i '/#define HAVE_QTGL 1/d' config.h
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  MAKEFLAGS="-j1" make install DESTDIR="${pkgdir}/"
  rm "${pkgdir}/usr/bin/ivtv-ctl"
}
