# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=calligra
pkgdesc='A set of applications for productivity and creative usage'
pkgver=3.2.1
pkgrel=37
arch=(x86_64)
url='https://www.calligra-suite.org/'
license=(FDL1.2 GPL2 LGPL)
depends=(kcmutils knotifyconfig kross kactivities kdiagram libspnav kdelibs4support
         gsl imath kcontacts qca-qt5 poppler-qt5 libodfgen khtml kinit cauchy)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin libwpg okular eigen marble-common
             boost pstoedit libvisio libetonyek libwps libgit2 kcalendarcore akonadi)
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'libgit2: Calligra Gemini git plugin'
            'kirigami2: for Calligra Gemini'
            'qt5-quickcontrols: for Calligra Gemini'
            'qt5-webengine: for Calligra Gemini')
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        calligra-openexr3.patch
        068cd9ae.patch
        2ac46db5.patch
        62f51070.patch
        https://invent.kde.org/office/calligra/-/commit/236bacbe.patch
        https://invent.kde.org/office/calligra/-/commit/6b75bec7.patch)
sha256sums=('b1f8a6eb8208543a2f908caa83d089075fcd1e530d00e19f5362b2f9cc371147'
            'SKIP'
            '96fbe4f06bf184e60ff653a1574f0f0523af5b4672ced2a501cd54642961dffe'
            '4516d15421209e5d8c8b5008140dbcb1eefa96b0e96e0da49b343e1799a8cefd'
            'c88e6d7a1f67c1b5413b624aa67fca2841205fdf4201f6682f69bae737582922'
            '8a94e076c09887ff0741da3276ce4652063351b884c66d4c9ba0cde431dbb867'
            '94ce2242774d18df21dd594f2d9dce752ffda0349b2c2c872dfe39cb73353aab'
            '0eb880b19f32fe4045831415fe5849e43db09be3f7550c4abfe21e4511dfb1a5')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD  # Boudewijn Rempt <foundation@krita.org>
              42A60D06F449E895F40F09C01638B8A616108B8A) # Dag Andersen <danders@get2net.dk>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < calligra-openexr3.patch # Fix build with OpenEXR 3
  patch -d $pkgname-$pkgver -p1 < 068cd9ae.patch # Remove dynamic exception specifications
  patch -d $pkgname-$pkgver -p1 < 2ac46db5.patch # Prerequisite for the following patch
  patch -d $pkgname-$pkgver -p1 < 62f51070.patch # Fix fontconfig linking
  patch -d $pkgname-$pkgver -p1 < 236bacbe.patch # Fix build with poppler 22.03.0
  patch -d $pkgname-$pkgver -p1 < 6b75bec7.patch # Fix build with poppler 22.04.0
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_CXX_STANDARD=17 \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Remove utterly broken thumbnailers
  rm "$pkgdir"/usr/lib/qt/plugins/calligra*thumbnail.so
}
