# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc
#  - fedora patch to fix ARM build with GCC 6

pkgname=calligra
pkgdesc="A set of applications for productivity and creative usage"
pkgver=3.0.1
pkgrel=3
arch=('i686' 'x86_64')
url='http://www.calligra-suite.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('kcmutils' 'knotifyconfig' 'kross' 'kactivities' 'kdiagram' 'kreport' 'libspnav'
         'gsl' 'openexr' 'kcontacts' 'kcalcore' 'qca-qt5' 'poppler-qt5' 'libodfgen' 'qt5-webkit' 'cauchy' 'khtml')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'kdesignerplugin' 'boost' 'openjpeg'
             'libwpg' 'okular' 'eigen' 'marble-common' 'pstoedit' 'libvisio' 'libetonyek' 'libwps')
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'openjpeg: PDF to SVG filer')
conflicts=('calligra-devtools' 'calligra-extras' 'calligra-filters' 'calligra-karbon' 'karbon' 'calligra-libs' 'calligra-plan'
          'calligra-plugins' 'calligra-sheets' 'calligra-stage' 'calligra-words' 'calligra-l10n' 'calligra-meta'
          'calligra-flow' 'calligra-braindump' 'calligra-gemini' 'calligra-handbook')
replaces=('calligra-devtools' 'calligra-extras' 'calligra-filters' 'calligra-karbon' 'karbon' 'calligra-libs' 'calligra-plan'
          'calligra-plugins' 'calligra-sheets' 'calligra-stage' 'calligra-words' 'calligra-l10n' 'calligra-meta')
for _lang in bs ca ca@valencia cs da de el en_gb es et fi fr gl hu it ja kk nb nl \
             pl pt pt_br ru sk sv uk zh_cn zh_tw; do
  conflicts+=(calligra-l10n-$_lang)
  replaces+=(calligra-l10n-$_lang)
done
source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        'calligra-2.9.11-gcc6_arm.patch')
sha256sums=('452bddc4e03cfbf48de508b991dc94209d386d86bf010ea9d10afed0f5694db2'
            'SKIP'
            '33d803c49abefc37f51f824543e889f9e369e86df9ce64d980e0a7dbca2ea27d')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD  # Boudewijn Rempt <foundation@krita.org>
              42A60D06F449E895F40F09C01638B8A616108B8A) # Dag Andersen <danders@get2net.dk>

prepare() {
  mkdir -p build

  # Fix build on ARM
  cd $pkgbase-$pkgver
  patch -p1 -i ../calligra-2.9.11-gcc6_arm.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
