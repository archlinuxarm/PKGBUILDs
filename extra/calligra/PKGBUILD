# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=calligra
pkgdesc='A set of applications for productivity and creative usage'
pkgver=25.08.3
pkgrel=2
arch=(x86_64)
url='https://www.calligra-suite.org/'
license=(GPL-2.0-only)
depends=(cauchy
         fontconfig
         freetype2
         gcc-libs
         glibc
         gsl
         imath
         karchive
         kcolorscheme
         kcompletion
         kconfigwidgets
         kcoreaddons
         kdbusaddons
         kdiagram
         kcmutils
         kconfig
         kcrash
         kguiaddons
         ki18n
         kiconthemes
         kio
         kitemviews
         knotifications
         knotifyconfig
         ktextwidgets
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libodfgen
         librevenge
         openssl
         phonon-qt6
         qca-qt6
         qt6-base
         qt6-declarative
         qt6-svg
         qtkeychain-qt6
         sonnet
         zlib)
makedepends=(boost
             eigen
             extra-cmake-modules
             kdoctools
             libetonyek
             libvisio
             libwpg
             libwps
             okular
             pstoedit
             qt6-webengine)
optdepends=('kirigami-addons: for calligralauncher'
            'libetonyek: Apple Keynote import filter'
            'libvisio: Microsoft Visio import filter'
            'libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'poppler: PDF to SVG filter'
            'pstoedit: EPS to SVG filter'
            'qqc2-desktop-style: for calligralauncher'
            'qt6-webengine: Braindump web shape')
groups=(kde-applications
        kde-office)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        poppler25.10.0.patch)
sha256sums=('74fac78253c429853e99fae00bcf2b84332dbc3a8f538ccf70674259266b9251'
            'SKIP'
            'b28e9f888144c90556bb0bd9b2a9116b084570e32294e1e49701c7b3846b2420')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  # https://invent.kde.org/office/calligra/-/commit/45e8b302bce1d318f310ea13599d7ce84acc477e
  patch -d $pkgname-$pkgver -p1 < poppler25.10.0.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DRELEASE_BUILD=ON \
    -DPRODUCTSET=DESKTOP \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
