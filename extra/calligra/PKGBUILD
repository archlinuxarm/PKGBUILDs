# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc
#  - fedora patch to fix ARM build with GCC 6

pkgname=calligra
pkgdesc="A set of applications for productivity and creative usage"
pkgver=3.0.1
pkgrel=9
arch=(x86_64)
url='https://www.calligra-suite.org/'
license=(FDL1.2 GPL2 LGPL)
depends=(kcmutils knotifyconfig kross kactivities kdiagram kreport libspnav kdelibs4support
         gsl openexr kcontacts kcalcore qca-qt5 poppler-qt5 libodfgen qt5-webkit cauchy khtml)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin boost openjpeg akonadi-contacts
             libwpg okular eigen marble-common pstoedit libvisio libetonyek libwps)
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'openjpeg: PDF to SVG filer')
conflicts=(calligra-devtools calligra-extras calligra-filters calligra-karbon karbon calligra-libs calligra-plan
           calligra-plugins calligra-sheets calligra-stage calligra-words calligra-l10n calligra-meta
           calligra-flow calligra-braindump calligra-gemini calligra-handbook)
replaces=(calligra-devtools calligra-extras calligra-filters calligra-karbon karbon calligra-libs calligra-plan
          calligra-plugins calligra-sheets calligra-stage calligra-words calligra-l10n calligra-meta)
for _lang in bs ca ca@valencia cs da de el en_gb es et fi fr gl hu it ja kk nb nl \
             pl pt pt_br ru sk sv uk zh_cn zh_tw; do
  conflicts+=(calligra-l10n-$_lang)
  replaces+=(calligra-l10n-$_lang)
done
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        calligra-kcalcore-17.12.patch::"https://cgit.kde.org/calligra.git/patch/?id=9a02a545"
        'calligra-2.9.11-gcc6_arm.patch')
sha256sums=('452bddc4e03cfbf48de508b991dc94209d386d86bf010ea9d10afed0f5694db2'
            'SKIP'
            '83b8eff3e5fa50decf826b22912d0f77ae855c230c470ad4c260870a888cdce0'
            '33d803c49abefc37f51f824543e889f9e369e86df9ce64d980e0a7dbca2ea27d')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD  # Boudewijn Rempt <foundation@krita.org>
              42A60D06F449E895F40F09C01638B8A616108B8A) # Dag Andersen <danders@get2net.dk>

prepare() {
  mkdir -p build

  cd $pkgbase-$pkgver
  patch -p1 -i ../calligra-kcalcore-17.12.patch # Fix build with kcalcore 17.12
  patch -p1 -i ../calligra-2.9.11-gcc6_arm.patch
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
