# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org
#  - remove make/optdepend on cuda and configure option

pkgname=openmpi
pkgver=4.1.4
pkgrel=1
pkgdesc='High performance message passing library (MPI)'
arch=(x86_64)
url='https://www.open-mpi.org'
license=('custom:OpenMPI')
depends=(gcc-libs glibc hwloc libevent libnl openpmix zlib)
makedepends=(gcc-fortran inetutils valgrind)
optdepends=(
  'gcc-fortran: fortran support'
  'openssh: for offloading to remote hosts'
  'perl: for aggregate_profile.pl and profile2mat.pl'
)
provides=(
  libmca_common_cuda.so
  libmca_common_monitoring.so
  libmca_common_ompio.so
  libmca_common_sm.so
  libmpi.so
  libmpi_cxx.so
  libmpi_mpifh.so
  libmpi_usempi_ignore_tkr.so
  libmpi_usempif08.so
  libompitrace.so
  libopen-pal.so
  libopen-rte.so
)
options=(debug)
source=(https://www.open-mpi.org/software/ompi/v${pkgver%.*}/downloads/$pkgname-$pkgver.tar.bz2)
sha256sums=('92912e175fd1234368c8730c03f4996fe5942e7479bb1d10059405e7f2b3930d')
b2sums=('b020e3530ae5dde7b144e7c33b1a3f26f622526a4b48a97a0956fc6f49bbf9dfd5be9ebeeaf3bdc5168a307507408ba5dd8e2a537148821e1d476678177dc5d6')

build() {
  cd $pkgname-$pkgver

  # TODO: depend on prrte with openmpi >= 5
  ./configure \
    --prefix=/usr \
    --enable-builtin-atomics \
    --enable-memchecker \
    --enable-mpi-cxx \
    --enable-mpi-fortran=all \
    --enable-pretty-print-stacktrace \
    --libdir=/usr/lib \
    --sysconfdir=/etc/$pkgname \
    --with-hwloc=external \
    --with-libevent=external \
    --with-pmix=external \
    --with-valgrind \
    --without-slurm

  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make V=1
}

check() {
  make check -C $pkgname-$pkgver
}

package() {
  depends+=(libpmix.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -Dm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
