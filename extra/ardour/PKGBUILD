# Maintainer: tobias <tobias@archlinux.org>
# Contributor: ... let's say it was teamwork ...

# ardour, liblrdf and raptor are heavily dependent on each other. Updating of
# one package mostly will require an update of all the other packages.

pkgname=ardour
pkgver=2.8
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('arm')
license=('GPL2')
url="http://ardour.org"
depends=('liblrdf>=0.4.0-5'
         'ladspa>=1.13' 'libgnomecanvasmm>=2.26.0'
         'liblo>=0.25' 'libusb>=0.1.12' 'aubio>=0.3.2' 'libsndfile>=1.0.19')
makedepends=('scons' 'boost' 'soundtouch')
install=${pkgname}.install
source=(ftp://ftp.archlinux.org/other/ardour/${pkgname}-${pkgver}.tar.bz2
        ${pkgname}.desktop
		100_syslibs.patch)
md5sums=('24bd768dbe08f1f2724dc97704ee0518'
         '8aeaf433ebf781733db48e5a16b0c4da'
	 	 '98b6f1d56af73b18f126b200456ce312')

build() {
  # start building ardour now
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/100_syslibs.patch" || return 1
  sed -i -e "s@/configure@& --prefix=/usr@g" libs/libsndfile/SConscript || return 1
  sed -i -e "s/lib64/lib/g" SConstruct || return 1
  sed -i "s/_protocol.h>/\0\n#include <cstring>/g" libs/surfaces/tranzport/screen.cc || return 1

  # force -O2 from the CFLAGS in makepkg.conf
  sed -i '/-O3/d' SConstruct || return 1
  scons ARCH="${CFLAGS}" \
        PREFIX="/usr" \
	NLS=yes \
	FREEDESKTOP=no \
	NJOBS="${MAKEFLAGS}" \
	SYSLIBS=yes \
	VST=0 \
        DESTDIR="${pkgdir}" || return 1
  scons PREFIX="/usr" \
  	NLS=yes \
	FREEDESKTOP=no \
	SYSLIBS=yes \
	VST=0 \
        DESTDIR="${pkgdir}" install || return 1
  # install some freedesktop.org compatibility
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/" || return 1
}
