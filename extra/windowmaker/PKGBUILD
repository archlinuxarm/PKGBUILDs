# $Id: PKGBUILD 43922 2009-06-30 07:06:04Z allan $
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
pkgname=windowmaker
pkgver=0.92.0
pkgrel=6
pkgdesc="An X11 window manager with a NEXTSTEP look and feel"
arch=(i686 x86_64)
url="http://www.windowmaker.info/"
license=('GPL' 'custom')
depends=('libxinerama' 'libpng' 'libxpm' 'libxft' 'libtiff>=3.8.2-5' 'giflib')
options=('!libtool')
source=(http://windowmaker.info/pub/source/release/WindowMaker-$pkgver.tar.bz2 windowmaker-gcc4.patch.tar.bz2)
md5sums=('aaac5421b686ed2d3e6ab65229c98097' 'd9fb6a9c255f5c03d0e0c83dc3cd2320')

build() {
  cd $srcdir/WindowMaker-$pkgver
  patch -Np1 -i ../windowmaker-gcc4.patch || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  [ -z "$LINGUAS" ] && export LINGUAS="`ls po/*.po | sed 's:po/\(.*\)\.po$:\1:'`"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-gnustepdir=/usr/share/GNUstep --with-nlsdir=/usr/share/locale \
    --enable-xinerama  || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1
  install -D -m644 COPYING.WTFPL $pkgdir/usr/share/licenses/$pkgname/COPYING.WTFPL
}

