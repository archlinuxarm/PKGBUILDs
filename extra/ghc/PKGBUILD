# $Id$
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

# use the following command to print out packages that need to get rebuilt
# grep -r ghc /var/abs/ | awk -F '/' '{ print $5; }' | sort -u

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org
#        David FlemstrÃ¶m <david.flemstrom@gmail.com>
#  - Add patch for vfp flags (add-vfp-flags.patch)
#  - build.mk: BuildFlavor change from perf to perf-llvm
#  - Add LLVM as a dependency
#  - Correct gcc path after bulding with distcc

pkgname=ghc
pkgver=7.8.2
pkgrel=3
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp' 'gcc' 'libffi' 'llvm')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl')
checkdepends=('python2')
install='ghc.install'
options=('staticlibs' '!makeflags')
provides=('haskell-array=0.5.0.0'
          'haskell-base=4.7.0.0'
          'haskell-binary=0.7.1.0'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-bytestring=0.10.4.0'
          'haskell-containers=0.5.5.1'
          'haskell-deepseq=1.3.0.2'
          'haskell-directory=1.2.1.0'
          'haskell-filepath=1.3.0.2'
          'haskell-ghc-prim=0.3.1.0'
          'haskell-haskell2010=1.1.2.0'
          'haskell-haskell98=2.0.0.3'
          'haskell-hoopl=3.10.0.1'
          'haskell-hpc=0.6.0.1'
          'haskell-integer-gmp=0.5.1.0'
          'haskell-old-locale=1.0.0.6'
          'haskell-old-time=1.1.0.2'
          'haskell-pretty=1.1.1.1'
          'haskell-process=1.2.0.0'
          'haskell-template-haskell=2.9.0.0'
          'haskell-time=1.4.2'
          'haskell-transformers=0.3.0.0'
          'haskell-unix=2.7.0.1'
          'haskell-cabal=1.18.1.3'
         )
replaces=('haskell-array<0.5.0.0'
          'haskell-base<4.7.0.0'
          'haskell-binary<0.7.1.0'
          'haskell-bin-package-db<0.0.0.0'
          'haskell-bytestring<0.10.4.0'
          'haskell-containers<0.5.5.1'
          'haskell-deepseq<1.3.0.2'
          'haskell-directory<1.2.1.0'
          'haskell-filepath<1.3.0.2'
          'haskell-ghc-prim<0.3.1.0'
          'haskell-haskell2010<1.1.2.0'
          'haskell-haskell98<2.0.0.3'
          'haskell-hoopl<3.10.0.1'
          'haskell-hpc<0.6.0.1'
          'haskell-integer-gmp<0.5.1.0'
          'haskell-old-locale<1.0.0.6'
          'haskell-old-time<1.1.0.2'
          'haskell-pretty<1.1.1.1'
          'haskell-process<1.2.0.0'
          'haskell-template-haskell<2.9.0.0'
          'haskell-time<1.4.2'
          'haskell-transformers'
          'haskell-unix<2.7.0.1'
          'haskell-cabal<1.18.1.3'
         )
source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.xz"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.xz"
        'build.mk'
        'ghc'
        'add-vfp-flags.patch')
md5sums=('97578e0c27574a99e0d5071a7f35d136'
         'ac54f7fa453010149a573740705ecbfb'
         'cb89e280f907c60791d08613f05ec049'
         'c1d46d86752c50dd85e7143b029fa07f'
         '1eb0ed10fa9676c2e23a587c40166e0e')

build() {
  cd ghc-${pkgver}

  cp ${srcdir}/build.mk mk/build.mk

  patch -p1 < ${srcdir}/add-vfp-flags.patch

  # have to 're-boot' because the patch changes configure stuff
  ./boot

  ./configure \
    --prefix=/usr \
    --with-system-libffi \
    --with-ffi-includes=/usr/lib/libffi-3.0.13/include

  make
}

check() {
  # keep this otherwise a lot of tests will show as unexpected failures... need to investigate why using -jn for n > 1 does this...
  unset MAKEFLAGS

  cd ghc-${pkgver}

  # python2 rename
  sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

  # upstream known failures on both i686 and x86_64:
  #    ghci.debugger/scripts     break011 [bad stderr] (ghci)
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  make test

  # enabling threads to speed up
  # causes more failures than make test, need to investigate
  # upstream known failures on both i686 and x86_64:
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  #make THREADS=9 test

  # will most likely cause lots of failures
  # enable this when upstream has 0 test failures from a simple 'make test'
  # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
  #make fulltest
}

package() {
  cd ghc-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/licenses/ghc
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/ghc

  sed -r 's|("C compiler command", ")[^"]+|\1/usr/bin/gcc|' \
    -i "${pkgdir}/usr/lib/ghc-${pkgver}/settings"

  # provide non vanilla bash completion since ghc currently doesn't ship with any:
  # https://ghc.haskell.org/trac/ghc/ticket/9006
  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 ${srcdir}/ghc \
    ${pkgdir}/usr/share/bash-completion/completions
}
