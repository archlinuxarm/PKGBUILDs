# $Id$
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

# use the following command to print out packages that need to get rebuilt
# grep -r ghc /var/abs/ | awk -F '/' '{ print $5; }' | sort -u

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org
#        David FlemstrÃ¶m <david.flemstrom@gmail.com>
#  - Patch for vfp flags (add-vfp-flags.patch)
#  - Patch to fix dynamic linking on ARM
#  - build.mk: BuildFlavor change from perf to perf-llvm
#  - Add LLVM as a dependency
#  - Correct gcc path after bulding with distcc
#  - Use -j2 for building (RAM constraints)

noautobuild=1

pkgname=ghc
pkgver=7.10.1
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp' 'gcc' 'libffi' 'llvm')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl')
# will hopefully be able to depend on python 3 in a future version
# https://ghc.haskell.org/trac/ghc/ticket/9184
# title: Allow the use of Python 3 when building GHC
checkdepends=('python2')
install='ghc.install'
options=('staticlibs')
provides=('haskell-array=0.5.1.0'
          'haskell-base=4.8.0.0'
          'haskell-binary=0.7.3.0'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-bytestring=0.10.6.0'
          'haskell-containers=0.5.6.2'
          'haskell-deepseq=1.4.1.1'
          'haskell-directory=1.2.2.0'
          'haskell-filepath=1.4.0.0'
          'haskell-ghc-prim=0.4.0.0'
          'haskell-haskeline=0.7.2.1'
          'haskell-hoopl=3.10.0.2'
          'haskell-hpc=0.6.0.2'
          'haskell-integer-gmp=1.0.0.0'
          'haskell-pretty=1.1.2.0'
          'haskell-process=1.2.3.0'
          'haskell-template-haskell=2.10.0.0'
          'haskell-terminfo=0.4.0.1'
          'haskell-time=1.5.0.1'
          'haskell-transformers=0.4.2.0'
          'haskell-unix=2.7.1.0'
          'haskell-xhtml=3000.2.1'
          'haskell-cabal=1.22.2.0'
          )
replaces=('haskell-array'
          'haskell-base'
          'haskell-binary'
          'haskell-bin-package-db'
          'haskell-bytestring'
          'haskell-containers'
          'haskell-deepseq'
          'haskell-directory'
          'haskell-filepath'
          'haskell-ghc-prim'
          'haskell-haskeline'
          'haskell-hoopl'
          'haskell-hpc'
          'haskell-integer-gmp'
          'haskell-pretty'
          'haskell-process'
          'haskell-template-haskell'
          'haskell-terminfo'
          'haskell-time'
          'haskell-transformers'
          'haskell-unix'
          'haskell-xhtml'
          'haskell-cabal'
         )
source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.xz"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.xz"
        'build.mk'
        'ghc'
        'add-vfp-flags.patch'
        'ghc-7.8.1-mk-config.mk.in-ARM-dynlinking.patch')
md5sums=('f93ad0cdc9df4b0f2d4421debdecb54f'
         '41db579e247655236c2e35e66e645e71'
         'd9c7c61db2b7c0c7eef547092716addb'
         'c1d46d86752c50dd85e7143b029fa07f'
         '1eb0ed10fa9676c2e23a587c40166e0e'
         '041b801ef32c45c7c9fd8d7730e79cc8')

build() {
  cd ghc-${pkgver}

  cp ${srcdir}/build.mk mk/build.mk

  patch -p1 -i ../add-vfp-flags.patch
  patch -p1 -Ri ../ghc-7.8.1-mk-config.mk.in-ARM-dynlinking.patch
  MAKEFLAGS="-j2"

  # have to 're-boot' because the patch changes configure stuff
  ./boot

  ./configure \
    --prefix=/usr \
    --with-system-libffi \
    --with-ffi-includes=/usr/lib/libffi-3.2.1/include

  make
}

check() {
  # keep this otherwise a lot of tests will show as unexpected failures... need to investigate why using -jn for n > 1 does this...
  unset MAKEFLAGS

  cd ghc-${pkgver}

  # python2 rename
  sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

  # tests check out for 7.10.1 on x86_64 but are broken on i686
  #make test

  # enabling threads to speed up
  # causes more failures than make test, need to investigate
  # upstream known failures on both i686 and x86_64:
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  #make THREADS=9 test

  # will most likely cause lots of failures
  # enable this when upstream has 0 test failures from a simple 'make test'
  # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
  #make fulltest
}

package() {
  cd ghc-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/licenses/ghc
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/ghc

  sed -r 's|("C compiler command", ")[^"]+|\1/usr/bin/gcc|' \
    -i "${pkgdir}/usr/lib/ghc-${pkgver}/settings"

  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 ./utils/completion/ghc.bash \
    ${pkgdir}/usr/share/bash-completion/completions/ghc
}
