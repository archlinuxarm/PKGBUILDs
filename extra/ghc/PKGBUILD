# $Id: PKGBUILD 168343 2012-10-09 04:08:52Z tdziedzic $
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

# ALARM: David Flemstr√∂m <david.flemstrom@gmail.com>
#  - Add patch for vfp flags (add-vfp-flags.patch)
#  - Change build.mk to use the LLVM backend with the right
#    optimization passes
#  - Add LLVM as a dependency
#  - Disable distcc explicitly because the C compiler path is embedded
#    during build

pkgname=ghc
pkgver=7.6.1
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64' 'armv6h')
url='http://www.haskell.org/ghc/'
license=('custom')
# libffi might be needed in the future: http://hackage.haskell.org/trac/ghc/ticket/4496
depends=('llvm' 'perl' 'gmp>=5.0' 'gcc') # 'libffi')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl')
buildarch=16
checkdepends=('python2')
install='ghc.install'
# keep this otherwise a lot of tests will show as unexpected failures... need to investigate why using -jn for n > 1 does this...
#options=('!makeflags')
options=('!distcc')
# missing rtl?
provides=('haskell-array=0.4.0.1'
          'haskell-base=4.6.0.0'
          'haskell-binary=0.5.1.1'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-bytestring=0.10.0.0'
          'haskell-containers=0.5.0.0'
          'haskell-deepseq=1.3.0.1'
          'haskell-directory=1.2.0.0'
          'haskell-filepath=1.3.0.1'
          'haskell-ghc-prim=0.3.0.0'
          'haskell-haskell2010=1.1.1.0'
          'haskell-haskell98=2.0.0.2'
          'haskell-hoopl=3.9.0.0'
          'haskell-hpc=0.6.0.0'
          'haskell-integer-gmp=0.5.0.0'
          'haskell-old-locale=1.0.0.5'
          'haskell-old-time=1.1.0.1'
          'haskell-pretty=1.1.1.0'
          'haskell-process=1.1.0.2'
          'haskell-template-haskell=2.8.0.0'
          'haskell-time=1.4.0.1'
          'haskell-unix=2.6.0.0'
          'haskell-cabal=1.16.0')
replaces=('haskell-array<0.4.0.1'
          'haskell-base<4.6.0.0'
          'haskell-binary<0.5.1.1'
          'haskell-bin-package-db<0.0.0.0'
          'haskell-bytestring<0.10.0.0'
          'haskell-containers<0.5.0.0'
          'haskell-deepseq<1.3.0.1'
          'haskell-directory<1.2.0.0'
          'haskell-filepath<1.3.0.1'
          'haskell-ghc-prim<0.3.0.0'
          'haskell-haskell2010<1.1.1.0'
          'haskell-haskell98<2.0.0.2'
          'haskell-hoopl<3.9.0.0'
          'haskell-hpc<0.6.0.0'
          'haskell-integer-gmp<0.5.0.0'
          'haskell-old-locale<1.0.0.5'
          'haskell-old-time<1.1.0.1'
          'haskell-pretty<1.1.1.0'
          'haskell-process<1.1.0.2'
          'haskell-template-haskell<2.8.0.0'
          'haskell-time<1.4.0.1'
          'haskell-unix<2.6.0.0'
          'haskell-cabal<1.16.0')
source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.bz2"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.bz2"
        'build.mk'
        'add-vfp-flags.patch')

md5sums=('e9d892d2970550ec5e70130b3621f6b6'
         '0d93c458e3b445a7e524ef6ae459ce11'
         'cb89e280f907c60791d08613f05ec049'
         'bb3a7c6982ce8aeec4a1156f46d69d3f')

build() {
  cd ghc-${pkgver}

  cp ${srcdir}/build.mk mk/build.mk

  patch -p1 < ${srcdir}/add-vfp-flags.patch

  # have to 're-boot' because the patch changes configure stuff
  ./boot
  ./configure \
    --prefix=/usr

  make
}

check() {
  # keep this otherwise a lot of tests will show as unexpected failures... need to investigate why using -jn for n > 1 does this...
  unset MAKEFLAGS

  cd ghc-${pkgver}

  # python2 rename
  sed -e 's/PYTHON = python/&2/' -i testsuite/mk/boilerplate.mk
  sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

  # upstream known failures on both i686 and x86_64:
  #    ghci.debugger/scripts     break011 [bad stderr] (ghci)
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  make test

  # enabling threads to speed up
  # causes more failures than make test, need to investigate
  # upstream known failures on both i686 and x86_64:
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  #make THREADS=9 test

  # will most likely cause lots of failures
  # enable this when upstream has 0 test failures from a simple 'make test'
  # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
  #make fulltest
}

package() {
  cd ghc-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/licenses/ghc
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/ghc
}
