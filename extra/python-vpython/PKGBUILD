# $Id: PKGBUILD 47727 2009-07-25 17:23:06Z giovanni $
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Comete <la_comete@tiscali.fr>

pkgname=python-vpython
pkgver=5.11
pkgrel=1
pkgdesc="A Python module that offers real-time 3D output"
arch=('i686' 'x86_64')
url="http://www.vpython.org"
depends=('gtkglextmm' 'libglademm' 'python-numeric' 'boost>=1.39.0' 'tk')
makedepends=('pkgconfig')
license=('custom')
options=(!libtool)
source=(http://www.vpython.org/contents/download/visual-5.11.tar.bz2 idlepath.patch Makefile.in.patch)
md5sums=('b7c0acc12e395bfd050dada008891dc1'
         '565dfeb03de806ba2382d05c0ded27d6'
         '8327c39c71c34d208afe306791fca7d2')

build() {
  cd ${srcdir}/visual-${pkgver}_release
  patch -Np1 -i ${srcdir}/idlepath.patch || return 1
  patch -Np1 -i ${srcdir}/Makefile.in.patch || return 1
  pythondir=`python -c "from distutils import sysconfig; print sysconfig.get_python_lib()"`
  unset PYTHONPATH # ignore developers PYTHONPATH
  ./configure --prefix=/usr \
              --with-example-dir=$pythondir/visual/demos
  make || return 1
  make DESTDIR=${pkgdir} install
  install -D -m644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

