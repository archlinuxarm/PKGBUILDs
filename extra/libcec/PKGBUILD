# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>
# vim: ft=sh:

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Exynos support on v7
#  - Amlogic support on v8

pkgname=libcec
pkgver=6.0.2
pkgrel=6
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter"
arch=('x86_64')
url="http://libcec.pulse-eight.com/"
license=('GPL-2.0-or-later')
makedepends=('cmake' 'swig' 'python')
depends=('udev' 'p8-platform' 'libxrandr')
optdepends=('python: use cec in python applications')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/Pulse-Eight/$pkgname/archive/$pkgname-$pkgver.tar.gz"
  "https://src.fedoraproject.org/rpms/libcec/raw/ef9e6d4972e0d8a9beb934a2ddf8e707bfe34507/f/libcec-python13.patch"
)
sha256sums=('090696d7a4fb772d7acebbb06f91ab92e025531c7c91824046b9e4e71ecb3377'
            'a0a3add5d7d084d3ccf65d70c3978d323cb63fb3ff1a57a5a4268d1224551588')

prepare() {
    cd "$pkgname-$pkgname-$pkgver"
    patch -Np1 -i ../libcec-python13.patch
}

build() {
    cd "$pkgname-$pkgname-$pkgver"

    if [[ $CARCH == 'armv7h' ]]; then
      CONFIG='-DHAVE_EXYNOS_API=1 -DHAVE_IMX_API=1'
    fi

    if [[ $CARCH == 'aarch64' ]]; then
      CONFIG='-DHAVE_AOCEC_API=1'
    fi

    mkdir build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DHAVE_LINUX_API=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBDIR_NOARCH=/usr/lib \
        ${CONFIG}
    make
}

package() {
    cd "$pkgname-$pkgname-$pkgver/build"
    make DESTDIR="$pkgdir" install
}
