# $Id: PKGBUILD 8619 2008-08-14 04:02:03Z kevin $
# Committer: jlvsimoes <jlvsimoes@oninet.pt>
# Maintainer: kevin <kevin@archlinux.org>

pkgname=abuse
pkgver=0.7.1
pkgrel=1
pkgdesc="A side-scroller action game that pits you against ruthless alien killers"
url="http://abuse.zoy.org/"
depends=('gcc-libs' 'libgl' 'sdl')
# abuse-data below is the original commercial levels and sound effects.
# abuse-frabs contains community created levels.
# abuse-lib is the original demo levels.
# abuse-sfx is the original sound effects, included in abuse-data.
source=(http://abuse.zoy.org/raw/Downloads/abuse-${pkgver}.tar.gz
http://abuse.zoy.org/raw/Downloads/abuse-data-2.00.tar.gz
http://abuse.zoy.org/raw/Downloads/abuse-frabs-2.11.tar.gz
abuse.patch)
noextract=(abuse-data-2.00.tar.gz)
license=('GPL' 'custom')
arch=('i686' 'x86_64')
install=abuse.install
md5sums=('439b607f291560a8f9698a2f09cffa63' '2b857668849b2dc7cd29cdd84a33c19e'
         '0686f951289fe514846903bab36a2079' '86b5256f066e994db9d57dbe1ec2a58e')

build() {
  cd ${srcdir}/abuse-${pkgver}
  patch -p1 -i ${srcdir}/abuse.patch
  ./configure --prefix=/usr --datadir=/usr/share/abuse/orig --with-x
  make || return 1
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/share/abuse/{frabs,orig}
  mkdir -p ${pkgdir}/usr/share/licenses/abuse

  # original code license.  sdlport is gpl.
  sed -n '1,/^$/ p' src/view.cpp > \
      ${pkgdir}/usr/share/licenses/abuse/orig.code.license

  # original game data.
  cd ${pkgdir}/usr/share/abuse/orig
  tar -xzf  ${srcdir}/abuse-data-2.00.tar.gz
  mv README.datafiles ${pkgdir}/usr/share/licenses/abuse/orig.data.license

  # frabs data, link to sound in orignal game data.
  cd ${pkgdir}/usr/share/abuse/frabs
  mv ${srcdir}/abuse-frabs-2.11/* .
  rm -f abuse.exe setup.exe art/dos4gw.exe art/spaint.exe
  ln -s ../orig/sfx sfx
  cp docs/index.html ${pkgdir}/usr/share/licenses/abuse/frabs.license

  chown -R root:root ${pkgdir}/usr/share
}
