# Maintainer: Jami Kettunen <jami.kettunen@protonmail.com>
# Contributor: Raihan Ahamed <raihan1999ahamed@gmail.com>

buildarch=8

_pkgname="pd-mapper"
pkgname="$_pkgname-git"
pkgdesc="Qualcomm Protection Domain mapper"
pkgver=r23.0a43c8b
pkgrel=1
arch=("aarch64")
url="https://github.com/linux-msm/$_pkgname"
license=("BSD-3-Clause")
groups=("qcom-icnss-wlan")
depends=("qrtr")
makedepends=("git" "make" "gcc")
provides=("$_pkgname")
source=("git+https://github.com/linux-msm/$_pkgname.git")
md5sums=("SKIP")

pkgver() {
	cd "$_pkgname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$_pkgname"

	make prefix=/usr
}

package() {
	cd "$_pkgname"

	make prefix=/usr DESTDIR="$pkgdir/" install
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
