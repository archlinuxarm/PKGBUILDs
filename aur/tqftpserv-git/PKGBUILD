# Maintainer: Jami Kettunen <jami.kettunen@protonmail.com>
# Contributor: Raihan Ahamed <raihan1999ahamed@gmail.com>

buildarch=8

_pkgname="tqftpserv"
pkgname="$_pkgname-git"
pkgdesc="Trivial File Transfer Protocol server over AF_QIPCRTR"
pkgver=r55.408ca1e
pkgrel=1
arch=("aarch64")
url="https://github.com/linux-msm/$_pkgname"
license=("BSD-3-Clause")
groups=("qcom-icnss-wlan")
depends=("qrtr")
makedepends=("git" "meson" "gcc")
provides=("$_pkgname")
source=("git+https://github.com/linux-msm/$_pkgname.git")
md5sums=("SKIP")

pkgver() {
	cd "$_pkgname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	arch-meson $_pkgname build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	install -Dm644 "$_pkgname"/LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}

