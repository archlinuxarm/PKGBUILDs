# Maintainer: winlu
# Contributor: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: skunark <jim.casteel@torpedia.org>
# Contributor: graysky <graysky@archlinux.us>

pkgname=shairplay-git
_gitname=shairplay
pkgver=122.64d59e3
pkgrel=1
pkgdesc='Apple airplay and raop protocol server'
arch=('i686' 'x86_64')
url='https://github.com/juhovh/shairplay'
license=('GPLv3')
depends=('avahi' 'libao')
makedepends=('git')
source=("$_gitname::git://github.com/juhovh/shairplay.git"
        "shairplay.service")
sha256sums=('SKIP'
	    'ac18abf1fd5c6735bbeee5918b528b9df83ec85141b46e3bec44b5abe55dfe52')
options=(!libtool)
provides=('libshairport')
conflicts=('libshairport')

pkgver() {
	cd "$srcdir/$_gitname"
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd $_gitname
	./autogen.sh
	./configure --prefix=/usr/
	# installing airport.key to /etc/shairplay/
	sed 's/airport.key/\/etc\/shairplay\/airport.key/' -i "$srcdir/$_gitname"/src/shairplay.c
	make
}

package() {
	install -Dm755 "${srcdir}"/shairplay.service "${pkgdir}"/usr/lib/systemd/system/shairplay.service 
	cd $_gitname
	make DESTDIR="$pkgdir" install
	install -Dm644 airport.key "${pkgdir}"/etc/shairplay/airport.key
	install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
	install -Dm644 README.md "${pkgdir}"/usr/share/doc/${pkgname}/README.md
}
