# Maintainer: Eugene <TheDZhon@gmail.com> Mamin

# Original AUR Maintainer: Det (https://aur.archlinux.org/packages/jdk-devel/)
# Based on both jre (https://aur.archlinux.org/packages.php?ID=51908) and jdk (https://aur.archlinux.org/packages.php?ID=51906)

pkgname=jdk-devel
_major=8
_build=b132
_date=03_mar_2014
pkgver=$_major$_build
pkgrel=1
pkgdesc="Java $_major Development Kit Snapshot"
arch=('armv6h' 'armv7h')
url=http://jdk$_major.java.net/
license=('custom')
depends=('libx11' 'libxrender' 'libxslt' 'libxtst' 'xdg-utils')
optdepends=('alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-environment=$_major" "java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/7}")
backup=('etc/derby.conf'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
install=$pkgname.install
source=("http://download.java.net/jdk$_major/archive/$_build/binaries/jdk-$_major-fcs-$_build-linux-arm-vfp-hflt-$_date.tar.gz"
        'derby.conf'
        'derby.service'
        'javaws-launcher'
        'jdk.csh'
        'jdk.sh'
        'OTN-Early-Adopter-License-Terms.txt')
md5sums=(`curl -Ls ${source/.t*}.md5 | cut -d " " -f4` # jdk archive
         '4bdff6982c66d24a879c424aaac3d04d'  # derby.conf
         '77f3b7ddf55c112f97a665a825bf764f'  # derby.service
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         'e81bb7853b071df6adca0b90f0c2ac2d'  # jdk.csh
         'a06d46b277e2926d73d2ee11c9950973'  # jdk.sh
         '55acc7d9282c3826afeac6157582384c') # OTN-Early-Adopter-License-Terms.txt

package() {
  msg2 "Creating required dirs"
  cd jdk1.$_major.0
  mkdir -p "$pkgdir"/{opt/java/,usr/{lib/{mozilla/plugins,systemd/system},share/licenses/jdk},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing the redundancies"
  rm -r db/bin/*.bat jre/{COPYRIGHT,LICENSE,*.txt} man/ja # lib/{desktop,visualvm/platform/docs}

  msg2 "Moving stuff in place"
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jdk/
  mv * "$pkgdir"/opt/java/

  msg2 "Symlinking the plugin"
  ln -s /opt/java/jre/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing the additions"
  cd "$srcdir"
  install -m755 jdk.{,c}sh "$pkgdir"/etc/profile.d/
  install -m644 derby.conf "$pkgdir"/etc/
  install -m644 derby.service "$pkgdir"/usr/lib/systemd/system/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 OTN-Early-Adopter-License-Terms.txt "$pkgdir"/usr/share/licenses/jdk/
}
