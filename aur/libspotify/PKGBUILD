# Maintainer: Benjamin Hedrich <kiwisauce@pagenotfound.de>
pkgname=libspotify
pkgver=12.1.51
pkgrel=1
pkgdesc="C API package allowing third-party developers to write applications that utilize the Spotify music streaming service"
arch=(armv7h)
url="https://developer.spotify.com/technologies/libspotify/"
license=('custom')
depends=('glibc')

if [ "$CARCH" == "armv7h" ]; then
    SPOTIFY_ARCH="armv7"
    md5sums=('eb7e98849b3bb6d364fa74034602afbf')
    sha256sums=('ad27b6c5aee5382b66b39bfea3b1752076b7abcc445979ce25c1ec9d7ff3aeda')
    source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-Linux-${SPOTIFY_ARCH}-release.tar.gz)
fi

# TODO: Add more code for other cpu archs.

build() {
    cd "$srcdir/$pkgname-$pkgver-Linux-${SPOTIFY_ARCH}-release"

    # Don't do stupid things from a Makefile
    msg2 "Patching Makefile..."
    sed -i 's/ldconfig//' Makefile
}

package() {
    cd "$srcdir/$pkgname-$pkgver-Linux-${SPOTIFY_ARCH}-release"

    make prefix="$pkgdir/usr" install

    # Install documentation
    cp -R share "$pkgdir"/usr/share
    mkdir -p "$pkgdir"/usr/share/man
    mv "$pkgdir"/usr/share/man3 "$pkgdir"/usr/share/man/man3

    # Correct pkgconfig file
    sed -e s:PKG_PREFIX:/usr:g \
          < lib/pkgconfig/libspotify.pc \
          > "$pkgdir"/usr/lib/pkgconfig/libspotify.pc

    # Custom license
    install -Dm644 LICENSE licenses.xhtml "$pkgdir"/usr/share/doc/libspotify
    mkdir -p "$pkgdir"/usr/share/licenses/libspotify
    ln -s ../../doc/libspotify/LICENSE "$pkgdir"/usr/share/licenses/libspotify/LICENSE
}
