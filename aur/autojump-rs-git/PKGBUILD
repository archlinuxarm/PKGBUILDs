# Maintainer: graysky <therealgraysky AT proton DOT me>

pkgname=autojump-rs-git
_pkgname=autojump-rs
pkgver=0.5.1.r14.g65abf11
pkgrel=2
pkgdesc="A faster way to navigate your filesystem from the command line"
arch=(any)
url="https://github.com/xen0n/autojump-rs"
license=(GPL-3.0-only)
makedepends=(git python cargo)
conflicts=(autojump shonenjump)
provides=(autojump)
source=('git+https://github.com/xen0n/autojump-rs.git'
  autojump.1)
sha256sums=('SKIP'
            '836284d4b5ef0e75e70537138bc2449c07d339a9fea106dd57adde3278510cae')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^release\-v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname
  cargo build --release --locked --all-features
  cargo fix --bin "autojump"
}

check () {
  cd $_pkgname
  cargo test --release --locked --all-features
}

package() {
  install -Dm755 "$srcdir"/$_pkgname/target/release/${_pkgname/-rs} "$pkgdir"/usr/bin/${_pkgname/-rs}

  install -d "$pkgdir"/etc/fish/conf.d
  install -d "$pkgdir"/etc/profile.d
  install -d "$pkgdir"/usr/share/autojump
  install -d "$pkgdir"/usr/share/zsh/site-functions
  install -d "$pkgdir"/usr/share/man/man1

  install -p -m644 $_pkgname/integrations/${_pkgname/-rs}.bash "$pkgdir"/usr/share/${_pkgname/-rs}
  install -p -m644 $_pkgname/integrations/${_pkgname/-rs}.fish "$pkgdir"/etc/fish/conf.d
  sed -e '27,31d' -e 's|local/||' <$_pkgname/integrations/${_pkgname/-rs}.sh > "$pkgdir"/etc/profile.d/${_pkgname/-rs}.sh
  install -p -m644 $_pkgname/integrations/${_pkgname/-rs}.zsh "$pkgdir"/usr/share/${_pkgname/-rs}
  install -p -m644 $_pkgname/integrations/_j "$pkgdir"/usr/share/zsh/site-functions
  install -p -m644 "$srcdir"/${_pkgname/-rs}.1 "$pkgdir"/usr/share/man/man1
}
# vim:set ts=4 sw=4 et:
