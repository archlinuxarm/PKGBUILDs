# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=reaver-wps-svn
pkgver=119
pkgrel=1
pkgdesc="Brute force attack against Wifi Protected Setup (WPS) registrar PINs in order to recover WPA/WPA2 passphrases"
arch=('i686' 'x86_64' 'mips64el' 'armv6h')
url="http://code.google.com/p/reaver-wps/"
license=('GPL')
depends=('libpcap' 'sqlite')
makedepends=('subversion')
provides=('reaver-wps')
conflicts=('reaver-wps')
source=('svn+http://reaver-wps.googlecode.com/svn/trunk/')
sha512sums=('SKIP')

pkgver() {
  cd "${SRCDEST}/trunk/"
  svnversion
}

build() {
  cd "${srcdir}/trunk/src/"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/trunk/"
  pushd src
  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 reaver "${pkgdir}/usr/bin/"
  install -m755 wash "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/etc/reaver/"
  install -m644 reaver.db "${pkgdir}/etc/reaver/"
  popd
  pushd docs
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  install -m644 reaver.1.gz "${pkgdir}/usr/share/man/man1/"
  popd
}

# vim:set ts=2 sw=2 et:
