# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=despotify-svn
pkgver=521
pkgrel=2.1
pkgdesc='Console client for Spotify (development version)'
arch=('x86_64' 'i686')
url='http://despotify.sourceforge.net/'
license=('BSD')
depends=('libvorbis' 'openssl' 'libao' 'mpg123')
makedepends=('subversion' 'ncurses' 'libtool' 'expat')
conflicts=('despotify-pulse-svn' 'despotify-ao-svn')
install='despotify.install'
source=('despotify::svn://svn.code.sf.net/p/despotify/code/')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/despotify"

  svnversion | tr -d [A-z]
}

prepare() {
  cd "$srcdir/despotify/src"

  # Use libao instead of gstreamer
  sed -i 's/gstreamer/libao/' Makefile.local.mk.dist
  cp Makefile.local.mk.dist Makefile.local.mk

  # Anti-segfault fix, add format.matrix = NULL; to libao.c
  sed -i 's/_LITTLE;/_LITTLE; format.matrix = NULL;/' \
    clients/despotify/libao.c
}


build() {
  cd "$srcdir/despotify/src"

  make
}

package() {
  cd "$srcdir/despotify/src"

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/include"
  make INSTALL_PREFIX="$pkgdir/usr" DESTDIR="$pkgdir/" install
  chmod -x "$pkgdir/usr/include/despotify.h"
}

# vim:set ts=2 sw=2 et:
