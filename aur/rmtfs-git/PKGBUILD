# Maintainer: Jami Kettunen <jami.kettunen@protonmail.com>
# Contributor: Raihan Ahamed <raihan1999ahamed@gmail.com>

buildarch=8

_pkgname="rmtfs"
pkgname="$_pkgname-git"
pkgdesc="Qualcomm Remote Filesystem Service Implementation"
pkgver=r78.14cb1ee
pkgrel=1
arch=("aarch64")
url="https://github.com/linux-msm/$_pkgname"
license=("BSD-3-Clause")
groups=("qcom-icnss-wlan")
depends=("qrtr" "systemd")
makedepends=("git" "make" "gcc")
provides=("$_pkgname")
source=("git+https://github.com/linux-msm/$_pkgname" "udev.rules")
md5sums=("SKIP" "1e827788992863c3e8562e288f65ee05")

pkgver() {
	cd "$_pkgname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$_pkgname"

	make prefix=/usr
}

package() {
	cd "$_pkgname"

	make prefix=/usr DESTDIR="$pkgdir/" install
	install -Dm644 "$srcdir"/udev.rules "$pkgdir/usr/lib/udev/rules.d/65-$_pkgname.rules"
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
