# Maintainer: Amr Hassan <amr.hassan@gmail.com>
# Maintainer: Nathan Owe <ndowens.aur at gmail dot com>
# Maintainer: G. Richard Bellamy <rbellamy@pteradigm.com>

pkgname=flexget
_pkgname=FlexGet
pkgver=1.2.199
pkgrel=1

pkgdesc="Automate downloading or processing content (torrents, podcasts, etc.) from different sources like RSS-feeds, html-pages, various sites and more."

arch=('any')
url="http://flexget.com/"
license=('MIT')

depends=('python2'
         # documented in FlexGet.egg-info/requires.txt
         'python2-feedparser>=5.1.3'
         'python2-sqlalchemy>=0.7.5'
         'python2-sqlalchemy<0.9.99'
         'python2-yaml'
         'python2-beautifulsoup4>=4.1'
         'python2-beautifulsoup4<4.4'
         'python2-guessit>=0.9.3'
         'python2-html5lib>=0.11'
         'python2-pyrss2gen' #AUR#
         'python2-progressbar'
         'python2-rpyc' #AUR#
         'python2-jinja'
         'python2-path>=5.1' #AUR#
         'python2-requests>=1.0'
         'python2-requests<2.99'
         'python2-dateutil1' #AUR#
         'python2-jsonschema>=2.0' #AUR#
         'python2-tvrage' #AUR#
         'python2-tmdb3' #AUR#
         'python2-pynzb' #AUR#
         # undocumented in FlexGet.egg-info/requires.txt
         )
optdepends=('python2-guppy: for memusage plugin' #AUR#
            'python2-transmissionrpc: Transmission support' #AUR#
            'python2-cherrypy: web interface'
            'python2-flask: web interface'
            )
makedepends=('python2-distribute'
             'python2-paver'
             'python2-setuptools'
             )

source=("http://download.flexget.com/archive/${_pkgname}-${pkgver}.tar.gz"
        'flexget.service'
        )

changelog=ChangeLog

sha256sums=('5033d98ff447e9a51ae9daf989230f5900b4a037aa9f5af5cef1135192ef94ef'
            'e2c3a958ed0c286337cd37fba1d6cbdf4306c57fcddf2b9cc43615ce80ae83aa')

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Python setup
  python2 setup.py install --root="${pkgdir}"/ --prefix=/usr --optimize=1 || return 1

  # License
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

  # Make sure the perms allow reading by all
  chmod ugo+r ${pkgdir}/usr/lib/python2.7/site-packages/FlexGet-1.2-py2.7.egg-info/*

  # install systemd user unit
  install -Dm644 ../flexget.service "${pkgdir}"/usr/lib/systemd/user/flexget.service
}

# vim:set ts=2 sw=2 et:
