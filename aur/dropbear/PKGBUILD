# Maintainer Kai Uwe Jesussek <kajot@gmx.net>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <`echo 'moc tod liamg ta nosaj tod ecreip' | rev`>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>

pkgname=dropbear
pkgver=0.53.1
pkgrel=1
pkgdesc="Lightweight replacement for openssh"
arch=('arm' 'i686' 'x86_64')
url="http://matt.ucc.asn.au/dropbear/dropbear.html"
license=('custom')
depends=('zlib')
provides=('openssh')
conflicts=('openssh')
source=(http://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2 \
        $pkgname-conf.d \
        $pkgname-rc.d)
sha256sums=('e24d3cbecd3bc850b2b336b8eb50c845a285ceef8e22544938a582e163d36393'
            'd5a3b08e4e79b841ba1cd592d79cf91cd9cd7948e683d35de550cb08bd3f285f'
	    '29ab5438d15bfa727620fc04ec52ff55d676577b903c9261ce00bb291ae171a8')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  sed -i 's:usr/libexec/sftp:usr/lib/ssh/sftp:' options.h

  ./configure --prefix=/usr --enable-pam
  LIBS="-lcrypt" make PROGRAMS="dropbear dropbearkey dropbearconvert dbclient scp" MULTI=0 STATIC=0 SCPPROGRESS=1
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=${pkgdir}/usr install

#make it openssh compatible
  ln -s /usr/bin/dbclient ${pkgdir}/usr/bin/ssh  
  install -D -m755 scp ${pkgdir}/usr/bin/scp

#man pages
  install -D -m644 dbclient.1 ${pkgdir}/usr/share/man/man1/dbclient.1
  install -D -m644 $pkgname.8 ${pkgdir}/usr/share/man/man8/$pkgname.8
  install -D -m644 dropbearkey.8 ${pkgdir}/usr/share/man/man8/dropbearkey.8

#configuration files
  install -d ${pkgdir}/etc/$pkgname
  install -D -m644 ${srcdir}/$pkgname-conf.d ${pkgdir}/etc/conf.d/$pkgname
  install -D -m755 ${srcdir}/$pkgname-rc.d ${pkgdir}/etc/rc.d/$pkgname

#license file
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
