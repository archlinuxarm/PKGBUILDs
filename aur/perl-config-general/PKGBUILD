# Maintainer: Jason St. John <jstjohn .. purdue . edu>

_perlmod=Config-General
_modnamespace=Config
pkgname=perl-config-general
pkgver=2.52
pkgrel=1
pkgdesc="Config::General - Generic Config Module"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('9a4d6fd823c58c5aba78528997b17b7db8f303dd58d1763f797cf917ebd5f3257b17afc85dcec6f5607b7fdb874dfa60b540e98366a6bb3cf6000175fb32a7c5')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
