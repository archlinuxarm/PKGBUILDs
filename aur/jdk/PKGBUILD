# Maintainer: Det
# Contributor: omor1
# Original based on jre: https://aur.archlinux.org/packages/jre/
# Based on jdk: https://aur.archlinux.org/packages/jdk/

pkgname=jdk
_major=7
_minor=51
_build=b13
_pkgver=${_major}u${_minor}
pkgver=${_major}.${_minor}
pkgrel=1
pkgdesc="Java Development Kit"
arch=('armv6h' 'armv7h')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libx11' 'libxrender' 'libxslt' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-environment=$_major" "java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}")
backup=('etc/derby.conf'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
install=jdk.install
# Workaround for the AUR Web interface Source parser
_arch=arm-vfp-hflt
_arch2=arm
source=("http://download.oracle.com/otn-pub/java/jdk/$_pkgver-$_build/jdk-$_pkgver-linux-$_arch.tar.gz"
        'derby.conf'
        'derby.service'
        'java-mission-control.desktop'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'javaws-launcher'
        'jdk.csh'
        'jdk.sh')
md5sums=('80e14facc0aa784f44d8f142025dd020'  # jdk-$_pkgver-linx-arm-vfp-hftl.tar.gz
         '4bdff6982c66d24a879c424aaac3d04d'  # derby.conf
         '77f3b7ddf55c112f97a665a825bf764f'  # derby.service
         '536ea84bba5eb6bfe8f601b41af9769c'  # java-mission-control.desktop
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         'e81bb7853b071df6adca0b90f0c2ac2d'  # jdk.csh
         'a06d46b277e2926d73d2ee11c9950973') # jdk.sh
# # Alternative mirrors, if your local one is throttled:
# source[0]="http://uni-smr.ac.ru/archive/dev/java/SDKs/sun/j2se/$_major/jdk-$_pkgver-linux-$_arch.tar.gz"
# source[0]="http://ftp.wsisiz.edu.pl/pub/pc/pozyteczne%20oprogramowanie/java/jdk-$_pkgver-linux-$_arch.tar.gz"

DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')

package() {
  msg2 "Creating required dirs"
  cd jdk1.$_major.0_$_minor
  mkdir -p "$pkgdir"/{opt/java/,usr/{lib/{mozilla/plugins,systemd/system},share/{licenses/jdk,applications}},etc/{.java/.systemPrefs,profile.d}}

#  msg2 "Removing the redundancies"
  rm -r man/ja

  msg2 "Moving stuff in place"
  mv man "$pkgdir"/usr/share/
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jdk/
  mv * "$pkgdir"/opt/java/

  msg2 "Symlinking the plugin"
  ln -s /opt/java/jre/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing the scripts, .conf, .service and .desktops of our own"
  cd "$srcdir"
  install -m755 jdk.{,c}sh "$pkgdir"/etc/profile.d/
  install -m644 derby.conf "$pkgdir"/etc/
  install -m644 derby.service "$pkgdir"/usr/lib/systemd/system/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 *.desktop "$pkgdir"/usr/share/applications/
}
