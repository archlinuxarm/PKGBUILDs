# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Rob Sletten <rsletten@gmail.com>
# Contributor: Tom Moore <t.moore01@gmail.com>
# Contributor: monty <linksoft@gmx.de>
# Contributor: Jon Wiersma <archaur@jonw.org>
# Contributor: Arthur <arthur.darcet@m4x.org>
# Contributor: Praekon <praekon@googlemail.com>

#ALARM: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>
#	- Using Synology arm package as the base (added prepare() section)
#	- fix permissions via .install file

buildarch=4
pkgname=plex-media-server
pkgver=1.5.5.3634
_pkgsum=995f1dead
pkgrel=1
pkgdesc='Plex Media Server'
arch=('armv7h')
url='https://plex.tv/'
license=('custom')
depends=('systemd')
replaces=('plexmediaserver')
conflicts=('plexmediaserver' 'plexmediaserver-plexpass')
backup=('etc/conf.d/plexmediaserver')
install='plex-media-server.install'
source=("https://downloads.plex.tv/plex-media-server/${pkgver}-${_pkgsum}/PlexMediaServer-${pkgver}-${_pkgsum}-arm7.spk"
        'plexmediaserver.conf.d'
        'plexmediaserver.service'
        'terms.txt')

sha256sums=('e51029f586650d42f668421f432c5cecf3321bed11b0b78fcc7c9604b6cc3e37'
            '7ab1ee8da9012d257b7f473fb79d76b201ca592cbe3722f977a43b58bfad180e'
            '9da45cc3951ae03086ec663e6273c2de0183495fd15dc34ddd9aa100346d4d3a'
            '7bb97271eb2dc5d1dcb95f9763f505970d234df17f1b8d79b467b9020257915a')
 
prepare() {
   mkdir ${srcdir}/plexmediaserver
   cd ${srcdir}/plexmediaserver
   tar xzf ../package.tgz
}

package() {
  install -dm 755 "${pkgdir}"/{opt,etc/conf.d,usr/lib/systemd/system}
  cp -dr --no-preserve='ownership' plexmediaserver "${pkgdir}"/opt/
  install -m 644 plexmediaserver.service "${pkgdir}"/usr/lib/systemd/system/
  install -m 644 plexmediaserver.conf.d "${pkgdir}"/etc/conf.d/plexmediaserver

  install -dm 755 "${pkgdir}"/var/lib/plex
  chown 421:421 -R "${pkgdir}"/var/lib/plex

  install -dm 755 "${pkgdir}"/usr/share/licenses/plex-media-server
  install -m 644 terms.txt "${pkgdir}"/usr/share/licenses/plex-media-server/
}
