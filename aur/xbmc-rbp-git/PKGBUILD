
# Contributor tomasgroth at yahoo.dk
# Contributor WarheadsSE <max@warheads.net>

buildarch=16

pkgname=xbmc-rbp-git
pkgver=13.20131021
pkgrel=1
pkgdesc="A software media player and entertainment hub for digital media for the Raspberry Pi"
arch=('armv6h')
url="http://xbmc.org"
license=('GPL' 'custom')
depends=('hicolor-icon-theme' 'fribidi' 'lzo2' 'smbclient' 'libtiff' 'libva' 'libpng' 'libcdio' 'yajl' 'libmariadbclient' 'libjpeg-turbo' 'libsamplerate' 'libssh' 'libmicrohttpd' 'sdl_image' 'python2' 'libass' 'libmpeg2' 'libmad' 'libmodplug' 'jasper' 'rtmpdump' 'unzip' 'xorg-xdpyinfo' 'libbluray' 'libnfs' 'afpfs-ng' 'libshairport' 'avahi' 'bluez-libs' 'tinyxml' 'raspberrypi-firmware' 'libcec-rpi' 'libplist' 'swig' 'taglib')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'zip' 'udisks' 'upower' 'git' 'autoconf' 'java-environment')
optdepends=(
  'lirc: remote controller support'
  'udisks: automount external drives'
  'upower: used to trigger suspend functionality'
  'unrar: access compressed files without unpacking them'
)
provides=("xbmc")
conflicts=("xbmc")
install="xbmc.install"
source=('xbmc.service')
md5sums=('36780e43d432c488259d9a899b1bcb28')

_gitname="xbmc"
_gitroot="git://github.com/xbmc"

_prefix=/usr

prepare() {
  cd "${srcdir}"


  msg2 "Connecting to GIT server..."
  
  if [[ -d "${_gitname}" ]]; then
     cd "${_gitname}" && git pull origin
     msg2 "The local files are updated."
  else
     git clone --depth 1 "${_gitroot}/${_gitname}"
  fi
  
  msg2 "GIT checkout done or server timeout." 


  cd "${srcdir}/${_gitname}"

  # fix lsb_release dependency
  sed -i -e 's:/usr/bin/lsb_release -d:cat /etc/arch-release:' xbmc/utils/SystemInfo.cpp

  # Change outdated macro use 
  # find . -name 'configure*' | xargs grep -l AM_CONFIG_HEADER | xargs sed -i -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g'

  # Patch to fix TexturePacker build. 
  # patch -p1 -i ${srcdir}/xbmc-ae04d99-321-texturepacker-hostflags-and-rework.patch


  # fix samba4 libsmbclient.h location
  sed -e 's/\(#include \)<libsmbclient\.h>/\1<samba-4.0\/libsmbclient\.h>/g' \
    -i xbmc/filesystem/SmbFile.cpp \
    -i xbmc/filesystem/SMBDirectory.cpp
}

build() {
  cd "${srcdir}/${_gitname}"

  # Bootstrapping XBMC
  ./bootstrap

  # Configuring XBMC
  export PYTHON_VERSION=2  # external python v2
  # we need to compile for armv6 instead of armv5 to avoid problems compiling assembler code
  export CFLAGS="-Ofast -mcpu=arm1176jzf-s -mtune=arm1176jzf-s -fexcess-precision=fast -mfloat-abi=hard -mfpu=vfp -mabi=aapcs-linux -pipe -fstack-protector --param=ssp-buffer-size=4 -D_FORTIFY_SOURCE=2 -I/opt/vc/include/ -I/opt/vc/include/IL -I/opt/vc/include/interface/vcos/pthreads -I/opt/vc/include/interface/vmcs_host/linux"
  export CXXFLAGS="-Ofast -mcpu=arm1176jzf-s -mtune=arm1176jzf-s -fexcess-precision=fast -mfloat-abi=hard -mfpu=vfp -mabi=aapcs-linux -fstack-protector --param=ssp-buffer-size=4 -D_FORTIFY_SOURCE=2 -I/opt/vc/include/ -I/opt/vc/include/IL -I/opt/vc/include/interface/vcos/pthreads -I/opt/vc/include/interface/vmcs_host/linux"
  export LDFLAGS="$LDFLAGS -L/opt/vc/lib"
#  export MAKEFLAGS="-j1"
  ./configure --prefix=$_prefix --exec-prefix=$_prefix  \
  --with-platform=raspberry-pi --disable-gl --enable-gles --disable-x11 --disable-sdl \
  --enable-optimizations --disable-external-libraries --disable-goom --disable-hal \
  --disable-pulse --disable-vaapi --disable-vdpau --disable-xrandr --enable-airplay --disable-alsa \
  --enable-avahi --enable-libbluray --enable-dvdcss --disable-debug --disable-joystick --disable-mid \
  --enable-nfs --disable-profiling --disable-projectm --enable-rsxs --enable-rtmp --disable-vaapi \
  --disable-vdadecoder --disable-external-ffmpeg --enable-optical-drive --enable-player=omxplayer
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  # Running make install
  make DESTDIR="${pkgdir}" install

  # run feh with python2
  sed -i -e 's/python/python2/g' ${pkgdir}${_prefix}/bin/xbmc

  # Remove checks that doesn't apply to the raspberry pi
  head -n 171 "${pkgdir}${_prefix}/share/xbmc/FEH.py" > "${pkgdir}${_prefix}/share/xbmc/FEH.py.new"
  mv "${pkgdir}${_prefix}/share/xbmc/FEH.py.new"  "${pkgdir}${_prefix}/share/xbmc/FEH.py"

  # lsb_release fix
  sed -i -e 's/which lsb_release > \/dev\/null/\[ -f \/etc\/arch-release ]/g' "${pkgdir}${_prefix}/bin/xbmc"
  sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" "${pkgdir}${_prefix}/bin/xbmc"

  # Tools
  install -D -m 0755 "${srcdir}/${_gitname}/tools/TexturePacker/TexturePacker" "${pkgdir}${_prefix}/share/xbmc/"

  # Licenses
  install -d -m 0755 "${pkgdir}${_prefix}/share/licenses/${pkgname}"
  for licensef in LICENSE.GPL copying.txt; do
    mv "${pkgdir}${_prefix}/share/doc/xbmc/${licensef}" "${pkgdir}${_prefix}/share/licenses/${pkgname}"
  done

  # systemd stuff
  install -Dm0644 $srcdir/xbmc.service $pkgdir/usr/lib/systemd/system/xbmc.service

}
