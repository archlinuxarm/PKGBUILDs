# Maintainer: Jami Kettunen <jami.kettunen@protonmail.com>
# Contributor: Raihan Ahamed <raihan1999ahamed@gmail.com>

buildarch=8

_pkgname="qrtr"
pkgname="$_pkgname-git"
pkgdesc="Userspace reference for net/qrtr in the Linux kernel"
pkgver=r114.daf7f4c
pkgrel=1
arch=("aarch64")
url="https://github.com/andersson/$_pkgname"
license=("BSD-3-Clause")
groups=("qcom-icnss-wlan")
depends=("glibc")
makedepends=("git" "meson" "gcc" "linux-headers")
provides=("$_pkgname")
source=("git+https://github.com/andersson/$_pkgname")
md5sums=("SKIP")

pkgver() {
	cd "$_pkgname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	arch-meson $_pkgname build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	install -Dm644 "$_pkgname"/LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
