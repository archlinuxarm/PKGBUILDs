# Maintainer: Tomasz Zok <tomasz.zok [at] gmail.com>
# Maintainer: techryda <techryda at silentdome dot com>
# Contributor: Mathias R. <pu154r@overlinux.org>
pkgname=xrdp
pkgver=0.6.0
pkgrel=6
pkgdesc="An open source remote desktop protocol (RDP) server"
url="http://xrdp.sourceforge.net/"
arch=('arm' 'armv6h' 'armv7h')
license=('GPL')
depends=('tigervnc')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-v$pkgver.tar.gz"
        'xrdp.service'
        'xrdp-sesman.service'
        '0001-Fix-the-build-with-automake-1.13.patch')
md5sums=('b2577256b5a34bd72849cd40375c92de'
         '0cb760b3e8a34f9bdf4daa871444d74c'
         '58eb44bdc7ca5bb436d6fd66826f9b0f'
         'dd097b937263565e34b6526560ac00a3')

prepare() {
    cd "${pkgname}-v${pkgver}"
    patch < ../0001-Fix-the-build-with-automake-1.13.patch
    # Fix path in xrdp.sh file
    sed -i 's|/usr/local/sbin|/usr/bin|' instfiles/xrdp.sh
}

build() {
    cd "${pkgname}-v${pkgver}"
    ./bootstrap
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin
    make
}

package() {
    cd "${pkgname}-v${pkgver}"
    make DESTDIR="$pkgdir" install
    cd ${pkgdir}
    # Install systemd service files
    mkdir -p usr/lib/systemd/system
    cp ${srcdir}/*.service usr/lib/systemd/system
}
