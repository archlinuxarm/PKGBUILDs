# Contributor: Matthew Bauer <mjbauer95@gmail.com>
# Maintainer: Steven Allen <steven@stebalien.com>

_pkgname=pithos
pkgname=$_pkgname-git
pkgver=396
pkgrel=1
epoch=1
pkgdesc='Pandora Internet Radio player for GNOME'
arch=('any')
url="http://pithos.github.io/"
license=('GPL3')
depends=('python' 'gtk3' 'python-gobject' 'python-dbus' 'gst-plugins-good' 'gst-plugins-bad' 'gst-plugins-base' 'python-pylast' 'libnotify' 'python-setuptools')
makedepends=('git')
optdepends=('libkeybinder3: for media keys plugin'
            'gst-plugins-ugly: MP3 playback support'
            'libappindicator3: Unity indicator applet support'
            'python-pacparser: PAC proxy support')
provides=("$_pkgname")
conflicts=("$_pkgname-bzr" "$_pkgname")
install="$pkgname.install"

source=("$pkgname.install" "git+https://github.com/pithos/pithos.git")

pkgver() {
  cd "$srcdir/$_pkgname"
  git rev-list HEAD --count
}

prepare() {
  cd "$srcdir/$_pkgname"
}

package() {
  cd "$srcdir/$_pkgname"
  python setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}"
}

md5sums=('c9db50a1fd3d86efe4d6f8a7b044e718'
         'SKIP')

# vim:set ts=2 sw=2 et:
