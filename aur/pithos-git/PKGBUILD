# Contributor: Matthew Bauer <mjbauer95@gmail.com>
# Maintainer: Steven Allen <steven@stebalien.com>

_pkgname=pithos
pkgname=pithos-git
pkgver=294.759fd22
pkgver(){
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}
epoch=1
pkgrel=1
pkgdesc='Pandora Internet Radio player for GNOME'
arch=('any')
url="http://kevinmehall.net/p/pithos/"
license=('GPL3')
depends=('python2' 'pygtk' 'python2-notify' 'python2-dbus' 'gstreamer0.10-python' 'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-base-plugins' 'python2-xdg' 'hicolor-icon-theme')
makedepends=('git' 'python2-distribute' 'python2-distutils-extra')
optdepends=('python2-keybinder2: for media keys plugin'
            'gstreamer0.10-ugly-plugins: MP3 playback support')
provides=("$_pkgname")
conflicts=("$_pkgname-bzr" "$_pkgname")
install="$pkgname.install"

source=("git://github.com/kevinmehall/pithos.git")
md5sums=('SKIP')


build() {
  cd "$srcdir/$_pkgname"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname"
  python2 setup.py install --prefix=/usr --root="${pkgdir}/"
}

# vim:set ts=2 sw=2 et:
