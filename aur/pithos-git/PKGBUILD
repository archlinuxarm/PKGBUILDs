# Contributor: Matthew Bauer <mjbauer95@gmail.com>
# Maintainer: Steven Allen <steven@stebalien.com>

_pkgname=pithos
pkgname=$_pkgname-git
pkgver=308
pkgrel=1
epoch=1
pkgdesc='Pandora Internet Radio player for GNOME'
arch=('any')
url="http://kevinmehall.net/p/pithos/"
license=('GPL3')
depends=('python2' 'pygtk' 'python2-notify' 'python2-dbus' 'gstreamer0.10-python' 'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-base-plugins' 'python2-xdg' 'hicolor-icon-theme')
makedepends=('git' 'python2-setuptools' 'python2-distutils-extra')
optdepends=('python2-keybinder2: for media keys plugin'
            'gstreamer0.10-ugly-plugins: MP3 playback support')
provides=("$_pkgname")
conflicts=("$_pkgname-bzr" "$_pkgname")
install="$pkgname.install"

source=("$pkgname.install" "git://github.com/kevinmehall/pithos.git")

pkgver() {
  cd "$srcdir/$_pkgname"
  git rev-list HEAD --count
}

package() {
  cd "$srcdir/$_pkgname"
  python2 setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}"
}

md5sums=('c9db50a1fd3d86efe4d6f8a7b044e718'
         'SKIP')

# vim:set ts=2 sw=2 et:
