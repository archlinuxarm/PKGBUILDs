# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Tilman Blumenbach <tilman@ax86.net>

pkgname=hd-idle
pkgver=1.04
pkgrel=1
pkgdesc='Utility program for spinning-down external disks after a period of idle time.'
arch=(i686 x86_64)
backup=(etc/conf.d/hd-idle)
url="http://hd-idle.sourceforge.net/"
license=('GPL')
install="hd-idle.install"
source=("http://downloads.sf.net/$pkgname/$pkgname-$pkgver.tgz"
        "hd-idle.init")
md5sums=('41e52e669fc59fa82ee0c2bcce1336d3'
         '19c07682085d7fa99527c0f35073d331')

build() {
    cd "$srcdir/$pkgname"
    make
}

package() {
    cd "$srcdir/$pkgname"

    # Install binary
    make TARGET_DIR="$pkgdir/usr" install

    # Install custom init script and stock defaults file:
    install -Do 0 -g 0 "$srcdir/hd-idle.init" "$pkgdir/etc/rc.d/hd-idle"
    install -Dpm 644 -o 0 -g 0 debian/hd-idle.default "$pkgdir/etc/conf.d/hd-idle"

    # Install README:
    install -Dm 644 -o 0 -g 0 README "$pkgdir/usr/share/doc/$pkgname/README"
}

