# Contributor: Tilman Blumenbach <tilman@ax86.net>

# Plugbox changes: None. This package is just from AUR.

pkgname=hd-idle
pkgver=1.01
pkgrel=5
pkgdesc='Utility program for spinning-down external disks after a period of idle time.'
arch=(i686 x86_64)
backup=(etc/conf.d/hd-idle)
makedepends=(make)
url="http://hd-idle.sourceforge.net/"
license=('GPL')
install="${pkgname}.install"
source=("http://downloads.sf.net/${pkgname}/${pkgname}-${pkgver}.tgz"
        "hd-idle.init")

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Build binary
    make || return 1
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p "${pkgdir}/usr/sbin"

    # Install binary
    make TARGET_DIR="${pkgdir}/usr/sbin" install || return 1

    # Install init script and defaults file:
    install -Do 0 -g 0 "${srcdir}/hd-idle.init" "${pkgdir}/etc/rc.d/hd-idle"
    install -Dpm 644 -o 0 -g 0 "scripts/debian/default/hd-idle" "${pkgdir}/etc/conf.d/hd-idle"

    # Install README:
    install -Dm 644 -o 0 -g 0 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

md5sums=('bfb7db4d4372d3954b7cd96a11956741'
         '19c07682085d7fa99527c0f35073d331')
