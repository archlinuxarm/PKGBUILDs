# Maintainer: Alex Natallson <HarmoniesWorlds@gmail.com>
#
pkgname=gphotofs
pkgver=0.5
pkgrel=3
pkgdesc="FUSE filesystem module to mount your camera as a filesystem on Linux."
arch=('aarch64')
url="http://www.gphoto.org/proj/gphotofs/"
license=('GPL2')
depends=('fuse' 'glib2' 'libgphoto2')
makedepends=('pkgconfig')
source=("http://downloads.sourceforge.net/sourceforge/gphoto/gphotofs-${pkgver}.tar.bz2"
	"https://git.savannah.gnu.org/cgit/config.git/plain/config.guess"
	"https://git.savannah.gnu.org/cgit/config.git/plain/config.sub"
)
sha256sums=('676ec4de69a81c193ffc31bdc7b587ac2a2cc3780b14f0e7c9c4c0a517b343cc'
	    'd4b8b6815be8fe5eee29a72e6272ee2a67cfdd0e43f438c8ee2b33570335dc3d'
      '71b8d73e46e0c31b1dc91ba5306f5ef0af009273b3bb283f31d8dad69666fa9e'
)

prepare() {
  cd "${srcdir}/gphotofs-${pkgver}"
  cp ../config.guess auto-aux/
  cp ../config.sub auto-aux/
}

build() {
  cd "${srcdir}/gphotofs-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/gphotofs-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
