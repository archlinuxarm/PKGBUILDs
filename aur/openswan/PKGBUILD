# Contributor: uuwe
# Maintainer: xjpvictor Huang <ke [AT] xjpvictor [DOT] info>
pkgname=openswan
pkgver=2.6.42
pkgrel=2
pkgdesc="Open Source implementation of IPsec for the Linux operating system"
url="http://www.openswan.org"
license=('GPL' 'custom')
arch=('i686' 'x86_64')
depends=('iproute2>=2.6.8' 'gmp' 'perl')
optdepends=('python2')
makedepends=('flex' 'bison')
conflicts=('ipsec-tools')
backup=(etc/ipsec.conf \
        etc/ipsec.d/policies/{block,clear,clear-or-private,private,private-or-clear})
source=(http://download.openswan.org/openswan/openswan-$pkgver.tar.gz
        openswan.service)

prepare() {
  cd $srcdir/${pkgname}-$pkgver

  # Change install paths to Arch defaults
  sed -i 's|/usr/local|/usr|;s|libexec/ipsec|lib/openswan|;s|)/sbin|)/bin|' Makefile.inc

  # Replace invalid init script paths with systemd script path
  sed -i 's/^INC_RCDIRS.*/INC_RCDIRS\?\=\/usr\/lib\/systemd\/scripts/' Makefile.inc
}

build() {
  cd $srcdir/${pkgname}-$pkgver
  make USE_XAUTH=true USE_OBJDIR=true programs || return 1
}

package() {
  cd $srcdir/${pkgname}-$pkgver

  # Pre-create init script directory
  mkdir -p $pkgdir/usr/lib/systemd/scripts/

  make DESTDIR="${pkgdir}" install

  # Change permissions in /var
  chmod 700 $pkgdir/var/run/pluto

  # Copy License
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/openswan/LICENSE

  # Install init script
  install -Dm644 ../openswan.service $pkgdir/usr/lib/systemd/system/openswan.service

  # fix manpages
  mv $pkgdir/usr/man $pkgdir/usr/share/

  # fix python2
  sed -i '1s|python|python2|' $pkgdir/usr/lib/openswan/verify
}
md5sums=('22240ce28be48f051bfdc874083c8947'
         'd8b465c10838c72e31329d65011002b6')
