#Maintainer: Christian Stankowic <info at stankowic hypen development dot net>
#Contributor: Gary Wright <wriggary at gmail dot com>

#ALARM: moonman
#      -added .service file for systemd
#      -changed default port to 80 (required by .service)
#      -changed pid file dir from /var/log to /var/run
#      -changed default User from nobody to http (for compatibility)

pkgname=monkey
pkgver=1.3.0
pkgrel=1
pkgdesc="A very small and fast open source web server for Linux"
arch=('arm')
url="http://www.monkey-project.com/"
license=('GPL2')
depends=('gcc-libs')
optdepends=('php')
source=("http://www.monkey-project.com/releases/$(echo $pkgver |cut -c 1-3)/$pkgname-$pkgver.tar.gz"
    monkey
    monkey.service)
install=monkey.install
md5sums=('04e9c73734f6bd3fb53be9956361c635'
         '1527d1fbddddcfd69ad328639dcd0eed'
         'ae1f2da03aa2a515a3bed0fb16ab7130')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --bindir=/usr/bin --sysconfdir=/etc/$pkgname \
  --default-user=http --default-port=80 \
  --datadir=/srv/http --logdir=/var/log/$pkgname \
  --plugdir=/usr/lib/$pkgname
  make 
 }


package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  msg2 "Changing default pid file location from /var/log to /var/run"
  sed -i -e "s/\/var\/log\/monkey\/monkey.pid/\/var\/run\/monkey.pid/" $pkgdir/etc/monkey/monkey.conf

  install -Dm 755 "$srcdir/monkey" "$pkgdir/etc/rc.d/monkey"
  install -Dm 644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}
