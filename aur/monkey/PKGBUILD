#Maintainer: Christian Stankowic <info at stankowic hypen development dot net>
#Contributor: Gary Wright <wriggary at gmail dot com>
pkgname=monkey
pkgver=1.4.0
pkgrel=1
pkgdesc="A very small and fast open source web server for Linux"
arch=('i686' 'x86_64')
url="http://www.monkey-project.com/"
license=('GPL2')
depends=('gcc-libs')
optdepends=('php')
source=("http://www.monkey-project.com/releases/1.4/$pkgname-$pkgver.tar.gz"
    monkey
    monkey.service)
install=monkey.install
md5sums=('c17e394b985ead873fc7e7cb2a05cef7'
	 '1527d1fbddddcfd69ad328639dcd0eed'
	 'ae1f2da03aa2a515a3bed0fb16ab7130')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --bindir=/usr/bin --sysconfdir=/etc/$pkgname --default-user=http --default-port=80 \
  --datadir=/srv/http --logdir=/var/log/$pkgname \
  --plugdir=/usr/lib/$pkgname
  make 
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  msg2 "Change default pid file location from /var/log to /var/run/monkey"
  sed -i -e "s/\/var\/log\/monkey\/monkey.pid/\/var\/run\/monkey.pid/" $pkgdir/etc/monkey/monkey.conf

  install -Dm 755 "$srcdir/monkey" "$pkgdir/etc/rc.d/monkey"
  install -Dm 644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}
