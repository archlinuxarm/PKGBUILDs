# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl
pkgver=0.2
pkgrel=1
arch=(any)
pkgdesc="Network configuration and profile scripts"
url="http://archlinux.org/netctl/"
license=("GPL")
depends=("coreutils" "iproute2" "openresolv")
makedepends=('asciidoc')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support, or use dhcpcd'
            'dhcpcd: for DHCP support, or use dhclient'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ifenslave: for bond connections'
            'bridge-utils: for bridge connections'
            )
conflicts=("netcfg" "netctl")
provides=("netctl")

source=("https://github.com/joukewitteveen/$pkgname/archive/$pkgver.tar.gz"
        'zsh-completion')
md5sums=('cb144eda0e5dd09f5b07db00421a429f'
         '3428e0e7f061bbcde41f2fe64d8d96dd')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -Dm644 "$srcdir/zsh-completion" "$pkgdir/usr/share/zsh/site-functions/_netctl"
}

