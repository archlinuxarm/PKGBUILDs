# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Contributor: Vsevolod Balashov <vsevolod@balashov.name>, Kevin Zuber <uKev@knet.eu>
pkgname=uwsgi
pkgver=1.3
pkgrel=1
pkgdesc="a fast (pure C), self-healing, developer-friendly WSGI server"
arch=(i686 x86_64)
url="http://projects.unbit.it/$pkgname"
license=(GPL2)
depends=(python2 libxml2)
conflicts=(python-$pkgname)
makedepends=(gcc python2)
backup=(etc/conf.d/uwsgid etc/uwsgi/emperor.ini)
source=(http://projects.unbit.it/downloads/$pkgname-$pkgver.tar.gz
        uwsgid.sh
        emperor.ini
        emperor.uwsgi.service
        uwsgid.conf)
md5sums=('0e83c1ef1b7b01a16c87884d23030ce2'
         '3dd03dce734b50910b3a5e12da0fc062'
         '495afaafac6da631a859ab72c5983c36'
         '85a7401c62dc4834259fc27d9c0703ee'
         'a3ae324e43ba841aacd917542bd7e878')

build() {
  cd $srcdir/$pkgname-$pkgver
  python2 uwsgiconfig.py --build
}

package() {
  cd $srcdir/$pkgname-$pkgver
  mkdir -p $pkgdir/usr/bin
  install -Dm755 uwsgi $pkgdir/usr/bin/uwsgi
  mkdir -p $pkgdir/etc/rc.d
  install -Dm755 $srcdir/uwsgid.sh $pkgdir/etc/rc.d/uwsgid
  mkdir -p $pkgdir/etc/conf.d
  install -Dm644 $srcdir/uwsgid.conf $pkgdir/etc/conf.d/uwsgid
  mkdir -p $pkgdir/etc/uwsgi/apps
  install -Dm644 $srcdir/emperor.ini $pkgdir/etc/uwsgi/emperor.ini
  install -Dm644 $srcdir/emperor.uwsgi.service $pkgdir/etc/systemd/system/emperor.uwsgi.service
}
