# Maintainer: OK100 <ok100 at lavabit dot com>
# Previous Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=compton-git
pkgver=290.dfd4dd1
pkgver() {
	cd $srcdir/compton
	echo $(git rev-list --count master).$(git rev-parse --short master)
}
epoch=1
pkgrel=1
pkgdesc="X Compositor (a fork of xcompmgr-dana)"
arch=(i686 x86_64)
url="https://github.com/chjj/compton"
license=('MIT')
depends=('libx11' 'libxcomposite' 'libxdamage' 'libxfixes' 'libxext' 'libxrender' 'libxrandr' 'xproto' 'bash' 'xorg-xprop' 'xorg-xwininfo' 'pcre' 'libconfig')
makedepends=('git' 'make' 'pkg-config' 'asciidoc' 'libgl')
source=("compton::git://github.com/chjj/compton.git")

_gitname="compton"


build() {
  cd "$_gitname"
  make
  make docs
}

package() {
  cd "$_gitname"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  # install license
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  # example conf
  install -D -m644 "compton.sample.conf" "$pkgdir/etc/xdg/compton.conf.example"
}
md5sums=('SKIP')
