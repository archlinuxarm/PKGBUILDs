# Maintainer: Philipp Scholl <pscholl@bawue.de>

pkgname=db
pkgver=4.7.25
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=(arm)
license=('custom')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
groups=('base')
depends=('gcc-libs')
options=('!libtool')
source=(http://download-uk.oracle.com/berkeley-db/db-$pkgver.tar.gz
	http://www.oracle.com/technology/products/berkeley-db/db/update/4.7.25/patch.4.7.25.1
	http://www.oracle.com/technology/products/berkeley-db/db/update/4.7.25/patch.4.7.25.2
	http://www.oracle.com/technology/products/berkeley-db/db/update/4.7.25/patch.4.7.25.3)
install=db.install
md5sums=('ec2b87e833779681a0c3a814aa71359e'
         '5fdf101259e5164dea1c8c86214fde38'
         'bd410a11c71fee52fddb6aa2d8d4f80c'
         '6fcd69f64f5b34bfe8f0a63cc2e402c1')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -Np0 -i ${srcdir}/patch.4.7.25.1 || return 1
  patch -Np0 -i ${srcdir}/patch.4.7.25.2 || return 1
  patch -Np0 -i ${srcdir}/patch.4.7.25.3 || return 1
  cd ${srcdir}/$pkgname-$pkgver/build_unix
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --enable-static --enable-cxx
  make LIBSO_LIBS=-lpthread || return 1
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/docs
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
