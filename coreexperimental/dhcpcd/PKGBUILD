# $Id: PKGBUILD 40050 2009-05-24 21:34:22Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd
pkgver=5.0.4
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/dhcpcd/"
arch=(arm)
license=('BSD')
#groups=('base')
depends=('glibc' 'bash')
backup=('etc/conf.d/dhcpcd' 'etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.bz2" \
        'dhcpcd.conf.d')
md5sums=('0b920a8094ee3a8c16c973794561feef'
         '372d33485556982b64a97f301e17c5dd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Fix Installation Locations
  export PREFIX=/usr
  sed -i 's/${PREFIX}\/etc/\/etc/' Makefile || return 1
  sed -i 's/\/db/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' dhcpcd-hooks/Makefile || return 1
  sed -i 's/${PREFIX}\/sbin/\/sbin/' Makefile || return 1

  # Build
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # Create Binary Symlink
  install -d ${pkgdir}/usr/sbin || return 1
  ln -sf /sbin/dhcpcd ${pkgdir}/usr/sbin/dhcpcd || return 1

  # Install Configuration File used in /etc/rc.d/network
  install -D -m644 ../dhcpcd.conf.d $pkgdir/etc/conf.d/$pkgname || return 1

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname || return 1
  awk '{if(FNR<27)print $0}' ${srcdir}/${pkgname}-${pkgver}/config.h \
	>> ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  # Make Man Pages FHS Compliant
  mv -f ${pkgdir}/usr/man ${pkgdir}/usr/share/man || return 1

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf || return 1 # Disable ip4vall
}
