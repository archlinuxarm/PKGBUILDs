# Maintainer: Krzysztof "hiciu" Warzecha <kwarzecha7@gmail.com>

pkgname=kernel26-openmoko
pkgver=2.6.29
_gitver=dcd90798cdab0ab1e7c1fb56af8841bb225b5fdc
pkgrel=4
pkgdesc="The Linux Kernel and modules"
arch=(arm)
license=('GPL2')
url="http://wiki.openmoko.org/wiki/Kernel"
depends=('coreutils')
makedepends=('git' 'uboot-mkimage-native')
replaces=('kernel26')
conflicts=('kernel26')
provides=('kernel26')
source=('append-GTA02' 'ppoll.patch' 'gcc-4.4.patch' 'aufs2-29.patch' 'config')

md5sums=('ad1cb2f0a3000f5285c58b22fe8f4de3'
         '54b7414a29b007cfb58de8dae9398021'
         'e47d7b36a921ecdf913bdd3f9af08a0a'
         'd41d8cd98f00b204e9800998ecf8427e'
         'b70b0eae868962745d06f6cfe56b417e')

build() {
  cd ${srcdir}

  # make this build clean
  if [ -x linux-2.6 ]; then
    cd linux-2.6
  else
    git clone --depth 1 -n git://git.openmoko.org/git/kernel.git linux-2.6
    cd linux-2.6
    git checkout --track -b andy-tracking origin/andy-tracking

    # include ppoll.patch needed by klibc
    # gathered from arm linux kernel tree, should be merged in official
    patch -p1 < ${srcdir}/ppoll.patch

    # without this patch modules are unloadable
    patch -p0 < ${srcdir}/gcc-4.4.patch

    # add aufs2 support to kernel
    patch -p1 < ${srcdir}/aufs2-29.patch
  fi

  cp ${srcdir}/config .config
  make -j4 ARCH=arm

  mkdir ${pkgdir}/boot

  # these are copied from build sh in andy-tracking
  objcopy -O binary -R .note -R .comment -S arch/arm/boot/compressed/vmlinux linux.bin
  mkimage -A arm -O linux -T kernel -C none -a 30008000 -n "OM GTA02 andy-tracking" -d linux.bin ${pkgdir}/boot/uImage-moredrivers-GTA02.bin

  make INSTALL_MOD_PATH=${pkgdir} ARCH=arm modules_install

  cd ${pkgdir}/boot
  ln -s uImage-moredrivers-*.bin uImage-GTA02.bin
  cp ${srcdir}/append-GTA02 ${pkgdir}/boot
}
