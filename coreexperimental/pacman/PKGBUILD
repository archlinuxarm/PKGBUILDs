# Maintainer: Alexander Foremny <alexanderforemny@gmail.com>

pkgname=pacman
pkgver=3.3.0
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('arm')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('libarchive>=2.7.0' 'libfetch>=2.20')
optdepends=('fakeroot: for makepkg usage as normal user'
            'python: for rankmirrors script usage')
backup=(etc/pacman.conf etc/makepkg.conf)
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf makepkg.conf)
md5sums=('945b95633cc7340efb4d4564b463c6b1'
         'c4a4a737fe9839a1d86351c3b7b80ba5'
         'bd55e52adf11eb4f6459b9a1bb8aff36')

build() {
  cd "$srcdir/pacman-$pkgver"
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # Install Arch specific stuff.
  install -Dm644 "$srcdir/pacman.conf" "$pkgdir/etc/"
  install -Dm644 "$srcdir/makepkg.conf" "$pkgdir/etc/"

  # install completion files
  mkdir -p "$pkgdir/etc/bash_completion.d/"
  install -m644 contrib/bash_completion "$pkgdir/etc/bash_completion.d/pacman"
  mkdir -p "$pkgdir/usr/share/zsh/site-functions/"
  install -m644 contrib/zsh_completion \
                "$pkgdir/usr/share/zsh/site-functions/_pacman"
}
